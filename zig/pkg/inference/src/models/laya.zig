// Copyright 2026 Antfly, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

//! Checkpoint-owned Laya decision configuration; no borrowed JSON storage.
const std = @import("std");
pub const QuestionType = enum(u8) { choice, score, noul };

/// Released checkpoints encode one sequence per question and admit 20 options.
pub const max_options = 20;
/// Candidate-branch packing gives every option its own branch (see LAYA.md).
pub const max_packed_options = 255;
/// Physical tokens per packed row. Segment attention keeps no `[L, L]` state;
/// the bound keeps u32 kernel indexing and per-row staging modest.
pub const max_packed_len_limit = 32768;
/// Bound on `packing.fuse_layers`; checked against the real stack depth
/// wherever the encoder config is known.
pub const max_fuse_layers = 256;

/// Tree-packed execution (zig/pkg/inference/models/laya/LAYA.md). The state is a
/// shared trunk that attends only to itself; each question, and in candidate
/// mode each option, is a branch that attends to its ancestors and itself.
/// Positions restart after the parent at every branch. Released checkpoints
/// use `.none`; a packed mode requires weights fine-tuned for that layout.
pub const PackingMode = enum(u8) { none, question, candidate };

/// Two-stage choice (roadmap 2b, LAYA.md "Two-stage choice for many options").
/// Stage 1 scores every option as its own candidate branch; when a `choice`
/// question has more options than `top_k`, stage 2 packs the surviving
/// finalists into one joint (question-style) branch off the same trunk, so
/// they attend to each other, and its distribution replaces their share of
/// the stage-1 mass. Only meaningful with `PackingMode.candidate`, and only a
/// checkpoint fine-tuned on a mix of candidate and joint branches should serve
/// it (see `finetune/laya/data.zig`).
pub const TwoStage = struct {
    /// Finalists kept for the joint branch. 0 disables two-stage choice.
    top_k: usize = 0,
    /// Optional cumulative stage-1 probability-mass cutoff in (0, 1] that can
    /// shortlist fewer than `top_k` finalists; 0 disables it (always `top_k`,
    /// or every option when there are fewer). Never fewer than 2 finalists.
    mass_cutoff: f32 = 0,

    pub fn enabled(self: TwoStage) bool {
        return self.top_k >= 2;
    }
};

pub const Packing = struct {
    mode: PackingMode = .none,
    /// Physical tokens in one packed row. `Config.max_len` still bounds the
    /// logical length (trunk plus the longest root-to-leaf branch path).
    max_packed_len: usize = 0,
    two_stage: TwoStage = .{},
    /// `trunk_sees: "questions"`: state tokens also attend to every branch in
    /// their tree, restoring early fusion at the cost of trunk reuse
    /// (LAYA.md, "Question-aware trunk"). Default `"state"`.
    trunk_sees_questions: bool = false,
    /// Per-question upper layers (LAYA.md, "Per-question upper layers"):
    /// the top `fuse_layers` layers of the encoder-plus-head stack let each
    /// question's copy of the state attend to that question. Rows then hold
    /// one question per tree, so questions stay isolated, and the layers
    /// below stay question-blind. 0 disables; question mode only. The top
    /// layer's state output is never read (the scorer reads option markers),
    /// so `fuse_layers` K gives questions K-1 layers of question-aware state
    /// and must be at least 2.
    fuse_layers: u32 = 0,
    /// Question-first positions (LAYA.md): every branch starts at logical
    /// position 0, as the question does in the unpacked layout, and the trunk
    /// starts after the question budget (`laya_tree.trunkOffset`). Only
    /// positions change; visibility does not.
    question_first: bool = false,

    pub fn enabled(self: Packing) bool {
        return self.mode != .none;
    }

    /// First layer of the encoder-plus-head stack (`layers` deep) that runs
    /// per question; `layers` when fusion is off.
    pub fn fuseFrom(self: Packing, layers: usize) usize {
        return layers - @min(self.fuse_layers, layers);
    }
};

/// Serving precision of the encoder and decision-head linear weights (LAYA.md,
/// step 1d). Embeddings, norms, the type embedding, the scorer and the action
/// head stay dense. Quantization happens at load from the dense checkpoint.
pub const WeightQuantization = enum(u8) { none, q8_0 };

/// Whether `name` (a checkpoint tensor name) is a linear weight that
/// `weight_quantization` applies to.
pub fn quantizedLinear(name: []const u8) bool {
    const encoder = [_][]const u8{ ".attn.Wqkv.weight", ".attn.Wo.weight", ".mlp.Wi.weight", ".mlp.Wo.weight" };
    const head = [_][]const u8{ ".self_attn.in_proj_weight", ".self_attn.out_proj.weight", ".linear1.weight", ".linear2.weight" };
    if (std.mem.startsWith(u8, name, "encoder.layers.")) {
        for (encoder) |suffix| if (std.mem.endsWith(u8, name, suffix)) return true;
    } else if (std.mem.startsWith(u8, name, "head.layers.")) {
        for (head) |suffix| if (std.mem.endsWith(u8, name, suffix)) return true;
    }
    return false;
}

pub const DecisionHead = enum { scorer, pointer };

/// Checkpoint family. `laya`: upstream Laya's layout and heads. `opendecider`:
/// OpenDecider-nano (manjunathshiva/opendecider-nano), whose input is
/// `[CLS] question: … [SEP] ([MASK] option)* [SEP] input: <state> [SEP]` and
/// whose head is only a marker MLP, `scorer.0` (linear), GELU, `scorer.2`
/// (LayerNorm) and `scorer.3` (linear), with no type embedding, head layers
/// or action head.
pub const Format = enum { laya, opendecider };

pub const Config = struct {
    mask_token: [128]u8 = "[MASK]".* ++ (@as([122]u8, @splat(0))),
    mask_token_len: usize = 6,
    head_layers: usize = 2,
    max_len: usize = 512,
    head_max_len: usize = 192,
    n_act: usize = 2,
    temperature: [3]f32 = .{ 1, 1, 1 },
    buckets: [3][4]?f32 = .{ .{ null, null, null, null }, .{ null, null, null, null }, .{ null, null, null, null } },
    packing: Packing = .{},
    weight_quantization: WeightQuantization = .none,
    /// How options are scored from the head's output. `scorer` (upstream):
    /// an MLP over each option marker. `pointer` (LAYA.md, "Lessons from
    /// Jeeves"): a scaled dot product between a query projection of the
    /// question's `[CLS]` anchor and a key projection of each option marker,
    /// `pointer.q`/`pointer.k`, each `[pointer_dim, hidden]`, both reading
    /// the head's output through the LayerNorm `pointer.norm`.
    decision_head: DecisionHead = .scorer,
    pointer_dim: usize = 256,
    format: Format = .laya,
    /// Cut the end of a state that does not fit `max_len` instead of
    /// rejecting it, as upstream Laya and OpenDecider do. Never read from a
    /// config; `finetune eval laya --truncate-state` sets it to score the
    /// community benchmark, whose longest states exceed 512 tokens.
    truncate_state: bool = false,

    /// The configured precision, unless ANTFLY_LAYA_WEIGHT_QUANT names one.
    pub fn effectiveWeightQuantization(self: Config) !WeightQuantization {
        const value = @import("antfly_platform").env.getenv("ANTFLY_LAYA_WEIGHT_QUANT") orelse return self.weight_quantization;
        return std.meta.stringToEnum(WeightQuantization, value) orelse error.InvalidLayaConfig;
    }

    pub fn scale(self: Config, kind: QuestionType, count: usize) f32 {
        const bucket: usize = if (count <= 2) 0 else if (count <= 5) 1 else if (count <= 10) 2 else 3;
        return @max(0.001, self.buckets[@backingInt(kind)][bucket] orelse self.temperature[@backingInt(kind)]);
    }

    pub fn maxOptions(self: Config) usize {
        return if (self.packing.mode == .candidate) max_packed_options else max_options;
    }

    pub fn parse(value: std.json.Value) !Config {
        if (value != .object) return error.InvalidLayaConfig;
        const obj = value.object;
        var out = Config{};
        if (obj.get("mask_token")) |v| {
            if (v != .string or v.string.len == 0 or v.string.len > out.mask_token.len) return error.InvalidLayaConfig;
            @memcpy(out.mask_token[0..v.string.len], v.string);
            out.mask_token_len = v.string.len;
        }
        inline for (.{ "head_layers", "max_len", "head_max_len" }) |name| {
            if (obj.get(name)) |v| {
                if (v != .integer or v.integer < 0) return error.InvalidLayaConfig;
                @field(out, name) = std.math.cast(usize, v.integer) orelse return error.InvalidLayaConfig;
            }
        }
        if (out.head_layers > 16 or out.max_len < 16 or out.max_len > 8192 or out.head_max_len < 16 or out.head_max_len >= out.max_len) return error.InvalidLayaConfig;
        if (obj.get("act_costs")) |v| {
            if (v != .object or v.object.count() > 32) return error.InvalidLayaConfig;
            out.n_act = v.object.count() + 1;
        }
        if (obj.get("temperature")) |v| {
            if (v != .array or v.array.items.len != 3) return error.InvalidLayaConfig;
            for (v.array.items, &out.temperature) |item, *dest| dest.* = try positive(item);
        }
        if (obj.get("temperature_by_options")) |v| {
            if (v != .object) return error.InvalidLayaConfig;
            inline for (.{ "choice", "score", "noul" }, 0..) |kind, k| {
                inline for (.{ "2", "3-5", "6-10", "11+" }, 0..) |bucket, b| {
                    if (v.object.get(kind ++ ":" ++ bucket)) |item| out.buckets[k][b] = try positive(item);
                }
            }
        }
        if (obj.get("packing")) |v| out.packing = try parsePacking(v, out.max_len);
        if (obj.get("weight_quantization")) |v| {
            if (v != .string) return error.InvalidLayaConfig;
            out.weight_quantization = std.meta.stringToEnum(WeightQuantization, v.string) orelse return error.InvalidLayaConfig;
        }
        if (obj.get("decision_head")) |v| {
            if (v != .string) return error.InvalidLayaConfig;
            out.decision_head = std.meta.stringToEnum(DecisionHead, v.string) orelse return error.InvalidLayaConfig;
        }
        if (obj.get("pointer_dim")) |v| {
            if (v != .integer or v.integer < 8 or v.integer > 4096 or out.decision_head != .pointer) return error.InvalidLayaConfig;
            out.pointer_dim = @intCast(v.integer);
        }
        if (obj.get("format")) |v| {
            if (v != .string) return error.InvalidLayaConfig;
            out.format = std.meta.stringToEnum(Format, v.string) orelse return error.InvalidLayaConfig;
        }
        if (out.format == .opendecider) {
            // Only the marker MLP exists; the unpacked layout is the only one
            // it was trained on.
            if (out.head_layers != 0 or obj.get("act_costs") != null or out.decision_head != .scorer or out.packing.enabled()) return error.InvalidLayaConfig;
            out.n_act = 0;
        }
        return out;
    }
};

fn parsePacking(value: std.json.Value, max_len: usize) !Packing {
    if (value != .object) return error.InvalidLayaConfig;
    var out = Packing{};
    for (value.object.keys()) |key| {
        if (!std.mem.eql(u8, key, "mode") and !std.mem.eql(u8, key, "max_packed_len") and !std.mem.eql(u8, key, "two_stage") and !std.mem.eql(u8, key, "trunk_sees") and !std.mem.eql(u8, key, "fuse_layers") and !std.mem.eql(u8, key, "question_first")) return error.InvalidLayaConfig;
    }
    const mode = value.object.get("mode") orelse return error.InvalidLayaConfig;
    if (mode != .string) return error.InvalidLayaConfig;
    out.mode = std.meta.stringToEnum(PackingMode, mode.string) orelse return error.InvalidLayaConfig;
    if (out.mode == .none) {
        if (value.object.get("two_stage") != null or value.object.get("trunk_sees") != null or value.object.get("fuse_layers") != null or value.object.get("question_first") != null) return error.InvalidLayaConfig;
        return out;
    }
    out.max_packed_len = @min(4 * max_len, max_packed_len_limit);
    if (value.object.get("max_packed_len")) |v| {
        if (v != .integer or v.integer < 0) return error.InvalidLayaConfig;
        out.max_packed_len = std.math.cast(usize, v.integer) orelse return error.InvalidLayaConfig;
    }
    if (out.max_packed_len < max_len or out.max_packed_len > max_packed_len_limit) return error.InvalidLayaConfig;
    if (value.object.get("two_stage")) |v| {
        if (out.mode != .candidate) return error.InvalidLayaConfig;
        out.two_stage = try parseTwoStage(v);
    }
    if (value.object.get("trunk_sees")) |v| {
        if (v != .string) return error.InvalidLayaConfig;
        if (std.mem.eql(u8, v.string, "questions")) {
            out.trunk_sees_questions = true;
        } else if (!std.mem.eql(u8, v.string, "state")) return error.InvalidLayaConfig;
    }
    if (value.object.get("fuse_layers")) |v| {
        // A state copy that saw its candidates would leak between sibling
        // candidates, and a trunk that already sees every question has
        // nothing left to fuse.
        if (v != .integer or v.integer < 2 or v.integer > max_fuse_layers or out.mode != .question or out.trunk_sees_questions) return error.InvalidLayaConfig;
        out.fuse_layers = @intCast(v.integer);
    }
    if (value.object.get("question_first")) |v| {
        if (v != .bool) return error.InvalidLayaConfig;
        out.question_first = v.bool;
    }
    return out;
}

fn parseTwoStage(value: std.json.Value) !TwoStage {
    if (value != .object) return error.InvalidLayaConfig;
    for (value.object.keys()) |key| {
        if (!std.mem.eql(u8, key, "top_k") and !std.mem.eql(u8, key, "mass_cutoff")) return error.InvalidLayaConfig;
    }
    const top_k = value.object.get("top_k") orelse return error.InvalidLayaConfig;
    if (top_k != .integer or top_k.integer < 2 or top_k.integer > max_packed_options) return error.InvalidLayaConfig;
    var out = TwoStage{ .top_k = @intCast(top_k.integer) };
    if (value.object.get("mass_cutoff")) |v| {
        const n: f32 = switch (v) {
            .integer => |i| @floatFromInt(i),
            .float => |f| @floatCast(f),
            else => return error.InvalidLayaConfig,
        };
        if (!std.math.isFinite(n) or n <= 0 or n > 1) return error.InvalidLayaConfig;
        out.mass_cutoff = n;
    }
    return out;
}

test "laya packing config defaults, bounds, and rejects unknown fields" {
    const a = std.testing.allocator;
    const cases = [_]struct { json: []const u8, mode: ?PackingMode, len: usize = 0, fuse: u32 = 0 }{
        .{ .json = "{}", .mode = .none },
        .{ .json = "{\"packing\":{\"mode\":\"question\"}}", .mode = .question, .len = 2048 },
        .{ .json = "{\"packing\":{\"mode\":\"candidate\",\"max_packed_len\":1024}}", .mode = .candidate, .len = 1024 },
        .{ .json = "{\"packing\":{\"mode\":\"none\"}}", .mode = .none },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"max_packed_len\":256}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"max_packed_len\":65536}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"tree\"}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"shared\":true}}", .mode = null },
        // Two-stage choice only makes sense with candidate branches.
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"two_stage\":{\"top_k\":8}}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"none\",\"two_stage\":{\"top_k\":8}}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"candidate\",\"two_stage\":{\"top_k\":1}}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"candidate\",\"two_stage\":{\"top_k\":8,\"mass_cutoff\":1.5}}}", .mode = null },
        // Per-question upper layers: question mode only, not with a trunk
        // that already sees every question, and at least one layer.
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"fuse_layers\":4}}", .mode = .question, .len = 2048, .fuse = 4 },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"fuse_layers\":0}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"fuse_layers\":1}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"candidate\",\"fuse_layers\":2}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"none\",\"fuse_layers\":2}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"fuse_layers\":2,\"trunk_sees\":\"questions\"}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"question_first\":true}}", .mode = .question, .len = 2048 },
        .{ .json = "{\"packing\":{\"mode\":\"question\",\"question_first\":1}}", .mode = null },
        .{ .json = "{\"packing\":{\"mode\":\"none\",\"question_first\":true}}", .mode = null },
    };
    for (cases) |case| {
        const parsed = try std.json.parseFromSlice(std.json.Value, a, case.json, .{});
        defer parsed.deinit();
        if (case.mode) |mode| {
            const cfg = try Config.parse(parsed.value);
            try std.testing.expectEqual(mode, cfg.packing.mode);
            try std.testing.expectEqual(case.len, cfg.packing.max_packed_len);
            try std.testing.expectEqual(case.fuse, cfg.packing.fuse_layers);
            try std.testing.expectEqual(@as(usize, if (mode == .candidate) 255 else 20), cfg.maxOptions());
        } else try std.testing.expectError(error.InvalidLayaConfig, Config.parse(parsed.value));
    }
}

test "laya two-stage config parses top_k and mass_cutoff and defaults to disabled" {
    const a = std.testing.allocator;
    const disabled = try std.json.parseFromSlice(std.json.Value, a, "{\"packing\":{\"mode\":\"candidate\"}}", .{});
    defer disabled.deinit();
    const cfg = try Config.parse(disabled.value);
    try std.testing.expect(!cfg.packing.two_stage.enabled());

    const enabled = try std.json.parseFromSlice(std.json.Value, a, "{\"packing\":{\"mode\":\"candidate\",\"two_stage\":{\"top_k\":8,\"mass_cutoff\":0.9}}}", .{});
    defer enabled.deinit();
    const two_stage = (try Config.parse(enabled.value)).packing.two_stage;
    try std.testing.expect(two_stage.enabled());
    try std.testing.expectEqual(@as(usize, 8), two_stage.top_k);
    try std.testing.expectApproxEqAbs(@as(f32, 0.9), two_stage.mass_cutoff, 1e-9);
}
fn positive(value: std.json.Value) !f32 {
    const n: f32 = switch (value) {
        .integer => |v| @floatFromInt(v),
        .float => |v| @floatCast(v),
        else => return error.InvalidLayaConfig,
    };
    if (!std.math.isFinite(n) or n <= 0) return error.InvalidLayaConfig;
    return n;
}

test "laya opendecider format has no action head and refuses Laya-only settings" {
    const a = std.testing.allocator;
    const p = try std.json.parseFromSlice(std.json.Value, a, "{\"format\":\"opendecider\",\"head_layers\":0,\"max_len\":2048}", .{});
    defer p.deinit();
    const cfg = try Config.parse(p.value);
    try std.testing.expectEqual(Format.opendecider, cfg.format);
    try std.testing.expectEqual(@as(usize, 0), cfg.n_act);
    for ([_][]const u8{
        "{\"format\":\"opendecider\"}",
        "{\"format\":\"opendecider\",\"head_layers\":0,\"act_costs\":{\"tool\":1}}",
        "{\"format\":\"opendecider\",\"head_layers\":0,\"decision_head\":\"pointer\"}",
        "{\"format\":\"opendecider\",\"head_layers\":0,\"packing\":{\"mode\":\"question\"}}",
    }) |json| {
        const bad = try std.json.parseFromSlice(std.json.Value, a, json, .{});
        defer bad.deinit();
        try std.testing.expectError(error.InvalidLayaConfig, Config.parse(bad.value));
    }
}

test "laya calibration bucket takes precedence and invalid temperatures fail" {
    const a = std.testing.allocator;
    const p = try std.json.parseFromSlice(std.json.Value, a, "{\"temperature\":[2,3,4],\"temperature_by_options\":{\"choice:3-5\":1.5}}", .{});
    defer p.deinit();
    const cfg = try Config.parse(p.value);
    try std.testing.expectEqual(@as(f32, 1.5), cfg.scale(.choice, 4));
    try std.testing.expectEqual(@as(f32, 2), cfg.scale(.choice, 2));
    try std.testing.expectEqual(@as(f32, 4), cfg.scale(.noul, 2));
    const bad = try std.json.parseFromSlice(std.json.Value, a, "{\"temperature\":[0,1,1]}", .{});
    defer bad.deinit();
    try std.testing.expectError(error.InvalidLayaConfig, Config.parse(bad.value));
}

/// Validate the complete source checkpoint before a backend can execute it.
/// Initial support intentionally accepts one dense safetensors artifact.
pub fn validateWeights(store: @import("tensor_store.zig").TensorStore, cfg: Config, encoder: anytype) !void {
    const reader = store.singleSafetensorsReader() orelse return error.UnsupportedLayaArtifact;
    return validateReader(reader, cfg, encoder);
}

pub fn validateReader(reader: *const @import("safetensors.zig").MMapReader, cfg: Config, encoder: anytype) !void {
    const Check = struct {
        fn tensor(r: @TypeOf(reader), name: []const u8, shape: []const i64) !void {
            const meta = r.header.tensors.get(name) orelse return error.InvalidLayaWeights;
            if (!std.mem.eql(i64, meta.shape, shape)) return error.InvalidLayaWeights;
            switch (meta.dtype) {
                .f32, .f16, .bf16 => {},
                else => return error.InvalidLayaWeights,
            }
        }
        fn pair(r: @TypeOf(reader), prefix: []const u8, input: i64, output: i64) !void {
            var buf: [256]u8 = undefined;
            try tensor(r, try std.fmt.bufPrint(&buf, "{s}.weight", .{prefix}), &.{ output, input });
            try tensor(r, try std.fmt.bufPrint(&buf, "{s}.bias", .{prefix}), &.{output});
        }
        fn norm(r: @TypeOf(reader), prefix: []const u8, dim: i64, bias: bool) !void {
            var buf: [256]u8 = undefined;
            try tensor(r, try std.fmt.bufPrint(&buf, "{s}.weight", .{prefix}), &.{dim});
            if (bias) try tensor(r, try std.fmt.bufPrint(&buf, "{s}.bias", .{prefix}), &.{dim});
        }
    };
    const d: i64 = encoder.hidden_size;
    const f: i64 = encoder.intermediate_size;
    try Check.tensor(reader, "encoder.embeddings.tok_embeddings.weight", &.{ encoder.vocab_size, d });
    try Check.norm(reader, "encoder.embeddings.norm", d, false);
    try Check.norm(reader, "encoder.final_norm", d, false);
    var name: [128]u8 = undefined;
    for (0..encoder.num_hidden_layers) |layer| {
        if (layer > 0) try Check.norm(reader, try std.fmt.bufPrint(&name, "encoder.layers.{d}.attn_norm", .{layer}), d, false);
        try Check.norm(reader, try std.fmt.bufPrint(&name, "encoder.layers.{d}.mlp_norm", .{layer}), d, false);
        try Check.tensor(reader, try std.fmt.bufPrint(&name, "encoder.layers.{d}.attn.Wqkv.weight", .{layer}), &.{ d * 3, d });
        try Check.tensor(reader, try std.fmt.bufPrint(&name, "encoder.layers.{d}.attn.Wo.weight", .{layer}), &.{ d, d });
        try Check.tensor(reader, try std.fmt.bufPrint(&name, "encoder.layers.{d}.mlp.Wi.weight", .{layer}), &.{ f * 2, d });
        try Check.tensor(reader, try std.fmt.bufPrint(&name, "encoder.layers.{d}.mlp.Wo.weight", .{layer}), &.{ d, f });
    }
    if (cfg.format == .opendecider) {
        try Check.pair(reader, "scorer.0", d, d);
        try Check.norm(reader, "scorer.2", d, true);
        try Check.pair(reader, "scorer.3", d, 1);
        return;
    }
    try Check.tensor(reader, "type_emb.weight", &.{ 3, d });
    try Check.norm(reader, "scorer.0", d, true);
    try Check.pair(reader, "scorer.1", d, d);
    try Check.pair(reader, "scorer.3", d, 1);
    if (cfg.decision_head == .pointer) {
        try Check.norm(reader, "pointer.norm", d, true);
        try Check.pair(reader, "pointer.q", d, @intCast(cfg.pointer_dim));
        try Check.pair(reader, "pointer.k", d, @intCast(cfg.pointer_dim));
    }
    try Check.pair(reader, "act_head.0", d + 4, 256);
    try Check.pair(reader, "act_head.2", 256, @intCast(cfg.n_act));
    for (0..cfg.head_layers) |layer| {
        try Check.tensor(reader, try std.fmt.bufPrint(&name, "head.layers.{d}.self_attn.in_proj_weight", .{layer}), &.{ d * 3, d });
        try Check.tensor(reader, try std.fmt.bufPrint(&name, "head.layers.{d}.self_attn.in_proj_bias", .{layer}), &.{d * 3});
        try Check.pair(reader, try std.fmt.bufPrint(&name, "head.layers.{d}.self_attn.out_proj", .{layer}), d, d);
        try Check.pair(reader, try std.fmt.bufPrint(&name, "head.layers.{d}.linear1", .{layer}), d, d * 4);
        try Check.pair(reader, try std.fmt.bufPrint(&name, "head.layers.{d}.linear2", .{layer}), d * 4, d);
        try Check.norm(reader, try std.fmt.bufPrint(&name, "head.layers.{d}.norm1", .{layer}), d, true);
        try Check.norm(reader, try std.fmt.bufPrint(&name, "head.layers.{d}.norm2", .{layer}), d, true);
    }
}

test "laya weight quantization covers encoder and head linears only" {
    for ([_][]const u8{ "encoder.layers.0.attn.Wqkv.weight", "encoder.layers.27.mlp.Wo.weight", "head.layers.1.self_attn.in_proj_weight", "head.layers.0.linear2.weight" }) |name|
        try std.testing.expect(quantizedLinear(name));
    for ([_][]const u8{ "encoder.embeddings.tok_embeddings.weight", "encoder.layers.0.mlp_norm.weight", "head.layers.0.self_attn.in_proj_bias", "head.layers.0.linear1.bias", "scorer.1.weight", "act_head.0.weight", "type_emb.weight" }) |name|
        try std.testing.expect(!quantizedLinear(name));
    const parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, "{\"weight_quantization\":\"q8_0\"}", .{});
    defer parsed.deinit();
    try std.testing.expectEqual(WeightQuantization.q8_0, (try Config.parse(parsed.value)).weight_quantization);
    const bad = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, "{\"weight_quantization\":\"q4\"}", .{});
    defer bad.deinit();
    try std.testing.expectError(error.InvalidLayaConfig, Config.parse(bad.value));
}
