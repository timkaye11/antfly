// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at https://www.antfly.io/licensing/ELv2-license.

//! Typed dependency expansion, shared by HTTP mutations and integrity workers.
//! A catalog supplies durable generation bindings; no client supplied state or
//! schema-version hash may manufacture a claim generation. The result must be
//! compiled on each claim owner and enlisted alongside the primary mutations in
//! the existing durable distributed transaction protocol.
const std = @import("std");
const registry = @import("../storage/db/schema_registry.zig");
const codec = @import("../storage/db/algebraic/relational_row_codec.zig");
const tuples = @import("../storage/db/relational_index_keys.zig");
const partial_predicate = @import("../storage/db/relational_index_predicate.zig");
const native = @import("../storage/relational_index.zig");
pub const storage = @import("../storage/db/relational_integrity_contract.zig");
const Allocator = std.mem.Allocator;

pub const UniqueBinding = struct {
    generation: storage.Generation,
    definition: native.UniqueConstraint,
};
pub const ForeignBinding = struct {
    generation: storage.Generation,
    parent_generation: storage.Generation,
    definition: native.ForeignKey,
    /// A pinned parent epoch proves exact type compatibility. The generation
    /// must identify this parent's declared unique key, in the same order.
    parent: registry.SchemaView,
    parent_unique: native.UniqueConstraint,
};
const BoundUnique = struct { generation: storage.Generation, definition: native.UniqueConstraint, tuple: tuples.TuplePlan, predicate: ?partial_predicate.Plan = null };
const BoundForeign = struct { generation: storage.Generation, parent_generation: storage.Generation, definition: native.ForeignKey, tuple: tuples.TuplePlan };

pub const Mutation = struct {
    key: []const u8,
    before: ?codec.OrdinalRowView = null,
    after: ?codec.OrdinalRowView = null,
    repair: bool = false,
};

pub const RoutedCommand = struct { table_name: []const u8, command: storage.Command };
pub const ParentTransition = struct {
    table_name: []const u8,
    parent_key: []const u8,
    address: storage.Address,
    target_tuple: ?[]const u8,
};

pub const PartialDependency = struct {
    definition: native.ForeignKey,
    parent_generation: storage.Generation,
    reference: storage.Reference,
    before: ?[]const u8,
    after: ?[]const u8,
    repair: bool,
};

pub const Expansion = struct {
    arena: std.heap.ArenaAllocator,
    commands: []const RoutedCommand,
    /// Parent transitions cannot be committed as ordinary row deletion. The
    /// coordinator first resolves incoming descriptors and either proves
    /// RESTRICT or schedules a gated, bounded parent-action job.
    parents: []const ParentTransition,
    partials: []const PartialDependency = &.{},
    pub fn deinit(self: *Expansion) void {
        self.arena.deinit();
        self.* = undefined;
    }
};

pub const Plan = struct {
    arena: std.heap.ArenaAllocator,
    view: registry.SchemaView,
    table_name: []const u8,
    uniques: []const BoundUnique,
    foreign: []const BoundForeign,

    pub fn init(alloc: Allocator, table_name: []const u8, view: registry.SchemaView, uniques: []const UniqueBinding, foreign: []const ForeignBinding) !Plan {
        if (view.storageMode() != .relational or table_name.len == 0 or uniques.len + foreign.len > 256) return error.InvalidIntegrityDefinition;
        var arena = std.heap.ArenaAllocator.init(alloc);
        errdefer arena.deinit();
        const owned = arena.allocator();
        const bound_uniques = try owned.alloc(BoundUnique, uniques.len);
        var initialized_uniques: usize = 0;
        errdefer for (bound_uniques[0..initialized_uniques]) |unique| {
            if (unique.predicate) |predicate_value| {
                var predicate = predicate_value;
                predicate.deinit();
            }
        };
        for (uniques, bound_uniques) |binding, *bound| {
            const def = binding.definition;
            if (std.mem.allEqual(u8, &binding.generation, 0) or (def.columns.len == 0 and def.keys.len == 0) or (def.columns.len != 0 and def.keys.len != 0) or def.expressions.len != 0 or
                def.include_columns.len != 0 or def.where_expressions.len != 0 or
                def.without_overlaps_period != null or (def.timing == .deferred and !def.deferrable)) return error.UnsupportedIntegrityDefinition;
            var copied = def;
            copied.name = try owned.dupe(u8, def.name);
            copied.columns = try copyColumns(owned, def.columns);
            const keys = try owned.dupe(native.RelationalIndexKey, def.keys);
            for (keys) |*key| {
                key.column = try owned.dupe(u8, key.column);
                if (key.expression_json) |json| key.expression_json = try owned.dupe(u8, json);
                if (key.collation) |collation| key.collation = try owned.dupe(u8, collation);
            }
            copied.keys = keys;
            const copied_where = try owned.alloc(native.UniquePredicate, def.where.len);
            for (def.where, copied_where) |source, *predicate| {
                predicate.* = source;
                predicate.field = try owned.dupe(u8, source.field);
                if (source.value_json) |value| predicate.value_json = try owned.dupe(u8, value);
                if (source.collation) |collation| predicate.collation = try owned.dupe(u8, collation);
            }
            copied.where = copied_where;
            const tuple = if (keys.len != 0) try tuples.TuplePlan.init(owned, view.tableSchema().*, view.physicalLayout(), keys) else try bindTuple(owned, view, def.columns);
            const predicate = if (def.where.len != 0) try partial_predicate.Plan.init(alloc, view.tableSchema().*, view.physicalLayout(), def.where) else null;
            bound.* = .{ .generation = binding.generation, .definition = copied, .tuple = tuple, .predicate = predicate };
            initialized_uniques += 1;
        }
        const bound_foreign = try owned.alloc(BoundForeign, foreign.len);
        for (foreign, bound_foreign) |binding, *bound| {
            const def = binding.definition;
            if (std.mem.allEqual(u8, &binding.generation, 0) or std.mem.allEqual(u8, &binding.parent_generation, 0) or
                def.child_columns.len == 0 or def.child_columns.len != def.parent_columns.len or
                def.child_period != null or def.parent_period != null or (def.timing == .deferred and !def.deferrable))
                return error.UnsupportedIntegrityDefinition;
            if (binding.parent_unique.deferrable or binding.parent_unique.timing != .immediate or binding.parent_unique.where.len != 0 or binding.parent_unique.keys.len != 0) return error.ForeignKeyTargetNotUnique;
            if (!sameColumns(def.parent_columns, binding.parent_unique.columns)) return error.ForeignKeyTargetNotUnique;
            var parent_tuple = try bindTuple(owned, binding.parent, def.parent_columns);
            defer parent_tuple.deinit();
            const child_tuple = try bindTuple(owned, view, def.child_columns);
            for (child_tuple.keys, parent_tuple.keys) |child, parent| {
                if (child.column_type != parent.column_type) return error.ForeignKeyTypeMismatch;
            }
            var copied = def;
            copied.name = try owned.dupe(u8, def.name);
            copied.child_columns = try copyColumns(owned, def.child_columns);
            copied.parent_table = try owned.dupe(u8, def.parent_table);
            copied.parent_columns = try copyColumns(owned, def.parent_columns);
            bound.* = .{ .generation = binding.generation, .parent_generation = binding.parent_generation, .definition = copied, .tuple = child_tuple };
        }
        const owned_name = try owned.dupe(u8, table_name);
        return .{ .arena = arena, .view = view.clone(), .table_name = owned_name, .uniques = bound_uniques, .foreign = bound_foreign };
    }

    pub fn deinit(self: *Plan) void {
        for (self.uniques) |unique| if (unique.predicate) |predicate_value| {
            var predicate = predicate_value;
            predicate.deinit();
        };
        self.view.release();
        self.arena.deinit();
        self.* = undefined;
    }

    /// Bind the SQL conflict shape against durable native unique generations,
    /// once per statement. Column order does not change arbiter inference;
    /// encoding always follows the constraint's canonical native tuple order.
    /// An empty target selects every supported immediate unique constraint for
    /// DO NOTHING; the SQL boundary independently arbitrates physical row IDs.
    pub fn bindConflictTarget(self: *const Plan, alloc: Allocator, columns: []const []const u8, arbiter_predicate: []const native.UniquePredicate) ![]const usize {
        return self.bindConflictExpressions(alloc, columns, &.{}, arbiter_predicate);
    }

    pub fn bindConflictExpressions(self: *const Plan, alloc: Allocator, columns: []const []const u8, expressions: []const native.RelationalIndexKey, arbiter_predicate: []const native.UniquePredicate) ![]const usize {
        const target_count = columns.len + expressions.len;
        if (target_count > 32) return error.InvalidIntegrityDefinition;
        const keys = try alloc.alloc(native.RelationalIndexKey, target_count);
        defer alloc.free(keys);
        for (columns, keys[0..columns.len]) |column, *key| key.* = .{ .column = column };
        @memcpy(keys[columns.len..], expressions);
        var target = if (target_count != 0) try tuples.TuplePlan.init(alloc, self.view.tableSchema().*, self.view.physicalLayout(), keys) else null;
        defer if (target) |*tuple| tuple.deinit();
        if (target) |tuple| for (0..target_count) |i| for (0..i) |j| {
            if (tuple.sameEqualityKey(i, tuple, j)) return error.InvalidIntegrityDefinition;
        };
        var selected: std.ArrayList(usize) = .empty;
        errdefer selected.deinit(alloc);
        var target_plan: ?partial_predicate.Plan = if (arbiter_predicate.len != 0) try partial_predicate.Plan.init(alloc, self.view.tableSchema().*, self.view.physicalLayout(), arbiter_predicate) else null;
        defer if (target_plan) |*plan| plan.deinit();
        for (self.uniques, 0..) |unique, index| {
            if (target_count == 0) {
                if (unique.definition.deferrable) return error.DeferrableConflictArbiter;
                try selected.append(alloc, index);
                continue;
            }
            if (unique.definition.where.len != 0) {
                const inferred = target_plan orelse continue;
                if (!(unique.predicate orelse return error.InvalidIntegrityDefinition).impliedBy(inferred.conditions)) continue;
            }
            if (unique.tuple.keys.len != target_count) continue;
            const matches = for (0..target_count) |i| {
                const present = for (0..unique.tuple.keys.len) |j| {
                    if (target.?.sameEqualityKey(i, unique.tuple, j)) break true;
                } else false;
                if (!present) break false;
            } else true;
            if (matches) {
                if (unique.definition.deferrable) return error.DeferrableConflictArbiter;
                try selected.append(alloc, index);
            }
        }
        if (selected.items.len == 0 and target_count != 0) return error.ConflictArbiterNotFound;
        return selected.toOwnedSlice(alloc);
    }

    pub const ConflictAddress = struct { address: storage.Address, tuple: []const u8 };

    /// Uses exactly the ordinary uniqueness claim codec. NULL-distinct tuples
    /// have no conflict address (their native witness is row-specific), while
    /// NULLS NOT DISTINCT remains a real arbiter. Output is request-owned.
    pub fn conflictAddresses(self: *const Plan, alloc: Allocator, selected: []const usize, row: codec.OrdinalRowView) ![]const ConflictAddress {
        if (row.layout != self.view.physicalLayout() or row.table_schema.relational_columns.ptr != self.view.tableSchema().relational_columns.ptr) return error.PreparedGenerationChanged;
        var output: std.ArrayList(ConflictAddress) = .empty;
        errdefer {
            for (output.items) |item| alloc.free(item.tuple);
            output.deinit(alloc);
        }
        for (selected) |index| {
            if (index >= self.uniques.len) return error.InvalidIntegrityDefinition;
            const unique = self.uniques[index];
            if (unique.predicate) |predicate| {
                var scratch: std.ArrayList(u8) = .empty;
                defer scratch.deinit(alloc);
                if (!try predicate.active.matches(alloc, &scratch, row)) continue;
            }
            var tuple = try unique.tuple.encodeAlloc(alloc, row);
            if (tuple.has_null and !unique.definition.nulls_not_distinct) {
                tuple.deinit(alloc);
                continue;
            }
            errdefer tuple.deinit(alloc);
            try output.append(alloc, .{ .address = try storage.Address.init(unique.generation, tuple.bytes), .tuple = tuple.bytes });
        }
        return output.toOwnedSlice(alloc);
    }

    pub fn expand(self: *const Plan, alloc: Allocator, mutations: []const Mutation) !Expansion {
        if (mutations.len > 4096) return error.TransactionTooLarge;
        var arena = std.heap.ArenaAllocator.init(alloc);
        errdefer arena.deinit();
        const owned = arena.allocator();
        var commands = std.ArrayList(RoutedCommand).empty;
        var parents = std.ArrayList(ParentTransition).empty;
        var partials = std.ArrayList(PartialDependency).empty;
        const table_name = try owned.dupe(u8, self.table_name);
        for (mutations) |mutation| {
            const key = try owned.dupe(u8, mutation.key);
            if (mutation.after) |after| if (after.layout != self.view.physicalLayout() or
                after.table_schema.relational_columns.ptr != self.view.tableSchema().relational_columns.ptr) return error.PreparedGenerationChanged;
            for (self.uniques) |unique| {
                const before_member = try uniqueMatches(owned, self.view, unique, mutation.before);
                const after_member = try uniqueMatches(owned, self.view, unique, mutation.after);
                const before = try encodeUnique(owned, unique.tuple, if (before_member) mutation.before else null, unique.definition.nulls_not_distinct, key);
                const after = try encodeUnique(owned, unique.tuple, if (after_member) mutation.after else null, unique.definition.nulls_not_distinct, key);
                if (sameTuple(before, after) and !mutation.repair) {
                    if (after) |tuple| try commands.append(owned, .{ .table_name = table_name, .command = .{
                        .address = try storage.Address.init(unique.generation, tuple),
                        .operation = .{ .check_owner = .{ .parent_table = table_name, .parent_key = key } },
                    } });
                    continue;
                }
                if (!sameTuple(before, after)) if (before) |tuple| try parents.append(owned, .{ .table_name = table_name, .parent_key = key, .address = try storage.Address.init(unique.generation, tuple), .target_tuple = after });
                if (after) |tuple| try commands.append(owned, .{ .table_name = table_name, .command = .{
                    .address = try storage.Address.init(unique.generation, tuple),
                    .operation = .{ .establish = .{ .tuple = tuple, .parent_table = table_name, .parent_key = key, .schema_version = self.view.version() } },
                } });
            }
            for (self.foreign) |foreign| {
                if (foreign.definition.match == .partial) {
                    const encoded = try std.json.Stringify.valueAlloc(owned, foreign.definition, .{});
                    const definition = try std.json.parseFromSliceLeaky(native.ForeignKey, owned, encoded, .{ .allocate = .alloc_always });
                    try partials.append(owned, .{
                        .definition = definition,
                        .parent_generation = foreign.parent_generation,
                        .reference = .{ .child_table = table_name, .child_key = key, .constraint_name = definition.name, .constraint_generation = foreign.generation },
                        .before = if (mutation.before) |row| try row.reconstructValueAlloc(owned) else null,
                        .after = if (mutation.after) |row| try row.reconstructValueAlloc(owned) else null,
                        .repair = mutation.repair,
                    });
                    continue;
                }
                const before = try encode(owned, foreign.tuple, mutation.before, true, foreign.definition.match == .full and !mutation.repair);
                const after = try encode(owned, foreign.tuple, mutation.after, true, foreign.definition.match == .full);
                const reference: storage.Reference = .{
                    .child_table = table_name,
                    .child_key = key,
                    .constraint_name = try owned.dupe(u8, foreign.definition.name),
                    .constraint_generation = foreign.generation,
                };
                const parent_table = try owned.dupe(u8, foreign.definition.parent_table);
                if (!sameTuple(before, after)) if (before) |tuple| try commands.append(owned, .{ .table_name = parent_table, .command = .{ .address = try storage.Address.init(foreign.parent_generation, tuple), .operation = if (mutation.repair) .{ .repair_detach = reference } else .{ .detach = reference } } });
                if (after) |tuple| try commands.append(owned, .{ .table_name = parent_table, .command = .{ .address = try storage.Address.init(foreign.parent_generation, tuple), .operation = .{ .attach = reference } } });
            }
            if (commands.items.len + parents.items.len > storage.max_commands) return error.TransactionTooLarge;
        }
        const owned_result_commands = try commands.toOwnedSlice(owned);
        const owned_result_parents = try parents.toOwnedSlice(owned);
        const owned_result_partials = try partials.toOwnedSlice(owned);
        return .{ .arena = arena, .commands = owned_result_commands, .parents = owned_result_parents, .partials = owned_result_partials };
    }
};

fn uniqueMatches(alloc: Allocator, view: registry.SchemaView, unique: BoundUnique, optional_row: ?codec.OrdinalRowView) !bool {
    const row = optional_row orelse return false;
    const predicate = unique.predicate orelse return true;
    if (row.layout == view.physicalLayout() and row.table_schema.relational_columns.ptr == view.tableSchema().relational_columns.ptr) {
        var scratch: std.ArrayList(u8) = .empty;
        defer scratch.deinit(alloc);
        return predicate.active.matches(alloc, &scratch, row);
    }
    var source = try predicate.projectSource(alloc, row.table_schema, row.layout);
    defer source.deinit();
    var scratch: std.ArrayList(u8) = .empty;
    defer scratch.deinit(alloc);
    return source.matches(alloc, &scratch, row);
}

fn sameTuple(a: ?[]const u8, b: ?[]const u8) bool {
    return if (a) |bytes| if (b) |other| std.mem.eql(u8, bytes, other) else false else b == null;
}

fn copyColumns(alloc: Allocator, columns: []const []const u8) ![]const []const u8 {
    const copied = try alloc.alloc([]const u8, columns.len);
    for (columns, copied) |column, *copy| copy.* = try alloc.dupe(u8, column);
    return copied;
}

fn sameColumns(a: []const []const u8, b: []const []const u8) bool {
    if (a.len != b.len) return false;
    for (a, b) |left, right| if (!std.mem.eql(u8, left, right)) return false;
    return true;
}

fn bindTuple(alloc: Allocator, view: registry.SchemaView, columns: []const []const u8) !tuples.TuplePlan {
    const keys = try alloc.alloc(native.RelationalIndexKey, columns.len);
    for (columns, keys) |column, *key| key.* = .{ .column = column };
    return tuples.TuplePlan.init(alloc, view.tableSchema().*, view.physicalLayout(), keys);
}

fn encode(alloc: Allocator, current: tuples.TuplePlan, optional_row: ?codec.OrdinalRowView, skip_null: bool, match_full: bool) !?[]const u8 {
    const row = optional_row orelse return null;
    var source = if (row.layout == current.layout) current else try current.projectSource(alloc, row.table_schema, row.layout);
    defer if (row.layout != current.layout) source.deinit();
    var result = try source.encodeAlloc(alloc, row);
    if (skip_null and result.has_null) {
        defer result.deinit(alloc);
        if (match_full) {
            var nonnull = false;
            for (source.keys) |key| if (try row.findCell(key.ordinal)) |cell| {
                if (!cell.is_null) nonnull = true;
            };
            if (nonnull) return error.ForeignKeyMatchFullViolation;
        }
        return null;
    }
    return result.bytes;
}

/// NULL-distinct keys are still FK witnesses, but are not unique. Append the
/// exact primary identity only to NULL-containing tuples; their encoded NULL
/// marker already separates this domain from ordinary non-null unique keys.
pub fn encodeUnique(alloc: Allocator, current: tuples.TuplePlan, optional_row: ?codec.OrdinalRowView, nulls_not_distinct: bool, row_key: []const u8) !?[]const u8 {
    const row = optional_row orelse return null;
    var projected = if (row.layout == current.layout) current else try current.projectSource(alloc, row.table_schema, row.layout);
    defer if (row.layout != current.layout) projected.deinit();
    var result = try projected.encodeAlloc(alloc, row);
    if (!result.has_null or nulls_not_distinct) return result.bytes;
    defer result.deinit(alloc);
    var output: std.Io.Writer.Allocating = .init(alloc);
    errdefer output.deinit();
    try output.writer.writeAll(result.bytes);
    try output.writer.writeAll("partial-null-witness-v1");
    try output.writer.writeInt(u32, std.math.cast(u32, row_key.len) orelse return error.TransactionTooLarge, .big);
    try output.writer.writeAll(row_key);
    return try output.toOwnedSlice();
}

test "distributed txn typed integrity expansion matches composite parent claim without float conversion" {
    const alloc = std.testing.allocator;
    const schema = @import("../storage/schema.zig");
    const columns = [_]schema.RelationalColumn{
        .{ .name = "tenant", .path = "tenant", .column_type = .string },
        .{ .name = "id", .path = "id", .column_type = .integer },
    };
    var view = registry.SchemaView{ .epoch = try registry.Epoch.createCloned(alloc, .{ .version = 1, .storage_mode = .relational, .relational_columns = &columns }) };
    defer view.release();
    const unique: native.UniqueConstraint = .{ .name = "identity", .columns = &.{ "tenant", "id" } };
    const fk: native.ForeignKey = .{ .name = "parent", .child_columns = &.{ "tenant", "id" }, .parent_table = "parents", .parent_columns = &.{ "tenant", "id" }, .match = .full };
    var parent = try Plan.init(alloc, "parents", view, &.{.{ .generation = @splat(1), .definition = unique }}, &.{});
    defer parent.deinit();
    var child = try Plan.init(alloc, "children", view, &.{}, &.{.{ .generation = @splat(2), .parent_generation = @splat(1), .definition = fk, .parent = view, .parent_unique = unique }});
    defer child.deinit();
    const primary_only = try child.bindConflictTarget(alloc, &.{}, &.{});
    defer alloc.free(primary_only);
    try std.testing.expectEqual(@as(usize, 0), primary_only.len);
    const encoded = try codec.serializeOrdinal(alloc, 1, view.tableSchema().relational_columns, &.{
        .{ .ordinal = 0, .path = "tenant", .value_type = .bytes_val, .value = .{ .bytes_val = "Acme" } },
        .{ .ordinal = 1, .path = "id", .value_type = .i64_val, .value = .{ .i64_val = 9_007_199_254_740_993 } },
    }, @splat(0));
    defer alloc.free(encoded);
    const row = try codec.ordinalRowView(encoded, view.tableSchema().*, view.physicalLayout());
    {
        var all = try Plan.init(alloc, "parents", view, &.{
            .{ .generation = @splat(1), .definition = unique },
            .{ .generation = @splat(3), .definition = .{ .name = "tenant_key", .columns = &.{"tenant"} } },
        }, &.{});
        defer all.deinit();
        const selected = try all.bindConflictTarget(alloc, &.{}, &.{});
        defer alloc.free(selected);
        try std.testing.expectEqualSlices(usize, &.{ 0, 1 }, selected);
        const addresses = try all.conflictAddresses(alloc, selected, row);
        defer {
            for (addresses) |address| alloc.free(address.tuple);
            alloc.free(addresses);
        }
        try std.testing.expectEqual(@as(usize, 2), addresses.len);
        try std.testing.expect(!std.meta.eql(addresses[0].address, addresses[1].address));
    }
    const target = try parent.bindConflictTarget(alloc, &.{ "id", "tenant" }, &.{});
    defer alloc.free(target);
    const arbiters = try parent.conflictAddresses(alloc, target, row);
    defer {
        for (arbiters) |arbiter| alloc.free(arbiter.tuple);
        alloc.free(arbiters);
    }
    var parent_insert = try parent.expand(alloc, &.{.{ .key = "p1", .after = row }});
    defer parent_insert.deinit();
    var child_insert = try child.expand(alloc, &.{.{ .key = "c1", .after = row }});
    defer child_insert.deinit();
    try std.testing.expectEqual(@as(usize, 1), parent_insert.commands.len);
    try std.testing.expectEqual(@as(usize, 1), arbiters.len);
    try std.testing.expectEqualDeep(parent_insert.commands[0].command.address, arbiters[0].address);
    try std.testing.expectEqual(@as(usize, 1), child_insert.commands.len);
    try std.testing.expectEqualDeep(parent_insert.commands[0].command.address, child_insert.commands[0].command.address);
    try std.testing.expectEqualStrings("parents", child_insert.commands[0].table_name);
    try std.testing.expectEqualStrings("children", child_insert.commands[0].command.operation.attach.child_table);
    var unchanged = try child.expand(alloc, &.{.{ .key = "c1", .before = row, .after = row }});
    defer unchanged.deinit();
    try std.testing.expectEqual(@as(usize, 1), unchanged.commands.len);
    var parent_delete = try parent.expand(alloc, &.{.{ .key = "p1", .before = row }});
    defer parent_delete.deinit();
    try std.testing.expectEqual(@as(usize, 1), parent_delete.parents.len);
    try std.testing.expectEqual(@as(usize, 0), parent_delete.commands.len);

    const partial = try codec.serializeOrdinal(alloc, 1, view.tableSchema().relational_columns, &.{
        .{ .ordinal = 0, .path = "tenant", .value_type = .bytes_val, .value = .{ .bytes_val = "Acme" } },
    }, @splat(0));
    defer alloc.free(partial);
    const partial_row = try codec.ordinalRowView(partial, view.tableSchema().*, view.physicalLayout());
    const null_arbiters = try parent.conflictAddresses(alloc, target, partial_row);
    defer alloc.free(null_arbiters);
    try std.testing.expectEqual(@as(usize, 0), null_arbiters.len);
    try std.testing.expectError(error.ForeignKeyMatchFullViolation, child.expand(alloc, &.{.{ .key = "c2", .after = partial_row }}));
}

test "distributed txn partial unique integrity claims and SQL inference share typed membership proofs" {
    const alloc = std.testing.allocator;
    const schema = @import("../storage/schema.zig");
    const columns = [_]schema.RelationalColumn{
        .{ .name = "email", .path = "email", .column_type = .string },
        .{ .name = "active", .path = "active", .column_type = .boolean },
    };
    var view = registry.SchemaView{ .epoch = try registry.Epoch.createCloned(alloc, .{ .version = 1, .storage_mode = .relational, .relational_columns = &columns }) };
    defer view.release();
    const definition: native.UniqueConstraint = .{
        .name = "active_email",
        .columns = &.{"email"},
        .where = &.{.{ .field = "active", .op = .eq, .value_json = "true" }},
    };
    var plan = try Plan.init(alloc, "users", view, &.{.{ .generation = @splat(7), .definition = definition }}, &.{});
    defer plan.deinit();
    try std.testing.expectError(error.ConflictArbiterNotFound, plan.bindConflictTarget(alloc, &.{"email"}, &.{}));
    const inferred = try plan.bindConflictTarget(alloc, &.{"email"}, &.{
        .{ .field = "active", .op = .eq, .value_json = "true" },
        .{ .field = "email", .op = .is_not_null },
    });
    defer alloc.free(inferred);
    try std.testing.expectEqualSlices(usize, &.{0}, inferred);
    try std.testing.expectError(error.ConflictArbiterNotFound, plan.bindConflictTarget(alloc, &.{"email"}, &.{.{ .field = "active", .op = .eq, .value_json = "false" }}));

    const active_bytes = try codec.serializeOrdinal(alloc, 1, view.tableSchema().relational_columns, &.{
        .{ .ordinal = 0, .path = "email", .value_type = .bytes_val, .value = .{ .bytes_val = "same@example.test" } },
        .{ .ordinal = 1, .path = "active", .value_type = .bool_val, .value = .{ .bool_val = true } },
    }, @splat(0));
    defer alloc.free(active_bytes);
    const inactive_bytes = try codec.serializeOrdinal(alloc, 1, view.tableSchema().relational_columns, &.{
        .{ .ordinal = 0, .path = "email", .value_type = .bytes_val, .value = .{ .bytes_val = "same@example.test" } },
        .{ .ordinal = 1, .path = "active", .value_type = .bool_val, .value = .{ .bool_val = false } },
    }, @splat(0));
    defer alloc.free(inactive_bytes);
    const active_row = try codec.ordinalRowView(active_bytes, view.tableSchema().*, view.physicalLayout());
    const inactive_row = try codec.ordinalRowView(inactive_bytes, view.tableSchema().*, view.physicalLayout());
    const active_addresses = try plan.conflictAddresses(alloc, inferred, active_row);
    defer {
        for (active_addresses) |item| alloc.free(item.tuple);
        alloc.free(active_addresses);
    }
    const inactive_addresses = try plan.conflictAddresses(alloc, inferred, inactive_row);
    defer alloc.free(inactive_addresses);
    try std.testing.expectEqual(@as(usize, 1), active_addresses.len);
    try std.testing.expectEqual(@as(usize, 0), inactive_addresses.len);
    var inactive_insert = try plan.expand(alloc, &.{.{ .key = "inactive-row", .after = inactive_row }});
    defer inactive_insert.deinit();
    try std.testing.expectEqual(@as(usize, 0), inactive_insert.commands.len);
    var active_insert = try plan.expand(alloc, &.{.{ .key = "active-row", .after = active_row }});
    defer active_insert.deinit();
    try std.testing.expectEqual(@as(usize, 1), active_insert.commands.len);
    var leaves_partial_index = try plan.expand(alloc, &.{.{ .key = "active-row", .before = active_row, .after = inactive_row }});
    defer leaves_partial_index.deinit();
    try std.testing.expectEqual(@as(usize, 0), leaves_partial_index.commands.len);
    try std.testing.expectEqual(@as(usize, 1), leaves_partial_index.parents.len);
    var enters_partial_index = try plan.expand(alloc, &.{.{ .key = "inactive-row", .before = inactive_row, .after = active_row }});
    defer enters_partial_index.deinit();
    try std.testing.expectEqual(@as(usize, 1), enters_partial_index.commands.len);
    try std.testing.expectEqual(@as(usize, 0), enters_partial_index.parents.len);
}
