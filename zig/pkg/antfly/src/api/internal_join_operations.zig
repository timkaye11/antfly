// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0.

//! Transport-neutral operations for internal distributed-join coordination.

const std = @import("std");
const distributed_join = @import("distributed_join.zig");
const operation = @import("operation.zig");
const table_reads = @import("table_read_source.zig");

pub const Error = operation.ApiError || error{
    InvalidQueryRequest,
    UnsupportedQueryRequest,
    Timeout,
    TopologyChanged,
    DocIdentityNamespaceMismatch,
};

pub const JobState = struct {
    parsed: std.json.Parsed(distributed_join.EncodedJoinJobState),

    pub fn deinit(self: *JobState) void {
        self.parsed.deinit();
        self.* = undefined;
    }
};

pub const Operations = struct {
    job_store: *distributed_join.JoinJobStore,
    join_context: ?distributed_join.JoinContext = null,
    reads: ?table_reads.TableReadSource = null,

    fn mapExecutionError(err: anyerror) Error {
        if (distributed_join.normalizeDistributedJoinOperationalError(err) == error.DistributedQueryUnavailable)
            return error.Unavailable;
        return switch (err) {
            error.Canceled, error.Cancelled => error.Canceled,
            error.DeadlineExceeded => error.DeadlineExceeded,
            error.InvalidQueryRequest => error.InvalidQueryRequest,
            error.UnsupportedQueryRequest => error.UnsupportedQueryRequest,
            error.TableNotFound => error.NotFound,
            error.Timeout, error.CatalogRoutingSnapshotTimeout => error.Timeout,
            error.CatalogRoutingUnavailable, error.CatalogProjectionRefreshRequired => error.Unavailable,
            error.TopologyChanged => error.TopologyChanged,
            error.DocIdentityNamespaceMismatch => error.DocIdentityNamespaceMismatch,
            else => {
                std.log.err("internal distributed join execution failed err={}", .{err});
                return error.Internal;
            },
        };
    }

    /// Bind transport-neutral request lifetime to every internal worker
    /// execution. `join_context` is process-scoped; cancellation and deadlines
    /// are request-scoped and must never be lost when crossing this boundary.
    fn requestJoinContext(context: distributed_join.JoinContext, request: operation.RequestContext) distributed_join.JoinContext {
        var out = context.withDeadlineFrom(.{ .deadline_ns = request.deadline_ns, .io = request.deadline_io });
        if (context.execution_deadline_ns) |deadline| {
            out.execution_deadline_ns = if (out.execution_deadline_ns) |converted| @min(deadline, converted) else deadline;
        }
        if (request.cancellation.ptr != null and request.cancellation.is_cancelled_fn != null) {
            out = out.withCancellation(request.cancellation);
        }
        return out;
    }

    fn executionDependencies(self: Operations, alloc: std.mem.Allocator, request: operation.RequestContext, group_id: u64) Error!struct {
        context: distributed_join.JoinContext,
        reads: table_reads.TableReadSource,
    } {
        var reads = self.reads orelse return error.NotFound;
        reads.bindCatalogRouteFenceJson(alloc, request.catalog_route_fence_json, group_id, request.deadline_ns, request.cancellation) catch |err| switch (err) {
            error.UnsupportedCatalogRouteFence => return error.Unsupported,
            else => return error.InvalidArgument,
        };
        const context = self.join_context orelse return error.Unavailable;
        return .{
            .context = context,
            .reads = reads,
        };
    }

    pub fn jobState(
        self: Operations,
        alloc: std.mem.Allocator,
        request: operation.RequestContext,
        job_id: u64,
    ) Error!JobState {
        try request.ensureActive();
        if (self.join_context) |context| self.job_store.setContext(context);
        const encoded = (self.job_store.loadJoinJobStateSnapshot(alloc, job_id) catch return error.Internal) orelse
            return error.NotFound;
        defer alloc.free(encoded);
        const parsed = std.json.parseFromSlice(
            distributed_join.EncodedJoinJobState,
            alloc,
            encoded,
            .{ .allocate = .alloc_always, .ignore_unknown_fields = true },
        ) catch return error.Internal;
        return .{ .parsed = parsed };
    }

    pub fn finalize(
        self: Operations,
        alloc: std.mem.Allocator,
        request: operation.RequestContext,
        group_id: u64,
        table_name: []const u8,
        input: distributed_join.JoinFinalizeRequest,
    ) Error!distributed_join.JoinPartitionExecutionResult {
        try request.ensureActive();
        const deps = try self.executionDependencies(alloc, request, group_id);
        return distributed_join.executeJoinFinalizeWorkerLocalTyped(
            requestJoinContext(deps.context, request),
            self.job_store,
            alloc,
            deps.reads,
            group_id,
            table_name,
            input,
        ) catch |err| return mapExecutionError(err);
    }

    pub fn rows(
        self: Operations,
        alloc: std.mem.Allocator,
        request: operation.RequestContext,
        group_id: u64,
        table_name: []const u8,
        input: distributed_join.JoinRowsRequest,
    ) Error![]std.json.Value {
        try request.ensureActive();
        const deps = try self.executionDependencies(alloc, request, group_id);
        return distributed_join.executeJoinRowsLocalTyped(
            requestJoinContext(deps.context, request),
            alloc,
            deps.reads,
            group_id,
            table_name,
            input,
        ) catch |err| return mapExecutionError(err);
    }

    pub fn unmatched(
        self: Operations,
        alloc: std.mem.Allocator,
        request: operation.RequestContext,
        group_id: u64,
        table_name: []const u8,
        input: distributed_join.JoinUnmatchedRequest,
    ) Error!distributed_join.EncodedJoinUnmatchedResponse {
        try request.ensureActive();
        const deps = try self.executionDependencies(alloc, request, group_id);
        return distributed_join.executeJoinUnmatchedLocalTyped(
            requestJoinContext(deps.context, request),
            alloc,
            deps.reads,
            group_id,
            table_name,
            input,
        ) catch |err| return mapExecutionError(err);
    }

    pub fn partition(
        self: Operations,
        alloc: std.mem.Allocator,
        request: operation.RequestContext,
        group_id: u64,
        table_name: []const u8,
        input: distributed_join.JoinPartitionRequest,
    ) Error!distributed_join.JoinPartitionExecutionResult {
        try request.ensureActive();
        const deps = try self.executionDependencies(alloc, request, group_id);
        return distributed_join.executeJoinPartitionWorkerLocalTyped(
            requestJoinContext(deps.context, request),
            self.job_store,
            alloc,
            deps.reads,
            group_id,
            table_name,
            input,
        ) catch |err| return mapExecutionError(err);
    }
};

test "internal join maps resource and ownership failures to unavailable" {
    try std.testing.expectEqual(error.Unavailable, Operations.mapExecutionError(error.DistributedQueryUnavailable));
    try std.testing.expectEqual(error.Unavailable, Operations.mapExecutionError(error.ResourceBudgetExceeded));
    try std.testing.expectEqual(error.Unavailable, Operations.mapExecutionError(error.PersistentDescriptorAdmissionExhausted));
    try std.testing.expectEqual(error.Unavailable, Operations.mapExecutionError(error.UnknownGroup));
    try std.testing.expectEqual(error.Unavailable, Operations.mapExecutionError(error.NotLeader));
    try std.testing.expectEqual(error.Unavailable, Operations.mapExecutionError(error.SendFailed));
    try std.testing.expectEqual(error.NotFound, Operations.mapExecutionError(error.TableNotFound));
}

test "internal join job state is callable without an HTTP request" {
    const alloc = std.testing.allocator;
    var store = distributed_join.JoinJobStore.init(alloc, .{});
    defer store.deinit();
    const operations = Operations{ .job_store = &store };
    try std.testing.expectError(error.NotFound, operations.jobState(alloc, .{}, 7));

    var canceled = std.atomic.Value(bool).init(true);
    try std.testing.expectError(error.Canceled, operations.jobState(alloc, .{
        .cancellation = operation.CancellationToken.fromAtomic(&canceled),
    }, 7));

    var rows_request = try distributed_join.parseJoinRowsRequest(
        alloc,
        "{\"join\":{\"right_table\":\"documents\",\"on\":{\"left_field\":\"customer_id\",\"right_field\":\"_id\"}}}",
    );
    defer rows_request.deinit(alloc);
    try std.testing.expectError(error.NotFound, operations.rows(
        alloc,
        .{},
        7,
        "documents",
        rows_request,
    ));
}
