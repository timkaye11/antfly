// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const platform_sync = @import("antfly_platform").sync;
const Allocator = std.mem.Allocator;
const artifacts_mod = @import("../artifacts/mod.zig");
const catalog_mod = @import("../catalog/mod.zig");
const manifest_mod = @import("../manifest/mod.zig");
const graph_segment_mod = @import("../graph_segment/mod.zig");
const graph_metric_config = @import("../build/graph_metric_config.zig");
const cache_mod = @import("cache.zig");
const bounded_decode = @import("../bounded_decode.zig");
const graph_reader = @import("graph_reader.zig");
const request_mod = @import("request.zig");
const operation = @import("../../api/operation.zig");
const CancellationToken = operation.CancellationToken;
const read_lease = @import("../manifest/read_lease.zig");

pub const QueryExecutionMetrics = struct {
    total_queries: u64 = 0,
    vector_queries: u64 = 0,
    hybrid_queries: u64 = 0,
    sparse_queries: u64 = 0,
    total_actual_probes: u64 = 0,
    total_shortlist_candidates: u64 = 0,
    total_quantized_candidates: u64 = 0,
    total_exact_reranks: u64 = 0,
    total_cluster_prunes: u64 = 0,
};

pub const NamespaceQueryExecutionMetrics = struct {
    namespace: []u8,
    metrics: QueryExecutionMetrics,

    pub fn deinit(self: *NamespaceQueryExecutionMetrics, alloc: Allocator) void {
        alloc.free(self.namespace);
        self.* = undefined;
    }
};

pub const AuthenticatedSubrange = cache_mod.AuthenticatedSubrange;
pub const AuthenticatedBlockPublication = cache_mod.AuthenticatedBlockPublication;
pub const max_authenticated_publication_blocks = cache_mod.max_authenticated_publication_blocks;

pub const GraphMetricReadLimits = struct {
    /// Shared by every graph-metric surface in one pinned request. These are
    /// deliberately aggregate limits, not per metric.
    max_range_requests: u64 = 128,
    max_range_bytes: u64 = 256 * 1024 * 1024,
    max_decoded_blocks: u64 = 16 * 1024,
    max_work_items: u64 = 32 * 1024 * 1024,
    max_retained_bytes: u64 = 128 * 1024 * 1024,
};

pub const GraphMetricRangeCapacity = struct { requests: u64, bytes: u64 };

pub const GraphMetricReadBudget = struct {
    mutex: std.atomic.Mutex = .unlocked,
    limits: GraphMetricReadLimits = .{},
    range_requests: u64 = 0,
    range_bytes: u64 = 0,
    decoded_blocks: u64 = 0,
    work_items: u64 = 0,
    retained_bytes: u64 = 0,

    /// Move-only live-memory ownership. Unlike cumulative I/O/work admission,
    /// scratch and replaced outputs release their capacity when destroyed.
    /// The shared request budget must outlive every reservation.
    pub const Reservation = struct {
        budget: ?*GraphMetricReadBudget = null,
        bytes: usize = 0,

        /// A read scope can collect conservative scratch/cache-lease charges
        /// from concurrent children. Only its owner may split or destroy it,
        /// after those children have joined.
        pub fn grow(self: *@This(), bytes: usize) !void {
            const budget = self.budget orelse return error.GraphMetricQueryBudgetExceeded;
            lockAtomic(&budget.mutex);
            defer budget.mutex.unlock();
            const owned = std.math.add(usize, self.bytes, bytes) catch return error.GraphMetricQueryBudgetExceeded;
            const retained = try checkedCharge(budget.retained_bytes, bytes, budget.limits.max_retained_bytes);
            self.bytes = owned;
            budget.retained_bytes = retained;
        }

        pub fn deinit(self: *@This()) void {
            if (self.budget) |budget| {
                lockAtomic(&budget.mutex);
                std.debug.assert(budget.retained_bytes >= self.bytes);
                budget.retained_bytes -= self.bytes;
                budget.mutex.unlock();
            }
            self.* = .{};
        }

        pub fn split(self: *@This(), bytes: usize) @This() {
            std.debug.assert(bytes <= self.bytes);
            self.bytes -= bytes;
            return .{ .budget = self.budget, .bytes = bytes };
        }

        /// Move two exclusively owned reservations into one without dropping
        /// admission between construction and publication. Not for shared
        /// grow-only scopes until their workers have joined.
        pub fn absorb(self: *@This(), other: *@This()) void {
            std.debug.assert(self.budget == other.budget);
            self.bytes += other.bytes;
            other.* = .{};
        }

        pub fn shrinkTo(self: *@This(), bytes: usize) void {
            std.debug.assert(bytes <= self.bytes);
            var released = self.split(self.bytes - bytes);
            released.deinit();
        }

        /// Escaping public output keeps its request charge, but must not keep
        /// a pointer to a query session that can already have been destroyed.
        pub fn detach(self: *@This()) void {
            self.* = .{};
        }
    };

    pub fn reserveRetained(self: *@This(), bytes: usize) !Reservation {
        try self.chargeRetained(bytes);
        return .{ .budget = self, .bytes = bytes };
    }

    pub fn remainingMemory(self: *@This()) usize {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        return std.math.cast(usize, self.limits.max_retained_bytes -| self.retained_bytes) orelse std.math.maxInt(usize);
    }

    fn checkedCharge(current: u64, amount: u64, limit: u64) !u64 {
        const next = std.math.add(u64, current, amount) catch return error.GraphMetricQueryBudgetExceeded;
        if (next > limit) return error.GraphMetricQueryBudgetExceeded;
        return next;
    }

    pub fn chargeRange(self: *@This(), bytes: usize) !void {
        return self.reserveRanges(1, bytes);
    }

    pub fn reserveRanges(self: *@This(), requests: usize, bytes: usize) !void {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const next_requests = try checkedCharge(self.range_requests, @intCast(requests), self.limits.max_range_requests);
        const next_bytes = try checkedCharge(self.range_bytes, @intCast(bytes), self.limits.max_range_bytes);
        self.range_requests = next_requests;
        self.range_bytes = next_bytes;
    }

    pub fn remainingRequests(self: *@This()) u64 {
        return self.remainingRanges().requests;
    }

    pub fn remainingRanges(self: *@This()) GraphMetricRangeCapacity {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        return .{ .requests = self.limits.max_range_requests -| self.range_requests, .bytes = self.limits.max_range_bytes -| self.range_bytes };
    }

    pub fn chargeDecode(self: *@This(), blocks: usize, work_items: usize) !void {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const next_blocks = try checkedCharge(self.decoded_blocks, @intCast(blocks), self.limits.max_decoded_blocks);
        const next_items = try checkedCharge(self.work_items, @intCast(work_items), self.limits.max_work_items);
        self.decoded_blocks = next_blocks;
        self.work_items = next_items;
    }

    pub fn chargeRetained(self: *@This(), bytes: usize) !void {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        self.retained_bytes = try checkedCharge(self.retained_bytes, @intCast(bytes), self.limits.max_retained_bytes);
    }
};

test "serverless graph metric request budget composes reads and rejects charges atomically" {
    var budget = GraphMetricReadBudget{ .limits = .{
        .max_range_requests = 2,
        .max_range_bytes = 10,
        .max_decoded_blocks = 2,
        .max_work_items = 10,
        .max_retained_bytes = 10,
    } };

    try budget.chargeRange(6);
    try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.chargeRange(5));
    try std.testing.expectEqual(@as(u64, 1), budget.range_requests);
    try std.testing.expectEqual(@as(u64, 6), budget.range_bytes);
    try budget.chargeRange(4);

    try budget.chargeDecode(1, 6);
    try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.chargeDecode(2, 1));
    try std.testing.expectEqual(@as(u64, 1), budget.decoded_blocks);
    try std.testing.expectEqual(@as(u64, 6), budget.work_items);
    try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.chargeDecode(1, 5));
    try std.testing.expectEqual(@as(u64, 1), budget.decoded_blocks);
    try std.testing.expectEqual(@as(u64, 6), budget.work_items);

    try budget.chargeRetained(10);
    try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.chargeRetained(1));
    try std.testing.expectEqual(@as(u64, 10), budget.retained_bytes);
}

test "serverless graph metric memory reservations transfer release and preserve work charges" {
    var budget = GraphMetricReadBudget{ .limits = .{ .max_retained_bytes = 64 } };
    try budget.chargeDecode(1, 10);
    var scratch = try budget.reserveRetained(48);
    var output = scratch.split(16);
    scratch.deinit();
    try std.testing.expectEqual(@as(u64, 16), budget.retained_bytes);
    for (0..100) |_| {
        var replacement = try budget.reserveRetained(48);
        try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.reserveRetained(1));
        replacement.deinit();
    }
    output.deinit();
    try std.testing.expectEqual(@as(u64, 0), budget.retained_bytes);
    try std.testing.expectEqual(@as(u64, 10), budget.work_items);
    var escaping = try budget.reserveRetained(8);
    escaping.detach();
    escaping.deinit();
    try std.testing.expectEqual(@as(u64, 8), budget.retained_bytes);
}

test "serverless graph metric score-plan reservation is atomic across ranges and bytes" {
    var budget = GraphMetricReadBudget{ .limits = .{ .max_range_requests = 3, .max_range_bytes = 10 } };
    try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.reserveRanges(2, 11));
    try std.testing.expectEqual(@as(u64, 0), budget.range_requests);
    try std.testing.expectEqual(@as(u64, 0), budget.range_bytes);
    try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.reserveRanges(4, 5));
    try std.testing.expectEqual(@as(u64, 3), budget.remainingRequests());
    try budget.reserveRanges(3, 10);
    try std.testing.expectEqual(@as(u64, 0), budget.remainingRequests());
    try std.testing.expectError(error.GraphMetricQueryBudgetExceeded, budget.chargeRange(1));
    try std.testing.expectEqual(@as(u64, 10), budget.range_bytes);
}

pub const QueryRuntime = struct {
    alloc: Allocator,
    artifacts: *artifacts_mod.ArtifactStore,
    manifests: *manifest_mod.ManifestStore,
    progress: *catalog_mod.ProgressStore,
    cache: ?*cache_mod.QueryCache = null,
    metrics_mu: std.atomic.Mutex = .unlocked,
    metrics: QueryExecutionMetrics = .{},
    namespace_metrics: std.StringHashMapUnmanaged(QueryExecutionMetrics) = .empty,
    read_leases: read_lease.Cache = .{},

    pub fn init(
        alloc: Allocator,
        artifacts: *artifacts_mod.ArtifactStore,
        manifests: *manifest_mod.ManifestStore,
        progress: *catalog_mod.ProgressStore,
    ) QueryRuntime {
        return .{
            .alloc = alloc,
            .artifacts = artifacts,
            .manifests = manifests,
            .progress = progress,
        };
    }

    pub fn deinit(self: *QueryRuntime) void {
        lockAtomic(&self.metrics_mu);
        var it = self.namespace_metrics.iterator();
        while (it.next()) |entry| self.alloc.free(entry.key_ptr.*);
        self.namespace_metrics.deinit(self.alloc);
        self.metrics_mu.unlock();
        self.* = undefined;
    }

    pub fn initWithCache(
        alloc: Allocator,
        artifacts: *artifacts_mod.ArtifactStore,
        manifests: *manifest_mod.ManifestStore,
        progress: *catalog_mod.ProgressStore,
        cache: *cache_mod.QueryCache,
    ) QueryRuntime {
        var runtime = init(alloc, artifacts, manifests, progress);
        runtime.cache = cache;
        return runtime;
    }

    pub fn openVersionSession(self: *QueryRuntime, namespace: []const u8, version: u64) !QuerySession {
        var manifest = try self.manifests.getAlloc(namespace, version);
        errdefer manifest.deinit(self.alloc);
        var lease: ?read_lease.Lease = null;
        for (manifest.artifacts) |artifact| {
            if (artifact.kind == .document_facts or (artifact.kind == .graph_segment and artifact.metadata_version == graph_segment_mod.page_graph.Root.metadata_version)) {
                lease = try self.read_leases.acquire(self.progress, namespace, version);
                break;
            }
        }
        return .{
            .alloc = self.alloc,
            .artifacts = self.artifacts,
            .cache = self.cache,
            .manifest = manifest,
            .read_lease = lease,
        };
    }

    pub fn openHeadSession(self: *QueryRuntime, namespace: []const u8) !QuerySession {
        var version = try self.progress.getHead(namespace);
        for (0..3) |_| {
            return self.openVersionSession(namespace, version) catch |err| switch (err) {
                error.FileNotFound, error.ManifestVersionRetired => {
                    const next = try self.progress.getHead(namespace);
                    if (next == version) return err;
                    version = next;
                    continue;
                },
                else => return err,
            };
        }
        return error.ManifestReadLeaseContended;
    }

    pub fn recordSearchStats(self: *QueryRuntime, namespace: []const u8, mode: request_mod.QueryMode, stats: anytype) !void {
        lockAtomic(&self.metrics_mu);
        defer self.metrics_mu.unlock();

        var namespace_metrics = self.namespace_metrics.getPtr(namespace);
        if (namespace_metrics == null) {
            try self.namespace_metrics.ensureUnusedCapacity(self.alloc, 1);
            const owned_namespace = try self.alloc.dupe(u8, namespace);
            self.namespace_metrics.putAssumeCapacityNoClobber(owned_namespace, .{});
            namespace_metrics = self.namespace_metrics.getPtr(owned_namespace).?;
        }

        self.metrics.total_queries += 1;
        applyModeCount(&self.metrics, mode);
        self.metrics.total_actual_probes += stats.actual_probe_count;
        self.metrics.total_shortlist_candidates += stats.actual_shortlist_count;
        self.metrics.total_quantized_candidates += stats.quantized_candidate_count;
        self.metrics.total_exact_reranks += stats.exact_rerank_count;
        self.metrics.total_cluster_prunes += stats.cluster_prune_count;

        namespace_metrics.?.total_queries += 1;
        applyModeCount(namespace_metrics.?, mode);
        namespace_metrics.?.total_actual_probes += stats.actual_probe_count;
        namespace_metrics.?.total_shortlist_candidates += stats.actual_shortlist_count;
        namespace_metrics.?.total_quantized_candidates += stats.quantized_candidate_count;
        namespace_metrics.?.total_exact_reranks += stats.exact_rerank_count;
        namespace_metrics.?.total_cluster_prunes += stats.cluster_prune_count;
    }

    pub fn metricsSnapshot(self: *QueryRuntime) QueryExecutionMetrics {
        lockAtomic(&self.metrics_mu);
        defer self.metrics_mu.unlock();
        return self.metrics;
    }

    pub fn namespaceMetricsAlloc(self: *QueryRuntime, alloc: Allocator) ![]NamespaceQueryExecutionMetrics {
        lockAtomic(&self.metrics_mu);
        defer self.metrics_mu.unlock();
        const out = try alloc.alloc(NamespaceQueryExecutionMetrics, self.namespace_metrics.count());
        errdefer alloc.free(out);
        var idx: usize = 0;
        errdefer for (out[0..idx]) |*entry| entry.deinit(alloc);
        var it = self.namespace_metrics.iterator();
        while (it.next()) |entry| : (idx += 1) {
            out[idx] = .{
                .namespace = try alloc.dupe(u8, entry.key_ptr.*),
                .metrics = entry.value_ptr.*,
            };
        }
        return out;
    }
};

pub const QuerySession = struct {
    alloc: Allocator,
    artifacts: *artifacts_mod.ArtifactStore,
    cache: ?*cache_mod.QueryCache = null,
    manifest: manifest_mod.Manifest,
    owns_manifest: bool = true,
    io: ?std.Io = null,
    cancellation: CancellationToken = .none,
    read_lease: ?read_lease.Lease = null,
    diagnostics: ?*operation.RequestDiagnostics = null,
    graph_metric_specs: ?[]graph_metric_config.IndexSpec = null,
    owns_graph_metric_specs: bool = true,
    graph_metric_read_budget: GraphMetricReadBudget = .{},
    graph_metric_read_budget_shared: ?*GraphMetricReadBudget = null,
    // Borrowed read-lifetime scratch reservation, propagated to joined child
    // reads. Output reservations are independent and may outlive this scope.
    graph_metric_retained_scope: ?*GraphMetricReadBudget.Reservation = null,
    // Pre-admitted transport workspace owned by a joined parent execution.
    graph_metric_transport_credit: usize = 0,

    pub fn graphAdjacencyCache(self: *QuerySession) ?@import("../graph_segment/topology_reader.zig").ReadCache {
        if (self.cache == null) return null;
        return .{ .ptr = self, .read = readGraphAdjacencyBlock };
    }

    fn readGraphAdjacencyBlock(ptr: *anyopaque, alloc: Allocator, artifacts: *artifacts_mod.ArtifactStore, source: manifest_mod.ArtifactRef, offset: u64, len: usize, checksum: [32]u8, cancellation: CancellationToken, remaining: *u64) ![]u8 {
        const self: *QuerySession = @ptrCast(@alignCast(ptr));
        const cache = self.cache.?;
        const fills = @import("authenticated_block_fills.zig");
        // Document bodies can be larger than a shared-cache fill. Cache
        // admission is an optimization, not a stricter document-size contract.
        if (len > fills.Cache.max_batch_bytes) {
            try cancellation.check();
            if (len > remaining.*) return error.GraphMetricBuildBudgetExceeded;
            remaining.* -= len;
            const bytes = try artifacts.getRangeAllocWithCancellationUsingAllocator(alloc, source.artifact_id, offset, len, cancellation);
            errdefer alloc.free(bytes);
            if (bytes.len != len) return error.ArtifactIntegrityMismatch;
            var digest: [32]u8 = undefined;
            std.crypto.hash.sha2.Sha256.hash(bytes, &digest, .{});
            if (!std.mem.eql(u8, &digest, &checksum)) return error.ArtifactIntegrityMismatch;
            try cancellation.check();
            return bytes;
        }
        const key = fills.blockKey(source.artifact_id, source.checksum, offset, len, &checksum);
        var batch = try cache.graph_metric_blocks.acquire(cache.alloc, alloc, &.{.{ .key = key, .len = len }}, self.io, cancellation);
        defer batch.deinit();
        const item = batch.items[0];
        if (item.producer) {
            if (len > remaining.*) return error.GraphMetricBuildBudgetExceeded;
            remaining.* -= len;
            const bytes = try artifacts.getRangeAllocWithCancellationUsingAllocator(alloc, source.artifact_id, offset, len, cancellation);
            defer alloc.free(bytes);
            if (bytes.len != len) return error.ArtifactIntegrityMismatch;
            var digest: [32]u8 = undefined;
            std.crypto.hash.sha2.Sha256.hash(bytes, &digest, .{});
            if (!std.mem.eql(u8, &digest, &checksum)) return error.ArtifactIntegrityMismatch;
            @memcpy(item.buffer(), bytes);
        }
        try cancellation.check();
        batch.publish(self.io);
        return alloc.dupe(u8, item.bytes());
    }

    pub fn deinit(self: *QuerySession) void {
        if (self.owns_graph_metric_specs) self.clearGraphMetricSpecs();
        if (self.owns_manifest) self.manifest.deinit(self.alloc);
        self.* = undefined;
    }

    /// Lazily parses graph metric configuration once for this pinned request.
    /// QuerySession is request-owned and, like its other mutable caches, must
    /// not be accessed concurrently without external synchronization.
    pub fn graphMetricSpecs(self: *QuerySession) ![]const graph_metric_config.IndexSpec {
        if (self.graph_metric_specs == null) {
            self.graph_metric_specs = try graph_metric_config.parseIndexSpecsAlloc(
                self.alloc,
                self.manifest.stats.indexes_json,
            );
        }
        return self.graph_metric_specs.?;
    }

    pub fn clearGraphMetricSpecs(self: *QuerySession) void {
        if (!self.owns_graph_metric_specs) {
            self.graph_metric_specs = null;
            return;
        }
        if (self.graph_metric_specs) |specs| graph_metric_config.freeIndexSpecs(self.alloc, specs);
        self.graph_metric_specs = null;
    }

    pub fn namespace(self: *const QuerySession) []const u8 {
        return self.manifest.namespace;
    }

    pub fn version(self: *const QuerySession) u64 {
        return self.manifest.version;
    }

    pub fn artifactCount(self: *const QuerySession) usize {
        return self.manifest.artifacts.len;
    }

    pub fn artifactRef(self: *const QuerySession, index: usize) ?manifest_mod.ArtifactRef {
        if (index >= self.manifest.artifacts.len) return null;
        return self.manifest.artifacts[index];
    }

    pub fn setCancellation(self: *QuerySession, cancellation: CancellationToken) void {
        self.cancellation = cancellation;
    }

    pub fn setDiagnostics(self: *QuerySession, diagnostics: ?*operation.RequestDiagnostics) void {
        self.diagnostics = diagnostics;
    }

    pub fn setIo(self: *QuerySession, io: ?std.Io) void {
        self.io = io;
    }

    /// Create a non-owning view of the pinned request for a concurrent range
    /// fetch. Callers provide a thread-safe allocator; the manifest, cache,
    /// cancellation token, and aggregate graph budget remain shared.
    pub fn forkGraphMetricRead(self: *QuerySession, alloc: Allocator) QuerySession {
        return .{
            .alloc = alloc,
            .artifacts = self.artifacts,
            .cache = self.cache,
            .manifest = self.manifest,
            .owns_manifest = false,
            .io = self.io,
            .cancellation = self.cancellation,
            .read_lease = self.read_lease,
            .diagnostics = null,
            .graph_metric_specs = self.graph_metric_specs,
            .owns_graph_metric_specs = false,
            .graph_metric_read_budget_shared = self.effectiveGraphMetricReadBudget(),
            .graph_metric_retained_scope = self.graph_metric_retained_scope,
            .graph_metric_transport_credit = self.graph_metric_transport_credit,
        };
    }

    fn effectiveGraphMetricReadBudget(self: *QuerySession) *GraphMetricReadBudget {
        return self.graph_metric_read_budget_shared orelse &self.graph_metric_read_budget;
    }

    pub fn recordGraphMetricRejection(
        self: *QuerySession,
        graph_index_name: []const u8,
        metric_name: []const u8,
        materializer_fingerprint: u64,
    ) void {
        const diagnostics = self.diagnostics orelse return;
        diagnostics.recordGraphMetricRejection(graph_index_name, metric_name, materializer_fingerprint);
    }

    pub fn checkCancellation(self: *const QuerySession) !void {
        if (self.read_lease) |lease| lease.check() catch return error.DeadlineExceeded;
        return self.cancellation.check();
    }

    /// Borrow only after the session is in its final request-owned location,
    /// exactly like graphAdjacencyCache. Joined children finish before deinit.
    pub fn readCancellation(self: *const QuerySession) CancellationToken {
        return .{ .ptr = self, .check_fn = checkReadCancellation };
    }

    fn checkReadCancellation(ptr: *const anyopaque) !void {
        const self: *const QuerySession = @ptrCast(@alignCast(ptr));
        return self.checkCancellation();
    }

    pub fn chargeGraphMetricRange(self: *QuerySession, bytes: usize) !void {
        return self.effectiveGraphMetricReadBudget().chargeRange(bytes);
    }

    pub fn graphMetricRangeBudget(self: *QuerySession) GraphMetricRangeCapacity {
        return self.effectiveGraphMetricReadBudget().remainingRanges();
    }

    pub fn reserveGraphMetricRanges(self: *QuerySession, requests: usize, bytes: usize) !void {
        try self.checkCancellation();
        return self.effectiveGraphMetricReadBudget().reserveRanges(requests, bytes);
    }

    pub fn chargeGraphMetricDecode(self: *QuerySession, blocks: usize, work_items: usize) !void {
        return self.effectiveGraphMetricReadBudget().chargeDecode(blocks, work_items);
    }

    pub fn chargeGraphMetricRetained(self: *QuerySession, bytes: usize) !void {
        if (self.graph_metric_retained_scope) |scope| return scope.grow(bytes);
        return self.effectiveGraphMetricReadBudget().chargeRetained(bytes);
    }

    pub fn reserveGraphMetricMemory(self: *QuerySession, bytes: usize) !GraphMetricReadBudget.Reservation {
        return self.effectiveGraphMetricReadBudget().reserveRetained(bytes);
    }

    pub fn graphMetricMemoryAvailable(self: *QuerySession) usize {
        return self.effectiveGraphMetricReadBudget().remainingMemory();
    }

    pub fn findArtifactIndex(self: *const QuerySession, kind: manifest_mod.ArtifactKind) ?usize {
        for (self.manifest.artifacts, 0..) |artifact, idx| {
            if (artifact.kind == kind) return idx;
        }
        return null;
    }

    pub fn findNamedArtifactIndex(self: *const QuerySession, kind: manifest_mod.ArtifactKind, name: []const u8) ?usize {
        for (self.manifest.artifacts, 0..) |artifact, idx| {
            if (artifact.kind != kind) continue;
            if (std.mem.eql(u8, artifact.name, name)) return idx;
        }
        return null;
    }

    pub fn fetchArtifactAlloc(self: *QuerySession, index: usize) ![]u8 {
        try self.checkCancellation();
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        try validateArtifactForQuery(artifact);
        const result = if (self.cache) |cache|
            try cache.getOrFetchVerifiedAllocWithCancellationUsingAllocator(
                self.alloc,
                self.artifacts,
                artifact.artifact_id,
                artifact.byte_len,
                artifact.checksum,
                self.readCancellation(),
            )
        else
            try self.artifacts.getVerifiedAllocWithCancellationUsingAllocator(
                self.alloc,
                artifact.artifact_id,
                artifact.byte_len,
                artifact.checksum,
                self.readCancellation(),
            );
        errdefer self.alloc.free(result);
        try self.checkCancellation();
        return result;
    }

    /// Authenticates an artifact against the manifest without materializing it
    /// in the query allocator. Object and filesystem backends use their
    /// bounded identity caches after the first full verification.
    pub fn verifyArtifact(self: *QuerySession, index: usize) !void {
        try self.checkCancellation();
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        try validateArtifactForQuery(artifact);
        try self.artifacts.verifyContentWithCancellationUsingAllocator(
            self.alloc,
            artifact.artifact_id,
            artifact.byte_len,
            artifact.checksum,
            self.readCancellation(),
        );
        try self.checkCancellation();
    }

    pub fn fetchArtifactRangeAlloc(self: *QuerySession, index: usize, offset: u64, len: usize) ![]u8 {
        try self.checkCancellation();
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        try validateArtifactRange(artifact, offset, len);
        const result = if (self.cache) |cache|
            try cache.getVerifiedRangeOrFetchAllocWithCancellationUsingAllocator(
                self.alloc,
                self.artifacts,
                artifact.artifact_id,
                artifact.byte_len,
                artifact.checksum,
                offset,
                len,
                self.readCancellation(),
            )
        else
            try self.artifacts.getVerifiedRangeAllocWithCancellationUsingAllocator(
                self.alloc,
                artifact.artifact_id,
                artifact.byte_len,
                artifact.checksum,
                offset,
                len,
                self.readCancellation(),
            );
        errdefer self.alloc.free(result);
        try self.checkCancellation();
        return result;
    }

    pub fn fetchArtifactBlockRangeAlloc(self: *QuerySession, index: usize, block_id: []const u8, offset: u64, len: usize) ![]u8 {
        try self.checkCancellation();
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        try validateArtifactRange(artifact, offset, len);
        const result = if (self.cache) |cache|
            try cache.getVerifiedBlockOrFetchRangeAllocWithCancellationUsingAllocator(
                self.alloc,
                self.artifacts,
                artifact.artifact_id,
                block_id,
                artifact.byte_len,
                artifact.checksum,
                offset,
                len,
                self.readCancellation(),
            )
        else
            try self.artifacts.getVerifiedRangeAllocWithCancellationUsingAllocator(
                self.alloc,
                artifact.artifact_id,
                artifact.byte_len,
                artifact.checksum,
                offset,
                len,
                self.readCancellation(),
            );
        errdefer self.alloc.free(result);
        try self.checkCancellation();
        return result;
    }

    pub fn fetchArtifactAuthenticatedBlockAlloc(
        self: *QuerySession,
        index: usize,
        block_id: []const u8,
        offset: u64,
        len: usize,
        checksum: *const [std.crypto.hash.sha2.Sha256.digest_length]u8,
    ) ![]u8 {
        try self.checkCancellation();
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        try validateArtifactRange(artifact, offset, len);
        if (len == 0) return error.InvalidArtifactRange;
        const result = if (self.cache) |cache|
            try cache.getAuthenticatedBlockOrFetchRangeAllocWithCancellationUsingAllocator(
                self.alloc,
                self.artifacts,
                artifact.artifact_id,
                block_id,
                artifact.byte_len,
                artifact.checksum,
                checksum,
                offset,
                len,
                self.readCancellation(),
            )
        else blk: {
            const bytes = try self.artifacts.getRangeAllocWithCancellationUsingAllocator(
                self.alloc,
                artifact.artifact_id,
                offset,
                len,
                self.readCancellation(),
            );
            errdefer self.alloc.free(bytes);
            if (bytes.len != len) return error.ArtifactIntegrityMismatch;
            var actual: [std.crypto.hash.sha2.Sha256.digest_length]u8 = undefined;
            std.crypto.hash.sha2.Sha256.hash(bytes, &actual, .{});
            if (!std.mem.eql(u8, &actual, checksum)) return error.ArtifactIntegrityMismatch;
            break :blk bytes;
        };
        errdefer self.alloc.free(result);
        try self.checkCancellation();
        return result;
    }

    pub fn readCachedAuthenticatedBlockAlloc(self: *QuerySession, alloc: Allocator, index: usize, block_id: []const u8, offset: u64, len: usize, checksum: *const [32]u8) !?[]u8 {
        var lease = (try self.readCachedAuthenticatedBlockLease(alloc, index, block_id, offset, len, checksum)) orelse return null;
        if (lease == .owned) return lease.owned.data;
        defer lease.deinit();
        return try alloc.dupe(u8, lease.bytes());
    }

    pub fn readCachedAuthenticatedBlockLease(self: *QuerySession, alloc: Allocator, index: usize, block_id: []const u8, offset: u64, len: usize, checksum: *const [32]u8) !?cache_mod.AuthenticatedBlockLease {
        try self.checkCancellation();
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        try validateArtifactRange(artifact, offset, len);
        const cache = self.cache orelse return null;
        return cache.readAuthenticatedBlockIfPresentLease(alloc, artifact.artifact_id, block_id, artifact.byte_len, artifact.checksum, checksum, offset, len, self.readCancellation()) catch |err| switch (err) {
            error.OutOfMemory, error.Canceled => return err,
            // A damaged/unavailable local cache is a miss, never authority.
            // The origin read still authenticates against the manifest digest.
            else => null,
        };
    }

    pub fn cacheAuthenticatedBlocks(self: *QuerySession, index: usize, blocks: []const AuthenticatedBlockPublication) !void {
        const cache = self.cache orelse return;
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        cache.retainAuthenticatedBlocks(artifact.artifact_id, artifact.byte_len, artifact.checksum, blocks);
    }

    /// Fetches a bounded range and authenticates every byte against digests
    /// rooted in the published manifest or an already-authenticated routing
    /// footer. Subranges must exactly and contiguously cover the response.
    pub fn fetchArtifactAuthenticatedRangeAlloc(
        self: *QuerySession,
        index: usize,
        offset: u64,
        len: usize,
        subranges: []const AuthenticatedSubrange,
    ) ![]u8 {
        return self.fetchAuthenticatedRangeAlloc(index, offset, len, subranges, true);
    }

    pub fn fetchArtifactAuthenticatedRangeUncachedAlloc(self: *QuerySession, index: usize, offset: u64, len: usize, subranges: []const AuthenticatedSubrange) ![]u8 {
        return self.fetchAuthenticatedRangeAlloc(index, offset, len, subranges, false);
    }

    fn fetchAuthenticatedRangeAlloc(self: *QuerySession, index: usize, offset: u64, len: usize, subranges: []const AuthenticatedSubrange, retain_range: bool) ![]u8 {
        try self.checkCancellation();
        const artifact = self.artifactRef(index) orelse return error.ArtifactNotFound;
        try validateArtifactRange(artifact, offset, len);
        if (len == 0 or subranges.len == 0) return error.InvalidArtifactRange;
        var covered: usize = 0;
        for (subranges) |subrange| {
            if (subrange.len == 0 or subrange.relative_offset != covered) return error.InvalidArtifactRange;
            covered = std.math.add(usize, covered, subrange.len) catch return error.InvalidArtifactRange;
            if (covered > len) return error.InvalidArtifactRange;
        }
        if (covered != len) return error.InvalidArtifactRange;

        if (if (retain_range) self.cache else null) |cache| {
            const result = try cache.getAuthenticatedRangeOrFetchAllocWithCancellationUsingAllocator(
                self.alloc,
                self.artifacts,
                artifact.artifact_id,
                artifact.byte_len,
                artifact.checksum,
                offset,
                len,
                subranges,
                self.readCancellation(),
            );
            errdefer self.alloc.free(result);
            try self.checkCancellation();
            return result;
        }

        const result = try self.artifacts.getRangeAllocWithCancellationUsingAllocator(
            self.alloc,
            artifact.artifact_id,
            offset,
            len,
            self.readCancellation(),
        );
        errdefer self.alloc.free(result);
        if (result.len != len) return error.ArtifactIntegrityMismatch;
        for (subranges, 0..) |subrange, subrange_index| {
            if (subrange_index % 64 == 0) try self.checkCancellation();
            var actual: [std.crypto.hash.sha2.Sha256.digest_length]u8 = undefined;
            std.crypto.hash.sha2.Sha256.hash(
                result[subrange.relative_offset..][0..subrange.len],
                &actual,
                .{},
            );
            if (!std.mem.eql(u8, &actual, &subrange.checksum)) return error.ArtifactIntegrityMismatch;
        }
        try self.checkCancellation();
        return result;
    }

    pub fn warmArtifact(self: *QuerySession, index: usize) !void {
        if (self.cache == null) return;
        const contents = try self.fetchArtifactAlloc(index);
        self.alloc.free(contents);
    }

    pub fn warmArtifactKind(self: *QuerySession, kind: manifest_mod.ArtifactKind) !void {
        const index = self.findArtifactIndex(kind) orelse return;
        try self.warmArtifact(index);
    }
};

fn validateArtifactRange(artifact: manifest_mod.ArtifactRef, offset: u64, len: usize) !void {
    try validateArtifactIdentity(artifact);
    if (len > (bounded_decode.Limits{}).max_artifact_bytes) return error.QueryArtifactBudgetExceeded;
    const len_u64 = std.math.cast(u64, len) orelse return error.ArtifactRangeTooLarge;
    const end = std.math.add(u64, offset, len_u64) catch return error.InvalidArtifactRange;
    if (end > artifact.byte_len) return error.InvalidArtifactRange;
}

fn validateArtifactForQuery(artifact: manifest_mod.ArtifactRef) !void {
    try validateArtifactIdentity(artifact);
    if (artifact.byte_len > (bounded_decode.Limits{}).max_artifact_bytes) {
        return error.QueryArtifactBudgetExceeded;
    }
}

fn validateArtifactIdentity(artifact: manifest_mod.ArtifactRef) !void {
    artifacts_mod.validateSha256ArtifactIdentity(artifact.artifact_id, artifact.checksum) catch
        return error.ArtifactIntegrityMismatch;
}

test "serverless query read lease expiry fences transport and joined metric sessions" {
    var session = QuerySession{
        .alloc = std.testing.allocator,
        .artifacts = undefined, // no I/O may escape the expired lease
        .manifest = .{ .namespace = @constCast("docs"), .version = 1, .built_at_ns = 1, .wal_start_lsn = 1, .wal_end_lsn = 1, .stats = .{}, .artifacts = @constCast(&.{}) },
        .owns_manifest = false,
        .read_lease = .{ .unix_deadline = 1, .authority_deadline = 1 },
    };
    defer session.deinit();
    try std.testing.expectError(error.DeadlineExceeded, session.checkCancellation());
    try std.testing.expectError(error.DeadlineExceeded, session.readCancellation().check());
    try std.testing.expectError(error.DeadlineExceeded, session.fetchArtifactAlloc(0));
    var joined = session.forkGraphMetricRead(std.testing.allocator);
    defer joined.deinit();
    try std.testing.expectError(error.DeadlineExceeded, joined.readCancellation().check());
}

test "serverless query runtime pins manifest version while head advances" {
    const alloc = std.testing.allocator;

    var artifact_root_buf: [256]u8 = undefined;
    var manifest_root_buf: [256]u8 = undefined;
    const artifact_root = tmpPath(&artifact_root_buf, "artifacts");
    const manifest_root = tmpPath(&manifest_root_buf, "manifests");
    defer cleanupTmp(artifact_root);
    defer cleanupTmp(manifest_root);

    var fs_artifacts = try artifacts_mod.FsStore.init(alloc, std.mem.span(artifact_root));
    var artifact_store = fs_artifacts.artifactStore();
    defer artifact_store.deinit();

    var fs_manifests = try manifest_mod.FsStore.init(alloc, std.mem.span(manifest_root));
    var manifest_store = fs_manifests.manifestStore();
    defer manifest_store.deinit();

    var fs_progress = try catalog_mod.FsProgressStore.init(alloc, std.mem.span(manifest_root));
    var progress_store = fs_progress.progressStore();
    defer progress_store.deinit();

    var artifact_v1 = try artifact_store.put("version-one");
    defer artifact_v1.deinit(alloc);
    var manifest_v1 = manifest_mod.Manifest{
        .namespace = try alloc.dupe(u8, "docs"),
        .version = 1,
        .built_at_ns = 1,
        .wal_start_lsn = 10,
        .wal_end_lsn = 11,
        .stats = .{ .document_count = 1, .text_segment_count = 1, .vector_segment_count = 0 },
        .artifacts = try alloc.alloc(manifest_mod.ArtifactRef, 1),
    };
    defer manifest_v1.deinit(alloc);
    manifest_v1.artifacts[0] = .{
        .kind = .text_segment,
        .artifact_id = try alloc.dupe(u8, artifact_v1.artifact_id),
        .byte_len = artifact_v1.byte_len,
        .checksum = try alloc.dupe(u8, artifact_v1.checksum),
    };
    try manifest_store.put(manifest_v1);
    try manifest_store.setHead("docs", 1);

    var runtime = QueryRuntime.init(alloc, &artifact_store, &manifest_store, &progress_store);
    defer runtime.deinit();
    var session_v1 = try runtime.openHeadSession("docs");
    defer session_v1.deinit();

    var artifact_v2 = try artifact_store.put("version-two");
    defer artifact_v2.deinit(alloc);
    var manifest_v2 = manifest_mod.Manifest{
        .namespace = try alloc.dupe(u8, "docs"),
        .version = 2,
        .built_at_ns = 2,
        .wal_start_lsn = 12,
        .wal_end_lsn = 13,
        .stats = .{ .document_count = 1, .text_segment_count = 1, .vector_segment_count = 0 },
        .artifacts = try alloc.alloc(manifest_mod.ArtifactRef, 1),
    };
    defer manifest_v2.deinit(alloc);
    manifest_v2.artifacts[0] = .{
        .kind = .text_segment,
        .artifact_id = try alloc.dupe(u8, artifact_v2.artifact_id),
        .byte_len = artifact_v2.byte_len,
        .checksum = try alloc.dupe(u8, artifact_v2.checksum),
    };
    try manifest_store.put(manifest_v2);
    try manifest_store.setHead("docs", 2);

    const pinned = try session_v1.fetchArtifactAlloc(0);
    defer alloc.free(pinned);
    try std.testing.expectEqual(@as(u64, 1), session_v1.version());
    try std.testing.expectEqualStrings("version-one", pinned);

    var session_v2 = try runtime.openHeadSession("docs");
    defer session_v2.deinit();
    const latest = try session_v2.fetchArtifactAlloc(0);
    defer alloc.free(latest);
    try std.testing.expectEqual(@as(u64, 2), session_v2.version());
    try std.testing.expectEqualStrings("version-two", latest);
}

test "serverless query session named artifact lookup does not fall back to unnamed artifact" {
    const alloc = std.testing.allocator;

    var session = QuerySession{
        .alloc = alloc,
        .artifacts = undefined,
        .cache = null,
        .manifest = .{
            .namespace = try alloc.dupe(u8, "docs"),
            .version = 1,
            .built_at_ns = 1,
            .wal_start_lsn = 0,
            .wal_end_lsn = 0,
            .stats = .{},
            .artifacts = try alloc.alloc(manifest_mod.ArtifactRef, 2),
        },
    };
    defer session.deinit();

    session.manifest.artifacts[0] = .{
        .kind = .sparse_segment,
        .name = &.{},
        .artifact_id = try alloc.dupe(u8, "artifact-default"),
        .byte_len = 0,
        .checksum = try alloc.dupe(u8, "checksum-default"),
    };
    session.manifest.artifacts[1] = .{
        .kind = .sparse_segment,
        .name = try alloc.dupe(u8, "sparse_b"),
        .artifact_id = try alloc.dupe(u8, "artifact-b"),
        .byte_len = 0,
        .checksum = try alloc.dupe(u8, "checksum-b"),
    };

    try std.testing.expectEqual(@as(?usize, 1), session.findNamedArtifactIndex(.sparse_segment, "sparse_b"));
    try std.testing.expectEqual(@as(?usize, null), session.findNamedArtifactIndex(.sparse_segment, "missing"));
}

test "serverless query runtime routes named graph artifacts by index name" {
    const alloc = std.testing.allocator;

    var artifact_root_buf: [256]u8 = undefined;
    var manifest_root_buf: [256]u8 = undefined;
    const artifact_root = tmpPath(&artifact_root_buf, "artifacts-graph-named-routing");
    const manifest_root = tmpPath(&manifest_root_buf, "manifests-graph-named-routing");
    defer cleanupTmp(artifact_root);
    defer cleanupTmp(manifest_root);

    var fs_artifacts = try artifacts_mod.FsStore.init(alloc, std.mem.span(artifact_root));
    var artifact_store = fs_artifacts.artifactStore();
    defer artifact_store.deinit();

    var fs_manifests = try manifest_mod.FsStore.init(alloc, std.mem.span(manifest_root));
    var manifest_store = fs_manifests.manifestStore();
    defer manifest_store.deinit();

    var fs_progress = try catalog_mod.FsProgressStore.init(alloc, std.mem.span(manifest_root));
    var progress_store = fs_progress.progressStore();
    defer progress_store.deinit();

    var reads: u64 = 1024 * 1024;
    var writes: u64 = reads;
    var pages: graph_segment_mod.page_store.PageStore = .{
        .domain = graph_segment_mod.page_store.PageStore.namespaceDomain("docs"),
        .attempt = @splat(1),
        .artifacts = &artifact_store,
        .remaining_read_bytes = &reads,
        .remaining_write_bytes = &writes,
    };
    const empty: graph_segment_mod.page_graph.Root = .{ .domain = pages.domain };
    var plan_a = try graph_segment_mod.page_graph.plan(alloc, pages.store(), empty, &.{.{ .id = "doc-a", .edges = &.{.{ .source = "doc-a", .target = "doc-b", .kind = "cites", .weight = 1 }} }});
    defer plan_a.deinit();
    const artifact_a = try pages.publishRoot(alloc, try plan_a.publish(pages.store(), empty), "graph_a");
    defer alloc.free(artifact_a.name);
    defer alloc.free(artifact_a.artifact_id);
    defer alloc.free(artifact_a.checksum);
    var plan_b = try graph_segment_mod.page_graph.plan(alloc, pages.store(), empty, &.{.{ .id = "doc-a", .edges = &.{.{ .source = "doc-a", .target = "doc-z", .kind = "rel", .weight = 2 }} }});
    defer plan_b.deinit();
    const artifact_b = try pages.publishRoot(alloc, try plan_b.publish(pages.store(), empty), "graph_b");
    defer alloc.free(artifact_b.name);
    defer alloc.free(artifact_b.artifact_id);
    defer alloc.free(artifact_b.checksum);

    var manifest = manifest_mod.Manifest{
        .namespace = try alloc.dupe(u8, "docs"),
        .version = 1,
        .built_at_ns = 1,
        .wal_start_lsn = 1,
        .wal_end_lsn = 1,
        .stats = .{ .graph_segment_count = 2 },
        .artifacts = try alloc.alloc(manifest_mod.ArtifactRef, 2),
    };
    defer manifest.deinit(alloc);
    manifest.artifacts[0] = .{
        .kind = .graph_segment,
        .name = try alloc.dupe(u8, "graph_a"),
        .artifact_id = try alloc.dupe(u8, artifact_a.artifact_id),
        .byte_len = artifact_a.byte_len,
        .checksum = try alloc.dupe(u8, artifact_a.checksum),
        .metadata_version = artifact_a.metadata_version,
    };
    manifest.artifacts[1] = .{
        .kind = .graph_segment,
        .name = try alloc.dupe(u8, "graph_b"),
        .artifact_id = try alloc.dupe(u8, artifact_b.artifact_id),
        .byte_len = artifact_b.byte_len,
        .checksum = try alloc.dupe(u8, artifact_b.checksum),
        .metadata_version = artifact_b.metadata_version,
    };
    try manifest_store.put(manifest);
    try std.testing.expect(try progress_store.compareAndSwapHead("docs", null, 1));

    var runtime = QueryRuntime.init(alloc, &artifact_store, &manifest_store, &progress_store);
    defer runtime.deinit();
    var session = try runtime.openHeadSession("docs");
    defer session.deinit();

    var req = request_mod.GraphNeighborsRequest{
        .index_name = try alloc.dupe(u8, "graph_b"),
        .doc_id = try alloc.dupe(u8, "doc-a"),
        .direction = .out,
        .limit = 10,
    };
    defer req.deinit(alloc);

    const neighbors = try graph_reader.neighborsAlloc(alloc, &session, req);
    defer graph_reader.freeNeighbors(alloc, neighbors);

    try std.testing.expectEqual(@as(usize, 1), neighbors.len);
    try std.testing.expectEqualStrings("doc-z", neighbors[0].doc_id);
    try std.testing.expectEqualStrings("rel", neighbors[0].edge_type);
}

test "serverless authenticated body reads bypass cache size limits but enforce integrity and admission" {
    const alloc = std.testing.allocator;
    var artifact_root_buf: [256]u8 = undefined;
    var cache_root_buf: [256]u8 = undefined;
    const artifact_root = tmpPath(&artifact_root_buf, "large-facts-artifacts");
    const cache_root = tmpPath(&cache_root_buf, "large-facts-cache");
    defer cleanupTmp(artifact_root);
    defer cleanupTmp(cache_root);
    var fs_artifacts = try artifacts_mod.FsStore.init(alloc, std.mem.span(artifact_root));
    var artifacts = fs_artifacts.artifactStore();
    defer artifacts.deinit();
    var cache = try cache_mod.QueryCache.init(alloc, std.mem.span(cache_root));
    defer cache.deinit();
    const len = @import("authenticated_block_fills.zig").Cache.max_batch_bytes + 1;
    const payload = try alloc.alloc(u8, len);
    defer alloc.free(payload);
    @memset(payload, 'a');
    var digest: [32]u8 = undefined;
    std.crypto.hash.sha2.Sha256.hash(payload, &digest, .{});
    var metadata = try artifacts.put(payload);
    defer metadata.deinit(alloc);
    const source: manifest_mod.ArtifactRef = .{ .kind = .document_facts, .artifact_id = metadata.artifact_id, .checksum = metadata.checksum, .byte_len = metadata.byte_len };
    var session = QuerySession{ .alloc = alloc, .artifacts = &artifacts, .cache = &cache, .owns_manifest = false, .manifest = .{
        .namespace = "docs",
        .version = 1,
        .built_at_ns = 1,
        .wal_start_lsn = 0,
        .wal_end_lsn = 0,
        .stats = .{},
        .artifacts = &.{},
    } };
    defer session.deinit();
    var remaining: u64 = len - 1;
    try std.testing.expectError(error.GraphMetricBuildBudgetExceeded, QuerySession.readGraphAdjacencyBlock(&session, alloc, &artifacts, source, 0, len, digest, .none, &remaining));
    try std.testing.expectEqual(@as(u64, len - 1), remaining);
    remaining = len;
    const loaded = try QuerySession.readGraphAdjacencyBlock(&session, alloc, &artifacts, source, 0, len, digest, .none, &remaining);
    defer alloc.free(loaded);
    try std.testing.expectEqualSlices(u8, payload, loaded);
    try std.testing.expectEqual(@as(u64, 0), remaining);
    try std.testing.expectEqual(@as(usize, 0), cache.graph_metric_blocks.retained);
    digest[0] ^= 1;
    remaining = len;
    try std.testing.expectError(error.ArtifactIntegrityMismatch, QuerySession.readGraphAdjacencyBlock(&session, alloc, &artifacts, source, 0, len, digest, .none, &remaining));
}

test "serverless query runtime warming keeps artifact available through cache" {
    const alloc = std.testing.allocator;

    var artifact_root_buf: [256]u8 = undefined;
    var manifest_root_buf: [256]u8 = undefined;
    var cache_root_buf: [256]u8 = undefined;
    const artifact_root = tmpPath(&artifact_root_buf, "artifacts-cache");
    const manifest_root = tmpPath(&manifest_root_buf, "manifests-cache");
    const cache_root = tmpPath(&cache_root_buf, "cache");
    defer cleanupTmp(artifact_root);
    defer cleanupTmp(manifest_root);
    defer cleanupTmp(cache_root);

    var fs_artifacts = try artifacts_mod.FsStore.init(alloc, std.mem.span(artifact_root));
    var artifact_store = fs_artifacts.artifactStore();
    defer artifact_store.deinit();

    var fs_manifests = try manifest_mod.FsStore.init(alloc, std.mem.span(manifest_root));
    var manifest_store = fs_manifests.manifestStore();
    defer manifest_store.deinit();

    var fs_progress = try catalog_mod.FsProgressStore.init(alloc, std.mem.span(manifest_root));
    var progress_store = fs_progress.progressStore();
    defer progress_store.deinit();

    var cache = try cache_mod.QueryCache.init(alloc, std.mem.span(cache_root));
    defer cache.deinit();

    var artifact = try artifact_store.put("warm-me");
    defer artifact.deinit(alloc);

    var manifest = manifest_mod.Manifest{
        .namespace = try alloc.dupe(u8, "docs"),
        .version = 1,
        .built_at_ns = 1,
        .wal_start_lsn = 1,
        .wal_end_lsn = 1,
        .stats = .{ .document_count = 1, .text_segment_count = 1, .vector_segment_count = 0 },
        .artifacts = try alloc.alloc(manifest_mod.ArtifactRef, 1),
    };
    defer manifest.deinit(alloc);
    manifest.artifacts[0] = .{
        .kind = .text_segment,
        .artifact_id = try alloc.dupe(u8, artifact.artifact_id),
        .byte_len = artifact.byte_len,
        .checksum = try alloc.dupe(u8, artifact.checksum),
    };
    try manifest_store.put(manifest);
    try manifest_store.setHead("docs", 1);

    var runtime = QueryRuntime.initWithCache(alloc, &artifact_store, &manifest_store, &progress_store, &cache);
    defer runtime.deinit();
    var session = try runtime.openHeadSession("docs");
    defer session.deinit();

    try session.warmArtifactKind(.text_segment);
    try artifact_store.delete(artifact.artifact_id);

    const cached = try session.fetchArtifactAlloc(0);
    defer alloc.free(cached);
    try std.testing.expectEqualStrings("warm-me", cached);
}

fn lockAtomic(mutex: *std.atomic.Mutex) void {
    platform_sync.lockYielding(mutex);
}

fn applyModeCount(metrics: *QueryExecutionMetrics, mode: request_mod.QueryMode) void {
    switch (mode) {
        .vector => metrics.vector_queries += 1,
        .hybrid => metrics.hybrid_queries += 1,
        .sparse => metrics.sparse_queries += 1,
        .text => {},
    }
}

test "serverless query runtime block range fetch uses cache after artifact deletion" {
    const alloc = std.testing.allocator;

    var artifact_root_buf: [256]u8 = undefined;
    var manifest_root_buf: [256]u8 = undefined;
    var cache_root_buf: [256]u8 = undefined;
    const artifact_root = tmpPath(&artifact_root_buf, "artifacts-block-cache");
    const manifest_root = tmpPath(&manifest_root_buf, "manifests-block-cache");
    const cache_root = tmpPath(&cache_root_buf, "cache-block");
    defer cleanupTmp(artifact_root);
    defer cleanupTmp(manifest_root);
    defer cleanupTmp(cache_root);

    var fs_artifacts = try artifacts_mod.FsStore.init(alloc, std.mem.span(artifact_root));
    var artifact_store = fs_artifacts.artifactStore();
    defer artifact_store.deinit();

    var fs_manifests = try manifest_mod.FsStore.init(alloc, std.mem.span(manifest_root));
    var manifest_store = fs_manifests.manifestStore();
    defer manifest_store.deinit();

    var fs_progress = try catalog_mod.FsProgressStore.init(alloc, std.mem.span(manifest_root));
    var progress_store = fs_progress.progressStore();
    defer progress_store.deinit();

    var cache = try cache_mod.QueryCache.init(alloc, std.mem.span(cache_root));
    defer cache.deinit();

    var artifact = try artifact_store.put("abcdefgh");
    defer artifact.deinit(alloc);

    var manifest = manifest_mod.Manifest{
        .namespace = try alloc.dupe(u8, "docs"),
        .version = 1,
        .built_at_ns = 1,
        .wal_start_lsn = 1,
        .wal_end_lsn = 1,
        .stats = .{ .document_count = 1, .text_segment_count = 0, .vector_segment_count = 1 },
        .artifacts = try alloc.alloc(manifest_mod.ArtifactRef, 1),
    };
    defer manifest.deinit(alloc);
    manifest.artifacts[0] = .{
        .kind = .vector_segment,
        .artifact_id = try alloc.dupe(u8, artifact.artifact_id),
        .byte_len = artifact.byte_len,
        .checksum = try alloc.dupe(u8, artifact.checksum),
    };
    try manifest_store.put(manifest);
    try manifest_store.setHead("docs", 1);

    var runtime = QueryRuntime.initWithCache(alloc, &artifact_store, &manifest_store, &progress_store, &cache);
    defer runtime.deinit();
    var session = try runtime.openHeadSession("docs");
    defer session.deinit();

    const first = try session.fetchArtifactBlockRangeAlloc(0, "vector-header", 2, 3);
    defer alloc.free(first);
    try std.testing.expectEqualStrings("cde", first);

    try artifact_store.delete(artifact.artifact_id);

    const second = try session.fetchArtifactBlockRangeAlloc(0, "vector-header", 2, 3);
    defer alloc.free(second);
    try std.testing.expectEqualStrings("cde", second);
}

test "serverless query session propagates cancellation through full and cached range transports" {
    const alloc = std.testing.allocator;
    const State = struct {
        cancelled: *std.atomic.Value(bool),
        full_calls: usize = 0,
        range_calls: usize = 0,

        fn deinit(_: Allocator, _: *anyopaque) void {}

        fn put(_: *anyopaque, _: Allocator, _: []const u8) !artifacts_mod.ArtifactMetadata {
            return error.UnexpectedPut;
        }

        fn getAlloc(_: *anyopaque, _: Allocator, _: []const u8) ![]u8 {
            return error.NonCancellableFullReadUsed;
        }

        fn getAllocWithCancellation(ptr: *anyopaque, _: Allocator, _: []const u8, cancellation: CancellationToken) ![]u8 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.full_calls += 1;
            self.cancelled.store(true, .release);
            try cancellation.check();
            return error.ExpectedCancellation;
        }

        fn getRangeAlloc(_: *anyopaque, _: Allocator, _: []const u8, _: u64, _: usize) ![]u8 {
            return error.NonCancellableRangeReadUsed;
        }

        fn getRangeAllocWithCancellation(ptr: *anyopaque, _: Allocator, _: []const u8, _: u64, _: usize, cancellation: CancellationToken) ![]u8 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.range_calls += 1;
            self.cancelled.store(true, .release);
            try cancellation.check();
            return error.ExpectedCancellation;
        }

        fn stat(_: *anyopaque, result_alloc: Allocator, artifact_id: []const u8) !artifacts_mod.ArtifactMetadata {
            const owned_id = try result_alloc.dupe(u8, artifact_id);
            errdefer result_alloc.free(owned_id);
            const checksum = try result_alloc.dupe(u8, "0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef");
            errdefer result_alloc.free(checksum);
            return .{
                .artifact_id = owned_id,
                .byte_len = 7,
                .checksum = checksum,
            };
        }

        fn delete(_: *anyopaque, _: []const u8) !void {
            return error.UnexpectedDelete;
        }

        const vtable = artifacts_mod.ArtifactStore.VTable{
            .deinit = deinit,
            .put = put,
            .get_alloc = getAlloc,
            .get_alloc_with_cancellation = getAllocWithCancellation,
            .get_range_alloc = getRangeAlloc,
            .get_range_alloc_with_cancellation = getRangeAllocWithCancellation,
            .stat = stat,
            .delete = delete,
        };
    };

    var cancelled = std.atomic.Value(bool).init(false);
    var state = State{ .cancelled = &cancelled };
    var artifact_store = artifacts_mod.ArtifactStore{
        .allocator = alloc,
        .ptr = &state,
        .vtable = &State.vtable,
    };
    defer artifact_store.deinit();
    var refs = [_]manifest_mod.ArtifactRef{.{
        .kind = .vector_segment,
        .artifact_id = "sha256:0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef",
        .byte_len = 7,
        .checksum = "0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef",
    }};
    var session = QuerySession{
        .alloc = alloc,
        .artifacts = &artifact_store,
        .manifest = .{
            .namespace = "docs",
            .version = 1,
            .built_at_ns = 1,
            .wal_start_lsn = 1,
            .wal_end_lsn = 1,
            .stats = .{},
            .artifacts = &refs,
        },
        .cancellation = CancellationToken.fromAtomic(&cancelled),
    };

    try std.testing.expectError(error.Canceled, session.fetchArtifactAlloc(0));
    try std.testing.expectEqual(@as(usize, 0), state.full_calls);
    try std.testing.expectEqual(@as(usize, 1), state.range_calls);

    var cache_root_buf: [256]u8 = undefined;
    const cache_root = tmpPath(&cache_root_buf, "transport-cancellation-cache");
    defer cleanupTmp(cache_root);
    var cache = try cache_mod.QueryCache.init(alloc, std.mem.span(cache_root));
    defer cache.deinit();
    session.cache = &cache;
    cancelled.store(false, .release);
    try std.testing.expectError(error.Canceled, session.fetchArtifactBlockRangeAlloc(0, "vector-header", 0, 7));
    try std.testing.expectEqual(@as(usize, 2), state.range_calls);
}

test "serverless query runtime tracks namespace-scoped search metrics" {
    const alloc = std.testing.allocator;

    var runtime = QueryRuntime.init(alloc, undefined, undefined, undefined);
    defer runtime.deinit();

    try runtime.recordSearchStats("docs-a", .vector, .{
        .actual_probe_count = 3,
        .actual_shortlist_count = 8,
        .quantized_candidate_count = 12,
        .exact_rerank_count = 4,
        .cluster_prune_count = 2,
    });
    try runtime.recordSearchStats("docs-a", .hybrid, .{
        .actual_probe_count = 2,
        .actual_shortlist_count = 6,
        .quantized_candidate_count = 9,
        .exact_rerank_count = 3,
        .cluster_prune_count = 1,
    });
    try runtime.recordSearchStats("docs-b", .sparse, .{
        .actual_probe_count = 0,
        .actual_shortlist_count = 0,
        .quantized_candidate_count = 0,
        .exact_rerank_count = 0,
        .cluster_prune_count = 0,
    });

    const global = runtime.metricsSnapshot();
    try std.testing.expectEqual(@as(u64, 3), global.total_queries);
    try std.testing.expectEqual(@as(u64, 1), global.vector_queries);
    try std.testing.expectEqual(@as(u64, 1), global.hybrid_queries);
    try std.testing.expectEqual(@as(u64, 1), global.sparse_queries);

    const namespace_metrics = try runtime.namespaceMetricsAlloc(alloc);
    defer {
        for (namespace_metrics) |*metric| metric.deinit(alloc);
        alloc.free(namespace_metrics);
    }
    try std.testing.expectEqual(@as(usize, 2), namespace_metrics.len);

    var docs_a: ?NamespaceQueryExecutionMetrics = null;
    var docs_b: ?NamespaceQueryExecutionMetrics = null;
    for (namespace_metrics) |metric| {
        if (std.mem.eql(u8, metric.namespace, "docs-a")) docs_a = metric;
        if (std.mem.eql(u8, metric.namespace, "docs-b")) docs_b = metric;
    }
    try std.testing.expect(docs_a != null);
    try std.testing.expect(docs_b != null);
    try std.testing.expectEqual(@as(u64, 2), docs_a.?.metrics.total_queries);
    try std.testing.expectEqual(@as(u64, 1), docs_a.?.metrics.vector_queries);
    try std.testing.expectEqual(@as(u64, 1), docs_a.?.metrics.hybrid_queries);
    try std.testing.expectEqual(@as(u64, 5), docs_a.?.metrics.total_actual_probes);
    try std.testing.expectEqual(@as(u64, 1), docs_b.?.metrics.total_queries);
    try std.testing.expectEqual(@as(u64, 1), docs_b.?.metrics.sparse_queries);
}

test "serverless query runtime metrics remain valid across every allocation failure" {
    const Runner = struct {
        fn run(alloc: Allocator) !void {
            var runtime = QueryRuntime.init(alloc, undefined, undefined, undefined);
            defer runtime.deinit();
            const stats = .{
                .actual_probe_count = @as(u32, 1),
                .actual_shortlist_count = @as(u32, 2),
                .quantized_candidate_count = @as(u32, 3),
                .exact_rerank_count = @as(u32, 4),
                .cluster_prune_count = @as(u32, 5),
            };
            try runtime.recordSearchStats("docs-a", .vector, stats);
            try runtime.recordSearchStats("docs-b", .hybrid, stats);
            const snapshot = try runtime.namespaceMetricsAlloc(alloc);
            defer {
                for (snapshot) |*entry| entry.deinit(alloc);
                alloc.free(snapshot);
            }
        }
    };
    try std.testing.checkAllAllocationFailures(std.testing.allocator, Runner.run, .{});
}

test "serverless query session validates content addresses and declared range bounds" {
    const alloc = std.testing.allocator;
    var refs = [_]manifest_mod.ArtifactRef{.{
        .kind = .vector_segment,
        .artifact_id = "sha256:0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef",
        .byte_len = 8,
        .checksum = "0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef",
    }};
    var session = QuerySession{
        .alloc = alloc,
        .artifacts = undefined,
        .manifest = .{
            .namespace = "docs",
            .version = 1,
            .built_at_ns = 1,
            .wal_start_lsn = 1,
            .wal_end_lsn = 1,
            .stats = .{},
            .artifacts = &refs,
        },
    };
    try std.testing.expectError(error.InvalidArtifactRange, session.fetchArtifactRangeAlloc(0, 7, 2));
    try std.testing.expectError(error.InvalidArtifactRange, session.fetchArtifactBlockRangeAlloc(0, "vector-header", std.math.maxInt(u64), 2));
    refs[0].artifact_id = "../../outside-cache";
    try std.testing.expectError(error.ArtifactIntegrityMismatch, session.fetchArtifactRangeAlloc(0, 0, 1));
}

var test_nonce: std.atomic.Value(u64) = .init(0);

fn threadedIo() std.Io.Threaded {
    return std.Io.Threaded.init(std.heap.page_allocator, .{});
}

fn nowNs() u64 {
    var io_impl = threadedIo();
    defer io_impl.deinit();
    const now = std.Io.Timestamp.now(io_impl.io(), .awake);
    return @intCast(now.toNanoseconds());
}

fn tmpPath(buf: []u8, label: []const u8) [*:0]const u8 {
    const nonce = test_nonce.fetchAdd(1, .monotonic);
    const slice = std.fmt.bufPrint(buf, "/tmp/antfly-serverless-query-{s}-{d}-{d}\x00", .{
        label,
        nowNs(),
        nonce,
    }) catch unreachable;
    return @ptrCast(slice.ptr);
}

fn cleanupTmp(path: [*:0]const u8) void {
    var io_impl = threadedIo();
    defer io_impl.deinit();
    std.Io.Dir.cwd().deleteTree(io_impl.io(), std.mem.span(path)) catch {};
}
