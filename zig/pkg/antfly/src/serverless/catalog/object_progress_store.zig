// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const platform_sync = @import("antfly_platform").sync;
const objectstore = @import("objectstore");
const catalog_types = @import("types.zig");
const progress_store = @import("progress_store.zig");
const head_coordination = @import("../head_coordination.zig");
const remote_uri = @import("../remote_uri.zig");
const object_store_support = @import("../object_store_support.zig");

const retired_head_doc_offset = std.math.maxInt(u64);

pub const ObjectProgressStore = struct {
    alloc: std.mem.Allocator,
    client: objectstore.Client,
    fs_client: ?*objectstore.FilesystemClient = null,
    gcs_client: ?*objectstore.Gcs.JsonApiClient = null,
    s3_client: ?*objectstore.S3.Client = null,
    owns_client: bool = true,
    bucket: []u8,
    prefix: []u8,
    mutex: std.atomic.Mutex = .unlocked,

    pub fn initRemoteUri(alloc: std.mem.Allocator, uri: []const u8) !ObjectProgressStore {
        return try initRemoteUriWithS3Options(alloc, uri, null);
    }

    pub fn initRemoteUriWithS3Options(
        alloc: std.mem.Allocator,
        uri: []const u8,
        s3_options: ?object_store_support.S3Options,
    ) !ObjectProgressStore {
        var parsed = try remote_uri.parseAlloc(alloc, uri);
        defer switch (parsed) {
            .file => |value| alloc.free(value),
            .gcs => |*value| value.deinit(alloc),
            .s3 => |*value| value.deinit(alloc),
        };

        return switch (parsed) {
            .file => |path| blk: {
                const file_uri = try std.fmt.allocPrint(alloc, "file://{s}", .{path});
                defer alloc.free(file_uri);
                break :blk try initFileUri(alloc, file_uri);
            },
            .gcs => |value| try initGcsUri(alloc, value.bucket, value.prefix),
            .s3 => |value| try initS3UriWithOptions(alloc, value.bucket, value.prefix, s3_options),
        };
    }

    pub fn initFileUri(alloc: std.mem.Allocator, uri: []const u8) !ObjectProgressStore {
        const path = try remote_uri.filePathFromUriAlloc(alloc, uri);
        defer alloc.free(path);
        const fs = try alloc.create(objectstore.FilesystemClient);
        errdefer alloc.destroy(fs);
        fs.* = try objectstore.FilesystemClient.init(alloc, path);

        var owned_client = fs.client();
        if (!(try owned_client.bucketExists("serverless-progress"))) try owned_client.makeBucket("serverless-progress");
        return .{
            .alloc = alloc,
            .client = owned_client,
            .fs_client = fs,
            .bucket = try alloc.dupe(u8, "serverless-progress"),
            .prefix = try alloc.dupe(u8, ""),
        };
    }

    pub fn initGcsUri(alloc: std.mem.Allocator, bucket: []const u8, prefix: []const u8) !ObjectProgressStore {
        const gcs = try alloc.create(objectstore.Gcs.JsonApiClient);
        errdefer alloc.destroy(gcs);
        const cfg = try objectstore.Gcs.jsonApiClientConfigFromEnvAlloc(alloc);
        gcs.* = try objectstore.Gcs.JsonApiClient.init(alloc, cfg);

        var owned_client = gcs.client();
        if (!(try owned_client.bucketExists(bucket))) try owned_client.makeBucket(bucket);
        return .{
            .alloc = alloc,
            .client = owned_client,
            .gcs_client = gcs,
            .bucket = try alloc.dupe(u8, bucket),
            .prefix = try alloc.dupe(u8, prefix),
        };
    }

    pub fn initS3Uri(alloc: std.mem.Allocator, bucket: []const u8, prefix: []const u8) !ObjectProgressStore {
        return try initS3UriWithOptions(alloc, bucket, prefix, null);
    }

    pub fn initS3UriWithOptions(
        alloc: std.mem.Allocator,
        bucket: []const u8,
        prefix: []const u8,
        options: ?object_store_support.S3Options,
    ) !ObjectProgressStore {
        const s3 = try alloc.create(objectstore.S3.Client);
        errdefer alloc.destroy(s3);
        const cfg = try object_store_support.s3ConfigAlloc(alloc, options);
        s3.* = try objectstore.S3.Client.init(alloc, cfg);

        var owned_client = s3.client();
        if (!(try owned_client.bucketExists(bucket))) try owned_client.makeBucket(bucket);
        return .{
            .alloc = alloc,
            .client = owned_client,
            .s3_client = s3,
            .bucket = try alloc.dupe(u8, bucket),
            .prefix = try alloc.dupe(u8, prefix),
        };
    }

    pub fn initWithClient(alloc: std.mem.Allocator, client: objectstore.Client, bucket: []const u8, prefix: []const u8) !ObjectProgressStore {
        var owned_client = client;
        if (!(try owned_client.bucketExists(bucket))) try owned_client.makeBucket(bucket);
        return .{
            .alloc = alloc,
            .client = owned_client,
            .owns_client = false,
            .bucket = try alloc.dupe(u8, bucket),
            .prefix = try alloc.dupe(u8, prefix),
        };
    }

    pub fn deinit(self: *ObjectProgressStore) void {
        if (self.owns_client) self.client.deinit();
        if (self.fs_client) |fs| self.alloc.destroy(fs);
        if (self.gcs_client) |gcs| self.alloc.destroy(gcs);
        if (self.s3_client) |s3| self.alloc.destroy(s3);
        self.alloc.free(self.bucket);
        self.alloc.free(self.prefix);
        self.* = undefined;
    }

    pub fn progressStore(self: *ObjectProgressStore) progress_store.ProgressStore {
        return .{
            .allocator = self.alloc,
            .ptr = self,
            .vtable = &vtable,
        };
    }

    pub fn getHead(self: *ObjectProgressStore, namespace: []const u8) !u64 {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "HEAD");
        defer self.alloc.free(key);
        var current = (try self.tryReadHeadCurrent(key)) orelse return error.FileNotFound;
        defer current.deinit(self.alloc);
        if (current.record.head_version == 0) return error.FileNotFound;
        return current.record.head_version;
    }

    pub fn compareAndSwapHead(self: *ObjectProgressStore, namespace: []const u8, expected: ?u64, version: u64) !bool {
        return try self.compareAndSwapHeadMaybeFenced(namespace, expected, version, null);
    }

    pub fn compareAndSwapHeadFenced(
        self: *ObjectProgressStore,
        namespace: []const u8,
        expected: ?u64,
        version: u64,
        fence: progress_store.PublicationFence,
    ) !bool {
        return try self.compareAndSwapHeadMaybeFenced(namespace, expected, version, fence);
    }

    fn compareAndSwapHeadMaybeFenced(
        self: *ObjectProgressStore,
        namespace: []const u8,
        expected: ?u64,
        version: u64,
        fence: ?progress_store.PublicationFence,
    ) !bool {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "HEAD");
        defer self.alloc.free(key);
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();

        var current = try self.tryReadHeadCurrent(key);
        defer if (current) |*entry| entry.deinit(self.alloc);
        const current_version: ?u64 = if (current) |entry|
            if (entry.record.head_version == 0) null else entry.record.head_version
        else
            null;
        if (current_version != expected) return false;
        if (fence) |required| {
            const entry = current orelse return error.WorkLeaseLost;
            if (entry.record.released or
                entry.record.fencing_token != required.fencing_token or
                entry.record.owner_id == null or
                !std.mem.eql(u8, entry.record.owner_id.?, required.owner_id))
            {
                return error.WorkLeaseLost;
            }
        }

        var proposed = if (current) |entry|
            entry.record
        else
            head_coordination.Record{};
        proposed.head_version = version;
        const payload = try head_coordination.payloadAlloc(self.alloc, proposed);
        defer self.alloc.free(payload);
        const content_type = try head_coordination.contentTypeAlloc(self.alloc, proposed);
        defer self.alloc.free(content_type);

        var result = self.client.putObject(self.bucket, key, payload, .{
            .content_type = content_type,
            .if_none_match = current == null,
            .if_match_etag = if (current) |entry| entry.etag else null,
        }) catch |err| switch (err) {
            error.PreconditionFailed => {
                if (fence) |required| {
                    var winner = try self.tryReadHeadCurrent(key);
                    defer if (winner) |*entry| entry.deinit(self.alloc);
                    const entry = winner orelse return error.WorkLeaseLost;
                    if (entry.record.released or
                        entry.record.fencing_token != required.fencing_token or
                        entry.record.owner_id == null or
                        !std.mem.eql(u8, entry.record.owner_id.?, required.owner_id))
                    {
                        return error.WorkLeaseLost;
                    }
                }
                return false;
            },
            else => return err,
        };
        defer result.deinit(self.alloc);
        return true;
    }

    pub fn getGcWatermark(self: *ObjectProgressStore, namespace: []const u8) !?u64 {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "GC_WATERMARK");
        defer self.alloc.free(key);
        return self.tryReadValue(key);
    }

    pub fn compareAndSwapGcWatermark(self: *ObjectProgressStore, namespace: []const u8, expected: ?u64, watermark: u64) !bool {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "GC_WATERMARK");
        defer self.alloc.free(key);
        if (expected) |current| {
            if (watermark < current) return false;
        }
        return try self.compareAndSwap(key, expected, watermark);
    }

    pub fn getManifestGcFloor(self: *ObjectProgressStore, namespace: []const u8) !?u64 {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "MANIFEST_GC_FLOOR");
        defer self.alloc.free(key);
        return self.tryReadValue(key);
    }

    pub fn compareAndSwapManifestGcFloor(self: *ObjectProgressStore, namespace: []const u8, expected: ?u64, floor: u64) !bool {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "MANIFEST_GC_FLOOR");
        defer self.alloc.free(key);
        if (expected) |current| {
            if (floor < current) return false;
        }
        return try self.compareAndSwap(key, expected, floor);
    }

    pub fn getEnrichmentHeadVersion(self: *ObjectProgressStore, namespace: []const u8) !?u64 {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "ENRICHMENT_HEAD_VERSION");
        defer self.alloc.free(key);
        return self.tryReadValue(key);
    }

    pub fn compareAndSwapEnrichmentHeadVersion(self: *ObjectProgressStore, namespace: []const u8, expected: ?u64, head_version: u64) !bool {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "ENRICHMENT_HEAD_VERSION");
        defer self.alloc.free(key);
        return try self.compareAndSwap(key, expected, head_version);
    }

    pub fn getEnrichmentStage(self: *ObjectProgressStore, namespace: []const u8) !?u64 {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "ENRICHMENT_STAGE");
        defer self.alloc.free(key);
        return self.tryReadValue(key);
    }

    pub fn compareAndSwapEnrichmentStage(self: *ObjectProgressStore, namespace: []const u8, expected: ?u64, stage: u64) !bool {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "ENRICHMENT_STAGE");
        defer self.alloc.free(key);
        return try self.compareAndSwap(key, expected, stage);
    }

    pub fn getEnrichmentDocOffset(self: *ObjectProgressStore, namespace: []const u8) !?u64 {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "ENRICHMENT_DOC_OFFSET");
        defer self.alloc.free(key);
        return self.tryReadValue(key);
    }

    pub fn compareAndSwapEnrichmentDocOffset(self: *ObjectProgressStore, namespace: []const u8, expected: ?u64, doc_offset: u64) !bool {
        const key = try keyAlloc(self.alloc, self.prefix, namespace, "ENRICHMENT_DOC_OFFSET");
        defer self.alloc.free(key);
        return try self.compareAndSwap(key, expected, doc_offset);
    }

    pub fn getEnrichmentStageHeadVersion(self: *ObjectProgressStore, namespace: []const u8, stage: catalog_types.EnrichmentStage) !?u64 {
        const key = try enrichmentStageKeyAlloc(self.alloc, self.prefix, namespace, stage, "HEAD_VERSION");
        defer self.alloc.free(key);
        return self.tryReadValue(key);
    }

    pub fn compareAndSwapEnrichmentStageHeadVersion(
        self: *ObjectProgressStore,
        namespace: []const u8,
        stage: catalog_types.EnrichmentStage,
        expected: ?u64,
        head_version: u64,
    ) !bool {
        if (expected) |current| {
            if (head_version < current) return false;
        }
        const key = try enrichmentStageKeyAlloc(self.alloc, self.prefix, namespace, stage, "HEAD_VERSION");
        defer self.alloc.free(key);
        return try self.compareAndSwap(key, expected, head_version);
    }

    pub fn getEnrichmentStageDocOffset(self: *ObjectProgressStore, namespace: []const u8, stage: catalog_types.EnrichmentStage) !?u64 {
        const key = try enrichmentStageKeyAlloc(self.alloc, self.prefix, namespace, stage, "DOC_OFFSET");
        defer self.alloc.free(key);
        return self.tryReadValue(key);
    }

    pub fn compareAndSwapEnrichmentStageDocOffset(
        self: *ObjectProgressStore,
        namespace: []const u8,
        stage: catalog_types.EnrichmentStage,
        expected: ?u64,
        doc_offset: u64,
    ) !bool {
        const key = try enrichmentStageKeyAlloc(self.alloc, self.prefix, namespace, stage, "DOC_OFFSET");
        defer self.alloc.free(key);
        return try self.compareAndSwap(key, expected, doc_offset);
    }

    pub fn getEnrichmentStageHeadDocOffset(
        self: *ObjectProgressStore,
        namespace: []const u8,
        stage: catalog_types.EnrichmentStage,
        head_version: u64,
    ) !?u64 {
        const key = try enrichmentStageHeadOffsetKeyAlloc(self.alloc, self.prefix, namespace, stage, head_version);
        defer self.alloc.free(key);
        const value = try self.tryReadValue(key);
        return if (value == retired_head_doc_offset) null else value;
    }

    pub fn compareAndSwapEnrichmentStageHeadDocOffset(
        self: *ObjectProgressStore,
        namespace: []const u8,
        stage: catalog_types.EnrichmentStage,
        head_version: u64,
        expected: ?u64,
        doc_offset: u64,
    ) !bool {
        if (doc_offset == retired_head_doc_offset) return error.InvalidEnrichmentDocOffset;
        const key = try enrichmentStageHeadOffsetKeyAlloc(self.alloc, self.prefix, namespace, stage, head_version);
        defer self.alloc.free(key);
        return try self.compareAndSwap(key, expected, doc_offset);
    }

    pub fn deleteEnrichmentStageHeadDocOffset(
        self: *ObjectProgressStore,
        namespace: []const u8,
        stage: catalog_types.EnrichmentStage,
        head_version: u64,
    ) !void {
        const key = try enrichmentStageHeadOffsetKeyAlloc(self.alloc, self.prefix, namespace, stage, head_version);
        defer self.alloc.free(key);
        // Keep a same-key tombstone so a stale null-expected CAS cannot
        // resurrect progress after retention removes the manifest. The
        // conditional put also composes across runtime processes.
        while (true) {
            const current = try self.tryReadValue(key);
            if (current == retired_head_doc_offset) return;
            if (try self.compareAndSwap(key, current, retired_head_doc_offset)) return;
        }
    }

    pub fn getEnrichmentStageProgress(
        self: *ObjectProgressStore,
        namespace: []const u8,
        stage: catalog_types.EnrichmentStage,
    ) !?progress_store.EnrichmentStageProgress {
        const key = try enrichmentStageKeyAlloc(self.alloc, self.prefix, namespace, stage, "STATE");
        defer self.alloc.free(key);
        const current = try self.tryReadStageProgressCurrent(key);
        defer if (current) |*entry| if (entry.etag) |etag| self.alloc.free(etag);
        return if (current) |entry| entry.value else null;
    }

    pub fn compareAndSwapEnrichmentStageProgress(
        self: *ObjectProgressStore,
        namespace: []const u8,
        stage: catalog_types.EnrichmentStage,
        expected: ?progress_store.EnrichmentStageProgress,
        desired: progress_store.EnrichmentStageProgress,
    ) !bool {
        const key = try enrichmentStageKeyAlloc(self.alloc, self.prefix, namespace, stage, "STATE");
        defer self.alloc.free(key);
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();

        const current = try self.tryReadStageProgressCurrent(key);
        defer if (current) |*entry| if (entry.etag) |etag| self.alloc.free(etag);
        const current_value = if (current) |entry| entry.value else null;
        if (!stageProgressOptionalEql(current_value, expected)) return false;
        if (current_value) |value| {
            if (desired.head_version < value.head_version) return false;
            if (desired.head_version == value.head_version and desired.doc_offset < value.doc_offset) return false;
        }
        // The provider version token is the cross-process CAS primitive. Do
        // not silently degrade an existing-object update to an unconditional
        // PUT when a backend omits it.
        if (current) |entry| {
            if (entry.etag == null) return error.MissingObjectEtag;
        }

        const payload = try std.fmt.allocPrint(self.alloc, "{d} {d}", .{ desired.head_version, desired.doc_offset });
        defer self.alloc.free(payload);
        var result = self.client.putObject(self.bucket, key, payload, .{
            .content_type = "text/plain",
            .if_none_match = current == null,
            .if_match_etag = if (current) |entry| entry.etag else null,
        }) catch |err| switch (err) {
            error.PreconditionFailed => return false,
            else => return err,
        };
        defer result.deinit(self.alloc);
        return true;
    }

    fn compareAndSwap(self: *ObjectProgressStore, key: []const u8, expected: ?u64, value: u64) !bool {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();

        const current = try self.tryReadCurrent(self.alloc, key);
        defer if (current) |*entry| if (entry.etag) |etag| self.alloc.free(etag);
        if ((if (current) |entry| entry.value else null) != expected) return false;

        const payload = try std.fmt.allocPrint(self.alloc, "{d}", .{value});
        defer self.alloc.free(payload);

        var result = self.client.putObject(self.bucket, key, payload, .{
            .content_type = "text/plain",
            .if_none_match = current == null,
            .if_match_etag = if (current) |entry| entry.etag else null,
        }) catch |err| switch (err) {
            error.PreconditionFailed => return false,
            else => return err,
        };
        defer result.deinit(self.alloc);
        return true;
    }

    fn readValue(self: *ObjectProgressStore, key: []const u8) !u64 {
        const maybe = try self.tryReadValue(key);
        return maybe orelse error.FileNotFound;
    }

    fn tryReadValue(self: *ObjectProgressStore, key: []const u8) !?u64 {
        const current = try self.tryReadCurrent(self.alloc, key);
        defer if (current) |*entry| if (entry.etag) |etag| self.alloc.free(etag);
        return if (current) |entry| entry.value else null;
    }

    const CurrentValue = struct {
        value: u64,
        etag: ?[]u8,
    };

    const CurrentStageProgress = struct {
        value: progress_store.EnrichmentStageProgress,
        etag: ?[]u8,
    };

    const CurrentHead = struct {
        record: head_coordination.Record,
        owned_owner_id: ?[]u8 = null,
        etag: ?[]u8 = null,

        fn deinit(self: *CurrentHead, alloc: std.mem.Allocator) void {
            if (self.owned_owner_id) |owner_id| alloc.free(owner_id);
            if (self.etag) |etag| alloc.free(etag);
            self.* = undefined;
        }
    };

    fn tryReadHeadCurrent(self: *ObjectProgressStore, key: []const u8) !?CurrentHead {
        var result = self.client.getObject(self.bucket, key, .{}) catch |err| switch (err) {
            error.FileNotFound => return null,
            else => return err,
        };
        defer result.deinit(self.alloc);

        const trimmed = std.mem.trim(u8, result.body, " \t\r\n");
        var record: head_coordination.Record = undefined;
        var owned_owner_id: ?[]u8 = null;
        if (trimmed.len != 0 and trimmed[0] == '{') {
            // Transitional compatibility with the short-lived JSON-body
            // format. The next successful write migrates it back to the
            // legacy-readable decimal-plus-whitespace representation.
            var parsed = try std.json.parseFromSlice(head_coordination.Record, self.alloc, trimmed, .{
                .ignore_unknown_fields = false,
            });
            defer parsed.deinit();
            if (!head_coordination.valid(parsed.value))
                return error.InvalidHeadCoordinationRecord;
            if (parsed.value.owner_id) |owner_id| {
                owned_owner_id = try self.alloc.dupe(u8, owner_id);
            }
            record = parsed.value;
            record.owner_id = owned_owner_id;
        } else {
            const body_head = try std.fmt.parseInt(u64, trimmed, 10);
            if (try head_coordination.parseContentTypeAlloc(self.alloc, result.metadata.content_type)) |parsed_value| {
                var parsed = parsed_value;
                defer parsed.deinit();
                if (!head_coordination.valid(parsed.value) or parsed.value.head_version != body_head)
                    return error.InvalidHeadCoordinationRecord;
                if (parsed.value.owner_id) |owner_id| {
                    owned_owner_id = try self.alloc.dupe(u8, owner_id);
                }
                record = parsed.value;
                record.owner_id = owned_owner_id;
            } else {
                record = head_coordination.Record.fromLegacyHead(body_head);
            }
        }
        errdefer if (owned_owner_id) |owner_id| self.alloc.free(owner_id);
        return .{
            .record = record,
            .owned_owner_id = owned_owner_id,
            .etag = if (result.metadata.etag) |etag| try self.alloc.dupe(u8, etag) else null,
        };
    }

    fn tryReadCurrent(self: *ObjectProgressStore, alloc: std.mem.Allocator, key: []const u8) !?CurrentValue {
        var result = self.client.getObject(self.bucket, key, .{}) catch |err| switch (err) {
            error.FileNotFound => return null,
            else => return err,
        };
        defer result.deinit(alloc);
        return .{
            .value = try std.fmt.parseInt(u64, std.mem.trim(u8, result.body, " \t\r\n"), 10),
            .etag = if (result.metadata.etag) |value| try alloc.dupe(u8, value) else null,
        };
    }

    fn tryReadStageProgressCurrent(self: *ObjectProgressStore, key: []const u8) !?CurrentStageProgress {
        var result = self.client.getObject(self.bucket, key, .{}) catch |err| switch (err) {
            error.FileNotFound => return null,
            else => return err,
        };
        defer result.deinit(self.alloc);
        return .{
            .value = try parseStageProgress(result.body),
            .etag = if (result.metadata.etag) |value| try self.alloc.dupe(u8, value) else null,
        };
    }

    const vtable: progress_store.ProgressStore.VTable = .{
        .deinit = erasedDeinit,
        .get_head = erasedGetHead,
        .compare_and_swap_head = erasedCompareAndSwapHead,
        .compare_and_swap_head_fenced = erasedCompareAndSwapHeadFenced,
        .get_gc_watermark = erasedGetGcWatermark,
        .compare_and_swap_gc_watermark = erasedCompareAndSwapGcWatermark,
        .get_manifest_gc_floor = erasedGetManifestGcFloor,
        .compare_and_swap_manifest_gc_floor = erasedCompareAndSwapManifestGcFloor,
        .get_enrichment_head_version = erasedGetEnrichmentHeadVersion,
        .compare_and_swap_enrichment_head_version = erasedCompareAndSwapEnrichmentHeadVersion,
        .get_enrichment_stage = erasedGetEnrichmentStage,
        .compare_and_swap_enrichment_stage = erasedCompareAndSwapEnrichmentStage,
        .get_enrichment_doc_offset = erasedGetEnrichmentDocOffset,
        .compare_and_swap_enrichment_doc_offset = erasedCompareAndSwapEnrichmentDocOffset,
        .get_enrichment_stage_head_version = erasedGetEnrichmentStageHeadVersion,
        .compare_and_swap_enrichment_stage_head_version = erasedCompareAndSwapEnrichmentStageHeadVersion,
        .get_enrichment_stage_doc_offset = erasedGetEnrichmentStageDocOffset,
        .compare_and_swap_enrichment_stage_doc_offset = erasedCompareAndSwapEnrichmentStageDocOffset,
        .get_enrichment_stage_head_doc_offset = erasedGetEnrichmentStageHeadDocOffset,
        .compare_and_swap_enrichment_stage_head_doc_offset = erasedCompareAndSwapEnrichmentStageHeadDocOffset,
        .delete_enrichment_stage_head_doc_offset = erasedDeleteEnrichmentStageHeadDocOffset,
        .get_enrichment_stage_progress = erasedGetEnrichmentStageProgress,
        .compare_and_swap_enrichment_stage_progress = erasedCompareAndSwapEnrichmentStageProgress,
    };

    fn erasedDeinit(_: std.mem.Allocator, ptr: *anyopaque) void {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        self.deinit();
    }

    fn erasedGetHead(ptr: *anyopaque, namespace: []const u8) !u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getHead(namespace);
    }

    fn erasedCompareAndSwapHead(ptr: *anyopaque, namespace: []const u8, expected: ?u64, version: u64) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapHead(namespace, expected, version);
    }

    fn erasedCompareAndSwapHeadFenced(
        ptr: *anyopaque,
        namespace: []const u8,
        expected: ?u64,
        version: u64,
        fence: progress_store.PublicationFence,
    ) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapHeadFenced(namespace, expected, version, fence);
    }

    fn erasedGetGcWatermark(ptr: *anyopaque, namespace: []const u8) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getGcWatermark(namespace);
    }

    fn erasedCompareAndSwapGcWatermark(ptr: *anyopaque, namespace: []const u8, expected: ?u64, watermark: u64) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapGcWatermark(namespace, expected, watermark);
    }

    fn erasedGetManifestGcFloor(ptr: *anyopaque, namespace: []const u8) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getManifestGcFloor(namespace);
    }

    fn erasedCompareAndSwapManifestGcFloor(ptr: *anyopaque, namespace: []const u8, expected: ?u64, floor: u64) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapManifestGcFloor(namespace, expected, floor);
    }

    fn erasedGetEnrichmentHeadVersion(ptr: *anyopaque, namespace: []const u8) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getEnrichmentHeadVersion(namespace);
    }

    fn erasedCompareAndSwapEnrichmentHeadVersion(ptr: *anyopaque, namespace: []const u8, expected: ?u64, head_version: u64) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapEnrichmentHeadVersion(namespace, expected, head_version);
    }

    fn erasedGetEnrichmentStage(ptr: *anyopaque, namespace: []const u8) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getEnrichmentStage(namespace);
    }

    fn erasedCompareAndSwapEnrichmentStage(ptr: *anyopaque, namespace: []const u8, expected: ?u64, stage: u64) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapEnrichmentStage(namespace, expected, stage);
    }

    fn erasedGetEnrichmentDocOffset(ptr: *anyopaque, namespace: []const u8) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getEnrichmentDocOffset(namespace);
    }

    fn erasedCompareAndSwapEnrichmentDocOffset(ptr: *anyopaque, namespace: []const u8, expected: ?u64, doc_offset: u64) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapEnrichmentDocOffset(namespace, expected, doc_offset);
    }

    fn erasedGetEnrichmentStageHeadVersion(ptr: *anyopaque, namespace: []const u8, stage_id: u8) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getEnrichmentStageHeadVersion(namespace, @enumFromInt(stage_id));
    }

    fn erasedCompareAndSwapEnrichmentStageHeadVersion(
        ptr: *anyopaque,
        namespace: []const u8,
        stage_id: u8,
        expected: ?u64,
        head_version: u64,
    ) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapEnrichmentStageHeadVersion(namespace, @enumFromInt(stage_id), expected, head_version);
    }

    fn erasedGetEnrichmentStageDocOffset(ptr: *anyopaque, namespace: []const u8, stage_id: u8) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getEnrichmentStageDocOffset(namespace, @enumFromInt(stage_id));
    }

    fn erasedCompareAndSwapEnrichmentStageDocOffset(
        ptr: *anyopaque,
        namespace: []const u8,
        stage_id: u8,
        expected: ?u64,
        doc_offset: u64,
    ) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapEnrichmentStageDocOffset(namespace, @enumFromInt(stage_id), expected, doc_offset);
    }

    fn erasedGetEnrichmentStageHeadDocOffset(
        ptr: *anyopaque,
        namespace: []const u8,
        stage_id: u8,
        head_version: u64,
    ) !?u64 {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getEnrichmentStageHeadDocOffset(namespace, @enumFromInt(stage_id), head_version);
    }

    fn erasedCompareAndSwapEnrichmentStageHeadDocOffset(
        ptr: *anyopaque,
        namespace: []const u8,
        stage_id: u8,
        head_version: u64,
        expected: ?u64,
        doc_offset: u64,
    ) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapEnrichmentStageHeadDocOffset(
            namespace,
            @enumFromInt(stage_id),
            head_version,
            expected,
            doc_offset,
        );
    }

    fn erasedDeleteEnrichmentStageHeadDocOffset(
        ptr: *anyopaque,
        namespace: []const u8,
        stage_id: u8,
        head_version: u64,
    ) !void {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.deleteEnrichmentStageHeadDocOffset(namespace, @enumFromInt(stage_id), head_version);
    }

    fn erasedGetEnrichmentStageProgress(
        ptr: *anyopaque,
        namespace: []const u8,
        stage_id: u8,
    ) !?progress_store.EnrichmentStageProgress {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.getEnrichmentStageProgress(namespace, @enumFromInt(stage_id));
    }

    fn erasedCompareAndSwapEnrichmentStageProgress(
        ptr: *anyopaque,
        namespace: []const u8,
        stage_id: u8,
        expected: ?progress_store.EnrichmentStageProgress,
        desired: progress_store.EnrichmentStageProgress,
    ) !bool {
        const self: *ObjectProgressStore = @ptrCast(@alignCast(ptr));
        return try self.compareAndSwapEnrichmentStageProgress(
            namespace,
            @enumFromInt(stage_id),
            expected,
            desired,
        );
    }
};

fn parseStageProgress(raw: []const u8) !progress_store.EnrichmentStageProgress {
    var fields = std.mem.tokenizeAny(u8, raw, " \t\r\n");
    const head_version = try std.fmt.parseInt(u64, fields.next() orelse return error.InvalidEnrichmentStageProgress, 10);
    const doc_offset = try std.fmt.parseInt(u64, fields.next() orelse return error.InvalidEnrichmentStageProgress, 10);
    if (fields.next() != null) return error.InvalidEnrichmentStageProgress;
    return .{ .head_version = head_version, .doc_offset = doc_offset };
}

fn stageProgressOptionalEql(
    lhs: ?progress_store.EnrichmentStageProgress,
    rhs: ?progress_store.EnrichmentStageProgress,
) bool {
    if (lhs == null or rhs == null) return lhs == null and rhs == null;
    return lhs.?.head_version == rhs.?.head_version and lhs.?.doc_offset == rhs.?.doc_offset;
}

fn keyAlloc(alloc: std.mem.Allocator, prefix: []const u8, namespace: []const u8, suffix: []const u8) ![]u8 {
    if (prefix.len == 0) return try std.fmt.allocPrint(alloc, "{s}/{s}", .{ namespace, suffix });
    return try std.fmt.allocPrint(alloc, "{s}/{s}/{s}", .{ prefix, namespace, suffix });
}

fn stageLeaf(stage: catalog_types.EnrichmentStage) []const u8 {
    return switch (stage) {
        .lexical_sparse => "LEXICAL_SPARSE",
        .chunk_preview => "CHUNK_PREVIEW",
        .chunk_embeddings => "CHUNK_EMBEDDINGS",
        .rerank_terms => "RERANK_TERMS",
    };
}

fn enrichmentStageKeyAlloc(
    alloc: std.mem.Allocator,
    prefix: []const u8,
    namespace: []const u8,
    stage: catalog_types.EnrichmentStage,
    suffix: []const u8,
) ![]u8 {
    if (prefix.len == 0) return try std.fmt.allocPrint(alloc, "{s}/ENRICHMENT/{s}/{s}", .{ namespace, stageLeaf(stage), suffix });
    return try std.fmt.allocPrint(alloc, "{s}/{s}/ENRICHMENT/{s}/{s}", .{ prefix, namespace, stageLeaf(stage), suffix });
}

fn enrichmentStageHeadOffsetKeyAlloc(
    alloc: std.mem.Allocator,
    prefix: []const u8,
    namespace: []const u8,
    stage: catalog_types.EnrichmentStage,
    head_version: u64,
) ![]u8 {
    const suffix = try std.fmt.allocPrint(alloc, "DOC_OFFSETS/{d}", .{head_version});
    defer alloc.free(suffix);
    return try enrichmentStageKeyAlloc(alloc, prefix, namespace, stage, suffix);
}

fn lockAtomic(mutex: *std.atomic.Mutex) void {
    platform_sync.lockYielding(mutex);
}

test "serverless manifest GC floor persists across object store owners and rejects rollback" {
    const alloc = std.testing.allocator;
    var path_buf: [256]u8 = undefined;
    const path = tmpPath(&path_buf, "manifest-gc-floor");
    defer cleanupTmp(path);
    const uri = try std.fmt.allocPrint(alloc, "file://{s}", .{std.mem.span(path)});
    defer alloc.free(uri);
    {
        var impl = try ObjectProgressStore.initFileUri(alloc, uri);
        var store = impl.progressStore();
        defer store.deinit();
        try std.testing.expectEqual(@as(?u64, null), try store.getManifestGcFloor("docs"));
        try std.testing.expect(try store.compareAndSwapManifestGcFloor("docs", null, 2));
    }
    var impl = try ObjectProgressStore.initFileUri(alloc, uri);
    var store = impl.progressStore();
    defer store.deinit();
    try std.testing.expectEqual(@as(?u64, 2), try store.getManifestGcFloor("docs"));
    try std.testing.expect(!try store.compareAndSwapManifestGcFloor("docs", null, 3));
    try std.testing.expect(!try store.compareAndSwapManifestGcFloor("docs", 2, 1));
    try std.testing.expect(try store.compareAndSwapManifestGcFloor("docs", 2, 3));
    try std.testing.expectEqual(@as(?u64, 3), try store.getManifestGcFloor("docs"));
}

test "serverless objectstore-backed progress store supports atomic stage CAS over file uri" {
    var path_buf: [256]u8 = undefined;
    const path = tmpPath(&path_buf, "progress");
    defer cleanupTmp(path);

    const uri = try std.fmt.allocPrint(std.testing.allocator, "file://{s}", .{std.mem.span(path)});
    defer std.testing.allocator.free(uri);

    var impl = try ObjectProgressStore.initFileUri(std.testing.allocator, uri);
    var store = impl.progressStore();
    defer store.deinit();

    try std.testing.expect(try store.compareAndSwapHead("docs", null, 1));
    try std.testing.expectEqual(@as(u64, 1), try store.getHead("docs"));
    try std.testing.expect(try store.compareAndSwapGcWatermark("docs", null, 10));
    try std.testing.expect(try store.compareAndSwapEnrichmentHeadVersion("docs", null, 1));
    try std.testing.expect(try store.compareAndSwapEnrichmentStage("docs", null, 2));
    try std.testing.expect(try store.compareAndSwapEnrichmentDocOffset("docs", null, 3));
    try std.testing.expect(try store.compareAndSwapEnrichmentStageHeadVersion("docs", .chunk_embeddings, null, 4));
    try std.testing.expect(!(try store.compareAndSwapEnrichmentStageHeadVersion("docs", .chunk_embeddings, 4, 3)));
    try std.testing.expect(try store.compareAndSwapEnrichmentStageDocOffset("docs", .chunk_embeddings, null, 5));
    try std.testing.expect(try store.compareAndSwapEnrichmentStageHeadDocOffset("docs", .chunk_embeddings, 4, 5, 6));
    try std.testing.expectEqual(@as(?u64, 6), try store.getEnrichmentStageHeadDocOffset("docs", .chunk_embeddings, 4));
    try std.testing.expectEqual(@as(?u64, null), try store.getEnrichmentStageHeadDocOffset("docs", .chunk_embeddings, 5));
    try store.deleteEnrichmentStageHeadDocOffset("docs", .chunk_embeddings, 4);
    try std.testing.expectEqual(@as(?u64, null), try store.getEnrichmentStageHeadDocOffset("docs", .chunk_embeddings, 4));
    try std.testing.expect(!(try store.compareAndSwapEnrichmentStageHeadDocOffset("docs", .chunk_embeddings, 4, null, 0)));
    const first = progress_store.EnrichmentStageProgress{ .head_version = 4, .doc_offset = 6 };
    try std.testing.expect(try store.compareAndSwapEnrichmentStageProgress("docs", .chunk_embeddings, null, first));
    const next = progress_store.EnrichmentStageProgress{ .head_version = 5, .doc_offset = 0 };
    try std.testing.expect(try store.compareAndSwapEnrichmentStageProgress("docs", .chunk_embeddings, first, next));
    try std.testing.expect(!(try store.compareAndSwapEnrichmentStageProgress(
        "docs",
        .chunk_embeddings,
        first,
        .{ .head_version = 4, .doc_offset = 7 },
    )));
    try std.testing.expectEqual(@as(?progress_store.EnrichmentStageProgress, next), try store.getEnrichmentStageProgress("docs", .chunk_embeddings));
}

var test_nonce: std.atomic.Value(u64) = .init(0);

fn threadedIo() std.Io.Threaded {
    return std.Io.Threaded.init(std.heap.page_allocator, .{});
}

fn nowNs() u64 {
    var io_impl = threadedIo();
    defer io_impl.deinit();
    const now = std.Io.Timestamp.now(io_impl.io(), .awake);
    return @intCast(now.toNanoseconds());
}

fn tmpPath(buf: []u8, label: []const u8) [*:0]const u8 {
    const nonce = test_nonce.fetchAdd(1, .monotonic);
    const slice = std.fmt.bufPrint(buf, "/tmp/antfly-serverless-object-progress-{s}-{d}-{d}\x00", .{ label, nowNs(), nonce }) catch unreachable;
    return @ptrCast(slice.ptr);
}

fn cleanupTmp(path: [*:0]const u8) void {
    var io_impl = threadedIo();
    defer io_impl.deinit();
    std.Io.Dir.cwd().deleteTree(io_impl.io(), std.mem.span(path)) catch {};
}
