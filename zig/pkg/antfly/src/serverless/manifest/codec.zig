// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const Allocator = std.mem.Allocator;
const bounded_decode = @import("../bounded_decode.zig");
const catalog_types = @import("../catalog/types.zig");
const manifest_base_source = @import("base_source.zig");
const manifest_types = @import("types.zig");
const artifact_ref = @import("artifact_ref.zig");
const search_sources = @import("../search_sources.zig");

pub const wire_magic = "AFSM";
pub const wire_version = artifact_ref.graph_metric_manifest_wire_version;

const policy_size = 98;
const header_size = 4 + 2 + 4 + 8 + 8 + 8 + 8 + 8 + 4 + 4 + 4 + 4 + 4 +
    4 + 4 + 4 + 4 + 4 + policy_size + 8 + 1 + 4 + 1 + 8 + 8;

fn encodePolicy(buf: []u8, policy: catalog_types.NamespacePolicy) void {
    var pos: usize = 0;
    buf[pos] = @intFromEnum(policy.default_query_view);
    pos += 1;
    std.mem.writeInt(u64, buf[pos..][0..8], @intCast(policy.keep_latest_versions), .little);
    pos += 8;
    std.mem.writeInt(u64, buf[pos..][0..8], policy.max_pending_records, .little);
    pos += 8;
    buf[pos] = @intFromBool(policy.compaction_enabled);
    pos += 1;
    std.mem.writeInt(u64, buf[pos..][0..8], @intCast(policy.compaction_trigger_version_count), .little);
    pos += 8;
    std.mem.writeInt(u32, buf[pos..][0..4], @bitCast(policy.vector_compaction_max_cluster_imbalance), .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], @bitCast(policy.vector_compaction_max_distance_span), .little);
    pos += 4;
    buf[pos] = @intCast(@intFromEnum(policy.vector_distance_metric));
    pos += 1;
    buf[pos] = @intFromBool(policy.enrichment_enabled);
    pos += 1;
    buf[pos] = @intFromEnum(policy.lexical_sparse_model_preference);
    pos += 1;
    std.mem.writeInt(u64, buf[pos..][0..8], @intCast(policy.enrichment_batch_size), .little);
    pos += 8;
    buf[pos] = @intFromEnum(policy.enrichment_failure_policy);
    pos += 1;
    std.mem.writeInt(u64, buf[pos..][0..8], policy.enrichment_publish_min_pending_records, .little);
    pos += 8;
    std.mem.writeInt(u32, buf[pos..][0..4], policy.enrichment_pipeline_version, .little);
    pos += 4;
    buf[pos] = @intFromBool(policy.chunk_preview_enabled);
    pos += 1;
    std.mem.writeInt(u32, buf[pos..][0..4], policy.chunk_preview_pipeline_version, .little);
    pos += 4;
    std.mem.writeInt(u64, buf[pos..][0..8], policy.chunk_preview_publish_min_pending_records, .little);
    pos += 8;
    buf[pos] = @intFromBool(policy.chunk_embeddings_enabled);
    pos += 1;
    buf[pos] = @intFromEnum(policy.chunk_embeddings_model_preference);
    pos += 1;
    std.mem.writeInt(u32, buf[pos..][0..4], policy.chunk_embeddings_pipeline_version, .little);
    pos += 4;
    std.mem.writeInt(u64, buf[pos..][0..8], policy.chunk_embeddings_publish_min_pending_records, .little);
    pos += 8;
    buf[pos] = @intFromBool(policy.rerank_terms_enabled);
    pos += 1;
    std.mem.writeInt(u32, buf[pos..][0..4], policy.rerank_terms_pipeline_version, .little);
    pos += 4;
    std.mem.writeInt(u64, buf[pos..][0..8], policy.rerank_terms_publish_min_pending_records, .little);
    pos += 8;
    std.debug.assert(pos == policy_size);
}

fn decodePolicy(data: []const u8, pos_ptr: *usize) !catalog_types.NamespacePolicy {
    var pos = pos_ptr.*;
    if (pos + policy_size > data.len) return error.InvalidManifest;
    const policy: catalog_types.NamespacePolicy = .{
        .default_query_view = switch (data[pos]) {
            0 => .published,
            1 => .latest,
            else => return error.InvalidManifest,
        },
        .keep_latest_versions = @intCast(std.mem.readInt(u64, data[pos + 1 ..][0..8], .little)),
        .max_pending_records = std.mem.readInt(u64, data[pos + 9 ..][0..8], .little),
        .compaction_enabled = data[pos + 17] != 0,
        .compaction_trigger_version_count = @intCast(std.mem.readInt(u64, data[pos + 18 ..][0..8], .little)),
        .vector_compaction_max_cluster_imbalance = @bitCast(std.mem.readInt(u32, data[pos + 26 ..][0..4], .little)),
        .vector_compaction_max_distance_span = @bitCast(std.mem.readInt(u32, data[pos + 30 ..][0..4], .little)),
        .vector_distance_metric = switch (data[pos + 34]) {
            0 => .l2_squared,
            1 => .inner_product,
            2 => .cosine,
            else => return error.InvalidManifest,
        },
        .enrichment_enabled = data[pos + 35] != 0,
        .lexical_sparse_model_preference = switch (data[pos + 36]) {
            1 => .deterministic_only,
            2 => .prefer_model,
            3 => .require_model,
            else => return error.InvalidManifest,
        },
        .enrichment_batch_size = @intCast(std.mem.readInt(u64, data[pos + 37 ..][0..8], .little)),
        .enrichment_failure_policy = switch (data[pos + 45]) {
            1 => .skip_document,
            2 => .fail_stage,
            else => return error.InvalidManifest,
        },
        .enrichment_publish_min_pending_records = std.mem.readInt(u64, data[pos + 46 ..][0..8], .little),
        .enrichment_pipeline_version = std.mem.readInt(u32, data[pos + 54 ..][0..4], .little),
        .chunk_preview_enabled = data[pos + 58] != 0,
        .chunk_preview_pipeline_version = std.mem.readInt(u32, data[pos + 59 ..][0..4], .little),
        .chunk_preview_publish_min_pending_records = std.mem.readInt(u64, data[pos + 63 ..][0..8], .little),
        .chunk_embeddings_enabled = data[pos + 71] != 0,
        .chunk_embeddings_model_preference = switch (data[pos + 72]) {
            1 => .deterministic_only,
            2 => .prefer_model,
            3 => .require_model,
            else => return error.InvalidManifest,
        },
        .chunk_embeddings_pipeline_version = std.mem.readInt(u32, data[pos + 73 ..][0..4], .little),
        .chunk_embeddings_publish_min_pending_records = std.mem.readInt(u64, data[pos + 77 ..][0..8], .little),
        .rerank_terms_enabled = data[pos + 85] != 0,
        .rerank_terms_pipeline_version = std.mem.readInt(u32, data[pos + 86 ..][0..4], .little),
        .rerank_terms_publish_min_pending_records = std.mem.readInt(u64, data[pos + 90 ..][0..8], .little),
    };
    pos += policy_size;
    pos_ptr.* = pos;
    return policy;
}

fn artifactEncodedSize(artifact: manifest_types.ArtifactRef) usize {
    const provenance_bytes = 2 + 8 + 8 + 8 + 8;
    const integrity_bytes: usize = if (artifact.kind == .graph_segment) 32 else if (artifact.kind == .graph_metric_segment) 4 + 4 + 32 + 32 + 32 + 8 + 32 + 32 + 1 + 1 else 0;
    return 1 + 4 + 4 + 8 + 4 + provenance_bytes + integrity_bytes + artifact.name.len + artifact.artifact_id.len + artifact.checksum.len;
}

fn publishedSearchSourceEncodedSize(source: search_sources.SearchSourceDescriptor) usize {
    return 1 + 1 + 4 + source.indexName().len;
}

fn publishedSearchSourcesEncodedSize(sources: search_sources.PublishedSearchSources) usize {
    const items: []const search_sources.SearchSourceDescriptor = sources.items orelse &.{};
    var size: usize = 0;
    for (items) |item| size += publishedSearchSourceEncodedSize(item);
    return size;
}

fn derivedOutputEncodedSize(output: ?search_sources.DerivedOutputDescriptor) usize {
    if (output) |value| return 1 + 4 + value.name.len;
    return 0;
}

fn stringEncodedSize(value: []const u8) usize {
    return 4 + value.len;
}

fn optionalStringEncodedSize(value: ?[]const u8) usize {
    return stringEncodedSize(value orelse &.{});
}

fn stringListEncodedSize(values: []const []const u8) usize {
    var size: usize = 4;
    for (values) |value| size += stringEncodedSize(value);
    return size;
}

fn baseSourceEncodedSize(base_source: manifest_types.BaseSourceDescriptor) usize {
    return switch (base_source) {
        .antfly_document_segments, .antfly_lsm_overlay => 1,
        .antfly_row_fragments => |source| 1 +
            stringEncodedSize(source.snapshot_id) +
            stringEncodedSize(source.schema_fingerprint) +
            stringListEncodedSize(source.row_fragment_artifacts) +
            stringListEncodedSize(source.row_fragment_stats_artifacts),
        .external_parquet, .external_iceberg, .external_lance => |source| 1 + 1 +
            stringEncodedSize(source.source_uri) +
            stringEncodedSize(source.snapshot_id) +
            stringEncodedSize(source.schema_fingerprint) +
            optionalStringEncodedSize(source.file_inventory_artifact) +
            optionalStringEncodedSize(source.row_group_metadata_artifact) +
            optionalStringEncodedSize(source.delete_metadata_artifact),
    };
}

pub fn encodeAlloc(alloc: Allocator, manifest: manifest_types.Manifest) ![]u8 {
    return try encodeForVersionAlloc(alloc, manifest, wire_version);
}

pub fn encodeForVersionAlloc(alloc: Allocator, manifest: manifest_types.Manifest, target_version: u16) ![]u8 {
    if (target_version != wire_version) {
        return error.UnsupportedManifestWriteVersion;
    }
    for (manifest.artifacts) |artifact| {
        if (artifact.kind != .graph_metric_segment) continue;
        if (artifact.metadata_version != artifact_ref.graph_metric_segment_wire_version or
            artifact.graph_metric_control_len == 0 or artifact.graph_metric_routing_footer_len == 0 or
            (artifact.graph_metric_materialization_state == .ready and artifact.graph_metric_rejection_reason != .none) or
            (artifact.graph_metric_materialization_state == .rejected and artifact.graph_metric_rejection_reason == .none))
        {
            return error.InvalidManifest;
        }
    }
    const derived_output_items: []const search_sources.DerivedOutputDescriptor = manifest.stats.derived_outputs.items orelse &.{};
    const published_source_items: []const search_sources.SearchSourceDescriptor = manifest.stats.published_search_sources.items orelse &.{};
    const base_source_len: u32 = if (manifest.base_source) |base_source|
        @intCast(baseSourceEncodedSize(base_source))
    else
        0;
    var size: usize = header_size + manifest.namespace.len +
        manifest.stats.schema_json.len +
        manifest.stats.read_schema_json.len +
        manifest.stats.indexes_json.len +
        publishedSearchSourcesEncodedSize(manifest.stats.published_search_sources) +
        base_source_len;
    for (derived_output_items) |output| size += derivedOutputEncodedSize(output);
    for (manifest.artifacts) |artifact| size += artifactEncodedSize(artifact);

    const buf = try alloc.alloc(u8, size);
    errdefer alloc.free(buf);

    var pos: usize = 0;
    @memcpy(buf[pos..][0..4], wire_magic);
    pos += 4;
    std.mem.writeInt(u16, buf[pos..][0..2], target_version, .little);
    pos += 2;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(manifest.namespace.len), .little);
    pos += 4;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.version, .little);
    pos += 8;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.built_at_ns, .little);
    pos += 8;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.wal_start_lsn, .little);
    pos += 8;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.wal_end_lsn, .little);
    pos += 8;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.stats.document_count, .little);
    pos += 8;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.stats.document_base_version, .little);
    pos += 8;
    buf[pos] = @intFromEnum(manifest.stats.document_publish_mode);
    pos += 1;
    std.mem.writeInt(u32, buf[pos..][0..4], manifest.stats.text_segment_count, .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], manifest.stats.vector_segment_count, .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], manifest.stats.sparse_segment_count, .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], manifest.stats.graph_segment_count, .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(manifest.artifacts.len), .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(published_source_items.len), .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(derived_output_items.len), .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(manifest.stats.schema_json.len), .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(manifest.stats.read_schema_json.len), .little);
    pos += 4;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(manifest.stats.indexes_json.len), .little);
    pos += 4;
    encodePolicy(buf[pos .. pos + policy_size], manifest.stats.policy);
    pos += policy_size;
    std.mem.writeInt(u32, buf[pos..][0..4], base_source_len, .little);
    pos += 4;
    buf[pos] = @intFromBool(manifest.publication_lineage_tracked);
    pos += 1;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.publication_parent_version orelse 0, .little);
    pos += 8;
    std.mem.writeInt(u64, buf[pos..][0..8], manifest.publication_fencing_token, .little);
    pos += 8;

    @memcpy(buf[pos..][0..manifest.namespace.len], manifest.namespace);
    pos += manifest.namespace.len;
    @memcpy(buf[pos..][0..manifest.stats.schema_json.len], manifest.stats.schema_json);
    pos += manifest.stats.schema_json.len;
    @memcpy(buf[pos..][0..manifest.stats.read_schema_json.len], manifest.stats.read_schema_json);
    pos += manifest.stats.read_schema_json.len;
    @memcpy(buf[pos..][0..manifest.stats.indexes_json.len], manifest.stats.indexes_json);
    pos += manifest.stats.indexes_json.len;

    for (published_source_items) |source| {
        switch (source) {
            .text => |value| {
                buf[pos] = 3;
                pos += 1;
                buf[pos] = 1;
                pos += 1;
                std.mem.writeInt(u32, buf[pos..][0..4], @intCast(value.index_name.len), .little);
                pos += 4;
                @memcpy(buf[pos..][0..value.index_name.len], value.index_name);
                pos += value.index_name.len;
            },
            .vector => |value| {
                buf[pos] = 1;
                pos += 1;
                buf[pos] = switch (value.document_source) {
                    .top_level_embedding => 1,
                    .chunk_embeddings => 2,
                    .chunk_embeddings_or_top_level => 3,
                };
                pos += 1;
                std.mem.writeInt(u32, buf[pos..][0..4], @intCast(value.index_name.len), .little);
                pos += 4;
                @memcpy(buf[pos..][0..value.index_name.len], value.index_name);
                pos += value.index_name.len;
            },
            .sparse => |value| {
                buf[pos] = 2;
                pos += 1;
                buf[pos] = switch (value.document_source) {
                    .sparse_embedding => 1,
                };
                pos += 1;
                std.mem.writeInt(u32, buf[pos..][0..4], @intCast(value.index_name.len), .little);
                pos += 4;
                @memcpy(buf[pos..][0..value.index_name.len], value.index_name);
                pos += value.index_name.len;
            },
        }
    }

    for (derived_output_items) |output| {
        buf[pos] = switch (output.kind) {
            .chunk_preview => 1,
            .chunk_embeddings => 2,
            .rerank_terms => 3,
        };
        pos += 1;
        std.mem.writeInt(u32, buf[pos..][0..4], @intCast(output.name.len), .little);
        pos += 4;
        @memcpy(buf[pos..][0..output.name.len], output.name);
        pos += output.name.len;
    }

    for (manifest.artifacts) |artifact| {
        buf[pos] = @intFromEnum(artifact.kind);
        pos += 1;
        std.mem.writeInt(u32, buf[pos..][0..4], @intCast(artifact.name.len), .little);
        pos += 4;
        std.mem.writeInt(u32, buf[pos..][0..4], @intCast(artifact.artifact_id.len), .little);
        pos += 4;
        std.mem.writeInt(u64, buf[pos..][0..8], artifact.byte_len, .little);
        pos += 8;
        std.mem.writeInt(u32, buf[pos..][0..4], @intCast(artifact.checksum.len), .little);
        pos += 4;
        std.mem.writeInt(u16, buf[pos..][0..2], artifact.metadata_version, .little);
        pos += 2;
        std.mem.writeInt(u64, buf[pos..][0..8], artifact.published_generation, .little);
        pos += 8;
        std.mem.writeInt(u64, buf[pos..][0..8], artifact.edge_generation, .little);
        pos += 8;
        std.mem.writeInt(u64, buf[pos..][0..8], artifact.computed_at_ms, .little);
        pos += 8;
        std.mem.writeInt(u64, buf[pos..][0..8], artifact.materializer_fingerprint, .little);
        pos += 8;
        if (artifact.kind == .graph_segment) {
            @memcpy(buf[pos..][0..32], &artifact.graph_topology_control_checksum);
            pos += 32;
        }
        if (artifact.kind == .graph_metric_segment) {
            std.mem.writeInt(u32, buf[pos..][0..4], artifact.graph_metric_control_len, .little);
            pos += 4;
            std.mem.writeInt(u32, buf[pos..][0..4], artifact.graph_metric_routing_footer_len, .little);
            pos += 4;
            @memcpy(buf[pos..][0..32], &artifact.graph_metric_control_checksum);
            pos += 32;
            @memcpy(buf[pos..][0..32], &artifact.graph_metric_routing_checksum);
            pos += 32;
            @memcpy(buf[pos..][0..32], &artifact.graph_metric_point_index_checksum);
            pos += 32;
            std.mem.writeInt(u64, buf[pos..][0..8], artifact.graph_metric_config_fingerprint, .little);
            pos += 8;
            @memcpy(buf[pos..][0..32], &artifact.graph_metric_source_checksum);
            pos += 32;
            @memcpy(buf[pos..][0..32], &artifact.graph_metric_topology_checksum);
            pos += 32;
            buf[pos] = @intFromEnum(artifact.graph_metric_materialization_state);
            pos += 1;
            buf[pos] = @intFromEnum(artifact.graph_metric_rejection_reason);
            pos += 1;
        }
        @memcpy(buf[pos..][0..artifact.name.len], artifact.name);
        pos += artifact.name.len;
        @memcpy(buf[pos..][0..artifact.artifact_id.len], artifact.artifact_id);
        pos += artifact.artifact_id.len;
        @memcpy(buf[pos..][0..artifact.checksum.len], artifact.checksum);
        pos += artifact.checksum.len;
    }

    if (manifest.base_source) |base_source| {
        encodeBaseSource(buf[pos .. pos + base_source_len], base_source);
        pos += base_source_len;
    }

    std.debug.assert(pos == buf.len);
    return buf;
}

fn encodeString(buf: []u8, pos_ptr: *usize, value: []const u8) void {
    var pos = pos_ptr.*;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(value.len), .little);
    pos += 4;
    @memcpy(buf[pos..][0..value.len], value);
    pos += value.len;
    pos_ptr.* = pos;
}

fn encodeOptionalString(buf: []u8, pos_ptr: *usize, value: ?[]const u8) void {
    encodeString(buf, pos_ptr, value orelse &.{});
}

fn encodeStringList(buf: []u8, pos_ptr: *usize, values: []const []const u8) void {
    var pos = pos_ptr.*;
    std.mem.writeInt(u32, buf[pos..][0..4], @intCast(values.len), .little);
    pos += 4;
    pos_ptr.* = pos;
    for (values) |value| encodeString(buf, pos_ptr, value);
}

fn encodeBaseSource(buf: []u8, descriptor: manifest_types.BaseSourceDescriptor) void {
    var pos: usize = 0;
    buf[pos] = @intFromEnum(std.meta.activeTag(descriptor));
    pos += 1;

    switch (descriptor) {
        .antfly_document_segments, .antfly_lsm_overlay => {},
        .antfly_row_fragments => |source| {
            encodeString(buf, &pos, source.snapshot_id);
            encodeString(buf, &pos, source.schema_fingerprint);
            encodeStringList(buf, &pos, source.row_fragment_artifacts);
            encodeStringList(buf, &pos, source.row_fragment_stats_artifacts);
        },
        .external_parquet, .external_iceberg, .external_lance => |source| {
            buf[pos] = @intFromEnum(source.format);
            pos += 1;
            encodeString(buf, &pos, source.source_uri);
            encodeString(buf, &pos, source.snapshot_id);
            encodeString(buf, &pos, source.schema_fingerprint);
            encodeOptionalString(buf, &pos, source.file_inventory_artifact);
            encodeOptionalString(buf, &pos, source.row_group_metadata_artifact);
            encodeOptionalString(buf, &pos, source.delete_metadata_artifact);
        },
    }

    std.debug.assert(pos == buf.len);
}

pub fn decodeAlloc(alloc: Allocator, data: []const u8) !manifest_types.Manifest {
    if (data.len < 6) return error.InvalidManifest;

    var pos: usize = 0;
    if (!std.mem.eql(u8, data[pos..][0..4], wire_magic)) return error.InvalidManifest;
    pos += 4;

    const version = std.mem.readInt(u16, data[pos..][0..2], .little);
    pos += 2;
    if (version != wire_version) return error.UnsupportedManifestVersion;
    if (data.len < header_size) return error.InvalidManifest;

    const namespace_len = std.mem.readInt(u32, data[pos..][0..4], .little);
    pos += 4;
    const manifest_version = std.mem.readInt(u64, data[pos..][0..8], .little);
    pos += 8;
    const built_at_ns = std.mem.readInt(u64, data[pos..][0..8], .little);
    pos += 8;
    const wal_start_lsn = std.mem.readInt(u64, data[pos..][0..8], .little);
    pos += 8;
    const wal_end_lsn = std.mem.readInt(u64, data[pos..][0..8], .little);
    pos += 8;
    const document_count = std.mem.readInt(u64, data[pos..][0..8], .little);
    pos += 8;
    const document_base_version = blk: {
        const value = std.mem.readInt(u64, data[pos..][0..8], .little);
        pos += 8;
        break :blk value;
    };
    const document_publish_mode = blk: {
        const value: catalog_types.DocumentPublishMode = switch (data[pos]) {
            1 => .append_mutation_tail,
            2 => .inline_rebase,
            3 => .head_republish,
            else => return error.InvalidManifest,
        };
        pos += 1;
        break :blk value;
    };
    const text_segment_count = std.mem.readInt(u32, data[pos..][0..4], .little);
    pos += 4;
    const vector_segment_count = std.mem.readInt(u32, data[pos..][0..4], .little);
    pos += 4;
    const sparse_segment_count = std.mem.readInt(u32, data[pos..][0..4], .little);
    pos += 4;
    const graph_segment_count = std.mem.readInt(u32, data[pos..][0..4], .little);
    pos += 4;
    const artifact_count = std.mem.readInt(u32, data[pos..][0..4], .little);
    pos += 4;
    const published_search_source_count = blk: {
        const value = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        break :blk value;
    };
    const derived_output_count = blk: {
        const value = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        break :blk value;
    };
    const schema_len = blk: {
        const value = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        break :blk value;
    };
    const read_schema_len = blk: {
        const value = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        break :blk value;
    };
    const indexes_len = blk: {
        const value = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        break :blk value;
    };
    const policy = try decodePolicy(data, &pos);
    const base_source_len = blk: {
        if (pos + 4 > data.len) return error.InvalidManifest;
        const value = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        break :blk value;
    };
    const publication_lineage_tracked = blk: {
        if (pos + 1 > data.len) return error.InvalidManifest;
        const value = switch (data[pos]) {
            0 => false,
            1 => true,
            else => return error.InvalidManifest,
        };
        pos += 1;
        break :blk value;
    };
    const publication_parent_version = blk: {
        if (pos + 8 > data.len) return error.InvalidManifest;
        const value = std.mem.readInt(u64, data[pos..][0..8], .little);
        pos += 8;
        break :blk if (value == 0) null else value;
    };
    if (!publication_lineage_tracked and publication_parent_version != null) return error.InvalidManifest;
    if (pos + 8 > data.len) return error.InvalidManifest;
    const publication_fencing_token = std.mem.readInt(u64, data[pos..][0..8], .little);
    pos += 8;
    if (publication_parent_version) |parent| {
        if (parent >= manifest_version) return error.InvalidManifest;
    }

    if (pos + namespace_len > data.len) return error.InvalidManifest;
    const namespace = try alloc.dupe(u8, data[pos .. pos + namespace_len]);
    pos += namespace_len;
    errdefer alloc.free(namespace);

    var schema_json: []u8 = &.{};
    errdefer if (schema_json.len > 0) alloc.free(schema_json);
    var read_schema_json: []u8 = &.{};
    errdefer if (read_schema_json.len > 0) alloc.free(read_schema_json);
    var indexes_json: []u8 = &.{};
    errdefer if (indexes_json.len > 0) alloc.free(indexes_json);
    if (pos + schema_len + read_schema_len + indexes_len > data.len) return error.InvalidManifest;
    if (schema_len > 0) schema_json = try alloc.dupe(u8, data[pos .. pos + schema_len]);
    pos += schema_len;
    if (read_schema_len > 0) read_schema_json = try alloc.dupe(u8, data[pos .. pos + read_schema_len]);
    pos += read_schema_len;
    if (indexes_len > 0) indexes_json = try alloc.dupe(u8, data[pos .. pos + indexes_len]);
    pos += indexes_len;

    var published_search_sources: search_sources.PublishedSearchSources = .{};
    errdefer search_sources.deinitPublishedSearchSources(alloc, &published_search_sources);
    if (published_search_source_count > 0) {
        if (published_search_source_count > (data.len - pos) / 6) return error.InvalidManifest;
        const items = try alloc.alloc(search_sources.SearchSourceDescriptor, published_search_source_count);
        errdefer alloc.free(items);
        var initialized_sources: usize = 0;
        errdefer {
            for (items[0..initialized_sources]) |*item| search_sources.deinitSearchSourceDescriptor(alloc, item);
        }
        for (0..published_search_source_count) |idx| {
            if (pos + 1 + 1 + 4 > data.len) return error.InvalidManifest;
            const source_kind = data[pos];
            pos += 1;
            const kind = data[pos];
            pos += 1;
            const index_name_len = std.mem.readInt(u32, data[pos..][0..4], .little);
            pos += 4;
            if (pos + index_name_len > data.len) return error.InvalidManifest;
            const index_name = try alloc.dupe(u8, data[pos .. pos + index_name_len]);
            errdefer alloc.free(index_name);
            pos += index_name_len;
            items[idx] = switch (source_kind) {
                1 => .{ .vector = .{
                    .index_name = index_name,
                    .document_source = switch (kind) {
                        1 => .top_level_embedding,
                        2 => .chunk_embeddings,
                        3 => .chunk_embeddings_or_top_level,
                        else => return error.InvalidManifest,
                    },
                } },
                2 => .{ .sparse = .{
                    .index_name = index_name,
                    .document_source = switch (kind) {
                        1 => .sparse_embedding,
                        else => return error.InvalidManifest,
                    },
                } },
                3 => .{ .text = .{
                    .index_name = index_name,
                } },
                else => return error.InvalidManifest,
            };
            initialized_sources += 1;
        }
        published_search_sources = blk: {
            const owned = items;
            var out = search_sources.PublishedSearchSources{ .items = owned };
            for (owned) |item| switch (item) {
                .text => |value| {
                    if (out.text == null) out.text = value;
                },
                .vector => |value| out.vector = value,
                .sparse => |value| out.sparse = value,
            };
            break :blk out;
        };
    }

    var derived_outputs: search_sources.MaterializedDerivedOutputs = .{};
    errdefer search_sources.deinitMaterializedDerivedOutputs(alloc, &derived_outputs);
    if (derived_output_count > 0) {
        if (derived_output_count > (data.len - pos) / 5) return error.InvalidManifest;
        const items = try alloc.alloc(search_sources.DerivedOutputDescriptor, derived_output_count);
        errdefer alloc.free(items);
        var initialized_outputs: usize = 0;
        errdefer {
            for (items[0..initialized_outputs]) |*item| search_sources.deinitDerivedOutputDescriptor(alloc, item);
        }
        for (0..derived_output_count) |idx| {
            if (pos + 1 + 4 > data.len) return error.InvalidManifest;
            const kind = data[pos];
            pos += 1;
            const name_len = std.mem.readInt(u32, data[pos..][0..4], .little);
            pos += 4;
            if (pos + name_len > data.len) return error.InvalidManifest;
            const name = try alloc.dupe(u8, data[pos .. pos + name_len]);
            errdefer alloc.free(name);
            pos += name_len;
            items[idx] = .{
                .name = name,
                .kind = switch (kind) {
                    1 => .chunk_preview,
                    2 => .chunk_embeddings,
                    3 => .rerank_terms,
                    else => return error.InvalidManifest,
                },
            };
            initialized_outputs += 1;
        }
        derived_outputs = .{ .items = items };
    }

    const min_artifact_header_len = 1 + 4 + 4 + 8 + 4 + 2 + 8 + 8 + 8 + 8;
    if (artifact_count > (data.len - pos) / min_artifact_header_len) return error.InvalidManifest;
    const artifacts = try alloc.alloc(manifest_types.ArtifactRef, artifact_count);
    errdefer alloc.free(artifacts);

    var initialized: usize = 0;
    errdefer {
        for (artifacts[0..initialized]) |artifact| {
            if (artifact.name.len > 0) alloc.free(artifact.name);
            alloc.free(artifact.artifact_id);
            alloc.free(artifact.checksum);
        }
    }

    for (0..artifact_count) |idx| {
        if (pos + min_artifact_header_len > data.len) return error.InvalidManifest;
        const kind = std.enums.fromInt(manifest_types.ArtifactKind, data[pos]) orelse return error.InvalidManifest;
        pos += 1;
        const name_len = blk: {
            const value = std.mem.readInt(u32, data[pos..][0..4], .little);
            pos += 4;
            break :blk value;
        };
        const artifact_id_len = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        const byte_len = std.mem.readInt(u64, data[pos..][0..8], .little);
        pos += 8;
        const checksum_len = std.mem.readInt(u32, data[pos..][0..4], .little);
        pos += 4;
        const metadata_version = blk: {
            const value = std.mem.readInt(u16, data[pos..][0..2], .little);
            pos += 2;
            break :blk value;
        };
        if (kind == .graph_metric_segment and metadata_version != artifact_ref.graph_metric_segment_wire_version) {
            return error.InvalidManifest;
        }
        const published_generation = blk: {
            const value = std.mem.readInt(u64, data[pos..][0..8], .little);
            pos += 8;
            break :blk value;
        };
        const edge_generation = blk: {
            const value = std.mem.readInt(u64, data[pos..][0..8], .little);
            pos += 8;
            break :blk value;
        };
        const computed_at_ms = blk: {
            const value = std.mem.readInt(u64, data[pos..][0..8], .little);
            pos += 8;
            break :blk value;
        };
        const materializer_fingerprint = blk: {
            const value = std.mem.readInt(u64, data[pos..][0..8], .little);
            pos += 8;
            break :blk value;
        };
        var graph_topology_control_checksum: [32]u8 = @splat(0);
        if (kind == .graph_segment) {
            if (data.len - pos < 32) return error.InvalidManifest;
            graph_topology_control_checksum = data[pos..][0..32].*;
            pos += 32;
        }
        var graph_metric_control_len: u32 = 0;
        var graph_metric_routing_footer_len: u32 = 0;
        var graph_metric_control_checksum: [32]u8 = @splat(0);
        var graph_metric_routing_checksum: [32]u8 = @splat(0);
        var graph_metric_point_index_checksum: [32]u8 = @splat(0);
        var graph_metric_config_fingerprint: u64 = 0;
        var graph_metric_source_checksum: [32]u8 = @splat(0);
        var graph_metric_topology_checksum: [32]u8 = @splat(0);
        var graph_metric_materialization_state: artifact_ref.GraphMetricMaterializationState = .ready;
        var graph_metric_rejection_reason: artifact_ref.GraphMetricRejectionReason = .none;
        // Graph metrics are admitted only on the current manifest wire above,
        // so there is no partially populated legacy integrity shape here.
        if (kind == .graph_metric_segment) {
            const integrity_len: usize = 4 + 4 + 32 + 32 + 32 + 8 + 32 + 32 + 1 + 1;
            if (pos + integrity_len > data.len) return error.InvalidManifest;
            graph_metric_control_len = std.mem.readInt(u32, data[pos..][0..4], .little);
            pos += 4;
            graph_metric_routing_footer_len = std.mem.readInt(u32, data[pos..][0..4], .little);
            pos += 4;
            @memcpy(&graph_metric_control_checksum, data[pos..][0..32]);
            pos += 32;
            @memcpy(&graph_metric_routing_checksum, data[pos..][0..32]);
            pos += 32;
            @memcpy(&graph_metric_point_index_checksum, data[pos..][0..32]);
            pos += 32;
            graph_metric_config_fingerprint = std.mem.readInt(u64, data[pos..][0..8], .little);
            pos += 8;
            @memcpy(&graph_metric_source_checksum, data[pos..][0..32]);
            pos += 32;
            @memcpy(&graph_metric_topology_checksum, data[pos..][0..32]);
            pos += 32;
            graph_metric_materialization_state = std.enums.fromInt(artifact_ref.GraphMetricMaterializationState, data[pos]) orelse return error.InvalidManifest;
            pos += 1;
            graph_metric_rejection_reason = std.enums.fromInt(artifact_ref.GraphMetricRejectionReason, data[pos]) orelse return error.InvalidManifest;
            pos += 1;
            if (graph_metric_control_len == 0 or graph_metric_routing_footer_len == 0 or
                (graph_metric_materialization_state == .ready and graph_metric_rejection_reason != .none) or
                (graph_metric_materialization_state == .rejected and graph_metric_rejection_reason == .none))
            {
                return error.InvalidManifest;
            }
        }

        if (pos + name_len + artifact_id_len + checksum_len > data.len) return error.InvalidManifest;
        const name = if (name_len > 0) try alloc.dupe(u8, data[pos .. pos + name_len]) else &.{};
        pos += name_len;
        errdefer if (name.len > 0) alloc.free(name);
        const artifact_id = try alloc.dupe(u8, data[pos .. pos + artifact_id_len]);
        pos += artifact_id_len;
        errdefer alloc.free(artifact_id);
        const checksum = try alloc.dupe(u8, data[pos .. pos + checksum_len]);
        pos += checksum_len;

        artifacts[idx] = .{
            .kind = kind,
            .name = name,
            .artifact_id = artifact_id,
            .byte_len = byte_len,
            .checksum = checksum,
            .metadata_version = metadata_version,
            .published_generation = published_generation,
            .edge_generation = edge_generation,
            .computed_at_ms = computed_at_ms,
            .materializer_fingerprint = materializer_fingerprint,
            .graph_metric_control_len = graph_metric_control_len,
            .graph_topology_control_checksum = graph_topology_control_checksum,
            .graph_metric_routing_footer_len = graph_metric_routing_footer_len,
            .graph_metric_control_checksum = graph_metric_control_checksum,
            .graph_metric_routing_checksum = graph_metric_routing_checksum,
            .graph_metric_point_index_checksum = graph_metric_point_index_checksum,
            .graph_metric_config_fingerprint = graph_metric_config_fingerprint,
            .graph_metric_source_checksum = graph_metric_source_checksum,
            .graph_metric_topology_checksum = graph_metric_topology_checksum,
            .graph_metric_materialization_state = graph_metric_materialization_state,
            .graph_metric_rejection_reason = graph_metric_rejection_reason,
        };
        initialized += 1;
    }

    var base_source: ?manifest_types.BaseSourceDescriptor = null;
    errdefer if (base_source) |*descriptor| manifest_base_source.freeOwnedDescriptor(alloc, descriptor);
    if (pos + base_source_len > data.len) return error.InvalidManifest;
    if (base_source_len > 0) {
        base_source = try decodeBaseSourceAlloc(alloc, data[pos .. pos + base_source_len]);
    }
    pos += base_source_len;

    if (pos != data.len) return error.InvalidManifest;

    return .{
        .namespace = namespace,
        .version = manifest_version,
        .built_at_ns = built_at_ns,
        .wal_start_lsn = wal_start_lsn,
        .wal_end_lsn = wal_end_lsn,
        .publication_lineage_tracked = publication_lineage_tracked,
        .publication_parent_version = publication_parent_version,
        .publication_fencing_token = publication_fencing_token,
        .base_source = base_source,
        .stats = .{
            .document_count = document_count,
            .document_base_version = document_base_version,
            .document_publish_mode = document_publish_mode,
            .text_segment_count = text_segment_count,
            .vector_segment_count = vector_segment_count,
            .sparse_segment_count = sparse_segment_count,
            .graph_segment_count = graph_segment_count,
            .published_search_sources = published_search_sources,
            .derived_outputs = derived_outputs,
            .policy = policy,
            .schema_json = schema_json,
            .read_schema_json = read_schema_json,
            .indexes_json = indexes_json,
        },
        .artifacts = artifacts,
    };
}

fn decodeStringAlloc(
    alloc: Allocator,
    data: []const u8,
    pos_ptr: *usize,
    budget: *bounded_decode.Budget,
) ![]const u8 {
    var pos = pos_ptr.*;
    if (pos > data.len or data.len - pos < 4) return error.InvalidManifest;
    const raw_len = std.mem.readInt(u32, data[pos..][0..4], .little);
    const len: usize = @intCast(raw_len);
    pos += 4;
    if (len > data.len - pos) return error.InvalidManifest;
    try budget.admitBytes(len);
    const value = try alloc.dupe(u8, data[pos .. pos + len]);
    pos += len;
    pos_ptr.* = pos;
    return value;
}

fn decodeOptionalStringAlloc(alloc: Allocator, data: []const u8, pos_ptr: *usize, budget: *bounded_decode.Budget) !?[]const u8 {
    const value = try decodeStringAlloc(alloc, data, pos_ptr, budget);
    if (value.len == 0) {
        alloc.free(value);
        return null;
    }
    return value;
}

fn decodeStringListAlloc(alloc: Allocator, data: []const u8, pos_ptr: *usize, budget: *bounded_decode.Budget) ![]const []const u8 {
    var pos = pos_ptr.*;
    if (pos > data.len or data.len - pos < 4) return error.InvalidManifest;
    const raw_count = std.mem.readInt(u32, data[pos..][0..4], .little);
    pos += 4;
    pos_ptr.* = pos;
    const count = try budget.admitCount([]const u8, raw_count, data.len - pos, 4);
    if (count == 0) return &.{};

    const out = try alloc.alloc([]const u8, count);
    errdefer alloc.free(out);
    var initialized: usize = 0;
    errdefer {
        for (out[0..initialized]) |item| alloc.free(item);
    }
    for (0..count) |idx| {
        out[idx] = try decodeStringAlloc(alloc, data, pos_ptr, budget);
        initialized += 1;
    }
    return out;
}

fn freeStringList(alloc: Allocator, values: []const []const u8) void {
    for (values) |value| alloc.free(value);
    if (values.len != 0) alloc.free(values);
}

fn decodeBaseSourceAlloc(alloc: Allocator, data: []const u8) !manifest_types.BaseSourceDescriptor {
    if (data.len == 0) return error.InvalidManifest;
    var budget = try bounded_decode.Budget.init(data.len, .{});
    var pos: usize = 0;
    const kind_value = data[pos];
    pos += 1;

    const descriptor: manifest_types.BaseSourceDescriptor = switch (kind_value) {
        @intFromEnum(manifest_types.BaseSourceKind.antfly_document_segments) => .{ .antfly_document_segments = {} },
        @intFromEnum(manifest_types.BaseSourceKind.antfly_lsm_overlay) => .{ .antfly_lsm_overlay = {} },
        @intFromEnum(manifest_types.BaseSourceKind.antfly_row_fragments) => blk: {
            const snapshot_id = try decodeStringAlloc(alloc, data, &pos, &budget);
            errdefer alloc.free(snapshot_id);
            const schema_fingerprint = try decodeStringAlloc(alloc, data, &pos, &budget);
            errdefer alloc.free(schema_fingerprint);
            const row_fragment_artifacts = try decodeStringListAlloc(alloc, data, &pos, &budget);
            errdefer freeStringList(alloc, row_fragment_artifacts);
            const row_fragment_stats_artifacts = try decodeStringListAlloc(alloc, data, &pos, &budget);
            errdefer freeStringList(alloc, row_fragment_stats_artifacts);
            break :blk .{ .antfly_row_fragments = .{
                .snapshot_id = snapshot_id,
                .schema_fingerprint = schema_fingerprint,
                .row_fragment_artifacts = row_fragment_artifacts,
                .row_fragment_stats_artifacts = row_fragment_stats_artifacts,
            } };
        },
        @intFromEnum(manifest_types.BaseSourceKind.external_parquet),
        @intFromEnum(manifest_types.BaseSourceKind.external_iceberg),
        @intFromEnum(manifest_types.BaseSourceKind.external_lance),
        => blk: {
            if (pos + 1 > data.len) return error.InvalidManifest;
            const format: manifest_types.ExternalBaseFormat = switch (data[pos]) {
                @intFromEnum(manifest_types.ExternalBaseFormat.parquet_prefix) => .parquet_prefix,
                @intFromEnum(manifest_types.ExternalBaseFormat.iceberg) => .iceberg,
                @intFromEnum(manifest_types.ExternalBaseFormat.lance) => .lance,
                else => return error.InvalidManifest,
            };
            pos += 1;
            const source_uri = try decodeStringAlloc(alloc, data, &pos, &budget);
            errdefer alloc.free(source_uri);
            const snapshot_id = try decodeStringAlloc(alloc, data, &pos, &budget);
            errdefer alloc.free(snapshot_id);
            const schema_fingerprint = try decodeStringAlloc(alloc, data, &pos, &budget);
            errdefer alloc.free(schema_fingerprint);
            const file_inventory_artifact = try decodeOptionalStringAlloc(alloc, data, &pos, &budget);
            errdefer if (file_inventory_artifact) |artifact_id| alloc.free(artifact_id);
            const row_group_metadata_artifact = try decodeOptionalStringAlloc(alloc, data, &pos, &budget);
            errdefer if (row_group_metadata_artifact) |artifact_id| alloc.free(artifact_id);
            const delete_metadata_artifact = try decodeOptionalStringAlloc(alloc, data, &pos, &budget);
            errdefer if (delete_metadata_artifact) |artifact_id| alloc.free(artifact_id);
            const source = manifest_types.ExternalBaseSource{
                .format = format,
                .source_uri = source_uri,
                .snapshot_id = snapshot_id,
                .schema_fingerprint = schema_fingerprint,
                .file_inventory_artifact = file_inventory_artifact,
                .row_group_metadata_artifact = row_group_metadata_artifact,
                .delete_metadata_artifact = delete_metadata_artifact,
            };
            break :blk switch (kind_value) {
                @intFromEnum(manifest_types.BaseSourceKind.external_parquet) => .{ .external_parquet = source },
                @intFromEnum(manifest_types.BaseSourceKind.external_iceberg) => .{ .external_iceberg = source },
                @intFromEnum(manifest_types.BaseSourceKind.external_lance) => .{ .external_lance = source },
                else => unreachable,
            };
        },
        else => return error.InvalidManifest,
    };

    if (pos != data.len) {
        var cleanup = descriptor;
        manifest_base_source.freeOwnedDescriptor(alloc, &cleanup);
        return error.InvalidManifest;
    }
    descriptor.validate() catch |err| {
        var cleanup = descriptor;
        manifest_base_source.freeOwnedDescriptor(alloc, &cleanup);
        return err;
    };
    return descriptor;
}

test "serverless manifest codec round-trips deterministically" {
    const alloc = std.testing.allocator;
    var manifest = manifest_types.Manifest{
        .namespace = try alloc.dupe(u8, "products"),
        .version = 42,
        .built_at_ns = 123456,
        .wal_start_lsn = 1000,
        .wal_end_lsn = 1050,
        .publication_lineage_tracked = true,
        .publication_parent_version = 40,
        .publication_fencing_token = 91,
        .stats = .{
            .document_count = 99,
            .document_base_version = 42,
            .document_publish_mode = .head_republish,
            .text_segment_count = 2,
            .vector_segment_count = 1,
            .sparse_segment_count = 1,
            .graph_segment_count = 1,
            .published_search_sources = try search_sources.defaultPublishedSearchSourcesAlloc(alloc),
            .derived_outputs = try search_sources.defaultMaterializedDerivedOutputsAlloc(alloc),
        },
        .artifacts = try alloc.alloc(manifest_types.ArtifactRef, 5),
    };
    defer manifest.deinit(alloc);

    manifest.artifacts[0] = .{
        .kind = .text_segment,
        .name = try alloc.dupe(u8, "full_text_index_v0"),
        .artifact_id = try alloc.dupe(u8, "text-0001"),
        .byte_len = 4096,
        .checksum = try alloc.dupe(u8, "sha256:text"),
    };
    manifest.artifacts[1] = .{
        .kind = .vector_segment,
        .artifact_id = try alloc.dupe(u8, "vec-0001"),
        .byte_len = 2048,
        .checksum = try alloc.dupe(u8, "sha256:vec"),
    };
    manifest.artifacts[2] = .{
        .kind = .sparse_segment,
        .artifact_id = try alloc.dupe(u8, "sparse-0001"),
        .byte_len = 1024,
        .checksum = try alloc.dupe(u8, "sha256:sparse"),
    };
    manifest.artifacts[3] = .{
        .kind = .graph_segment,
        .artifact_id = try alloc.dupe(u8, "graph-0001"),
        .byte_len = 512,
        .checksum = try alloc.dupe(u8, "sha256:graph"),
        .graph_topology_control_checksum = @splat(0x77),
    };
    manifest.artifacts[4] = .{
        .kind = .graph_metric_segment,
        .name = try alloc.dupe(u8, "5:graph8:pagerank"),
        .artifact_id = try alloc.dupe(u8, "metric-0001"),
        .byte_len = 256,
        .checksum = try alloc.dupe(u8, "sha256:metric"),
        .metadata_version = artifact_ref.graph_metric_segment_wire_version,
        .published_generation = 40,
        .edge_generation = 39,
        .computed_at_ms = 123,
        .materializer_fingerprint = 0x1234,
        .graph_metric_control_len = 73,
        .graph_metric_routing_footer_len = 17,
        .graph_metric_control_checksum = @splat(0x11),
        .graph_metric_routing_checksum = @splat(0x22),
        .graph_metric_point_index_checksum = @splat(0x44),
        .graph_metric_config_fingerprint = 0x5678,
        .graph_metric_source_checksum = @splat(0x33),
        .graph_metric_topology_checksum = @splat(0x55),
    };

    const encoded_a = try encodeAlloc(alloc, manifest);
    defer alloc.free(encoded_a);
    const encoded_b = try encodeAlloc(alloc, manifest);
    defer alloc.free(encoded_b);

    try std.testing.expectEqualSlices(u8, encoded_a, encoded_b);

    var decoded = try decodeAlloc(alloc, encoded_a);
    defer decoded.deinit(alloc);

    try std.testing.expectEqualStrings("products", decoded.namespace);
    try std.testing.expectEqual(@as(u64, 42), decoded.version);
    try std.testing.expect(decoded.publication_lineage_tracked);
    try std.testing.expectEqual(@as(?u64, 40), decoded.publication_parent_version);
    try std.testing.expectEqual(@as(u64, 91), decoded.publication_fencing_token);
    try std.testing.expectEqual(@as(u64, 99), decoded.stats.document_count);
    try std.testing.expectEqual(@as(u64, 42), decoded.stats.document_base_version);
    try std.testing.expectEqual(catalog_types.DocumentPublishMode.head_republish, decoded.stats.document_publish_mode);
    try std.testing.expectEqualStrings(search_sources.default_chunk_embedding_index_name, decoded.stats.published_search_sources.findVector().?.index_name);
    try std.testing.expectEqualStrings(search_sources.default_sparse_embedding_index_name, decoded.stats.published_search_sources.findSparse().?.index_name);
    try std.testing.expectEqualStrings(search_sources.default_chunk_preview_output_name, decoded.stats.derived_outputs.findByKind(.chunk_preview).?.name);
    try std.testing.expectEqualStrings(search_sources.default_chunk_embeddings_output_name, decoded.stats.derived_outputs.findByKind(.chunk_embeddings).?.name);
    try std.testing.expectEqualStrings(search_sources.default_rerank_terms_output_name, decoded.stats.derived_outputs.findByKind(.rerank_terms).?.name);
    try std.testing.expectEqual(@as(usize, 5), decoded.artifacts.len);
    try std.testing.expectEqual(manifest_types.ArtifactKind.text_segment, decoded.artifacts[0].kind);
    try std.testing.expectEqualStrings("vec-0001", decoded.artifacts[1].artifact_id);
    try std.testing.expectEqual(manifest_types.ArtifactKind.sparse_segment, decoded.artifacts[2].kind);
    try std.testing.expectEqual(manifest_types.ArtifactKind.graph_segment, decoded.artifacts[3].kind);
    try std.testing.expectEqual(manifest_types.ArtifactKind.graph_metric_segment, decoded.artifacts[4].kind);
    try std.testing.expectEqual(artifact_ref.graph_metric_segment_wire_version, decoded.artifacts[4].metadata_version);
    try std.testing.expectEqualSlices(u8, &manifest.artifacts[3].graph_topology_control_checksum, &decoded.artifacts[3].graph_topology_control_checksum);
    try std.testing.expectEqual(@as(u64, 40), decoded.artifacts[4].published_generation);
    try std.testing.expectEqual(@as(u64, 39), decoded.artifacts[4].edge_generation);
    try std.testing.expectEqual(@as(u64, 123), decoded.artifacts[4].computed_at_ms);
    try std.testing.expectEqual(@as(u64, 0x1234), decoded.artifacts[4].materializer_fingerprint);
    try std.testing.expectEqual(@as(u32, 73), decoded.artifacts[4].graph_metric_control_len);
    try std.testing.expectEqual(@as(u32, 17), decoded.artifacts[4].graph_metric_routing_footer_len);
    try std.testing.expectEqualSlices(u8, &([_]u8{0x11} ** 32), &decoded.artifacts[4].graph_metric_control_checksum);
    try std.testing.expectEqualSlices(u8, &([_]u8{0x44} ** 32), &decoded.artifacts[4].graph_metric_point_index_checksum);
    try std.testing.expectEqualSlices(u8, &([_]u8{0x55} ** 32), &decoded.artifacts[4].graph_metric_topology_checksum);

    // The current manifest layout retains the old field positions, but graph
    // metrics deliberately fail closed if a pre-release version is forged.
    const graph_metric_integrity_bytes: usize = 4 + 4 + 32 + 32 + 32 + 8 + 32 + 32 + 1 + 1;
    const materializer_bytes: usize = 8;
    const provenance_bytes: usize = 2 + 8 + 8 + 8;
    var current_artifact_bytes: usize = 0;
    for (manifest.artifacts) |artifact| current_artifact_bytes += artifactEncodedSize(artifact);
    const prefix_len = encoded_a.len - current_artifact_bytes;
    const encoded_v14 = try alloc.alloc(u8, encoded_a.len - materializer_bytes * manifest.artifacts.len - graph_metric_integrity_bytes - 32);
    defer alloc.free(encoded_v14);
    @memcpy(encoded_v14[0..prefix_len], encoded_a[0..prefix_len]);
    var src_pos = prefix_len;
    var dst_pos = prefix_len;
    const artifact_header_bytes: usize = 1 + 4 + 4 + 8 + 4;
    for (manifest.artifacts) |artifact| {
        const retained_header_bytes = artifact_header_bytes + provenance_bytes;
        @memcpy(encoded_v14[dst_pos..][0..retained_header_bytes], encoded_a[src_pos..][0..retained_header_bytes]);
        src_pos += retained_header_bytes + materializer_bytes;
        if (artifact.kind == .graph_metric_segment) src_pos += graph_metric_integrity_bytes;
        if (artifact.kind == .graph_segment) src_pos += 32;
        dst_pos += retained_header_bytes;
        const payload_len = artifact.name.len + artifact.artifact_id.len + artifact.checksum.len;
        @memcpy(encoded_v14[dst_pos..][0..payload_len], encoded_a[src_pos..][0..payload_len]);
        src_pos += payload_len;
        dst_pos += payload_len;
    }
    std.mem.writeInt(u16, encoded_v14[4..6], 14, .little);
    try std.testing.expectError(error.UnsupportedManifestVersion, decodeAlloc(alloc, encoded_v14));

    const encoded_v13 = try alloc.alloc(u8, encoded_v14.len - provenance_bytes * manifest.artifacts.len);
    defer alloc.free(encoded_v13);
    @memcpy(encoded_v13[0..prefix_len], encoded_v14[0..prefix_len]);
    src_pos = prefix_len;
    dst_pos = prefix_len;
    for (manifest.artifacts) |artifact| {
        @memcpy(encoded_v13[dst_pos..][0..artifact_header_bytes], encoded_v14[src_pos..][0..artifact_header_bytes]);
        src_pos += artifact_header_bytes + provenance_bytes;
        dst_pos += artifact_header_bytes;
        const payload_len = artifact.name.len + artifact.artifact_id.len + artifact.checksum.len;
        @memcpy(encoded_v13[dst_pos..][0..payload_len], encoded_v14[src_pos..][0..payload_len]);
        src_pos += payload_len;
        dst_pos += payload_len;
    }
    std.mem.writeInt(u16, encoded_v13[4..6], 13, .little);
    try std.testing.expectError(error.UnsupportedManifestVersion, decodeAlloc(alloc, encoded_v13));
}

test "serverless manifest codec round-trips optional lake base source" {
    const alloc = std.testing.allocator;
    var manifest = manifest_types.Manifest{
        .namespace = try alloc.dupe(u8, "events"),
        .version = 12,
        .built_at_ns = 123456,
        .wal_start_lsn = 0,
        .wal_end_lsn = 0,
        .base_source = .{ .external_iceberg = .{
            .format = .iceberg,
            .source_uri = try alloc.dupe(u8, "s3://bucket/warehouse/events"),
            .snapshot_id = try alloc.dupe(u8, "iceberg-123"),
            .schema_fingerprint = try alloc.dupe(u8, "schema-v1"),
            .file_inventory_artifact = try alloc.dupe(u8, "external-files-0001"),
        } },
        .stats = .{
            .document_count = 0,
            .document_base_version = 12,
        },
        .artifacts = try alloc.alloc(manifest_types.ArtifactRef, 1),
    };
    defer manifest.deinit(alloc);
    manifest.artifacts[0] = .{
        .kind = .external_base_source,
        .name = try alloc.dupe(u8, "events.files"),
        .artifact_id = try alloc.dupe(u8, "external-files-0001"),
        .byte_len = 4096,
        .checksum = try alloc.dupe(u8, "sha256:files"),
    };

    const encoded = try encodeAlloc(alloc, manifest);
    defer alloc.free(encoded);

    var decoded = try decodeAlloc(alloc, encoded);
    defer decoded.deinit(alloc);

    try std.testing.expect(decoded.base_source != null);
    try std.testing.expectEqual(manifest_types.BaseSourceKind.external_iceberg, std.meta.activeTag(decoded.base_source.?));
    try std.testing.expectEqualStrings("iceberg-123", decoded.base_source.?.external_iceberg.snapshot_id);
    try std.testing.expectEqualStrings("external-files-0001", decoded.base_source.?.external_iceberg.file_inventory_artifact.?);
    try std.testing.expectEqual(@as(usize, 1), decoded.artifacts.len);
    try std.testing.expectEqual(manifest_types.ArtifactKind.external_base_source, decoded.artifacts[0].kind);
    try std.testing.expectEqualStrings("external-files-0001", decoded.artifacts[0].artifact_id);
}

test "serverless manifest codec rejects bad magic" {
    const alloc = std.testing.allocator;
    const bad = [_]u8{ 'B', 'A', 'D', '!', 1, 0 };
    try std.testing.expectError(error.InvalidManifest, decodeAlloc(alloc, &bad));
}

test "serverless manifest readers and writers require the latest wire" {
    const alloc = std.testing.allocator;
    var manifest = manifest_types.Manifest{
        .namespace = "docs",
        .version = 1,
        .built_at_ns = 1,
        .wal_start_lsn = 0,
        .wal_end_lsn = 0,
        .stats = .{},
        .artifacts = &.{},
    };
    const encoded = try encodeForVersionAlloc(alloc, manifest, wire_version);
    defer alloc.free(encoded);
    try std.testing.expectEqual(wire_version, std.mem.readInt(u16, encoded[4..6], .little));
    var decoded = try decodeAlloc(alloc, encoded);
    defer decoded.deinit(alloc);
    try std.testing.expectEqualStrings("docs", decoded.namespace);

    for (0..wire_version + 2) |candidate| {
        if (candidate == wire_version) continue;
        const unsupported: u16 = @intCast(candidate);
        const forged = try alloc.dupe(u8, encoded);
        defer alloc.free(forged);
        std.mem.writeInt(u16, forged[4..6], unsupported, .little);
        try std.testing.expectError(error.UnsupportedManifestVersion, decodeAlloc(alloc, forged));
        try std.testing.expectError(error.UnsupportedManifestWriteVersion, encodeForVersionAlloc(alloc, manifest, unsupported));
    }
    for (0..header_size) |len| {
        try std.testing.expectError(error.InvalidManifest, decodeAlloc(alloc, encoded[0..len]));
    }

    var graph_artifacts = [_]manifest_types.ArtifactRef{.{
        .kind = .graph_metric_segment,
        .artifact_id = "sha256:aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa",
        .byte_len = 1,
        .checksum = "aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa",
        .metadata_version = artifact_ref.graph_metric_segment_wire_version,
        .graph_metric_control_len = 1,
        .graph_metric_routing_footer_len = 1,
    }};
    manifest.artifacts = &graph_artifacts;
    try std.testing.expectError(
        error.UnsupportedManifestWriteVersion,
        encodeForVersionAlloc(alloc, manifest, 15),
    );
    try std.testing.expectError(
        error.UnsupportedManifestWriteVersion,
        encodeForVersionAlloc(alloc, manifest, 16),
    );
    const encoded_current = try encodeForVersionAlloc(alloc, manifest, wire_version);
    defer alloc.free(encoded_current);
    var decoded_current = try decodeAlloc(alloc, encoded_current);
    defer decoded_current.deinit(alloc);
    try std.testing.expectEqual(artifact_ref.graph_metric_segment_wire_version, decoded_current.artifacts[0].metadata_version);
    for ([_]u16{ 15, 16, 17 }) |old_version| {
        const forged = try alloc.dupe(u8, encoded_current);
        defer alloc.free(forged);
        std.mem.writeInt(u16, forged[4..6], old_version, .little);
        try std.testing.expectError(error.UnsupportedManifestVersion, decodeAlloc(alloc, forged));
        try std.testing.expectError(error.UnsupportedManifestWriteVersion, encodeForVersionAlloc(alloc, manifest, old_version));
    }
    try std.testing.expectError(
        error.UnsupportedManifestWriteVersion,
        encodeForVersionAlloc(alloc, manifest, 13),
    );
}

test "serverless manifest rejects malformed descriptor tags without leaking names" {
    const alloc = std.testing.allocator;
    var sources = [_]search_sources.SearchSourceDescriptor{.{ .vector = .{
        .index_name = "vec",
        .document_source = .top_level_embedding,
    } }};
    var outputs = [_]search_sources.DerivedOutputDescriptor{.{ .name = "preview", .kind = .chunk_preview }};
    const manifest = manifest_types.Manifest{
        .namespace = "docs",
        .version = 1,
        .built_at_ns = 1,
        .wal_start_lsn = 0,
        .wal_end_lsn = 0,
        .stats = .{
            .published_search_sources = .{ .items = &sources },
            .derived_outputs = .{ .items = &outputs },
        },
        .artifacts = &.{},
    };
    const encoded = try encodeAlloc(alloc, manifest);
    defer alloc.free(encoded);
    const source_offset = header_size + manifest.namespace.len;
    const output_offset = source_offset + publishedSearchSourceEncodedSize(sources[0]);
    for ([_]usize{ source_offset, source_offset + 1, output_offset }) |offset| {
        const saved = encoded[offset];
        encoded[offset] = 255;
        try std.testing.expectError(error.InvalidManifest, decodeAlloc(alloc, encoded));
        encoded[offset] = saved;
    }
}

test "serverless lake manifest base source decoder rejects forged string-list counts before allocation" {
    const alloc = std.testing.allocator;
    var encoded = [_]u8{0} ** 13;
    encoded[0] = @intFromEnum(manifest_types.BaseSourceKind.antfly_row_fragments);
    std.mem.writeInt(u32, encoded[1..5], 0, .little);
    std.mem.writeInt(u32, encoded[5..9], 0, .little);
    std.mem.writeInt(u32, encoded[9..13], std.math.maxInt(u32), .little);
    try std.testing.expectError(error.DecodedArtifactTooLarge, decodeBaseSourceAlloc(alloc, &encoded));
}
