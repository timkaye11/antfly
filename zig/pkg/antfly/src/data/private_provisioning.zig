// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

//! Private placement proof validation. Results are request-arena owned and
//! never enter the public routing snapshot cache.
const std = @import("std");
const staging = @import("../metadata/restore_staging.zig");
const tables = @import("../metadata/table_manager.zig");
pub const Owner = struct {
    plan_id: [16]u8,
    plan_digest: [32]u8,
    table: tables.TableRecord,
    range: tables.RangeRecord,
    bootstrap: @import("../storage/db/restore_staging_contract.zig").OwnerBootstrap,
    scope: @import("../storage/db/restore_staging_contract.zig").Scope,
    cancel_recovery: bool = false,
};
pub const InitialOwner = struct {
    descriptor: staging.ProvisioningProjection.InitialFkOwner,
    table: tables.TableRecord,
    range: tables.RangeRecord,
};

pub fn validateInitial(alloc: std.mem.Allocator, public_tables: []const tables.TableRecord, public_ranges: []const tables.RangeRecord, projection: staging.ProvisioningProjection) ![]InitialOwner {
    var owners = std.ArrayListUnmanaged(InitialOwner).empty;
    errdefer owners.deinit(alloc);
    var seen: std.AutoHashMapUnmanaged(u64, void) = .empty;
    defer seen.deinit(alloc);
    for (projection.initial_fk_owners) |descriptor| {
        if (std.mem.allEqual(u8, &descriptor.plan_id, 0) or std.mem.allEqual(u8, &descriptor.plan_digest, 0) or
            std.mem.allEqual(u8, &descriptor.schema_digest, 0) or std.mem.allEqual(u8, &descriptor.catalog_digest, 0) or
            std.mem.allEqual(u8, &descriptor.public_schema_json_digest, 0) or descriptor.child_table_id == 0 or
            descriptor.child_group_id == 0 or descriptor.namespace.table_id != descriptor.child_table_id)
            return error.InvalidGenerationPublication;
        const table = for (projection.tables) |candidate| {
            if (candidate.table_id == descriptor.child_table_id) break candidate;
        } else return error.InvalidGenerationPublication;
        const range = for (projection.ranges) |candidate| {
            if (candidate.group_id == descriptor.child_group_id) break candidate;
        } else return error.InvalidGenerationPublication;
        if (range.table_id != table.table_id or
            descriptor.namespace.shard_id != tables.rangeDocIdentityShardId(range) or
            descriptor.namespace.range_id != tables.rangeDocIdentityRangeId(range)) return error.InvalidGenerationPublication;
        for (public_tables) |published| if (published.table_id == table.table_id) return error.InvalidGenerationPublication;
        for (public_ranges) |published| if (published.group_id == range.group_id) return error.InvalidGenerationPublication;
        var digest: [32]u8 = undefined;
        std.crypto.hash.Blake3.hash(table.schema_json, &digest, .{});
        if (!std.mem.eql(u8, &digest, &descriptor.public_schema_json_digest)) return error.InvalidGenerationPublication;
        var parsed = try @import("../schema/mod.zig").parseValidatedTableSchema(alloc, table.schema_json);
        defer parsed.deinit(alloc);
        if (parsed.version != descriptor.schema_version or parsed.storage_mode != .relational) return error.InvalidGenerationPublication;
        var compiled = try @import("../metadata/fk_generation_publication.zig").compileCatalog(alloc, parsed, table.table_id, null);
        defer compiled.deinit();
        std.crypto.hash.Blake3.hash(compiled.value, &digest, .{});
        if (!std.mem.eql(u8, &digest, &descriptor.catalog_digest) or
            !std.mem.eql(u8, &compiled.catalog.schema_digest, &descriptor.schema_digest)) return error.InvalidGenerationPublication;
        const entry = try seen.getOrPut(alloc, descriptor.child_group_id);
        if (entry.found_existing) return error.InvalidGenerationPublication;
        try owners.append(alloc, .{ .descriptor = descriptor, .table = table, .range = range });
    }
    return owners.toOwnedSlice(alloc);
}

pub fn validate(alloc: std.mem.Allocator, public_tables: []const tables.TableRecord, public_ranges: []const tables.RangeRecord, projection: staging.ProvisioningProjection) ![]Owner {
    const initial_owners = try validateInitial(alloc, public_tables, public_ranges, projection);
    defer alloc.free(initial_owners);
    if (projection.jobs_json.len > staging.max_active_attempts) return error.InvalidRestoreStaging;
    var owners = std.ArrayListUnmanaged(Owner).empty;
    errdefer owners.deinit(alloc);
    var seen_groups: std.AutoHashMapUnmanaged(u64, void) = .empty;
    defer seen_groups.deinit(alloc);
    for (projection.jobs_json) |bytes| {
        if (bytes.len > staging.max_encoded_bytes) return error.InvalidRestoreStaging;
        var parsed = try std.json.parseFromSlice(staging.Job, alloc, bytes, .{ .allocate = .alloc_always });
        defer parsed.deinit();
        try parsed.value.plan.validate(alloc);
        if (!std.mem.eql(u8, &parsed.value.plan_digest, &(try parsed.value.plan.digest(alloc)))) return error.InvalidRestoreStaging;
        switch (parsed.value.state) {
            .importing, .validating, .cutover, .activating, .canceling => {},
            .published, .canceled, .preparing_sources => return error.InvalidRestoreStaging,
        }
        if (parsed.value.revision == 0) return error.InvalidRestoreStaging;
        var new_owners: usize = 0;
        var old_owners: usize = 0;
        var parent_owners: usize = 0;
        for (parsed.value.plan.targets) |target| {
            new_owners += target.ranges.len;
            if (target.replace) |old| old_owners += old.ranges.len;
        }
        for (parsed.value.plan.external_fk_parents) |parent| parent_owners += parent.ranges.len;
        // The metadata progress counter is reused for each phase: parent
        // fences precede old-owner cutover, then activation counts parents
        // alone. Match that state machine even though only hidden target
        // owners are projected into this private routing view.
        const maximum = switch (parsed.value.state) {
            .cutover => old_owners + parent_owners,
            .activating => parent_owners,
            .canceling => new_owners + old_owners + parent_owners,
            else => new_owners,
        };
        if (parsed.value.completed_owners > maximum) return error.InvalidRestoreStaging;
        for (parsed.value.plan.targets) |target| {
            var expected = target.table;
            expected.restore_backup_id = "";
            expected.restore_location = "";
            expected.replication_sources_json = "[]";
            const maybe_actual: ?tables.TableRecord = for (projection.tables) |table| {
                if (table.table_id == expected.table_id) break table;
            } else null;
            const actual = maybe_actual orelse continue;
            for (public_tables) |published| if (published.table_id == target.table.table_id) return error.InvalidRestoreStaging;
            if (!tables.tableDefinitionsEqual(expected, actual)) return error.InvalidRestoreStaging;
            for (target.ranges, 0..) |range, range_index| {
                var expected_range = try tables.cloneRange(alloc, range);
                defer tables.freeRange(alloc, expected_range);
                try tables.clearOwnedRangeRestoreIntent(alloc, &expected_range);
                expected_range.completed_restore_fingerprint = tables.empty_restore_completion_fingerprint;
                const maybe_range: ?tables.RangeRecord = for (projection.ranges) |candidate| {
                    if (candidate.group_id == range.group_id) break candidate;
                } else null;
                const actual_range = maybe_range orelse continue;
                for (public_ranges) |published| if (published.group_id == actual_range.group_id) return error.InvalidRestoreStaging;
                if (!tables.rangeRecordsEqual(expected_range, actual_range)) return error.InvalidRestoreStaging;
                const inserted = try seen_groups.getOrPut(alloc, range.group_id);
                if (inserted.found_existing) return error.InvalidRestoreStaging;
                var bootstrap = try staging.ownerBootstrapForRangeIndex(alloc, parsed.value.plan, parsed.value.plan_digest, target, range_index);
                // Retain the complete immutable proof, borrowing strings from
                // the projection rather than the temporary decoded job.
                bootstrap.table_name = actual.name;
                bootstrap.schema_json = actual.schema_json;
                bootstrap.read_schema_json = actual.read_schema_json;
                bootstrap.indexes_json = actual.indexes_json;
                bootstrap.byte_range = .{ .start = actual_range.start_key, .end = actual_range.end_key orelse "" };
                try owners.append(alloc, .{
                    .plan_id = parsed.value.plan.id,
                    .plan_digest = parsed.value.plan_digest,
                    .table = actual,
                    .range = actual_range,
                    .bootstrap = bootstrap,
                    .scope = bootstrap.scope,
                    .cancel_recovery = parsed.value.state == .canceling,
                });
            }
        }
    }
    for (initial_owners) |owner| if (seen_groups.contains(owner.range.group_id)) return error.InvalidGenerationPublication;
    var seen_tables: std.AutoHashMapUnmanaged(u64, void) = .empty;
    defer seen_tables.deinit(alloc);
    for (projection.tables) |table| {
        const entry = try seen_tables.getOrPut(alloc, table.table_id);
        if (entry.found_existing) return error.InvalidRestoreStaging;
        const published: ?tables.TableRecord = for (public_tables) |candidate| {
            if (candidate.table_id == table.table_id) break candidate;
        } else null;
        if (published) |expected| {
            if (!tables.tableDefinitionsEqual(expected, table)) return error.MetadataSnapshotHeadMismatch;
        } else {
            for (owners.items) |owner| {
                if (owner.table.table_id == table.table_id) break;
            } else {
                for (initial_owners) |owner| {
                    if (owner.table.table_id == table.table_id) break;
                } else return error.InvalidRestoreStaging;
            }
        }
    }
    var projected_groups: std.AutoHashMapUnmanaged(u64, void) = .empty;
    defer projected_groups.deinit(alloc);
    for (projection.ranges) |range| {
        const entry = try projected_groups.getOrPut(alloc, range.group_id);
        if (entry.found_existing) return error.InvalidRestoreStaging;
        const published: ?tables.RangeRecord = for (public_ranges) |candidate| {
            if (candidate.group_id == range.group_id) break candidate;
        } else null;
        if (published) |expected| {
            if (!tables.rangeRecordsEqual(expected, range)) return error.MetadataSnapshotHeadMismatch;
        } else if (!seen_groups.contains(range.group_id)) {
            for (initial_owners) |owner| {
                if (owner.range.group_id == range.group_id) break;
            } else return error.InvalidRestoreStaging;
        }
    }
    return owners.toOwnedSlice(alloc);
}

pub fn find(owners: []const Owner, group_id: u64) ?Owner {
    for (owners) |owner| if (owner.range.group_id == group_id) return owner;
    return null;
}

/// Seed sidecars are immutable historical authority. Once an exact table ID
/// is published, routing takes over and its old hidden descriptor is retired.
/// This never relaxes validation of a live private provisioning response.
pub fn unpublishedProjection(alloc: std.mem.Allocator, public_tables: []const tables.TableRecord, projection: staging.ProvisioningProjection) !staging.ProvisioningProjection {
    var hidden_tables = std.ArrayListUnmanaged(tables.TableRecord).empty;
    var hidden_ranges = std.ArrayListUnmanaged(tables.RangeRecord).empty;
    for (projection.tables) |table| {
        for (public_tables) |published| {
            if (published.table_id == table.table_id) break;
        } else try hidden_tables.append(alloc, table);
    }
    for (projection.ranges) |range| {
        for (hidden_tables.items) |table| {
            if (table.table_id == range.table_id) {
                try hidden_ranges.append(alloc, range);
                break;
            }
        }
    }
    return .{ .tables = try hidden_tables.toOwnedSlice(alloc), .ranges = try hidden_ranges.toOwnedSlice(alloc), .jobs_json = projection.jobs_json, .initial_fk_owners = projection.initial_fk_owners };
}

test "initial FK private owner is exact, unpublished, and supports schema epoch zero" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();
    const schema_json = "{\"version\":0,\"storage_mode\":\"relational\"}";
    var schema_digest: [32]u8 = undefined;
    std.crypto.hash.Blake3.hash(schema_json, &schema_digest, .{});
    var parsed = try @import("../schema/mod.zig").parseValidatedTableSchema(alloc, schema_json);
    defer parsed.deinit(alloc);
    var compiled = try @import("../metadata/fk_generation_publication.zig").compileCatalog(alloc, parsed, 7, null);
    defer compiled.deinit();
    var catalog_digest: [32]u8 = undefined;
    std.crypto.hash.Blake3.hash(compiled.value, &catalog_digest, .{});
    var table_values = [_]tables.TableRecord{.{ .table_id = 7, .name = "hidden_fk", .schema_json = schema_json }};
    var range_values = [_]tables.RangeRecord{.{ .table_id = 7, .group_id = 9, .range_id = 9, .doc_identity_shard_id = 9, .doc_identity_range_id = 9, .start_key = "" }};
    var descriptors = [_]staging.ProvisioningProjection.InitialFkOwner{.{
        .plan_id = @splat(1),
        .plan_digest = @splat(2),
        .child_table_id = 7,
        .child_group_id = 9,
        .namespace = .{ .table_id = 7, .shard_id = 9, .range_id = 9 },
        .schema_version = 0,
        .schema_digest = compiled.catalog.schema_digest,
        .public_schema_json_digest = schema_digest,
        .catalog_digest = catalog_digest,
    }};
    const projection: staging.ProvisioningProjection = .{
        .tables = &table_values,
        .ranges = &range_values,
        .initial_fk_owners = &descriptors,
    };
    const owners = try validateInitial(alloc, &.{}, &.{}, projection);
    try std.testing.expectEqual(@as(usize, 1), owners.len);
    try std.testing.expectEqual(@as(usize, 0), (try validate(alloc, &.{}, &.{}, projection)).len);
    descriptors[0].namespace.range_id = 10;
    try std.testing.expectError(error.InvalidGenerationPublication, validateInitial(alloc, &.{}, &.{}, projection));
}

test "private provisioning validates immutable hidden owners and rejects forged descriptors" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();
    const target: staging.Target = .{
        .source_table_id = 1,
        .table = .{ .table_id = 11, .name = "hidden", .schema_json = "{}" },
        .ranges = &.{.{ .table_id = 11, .group_id = 701, .range_id = 701, .doc_identity_shard_id = 701, .doc_identity_range_id = 701, .start_key = "" }},
        .source_artifacts = &.{.{ .target_group_id = 701, .source_namespace = .{ .table_id = 1, .shard_id = 1, .range_id = 1 }, .format = .native, .snapshot_path = "source", .artifact_size_bytes = 1, .artifact_sha256 = @splat(3) }},
    };
    const plan: staging.Plan = .{ .id = @splat(1), .cohort_digest = @splat(2), .targets = &.{target} };
    const job: staging.Job = .{ .plan = plan, .plan_digest = try plan.digest(alloc) };
    const bytes = try std.json.Stringify.valueAlloc(alloc, job, .{});
    var table_values = [_]tables.TableRecord{target.table};
    var range_values = [_]tables.RangeRecord{target.ranges[0]};
    var projection: staging.ProvisioningProjection = .{ .tables = &table_values, .ranges = &range_values, .jobs_json = &.{bytes} };
    var portable_target = target;
    portable_target.source_table_name = "source";
    portable_target.target_schema_digest = try staging.runtimeSchemaDigestForTable(alloc, target.table.schema_json);
    var portable_artifacts = [_]staging.SourceArtifact{target.source_artifacts[0]};
    portable_artifacts[0].format = .portable;
    portable_target.source_artifacts = &portable_artifacts;
    const proof_digest = try @import("../storage/portable_backup.zig").sourceGenerationAdmissionSummaryDigest(portable_artifacts[0].source_namespace, &.{});
    portable_target.source_generation_admissions = &.{.{ .target_group_id = 701, .source_namespace = portable_artifacts[0].source_namespace, .entries = &.{}, .digest = proof_digest }};
    const portable_plan: staging.Plan = .{ .id = plan.id, .cohort_digest = plan.cohort_digest, .targets = &.{portable_target} };
    const portable_job: staging.Job = .{ .plan = portable_plan, .plan_digest = try portable_plan.digest(alloc) };
    const portable_bytes = try std.json.Stringify.valueAlloc(alloc, portable_job, .{});
    var portable_projection = projection;
    portable_projection.jobs_json = &.{portable_bytes};
    const portable_owners = try validate(alloc, &.{}, &.{}, portable_projection);
    const canonical = try staging.ownerBootstrapForRangeIndex(alloc, portable_plan, portable_job.plan_digest, portable_target, 0);
    try std.testing.expectEqualDeep(proof_digest, portable_owners[0].bootstrap.source_generation_proof_digest.?);
    try std.testing.expectEqualStrings(try canonical.encode(alloc), try portable_owners[0].bootstrap.encode(alloc));
    const owners = try validate(alloc, &.{}, &.{}, projection);
    try std.testing.expectEqual(@as(usize, 1), owners.len);
    try std.testing.expectEqual(@as(u64, 701), owners[0].range.group_id);
    try std.testing.expect(!owners[0].cancel_recovery);
    var cancel_job = job;
    cancel_job.state = .canceling;
    cancel_job.revision = 2;
    cancel_job.completed_owners = 1;
    const cancel_bytes = try std.json.Stringify.valueAlloc(alloc, cancel_job, .{});
    projection.jobs_json = &.{cancel_bytes};
    const cancel_owners = try validate(alloc, &.{}, &.{}, projection);
    try std.testing.expect(cancel_owners[0].cancel_recovery);
    const cancel_placement: @import("../raft/reconciler.zig").PlacementIntent = .{ .record = .{ .group_id = 701, .replica_id = 1, .local_node_id = 7 } };
    var scoped_cancel = try staging.scopeProvisioningForNode(std.testing.allocator, projection, 7, &.{cancel_placement});
    defer scoped_cancel.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings(cancel_bytes, scoped_cancel.jobs_json[0]);
    try std.testing.expect((try validate(alloc, &.{}, &.{}, scoped_cancel))[0].cancel_recovery);
    for ([_]staging.State{ .published, .canceled }) |terminal| {
        var terminal_job = cancel_job;
        terminal_job.state = terminal;
        const terminal_bytes = try std.json.Stringify.valueAlloc(alloc, terminal_job, .{});
        projection.jobs_json = &.{terminal_bytes};
        try std.testing.expectError(error.InvalidRestoreStaging, validate(alloc, &.{}, &.{}, projection));
    }
    cancel_job.completed_owners = 2;
    const excessive_progress = try std.json.Stringify.valueAlloc(alloc, cancel_job, .{});
    projection.jobs_json = &.{excessive_progress};
    try std.testing.expectError(error.InvalidRestoreStaging, validate(alloc, &.{}, &.{}, projection));
    projection.jobs_json = &.{bytes};
    table_values[0].schema_json = "{\"version\":2}";
    try std.testing.expectError(error.InvalidRestoreStaging, validate(alloc, &.{}, &.{}, projection));
    table_values[0] = target.table;
    projection.jobs_json = &.{};
    try std.testing.expectError(error.InvalidRestoreStaging, validate(alloc, &.{}, &.{}, projection));
    projection.jobs_json = &.{bytes};
    const placement: @import("../raft/reconciler.zig").PlacementIntent = .{ .record = .{ .group_id = 701, .replica_id = 1, .local_node_id = 7 } };
    var owner_projection = try staging.scopeProvisioningForNode(std.testing.allocator, projection, 7, &.{placement});
    defer owner_projection.deinit(std.testing.allocator);
    try std.testing.expectEqual(@as(usize, 1), owner_projection.tables.len);
    var other_projection = try staging.scopeProvisioningForNode(std.testing.allocator, projection, 8, &.{placement});
    defer other_projection.deinit(std.testing.allocator);
    try std.testing.expectEqual(@as(usize, 0), other_projection.tables.len);
    try std.testing.expectEqual(@as(usize, 0), other_projection.jobs_json.len);
}
