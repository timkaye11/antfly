// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const ant_json = @import("antfly-json");
const CancellationToken = @import("../common/cancellation.zig").CancellationToken;
const request_context = @import("execution_context.zig");
pub const RequestContext = request_context.RequestContext;
const platform_sync = @import("antfly_platform").sync;
const builtin = @import("builtin");
const httpx = @import("httpx");
const hbs = @import("handlebars");
const openai_api = @import("openai_api");
const google_auth = @import("antfly_google").auth;
const common_secrets = @import("../common/secrets.zig");
const credential_source_identity = @import("../common/credential_source_identity.zig");
const credential_safety = @import("../common/credential_safety.zig");
const provider_defaults = @import("../common/provider_defaults.zig");
const indexes_openapi = @import("antfly_indexes_openapi");
const embeddings_openapi = @import("antfly_embeddings_openapi");
const embeddings_types = @import("antfly_embeddings");
const scraping = @import("antfly_scraping");
const inference_types = @import("types.zig");
const bedrock_provider = @import("bedrock.zig");
const vertex_provider = @import("vertex.zig");
const openai_provider = @import("openai.zig");
const antfly_provider_mod = @import("local.zig");
const chunking_types = @import("../chunking/types.zig");
const inference_chunker = @import("inference_chunker");
const transcribing = @import("antfly_transcribing");
const readers = @import("antfly_readers");
const extracting = @import("antfly_extracting");
const template_mod = if (builtin.os.tag == .freestanding or builtin.is_test)
    @import("../storage/db/template_stub.zig")
else
    @import("../template.zig");
const template_remote = if (builtin.os.tag == .freestanding or builtin.is_test)
    @import("../storage/db/template_remote_stub.zig")
else
    @import("../template_remote.zig");
const db_embedder = @import("../storage/db/enrichment/embedder.zig");
const http_common = @import("../raft/transport/http_common.zig");
const std_http_listener = @import("../raft/transport/std_http_listener.zig");
const enrichment_types = @import("../storage/db/enrichment/enrichment_types.zig");
const runtime_callback_abi = @import("../runtime_callback_abi.zig");
const inference_work = @import("work.zig");
const embedding_wire = @import("embedding_wire.zig");
const remote_capabilities = @import("remote_capabilities.zig");
const execution_context = @import("execution_context.zig");
const shared_vector = @import("antfly_vector").vector;
const antfly_image = @import("antfly_image");
var traced_local_batches = std.atomic.Value(u64).init(0);

pub const SparseEmbedding = db_embedder.SparseEmbedding;

fn getenv(name: [*:0]const u8) ?[*:0]u8 {
    if (!builtin.link_libc) return null;
    return std.c.getenv(name);
}

pub const ProviderKind = enum {
    openai,
    ollama,
    bedrock,
    cohere,
    gemini,
    vertex,
    antfly,
};

/// Antfly assigns retrieval roles from the operation: artifact/index writes
/// are documents and semantic-search inputs are queries. Provider adapters
/// translate these canonical roles to their wire-specific spelling.
pub const EmbeddingTaskType = enum {
    retrieval_query,
    retrieval_document,

    pub fn canonical(self: EmbeddingTaskType) []const u8 {
        return switch (self) {
            .retrieval_query => "RETRIEVAL_QUERY",
            .retrieval_document => "RETRIEVAL_DOCUMENT",
        };
    }

    pub fn cohereInputType(self: EmbeddingTaskType) []const u8 {
        return switch (self) {
            .retrieval_query => "search_query",
            .retrieval_document => "search_document",
        };
    }
};

pub const EmbeddingRequestContext = struct {
    request: RequestContext,
    task_type: EmbeddingTaskType = .retrieval_document,
    instruction: ?[]const u8 = null,

    pub fn check(self: EmbeddingRequestContext) !void {
        return self.request.check();
    }
};

pub const AntflyProvider = struct {
    ptr: *anyopaque,
    /// Optional process/runtime-owned distributed capability cache. Stateless
    /// task adapters (for example reranking) borrow this rather than creating
    /// a cache for every query.
    remote_capability_cache: ?*remote_capabilities.Cache = null,
    boundary_dispatch: runtime_callback_abi.CallbackDispatch = AntflyProviderBoundary.local_dispatch,
    embed_dense_texts: *const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        texts: []const []const u8,
    ) anyerror![][]f32,
    embed_dense_texts_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        texts: []const []const u8,
        context: EmbeddingRequestContext,
    ) anyerror![][]f32 = null,
    embed_sparse_texts: *const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        texts: []const []const u8,
    ) anyerror![]db_embedder.SparseEmbedding,
    embed_sparse_texts_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        texts: []const []const u8,
        context: EmbeddingRequestContext,
    ) anyerror![]db_embedder.SparseEmbedding = null,
    embed_dense_parts: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        parts: []const template_mod.ContentPart,
    ) anyerror![][]f32 = null,
    embed_dense_parts_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        parts: []const template_mod.ContentPart,
        context: EmbeddingRequestContext,
    ) anyerror![][]f32 = null,
    rerank_texts: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        query: []const u8,
        documents: []const []const u8,
    ) anyerror![]f32 = null,
    rerank_texts_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        query: []const u8,
        documents: []const []const u8,
        context: RequestContext,
    ) anyerror![]f32 = null,
    generate_text: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        roles: []const []const u8,
        contents: []const []const u8,
        options: inference_types.GenerationOptions,
    ) anyerror![]u8 = null,
    generate_messages: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        messages: []const inference_types.ChatMessage,
        options: inference_types.GenerationOptions,
    ) anyerror![]u8 = null,
    /// Binary media remains borrowed for the synchronous call. Message media
    /// parts with empty data are matched to attachments in encounter order.
    generate_messages_with_attachments: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        messages: []const inference_types.ChatMessage,
        attachments: []const inference_work.Attachment,
    ) anyerror![]u8 = null,
    /// Capabilities are resolved by model and backend. Callers must not infer
    /// native batching from provider identity.
    model_capabilities: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        task: inference_work.Task,
    ) anyerror!inference_work.InferenceCapabilities = null,
    chunk_input: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        input: inference_chunker.Input,
        config: chunking_types.Config,
    ) anyerror![]inference_chunker.Chunk = null,
    chunk_input_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        input: inference_chunker.Input,
        config: chunking_types.Config,
        context: execution_context.RequestContext,
    ) anyerror![]inference_chunker.Chunk = null,
    /// The result slice and every returned string are owned by `alloc`.
    rewrite_texts: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        inputs: []const []const u8,
    ) anyerror![][]const u8 = null,
    /// The outer slice, every row, and every score label are owned by `alloc`.
    classify_texts: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: ClassificationRequest,
    ) anyerror![]const []const ClassificationScore = null,
    transcribe_audio: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: transcribing.Request,
    ) anyerror!transcribing.Response = null,
    read_images: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.Request,
    ) anyerror![]readers.Result = null,
    read_encoded_images: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.EncodedRequest,
    ) anyerror![]readers.Result = null,
    read_encoded_images_reported: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.EncodedRequest,
    ) anyerror!readers.BatchResult = null,
    extract: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: extracting.Request,
    ) anyerror!extracting.Response = null,
    /// Returns the task-keyed /ai/v1/models JSON body for the embedded node.
    list_models_json: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
    ) anyerror![]u8 = null,
    /// The concrete provider applies hard request/media/decoder/model admission
    /// inside every model callback. Request and result allocations still use
    /// the bounded allocator supplied by this public boundary. Only the linked
    /// inference-node boundary may normally set this; arbitrary callbacks fail
    /// closed when a public invocation plan depends on this guarantee. Keep new
    /// boundary fields append-only so callback offsets remain stable.
    owns_invocation_admission: bool = false,
    generate_text_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        roles: []const []const u8,
        contents: []const []const u8,
        options: inference_types.GenerationOptions,
        context: RequestContext,
    ) anyerror![]u8 = null,
    generate_messages_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        messages: []const inference_types.ChatMessage,
        options: inference_types.GenerationOptions,
        context: RequestContext,
    ) anyerror![]u8 = null,
    generate_messages_with_attachments_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        messages: []const inference_types.ChatMessage,
        attachments: []const inference_work.Attachment,
        context: RequestContext,
    ) anyerror![]u8 = null,
    model_capabilities_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        task: inference_work.Task,
        context: RequestContext,
    ) anyerror!inference_work.InferenceCapabilities = null,
    transcribe_audio_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: transcribing.Request,
        context: RequestContext,
    ) anyerror!transcribing.Response = null,
    read_images_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.Request,
        context: RequestContext,
    ) anyerror![]readers.Result = null,
    read_encoded_images_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.EncodedRequest,
        context: RequestContext,
    ) anyerror![]readers.Result = null,
    read_encoded_images_reported_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.EncodedRequest,
        context: RequestContext,
    ) anyerror!readers.BatchResult = null,
    extract_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: extracting.Request,
        context: RequestContext,
    ) anyerror!extracting.Response = null,
    /// Linked-process raw rasters stay borrowed for this synchronous call.
    /// Capability `borrowed_rasters` must be true before invoking it. Keep
    /// these fields append-only for provider boundary layout compatibility.
    read_raster_images_reported: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.RasterRequest,
    ) anyerror!readers.BatchResult = null,
    read_raster_images_reported_with_context: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        request: readers.RasterRequest,
        context: RequestContext,
    ) anyerror!readers.BatchResult = null,
    embed_dense_rasters: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        model: []const u8,
        rasters: []const antfly_image.BorrowedRasterAttachment,
        context: EmbeddingRequestContext,
    ) anyerror![][]f32 = null,
    /// Dense and raster responses use the owned numeric-row ABI, not JSON.
    typed_dense_results: bool = false,
    /// Canonical generation request/response on the admitted runtime route.
    generate_json: ?*const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        request_json: []const u8,
        context: ?RequestContext,
    ) anyerror![]u8 = null,

    pub fn generateJson(self: AntflyProvider, alloc: std.mem.Allocator, body: []const u8, context: ?RequestContext) ![]u8 {
        const callback = self.generate_json orelse return error.UnsupportedGeneratorProvider;
        return AntflyProviderBoundary.call("generate_json", self.boundary_dispatch, callback, .{ self.ptr, alloc, body, context });
    }
};

pub const ClassificationRequest = struct {
    texts: []const []const u8,
    labels: []const []const u8,
    hypothesis_template: ?[]const u8 = null,
    multi_label: bool = false,
};

pub const ClassificationScore = struct {
    label: []const u8,
    score: f32,
};

pub fn deinitRewrittenTexts(alloc: std.mem.Allocator, texts: []const []const u8) void {
    for (texts) |text| alloc.free(text);
    alloc.free(texts);
}

pub fn deinitClassificationScores(alloc: std.mem.Allocator, results: []const []const ClassificationScore) void {
    for (results) |row| {
        for (row) |score| alloc.free(score.label);
        alloc.free(row);
    }
    alloc.free(results);
}

/// The checked native boundary for every callback carried by AntflyProvider.
///
/// AntflyProvider crosses hidden static runtime units in the standalone build.
/// Consumers must invoke callbacks through this boundary so the owning unit can
/// validate method/signature/layout contracts and translate errors through the
/// stable status ABI. Keep this public and shared instead of defining
/// task-family-specific trampolines with independently drifting contracts.
pub const AntflyProviderBoundary = runtime_callback_abi.Boundary(AntflyProvider);

const BedrockCredentialPool = struct {
    alloc: std.mem.Allocator,
    io: std.Io,
    mutex: std.Io.Mutex = .init,
    by_region: std.StringHashMapUnmanaged(*bedrock_provider.CredentialCache) = .empty,

    fn init(alloc: std.mem.Allocator, io: std.Io) BedrockCredentialPool {
        return .{ .alloc = alloc, .io = io };
    }

    fn cacheForRegion(self: *BedrockCredentialPool, region: []const u8) !*bedrock_provider.CredentialCache {
        self.mutex.lockUncancelable(self.io);
        defer self.mutex.unlock(self.io);

        if (self.by_region.get(region)) |cache| return cache;

        const owned_region = try self.alloc.dupe(u8, region);
        errdefer self.alloc.free(owned_region);
        const cache = try self.alloc.create(bedrock_provider.CredentialCache);
        errdefer self.alloc.destroy(cache);
        cache.* = .{};
        try self.by_region.put(self.alloc, owned_region, cache);
        return cache;
    }

    fn deinit(self: *BedrockCredentialPool) void {
        var iterator = self.by_region.iterator();
        while (iterator.next()) |entry| {
            entry.value_ptr.*.deinit(self.alloc);
            self.alloc.destroy(entry.value_ptr.*);
            self.alloc.free(entry.key_ptr.*);
        }
        self.by_region.deinit(self.alloc);
        self.* = undefined;
    }
};

/// Long-lived provider resources shared by independently constructed managed
/// embedders. API runtimes should own one of these for their full service
/// lifetime so request-scoped embedders reuse credentials and refresh work.
pub const ProviderRuntime = struct {
    alloc: std.mem.Allocator,
    io: std.Io,
    limits: *provider_limits.Registry = &provider_limits.process_registry,
    google_credentials: google_auth.CredentialManager,
    bedrock_credentials: BedrockCredentialPool,
    http_mutex: std.atomic.Mutex = .unlocked,
    http_client: std.atomic.Value(?*httpx.Client) = .init(null),

    pub fn init(alloc: std.mem.Allocator, io: std.Io) ProviderRuntime {
        return .{
            .alloc = alloc,
            .io = io,
            .google_credentials = google_auth.CredentialManager.init(alloc, io),
            .bedrock_credentials = BedrockCredentialPool.init(alloc, io),
        };
    }

    /// Lazily publish one service-scoped transport. Provider request objects
    /// retain per-request URLs, authentication, cancellation, and deadlines;
    /// the client owns only reusable DNS/TLS/connection state.
    fn httpClient(self: *ProviderRuntime) !*httpx.Client {
        if (self.http_client.load(.acquire)) |client| return client;
        lockAtomic(&self.http_mutex);
        defer self.http_mutex.unlock();
        if (self.http_client.load(.acquire)) |client| return client;
        const client = try self.alloc.create(httpx.Client);
        errdefer self.alloc.destroy(client);
        // Request-local URL/header/response allocations happen concurrently;
        // keep them off a possibly arena-backed service owner allocator.
        client.* = httpx.Client.initWithConfig(std.heap.smp_allocator, self.io, .{
            .keep_alive = true,
            .cookies_enabled = false,
            .max_response_size = remote_embedding_max_response_bytes,
            .timeouts = httpx.Timeouts.uniform(max_embedding_request_timeout_ms),
        });
        self.http_client.store(client, .release);
        return client;
    }

    pub fn deinit(self: *ProviderRuntime) void {
        if (self.http_client.swap(null, .acq_rel)) |client| {
            client.deinit();
            self.alloc.destroy(client);
        }
        self.bedrock_credentials.deinit();
        self.google_credentials.deinit();
        self.* = undefined;
    }
};

pub const InitOptions = struct {
    antfly_provider: ?AntflyProvider = null,
    /// Runtime-owned cache shared by distributed task adapters. Managed
    /// embedders borrow it; standalone callers retain an owned fallback.
    remote_capability_cache: ?*remote_capabilities.Cache = null,
    io: ?std.Io = null,
    /// The supplied I/O executor can run the provider request and its timeout
    /// watchdog concurrently. Keep this explicit: merely having an Io value
    /// does not imply concurrency (query probes often use the global
    /// single-threaded executor).
    bounded_http_request: bool = false,
    deadline_ns: ?u64 = null,
    cancellation: ?CancellationToken = null,
    progress: ?request_context.ProgressSink = null,
    secret_store: ?*common_secrets.FileStore = null,
    remote_content: ?*const scraping.RemoteContentConfig = null,
    inference_api_url: ?[]const u8 = null,
    inference_api_key: ?[]const u8 = null,
    source_table: []const u8 = "",
    /// Borrowed for the lifetime of the constructed ManagedEmbedder. Services
    /// should supply their runtime; standalone embedders retain an owned
    /// fallback for compatibility.
    provider_runtime: ?*ProviderRuntime = null,
};

fn bindOwnedHttpIoIfNeeded(
    alloc: std.mem.Allocator,
    options: *InitOptions,
) !?*std.Io.Threaded {
    if (options.io != null) return null;
    const io_impl = try alloc.create(std.Io.Threaded);
    errdefer alloc.destroy(io_impl);
    // The caller allocator owns only this stable-lifetime shell. Threaded uses
    // its allocator from worker threads for futures, groups, and runtime
    // bookkeeping, so its internal allocator must be thread-safe regardless
    // of whether the public owner supplied an arena or another local allocator.
    io_impl.* = std.Io.Threaded.init(std.heap.smp_allocator, .{});
    options.io = io_impl.io();
    options.bounded_http_request = true;
    return io_impl;
}

fn deinitOwnedHttpIo(alloc: std.mem.Allocator, owned: ?*std.Io.Threaded) void {
    const io_impl = owned orelse return;
    io_impl.deinit();
    alloc.destroy(io_impl);
}

const DimensionProbeValidation = enum {
    strict,
    defer_probe,
};

pub const QueryTemplateError = error{
    PermanentPromptFailure,
    TransientPromptFailure,
};

const provider_limits = @import("../common/provider_limits.zig");
const default_pacing_burst: u32 = 1;
const max_embedding_request_timeout_ms: u64 = 30_000;
const max_embedding_index_sources: usize = 64;
const max_embedding_request_timeout_ns: u64 = max_embedding_request_timeout_ms * std.time.ns_per_ms;
const query_cache_secret_refresh_interval_ns: u64 = std.time.ns_per_s;
const dimension_probe_text = "antfly embedding dimension probe";

fn monotonicNowNs() u64 {
    var ts: std.posix.timespec = undefined;
    switch (std.posix.errno(std.posix.system.clock_gettime(.MONOTONIC, &ts))) {
        .SUCCESS => return @intCast(@as(i128, ts.sec) * std.time.ns_per_s + ts.nsec),
        else => return 0,
    }
}

fn lockAtomic(mutex: *std.atomic.Mutex) void {
    platform_sync.lockYielding(mutex);
}

pub const ManagedEmbeddingEntry = struct {
    alloc: std.mem.Allocator,
    io: ?std.Io = null,
    bounded_http_request: bool = false,
    deadline_ns: ?u64 = null,
    cancellation: ?CancellationToken = null,
    progress: ?request_context.ProgressSink = null,
    index_name: []u8,
    embedding_name: []u8 = "",
    embedding_names: [][]u8 = &.{},
    /// Additional public index names that select this producer for query
    /// embedding. Artifact names remain separate so vector-space validation
    /// only reasons about durable artifact streams.
    lookup_aliases: [][]u8 = &.{},
    provider: ProviderKind,
    model: []u8,
    base_url: []u8,
    source_table: []u8 = "",
    region: []u8 = "",
    project_id: []u8 = "",
    location: []u8 = "",
    credentials_path: []u8 = "",
    bedrock_request_format: bedrock_provider.RequestFormat = .auto,
    input_type: []u8 = "",
    /// Advanced provider overrides. When omitted, Antfly derives these from
    /// whether it is embedding an indexed document or a search query.
    query_input_type: []u8 = "",
    document_input_type: []u8 = "",
    query_instruction: []u8 = "",
    truncate: []u8 = "",
    /// Borrowed from the service ProviderRuntime, or from the owning
    /// ManagedEmbedder's standalone fallback. Service-scoped managers keep
    /// cloud credentials across request embedders and serialize refreshes.
    google_credentials: ?*google_auth.CredentialManager = null,
    bedrock_credentials: ?*bedrock_provider.CredentialCache = null,
    owns_bedrock_credentials: bool = false,
    api_key: ?common_secrets.SecretValue = null,
    auth_header_cache: common_secrets.BearerAuthHeaderCache = .{},
    secret_store: ?*common_secrets.FileStore = null,
    remote_content: ?*const scraping.RemoteContentConfig = null,
    /// Lifetime-owned by ManagedEmbedder and shared by all remote operations.
    /// Request-local overlays borrow this pointer, preserving connection/DNS/
    /// TLS state without moving client synchronization objects.
    shared_http_client: ?*httpx.Client = null,
    provider_runtime: ?*ProviderRuntime = null,
    dimensions: u32,
    sparse: bool = false,
    multimodal: bool = false,
    requests_per_minute: u32 = 0,
    burst: u32 = default_pacing_burst,
    rate_limit: provider_limits.Policy = .{},
    quota: ?provider_limits.Handle = null,
    antfly_provider: ?AntflyProvider = null,
    shared_remote_capability_cache: ?*remote_capabilities.Cache = null,
    remote_capability_cache: ?remote_capabilities.Cache = null,

    fn capabilityCache(self: *const ManagedEmbeddingEntry) ?*remote_capabilities.Cache {
        if (self.shared_remote_capability_cache) |cache| return cache;
        if (self.remote_capability_cache != null)
            return &@constCast(self).remote_capability_cache.?;
        return null;
    }

    fn requestOverlay(self: *const ManagedEmbeddingEntry) ManagedEmbeddingEntry {
        var overlay = self.*;
        // A request overlay may replace cancellation and credential caches,
        // but it must borrow the configured entry's synchronization owner.
        overlay.shared_remote_capability_cache = self.capabilityCache();
        overlay.remote_capability_cache = null;
        return overlay;
    }

    fn httpClient(self: *const ManagedEmbeddingEntry, alloc: std.mem.Allocator, fallback: *?httpx.Client) !*httpx.Client {
        if (self.shared_http_client) |client| return client;
        if (self.provider_runtime) |runtime| return runtime.httpClient();
        // Direct unit construction remains supported; production constructors
        // always attach the lifetime client below.
        fallback.* = httpx.Client.initWithConfig(alloc, try embeddingHttpIo(self), try embeddingHttpClientConfig(self));
        return &fallback.*.?;
    }

    fn deinit(self: *ManagedEmbeddingEntry, alloc: std.mem.Allocator) void {
        std.debug.assert(self.alloc.ptr == alloc.ptr);
        if (self.quota) |*quota| quota.release();
        alloc.free(self.index_name);
        if (self.embedding_name.len > 0) alloc.free(self.embedding_name);
        for (self.embedding_names) |name| alloc.free(name);
        if (self.embedding_names.len > 0) alloc.free(self.embedding_names);
        for (self.lookup_aliases) |name| alloc.free(name);
        if (self.lookup_aliases.len > 0) alloc.free(self.lookup_aliases);
        alloc.free(self.model);
        alloc.free(self.base_url);
        if (self.source_table.len > 0) alloc.free(self.source_table);
        if (self.region.len > 0) alloc.free(self.region);
        if (self.project_id.len > 0) alloc.free(self.project_id);
        if (self.location.len > 0) alloc.free(self.location);
        if (self.credentials_path.len > 0) alloc.free(self.credentials_path);
        if (self.input_type.len > 0) alloc.free(self.input_type);
        if (self.query_input_type.len > 0) alloc.free(self.query_input_type);
        if (self.document_input_type.len > 0) alloc.free(self.document_input_type);
        if (self.query_instruction.len > 0) alloc.free(self.query_instruction);
        if (self.truncate.len > 0) alloc.free(self.truncate);
        if (self.owns_bedrock_credentials) {
            const cache = self.bedrock_credentials.?;
            cache.deinit(alloc);
            alloc.destroy(cache);
        }
        if (self.remote_capability_cache) |*cache| cache.deinit();
        if (self.api_key) |*api_key| api_key.deinit(alloc);
        self.auth_header_cache.deinit(alloc);
        self.* = undefined;
    }
};

test "managed embedding request overlays borrow capability cache synchronization" {
    var entry = ManagedEmbeddingEntry{
        .alloc = std.testing.allocator,
        .index_name = @constCast("index"),
        .provider = .antfly,
        .model = @constCast("model"),
        .base_url = @constCast("http://inference.invalid"),
        .dimensions = 8,
        .remote_capability_cache = remote_capabilities.Cache.init(
            std.testing.allocator,
            std.Io.Threaded.global_single_threaded.io(),
        ),
    };
    defer entry.remote_capability_cache.?.deinit();

    const overlay = entry.requestOverlay();
    try std.testing.expect(overlay.remote_capability_cache == null);
    try std.testing.expect(overlay.shared_remote_capability_cache.? == &entry.remote_capability_cache.?);
}

fn attachProviderQuota(entry: *ManagedEmbeddingEntry, options: InitOptions) !void {
    if (entry.antfly_provider != null) {
        if (entry.rate_limit.enabled()) return error.UnsupportedLocalRateLimit;
        return;
    }
    const registry = if (options.provider_runtime) |runtime| runtime.limits else &provider_limits.process_registry;
    entry.quota = try registry.acquire(.{ .endpoint = managedEmbeddingEndpointIdentity(entry), .operation = .embedding }, entry.rate_limit);
}

fn managedEmbeddingEndpointIdentity(entry: *const ManagedEmbeddingEntry) provider_limits.EndpointIdentity {
    return .{
        .provider = std.meta.stringToEnum(provider_limits.Provider, @tagName(entry.provider)).?,
        .endpoint = entry.base_url,
        .model = entry.model,
        .region = entry.region,
        .project = entry.project_id,
        .location = entry.location,
        .credentials = managedEmbeddingCredentialSourceIdentity(entry),
    };
}

fn managedEmbeddingCredentialSourceIdentity(
    entry: *const ManagedEmbeddingEntry,
) credential_source_identity.CredentialSourceIdentity {
    const Identity = credential_source_identity.CredentialSourceIdentity;
    return switch (entry.provider) {
        .openai, .cohere, .gemini, .antfly => credential_source_identity.fromSecretValue(entry.api_key),
        .vertex => Identity.googleAdc(if (entry.credentials_path.len > 0) entry.credentials_path else null),
        // Managed Bedrock currently exposes the process-wide AWS default
        // chain. Profile and web-identity constructors live in the shared
        // identity type so future per-index sources cannot bypass these
        // execution/cache boundaries.
        .bedrock => Identity.awsDefaultChain(),
        .ollama => Identity.none(),
    };
}

fn managedEmbeddingEntriesEquivalentForLookup(
    lhs: *const ManagedEmbeddingEntry,
    rhs: *const ManagedEmbeddingEntry,
) bool {
    return managedEmbeddingEndpointIdentity(lhs).eql(managedEmbeddingEndpointIdentity(rhs)) and
        lhs.dimensions == rhs.dimensions and
        lhs.sparse == rhs.sparse and
        lhs.multimodal == rhs.multimodal and
        std.meta.eql(lhs.rate_limit, rhs.rate_limit) and
        lhs.requests_per_minute == rhs.requests_per_minute and
        lhs.burst == rhs.burst and
        (lhs.antfly_provider != null) == (rhs.antfly_provider != null) and
        managedEmbeddingCredentialSourceIdentity(lhs).eql(managedEmbeddingCredentialSourceIdentity(rhs)) and
        std.mem.eql(u8, lhs.model, rhs.model) and
        std.mem.eql(u8, lhs.base_url, rhs.base_url) and
        std.mem.eql(u8, lhs.region, rhs.region) and
        std.mem.eql(u8, lhs.project_id, rhs.project_id) and
        std.mem.eql(u8, lhs.location, rhs.location) and
        lhs.bedrock_request_format == rhs.bedrock_request_format and
        std.mem.eql(u8, lhs.input_type, rhs.input_type) and
        std.mem.eql(u8, lhs.query_input_type, rhs.query_input_type) and
        std.mem.eql(u8, lhs.document_input_type, rhs.document_input_type) and
        std.mem.eql(u8, lhs.query_instruction, rhs.query_instruction) and
        std.mem.eql(u8, lhs.truncate, rhs.truncate);
}

/// Compare only durable vector-production semantics. Credentials, pacing, and
/// the concrete in-process provider are execution state owned by the managed
/// index and are intentionally absent from artifact provenance.
fn managedEmbeddingEntriesSemanticallyEquivalent(
    lhs: *const ManagedEmbeddingEntry,
    rhs: *const ManagedEmbeddingEntry,
) bool {
    return lhs.provider == rhs.provider and
        lhs.dimensions == rhs.dimensions and
        lhs.sparse == rhs.sparse and
        lhs.multimodal == rhs.multimodal and
        std.mem.eql(u8, lhs.model, rhs.model) and
        std.mem.eql(u8, lhs.base_url, rhs.base_url) and
        std.mem.eql(u8, lhs.region, rhs.region) and
        std.mem.eql(u8, lhs.project_id, rhs.project_id) and
        std.mem.eql(u8, lhs.location, rhs.location) and
        lhs.bedrock_request_format == rhs.bedrock_request_format and
        std.mem.eql(u8, lhs.input_type, rhs.input_type) and
        std.mem.eql(u8, lhs.query_input_type, rhs.query_input_type) and
        std.mem.eql(u8, lhs.document_input_type, rhs.document_input_type) and
        std.mem.eql(u8, lhs.query_instruction, rhs.query_instruction) and
        std.mem.eql(u8, lhs.truncate, rhs.truncate);
}

const VectorSpaceMap = std.StringHashMapUnmanaged([]const u8);

fn collectEmbeddingVectorSpaces(value: std.json.Value, spaces: *VectorSpaceMap, alloc: std.mem.Allocator) !void {
    switch (value) {
        .object => |object| {
            if (object.get("enrichments")) |enrichments| {
                if (enrichments != .array) return error.InvalidManagedEmbeddingIndex;
                for (enrichments.array.items) |enrichment| {
                    if (enrichment != .object) return error.InvalidManagedEmbeddingIndex;
                    const kind = enrichment.object.get("kind") orelse continue;
                    if (kind != .string or !std.mem.eql(u8, kind.string, "embedding")) continue;
                    const name = enrichment.object.get("name") orelse return error.InvalidManagedEmbeddingIndex;
                    if (name != .string or name.string.len == 0) return error.InvalidManagedEmbeddingIndex;
                    const vector_space = if (enrichment.object.get("vector_space")) |space| blk: {
                        if (space != .string or space.string.len == 0) return error.InvalidManagedEmbeddingIndex;
                        break :blk space.string;
                    } else "";
                    const gop = try spaces.getOrPut(alloc, name.string);
                    if (gop.found_existing) {
                        if (!std.mem.eql(u8, gop.value_ptr.*, vector_space)) return error.InvalidManagedEmbeddingIndex;
                    } else {
                        gop.value_ptr.* = vector_space;
                    }
                }
            }
            var it = object.iterator();
            while (it.next()) |entry| {
                if (std.mem.eql(u8, entry.key_ptr.*, "enrichments")) continue;
                try collectEmbeddingVectorSpaces(entry.value_ptr.*, spaces, alloc);
            }
        },
        .array => |array| for (array.items) |item| try collectEmbeddingVectorSpaces(item, spaces, alloc),
        else => {},
    }
}

fn recordEntryVectorSpace(
    spaces: *const VectorSpaceMap,
    name: []const u8,
    explicit_space: *?[]const u8,
    has_implicit: *bool,
) !void {
    const vector_space: []const u8 = spaces.get(name) orelse &.{};
    if (vector_space.len == 0) {
        has_implicit.* = true;
    } else if (explicit_space.*) |expected| {
        if (!std.mem.eql(u8, expected, vector_space)) return error.InvalidManagedEmbeddingIndex;
    } else {
        explicit_space.* = vector_space;
    }
}

fn entryExplicitVectorSpace(entry: *const ManagedEmbeddingEntry, spaces: *const VectorSpaceMap) !?[]const u8 {
    var explicit_space: ?[]const u8 = null;
    var has_implicit = false;
    if (entry.embedding_name.len > 0) {
        try recordEntryVectorSpace(spaces, entry.embedding_name, &explicit_space, &has_implicit);
    }
    for (entry.embedding_names) |name| {
        try recordEntryVectorSpace(spaces, name, &explicit_space, &has_implicit);
    }
    if (has_implicit and explicit_space != null) return error.InvalidManagedEmbeddingIndex;
    return explicit_space;
}

fn validateEntryVectorSpaceMode(entry: *const ManagedEmbeddingEntry, spaces: *const VectorSpaceMap) !void {
    _ = try entryExplicitVectorSpace(entry, spaces);
}

fn validateManagedEmbeddingLookupName(
    alloc: std.mem.Allocator,
    names: *std.StringHashMapUnmanaged(*const ManagedEmbeddingEntry),
    vector_spaces: *const VectorSpaceMap,
    name: []const u8,
    entry: *const ManagedEmbeddingEntry,
) !void {
    const gop = try names.getOrPut(alloc, name);
    if (!gop.found_existing) {
        gop.value_ptr.* = entry;
        return;
    }
    // Dimensions and dense/sparse representation can never be overridden.
    if (gop.value_ptr.*.dimensions != entry.dimensions or gop.value_ptr.*.sparse != entry.sparse) {
        return error.InvalidManagedEmbeddingIndex;
    }
    // A stable vector_space is an explicit application assertion that otherwise
    // distinct producers emit compatible vectors. Without one, prove semantic
    // compatibility from the effective managed embedder configuration.
    if (vector_spaces.get(name)) |vector_space| {
        if (vector_space.len > 0) return;
    }
    // Artifact-backed multi-source indexes register one runtime entry for each
    // producer. Their shared query name is compatible when every producer's
    // durable artifact stream asserts the same explicit vector space.
    const existing_space = try entryExplicitVectorSpace(gop.value_ptr.*, vector_spaces);
    const candidate_space = try entryExplicitVectorSpace(entry, vector_spaces);
    if (existing_space) |expected| {
        if (candidate_space) |candidate| {
            if (std.mem.eql(u8, expected, candidate)) return;
        }
    }
    if (!managedEmbeddingEntriesEquivalentForLookup(gop.value_ptr.*, entry)) return error.InvalidManagedEmbeddingIndex;
}

fn validateManagedEmbeddingLookupNames(
    alloc: std.mem.Allocator,
    entries: []const ManagedEmbeddingEntry,
    vector_spaces: *const VectorSpaceMap,
) !void {
    var query_names = std.StringHashMapUnmanaged(*const ManagedEmbeddingEntry).empty;
    defer query_names.deinit(alloc);
    var artifact_names = std.StringHashMapUnmanaged(*const ManagedEmbeddingEntry).empty;
    defer artifact_names.deinit(alloc);

    for (entries) |*entry| {
        try validateEntryVectorSpaceMode(entry, vector_spaces);
        try validateManagedEmbeddingLookupName(alloc, &query_names, vector_spaces, entry.index_name, entry);
        if (entry.embedding_name.len > 0) try validateManagedEmbeddingLookupName(alloc, &artifact_names, vector_spaces, entry.embedding_name, entry);
        for (entry.embedding_names) |embedding_name| {
            try validateManagedEmbeddingLookupName(alloc, &artifact_names, vector_spaces, embedding_name, entry);
        }
        for (entry.lookup_aliases) |alias| {
            try validateManagedEmbeddingLookupName(alloc, &query_names, vector_spaces, alias, entry);
        }
    }
}

fn requestPacerScopeKeyAlloc(alloc: std.mem.Allocator, entry: *const ManagedEmbeddingEntry) ![]u8 {
    const key = (provider_limits.QuotaIdentity{ .endpoint = managedEmbeddingEndpointIdentity(entry), .operation = .embedding }).digest();
    return alloc.dupe(u8, &key);
}

pub fn testManagedEmbeddingCredentialSourceIdentities() !void {
    const alloc = std.testing.allocator;
    const base = ManagedEmbeddingEntry{
        .alloc = alloc,
        .index_name = @constCast("dense"),
        .provider = .vertex,
        .model = @constCast("gemini-embedding-001"),
        .base_url = @constCast("https://us-central1-aiplatform.googleapis.com/v1"),
        .project_id = @constCast("project-a"),
        .location = @constCast("us-central1"),
        .dimensions = 3072,
    };

    var vertex_default = base;
    var vertex_file_a = base;
    vertex_file_a.credentials_path = @constCast("credentials-a.json");
    var vertex_file_b = base;
    vertex_file_b.credentials_path = @constCast("credentials-b.json");
    try std.testing.expect(!managedEmbeddingEntriesEquivalentForLookup(&vertex_default, &vertex_file_a));
    try std.testing.expect(!managedEmbeddingEntriesEquivalentForLookup(&vertex_file_a, &vertex_file_b));

    var cohere_a = base;
    cohere_a.provider = .cohere;
    cohere_a.api_key = .{ .secret_ref = @constCast("cohere-a") };
    var cohere_b = cohere_a;
    cohere_b.api_key = .{ .secret_ref = @constCast("cohere-b") };
    try std.testing.expect(!managedEmbeddingEntriesEquivalentForLookup(&cohere_a, &cohere_b));

    var bedrock_a = base;
    bedrock_a.provider = .bedrock;
    bedrock_a.region = @constCast("us-east-1");
    var bedrock_b = bedrock_a;
    try std.testing.expect(managedEmbeddingEntriesEquivalentForLookup(&bedrock_a, &bedrock_b));
    try std.testing.expectEqual(
        credential_source_identity.CredentialSourceIdentity.Kind.aws_default_chain,
        managedEmbeddingCredentialSourceIdentity(&bedrock_a).kind,
    );

    const default_scope = try requestPacerScopeKeyAlloc(alloc, &vertex_default);
    defer alloc.free(default_scope);
    const file_scope = try requestPacerScopeKeyAlloc(alloc, &vertex_file_a);
    defer alloc.free(file_scope);
    try std.testing.expect(!std.mem.eql(u8, default_scope, file_scope));

    // These two scopes collided under delimiter-based concatenation because
    // the separator could be moved between adjacent user-controlled fields.
    var framed_a = base;
    framed_a.model = @constCast("alpha\x1fbeta");
    framed_a.project_id = @constCast("gamma");
    var framed_b = base;
    framed_b.model = @constCast("alpha");
    framed_b.project_id = @constCast("beta\x1fgamma");
    const framed_scope_a = try requestPacerScopeKeyAlloc(alloc, &framed_a);
    defer alloc.free(framed_scope_a);
    const framed_scope_b = try requestPacerScopeKeyAlloc(alloc, &framed_b);
    defer alloc.free(framed_scope_b);
    try std.testing.expect(!std.mem.eql(u8, framed_scope_a, framed_scope_b));
}

test "managed embedding execution identities include every credential source" {
    try testManagedEmbeddingCredentialSourceIdentities();
}

fn attachManagedGoogleCredentialManager(
    alloc: std.mem.Allocator,
    io: ?std.Io,
    entries: []ManagedEmbeddingEntry,
    provider_runtime: ?*ProviderRuntime,
) !?*google_auth.CredentialManager {
    var has_vertex = false;
    for (entries) |entry| {
        if (entry.provider == .vertex) {
            has_vertex = true;
            break;
        }
    }
    if (!has_vertex) return null;

    const owned_manager = if (provider_runtime == null)
        try alloc.create(google_auth.CredentialManager)
    else
        null;
    errdefer if (owned_manager) |manager| alloc.destroy(manager);
    if (owned_manager) |manager| {
        manager.* = google_auth.CredentialManager.init(
            alloc,
            io orelse std.Io.Threaded.global_single_threaded.io(),
        );
    }
    const manager = if (provider_runtime) |runtime|
        &runtime.google_credentials
    else
        owned_manager.?;
    for (entries) |*entry| {
        if (entry.provider == .vertex) entry.google_credentials = manager;
    }
    return owned_manager;
}

fn attachManagedBedrockCredentialCaches(
    alloc: std.mem.Allocator,
    entries: []ManagedEmbeddingEntry,
    provider_runtime: ?*ProviderRuntime,
) !void {
    for (entries) |*entry| {
        if (entry.provider != .bedrock) continue;
        if (provider_runtime) |runtime| {
            entry.bedrock_credentials = try runtime.bedrock_credentials.cacheForRegion(entry.region);
            continue;
        }

        const cache = try alloc.create(bedrock_provider.CredentialCache);
        cache.* = .{};
        entry.bedrock_credentials = cache;
        entry.owns_bedrock_credentials = true;
    }
}

pub const ManagedEmbedder = struct {
    alloc: std.mem.Allocator,
    entries: []ManagedEmbeddingEntry,
    /// Standalone owners without an injected runtime executor share one
    /// lifetime-owned concurrent service across every entry and invocation.
    /// The heap allocation keeps std.Io's self pointer stable if this aggregate
    /// is moved after construction.
    owned_http_io: ?*std.Io.Threaded = null,
    owned_http_client: ?*httpx.Client = null,
    owned_google_credentials: ?*google_auth.CredentialManager = null,

    pub fn initFromIndexesJson(alloc: std.mem.Allocator, indexes_json: []const u8) !ManagedEmbedder {
        return try initFromIndexesJsonWithOptions(alloc, indexes_json, .{});
    }

    pub fn initFromIndexesJsonWithAntflyProvider(
        alloc: std.mem.Allocator,
        indexes_json: []const u8,
        antfly_provider: ?AntflyProvider,
    ) !ManagedEmbedder {
        return try initFromIndexesJsonWithOptions(alloc, indexes_json, .{
            .antfly_provider = antfly_provider,
        });
    }

    pub fn initFromIndexesJsonWithOptions(alloc: std.mem.Allocator, indexes_json: []const u8, options: InitOptions) !ManagedEmbedder {
        var parsed = try std.json.parseFromSlice(std.json.Value, alloc, indexes_json, .{});
        defer parsed.deinit();
        return try initFromIndexValueObjectWithOptions(alloc, parsed.value, options);
    }

    pub fn initFromIndexValueObject(alloc: std.mem.Allocator, root: std.json.Value) !ManagedEmbedder {
        return try initFromIndexValueObjectWithOptions(alloc, root, .{});
    }

    pub fn initFromIndexValueObjectWithOptions(alloc: std.mem.Allocator, root: std.json.Value, options: InitOptions) !ManagedEmbedder {
        return try initFromIndexValueObjectWithOptionsAndKind(alloc, root, options, null);
    }

    pub fn initDenseFromIndexValueObjectWithOptions(alloc: std.mem.Allocator, root: std.json.Value, options: InitOptions) !ManagedEmbedder {
        return try initFromIndexValueObjectWithOptionsAndKind(alloc, root, options, false);
    }

    fn initFromIndexValueObjectWithOptionsAndKind(
        alloc: std.mem.Allocator,
        root: std.json.Value,
        supplied_options: InitOptions,
        sparse_kind: ?bool,
    ) !ManagedEmbedder {
        const object = switch (root) {
            .object => |object| object,
            else => return error.InvalidManagedEmbeddingIndex,
        };

        var options = supplied_options;
        const owned_http_io = try bindOwnedHttpIoIfNeeded(alloc, &options);
        errdefer deinitOwnedHttpIo(alloc, owned_http_io);

        var entries = std.ArrayListUnmanaged(ManagedEmbeddingEntry).empty;
        errdefer {
            for (entries.items) |*entry| entry.deinit(alloc);
            entries.deinit(alloc);
        }

        var it = object.iterator();
        while (it.next()) |entry| {
            var managed = try parseManagedEmbeddingEntry(alloc, entry.key_ptr.*, entry.value_ptr.*, options, sparse_kind) orelse continue;
            entries.append(alloc, managed) catch |err| {
                managed.deinit(alloc);
                return err;
            };
        }
        try validateAllEmbeddingEnrichmentProducers(alloc, root, options, entries.items, sparse_kind);
        try addArtifactBackedManagedEmbeddingEntries(alloc, root, options, &entries, sparse_kind);
        var vector_spaces = VectorSpaceMap.empty;
        defer vector_spaces.deinit(alloc);
        try collectEmbeddingVectorSpaces(root, &vector_spaces, alloc);
        try validateManagedEmbeddingLookupNames(alloc, entries.items, &vector_spaces);

        for (entries.items) |*entry| try attachProviderQuota(entry, options);

        const owned_google_credentials = try attachManagedGoogleCredentialManager(
            alloc,
            options.io,
            entries.items,
            options.provider_runtime,
        );
        errdefer if (owned_google_credentials) |manager| {
            manager.deinit();
            alloc.destroy(manager);
        };
        try attachManagedBedrockCredentialCaches(
            alloc,
            entries.items,
            options.provider_runtime,
        );

        const owned_entries = try entries.toOwnedSlice(alloc);
        entries = .empty;
        errdefer {
            for (owned_entries) |*entry| entry.deinit(alloc);
            alloc.free(owned_entries);
        }
        for (owned_entries) |*entry| {
            if (entry.antfly_provider == null) entry.provider_runtime = options.provider_runtime;
        }
        const owned_http_client = if (options.provider_runtime == null)
            try createManagedEmbeddingHttpClient(alloc, owned_entries)
        else
            null;
        errdefer deinitManagedEmbeddingHttpClient(alloc, owned_http_client);
        if (owned_http_client) |client| for (owned_entries) |*entry| {
            if (entry.antfly_provider == null) entry.shared_http_client = client;
        };
        return .{
            .alloc = alloc,
            .entries = owned_entries,
            .owned_http_io = owned_http_io,
            .owned_http_client = owned_http_client,
            .owned_google_credentials = owned_google_credentials,
        };
    }

    pub fn deinit(self: *ManagedEmbedder) void {
        for (self.entries) |*entry| entry.deinit(self.alloc);
        self.alloc.free(self.entries);
        deinitManagedEmbeddingHttpClient(self.alloc, self.owned_http_client);
        if (self.owned_google_credentials) |manager| {
            manager.deinit();
            self.alloc.destroy(manager);
        }
        // Every managed provider resource borrows this executor; tear it down
        // only after clients and credential managers have stopped using it.
        deinitOwnedHttpIo(self.alloc, self.owned_http_io);
        self.* = undefined;
    }

    pub fn hasEntries(self: ManagedEmbedder) bool {
        return self.entries.len > 0;
    }

    pub fn hasDenseEntries(self: ManagedEmbedder) bool {
        for (self.entries) |entry| {
            if (!entry.sparse) return true;
        }
        return false;
    }

    pub fn hasSparseEntries(self: ManagedEmbedder) bool {
        for (self.entries) |entry| {
            if (entry.sparse) return true;
        }
        return false;
    }

    pub fn denseInterface(self: *ManagedEmbedder) db_embedder.DenseEmbedder {
        return .{
            .ptr = self,
            .dense_embed_fn = embedDense,
            .dense_embed_batch_fn = embedDenseBatch,
            .dense_embed_parts_fn = embedDenseParts,
            .dense_embed_part_items_fn = embedDensePartItems,
            .dense_embed_raster_items_fn = embedDenseRasterItems,
            .dense_embed_part_items_with_context_fn = embedDensePartItemsWithContext,
            .dense_embed_part_items_planned_fn = embedDensePartItemsPlanned,
            .resolve_part_lease_fn = resolveDensePartLease,
            .part_batch_limit_fn = densePartBatchLimit,
            .capabilities_with_context_fn = denseCapabilitiesWithContext,
            .dense_embed_raster_items_with_context_fn = embedDenseRasterItemsWithContext,
            .dense_embed_with_context_fn = embedDenseWithContext,
            .dense_embed_batch_with_context_fn = embedDenseBatchWithContext,
            .dense_embed_parts_with_context_fn = embedDensePartsWithContext,
            .media_part_limit_fn = denseMediaPartLimit,
            .capabilities_fn = denseCapabilities,
            .part_invocation_memory_fn = densePartInvocationMemory,
            .deinit_fn = deinitDenseEmbedder,
            .set_cancellation_fn = setEmbedderCancellation,
            .set_progress_fn = setEmbedderProgress,
            .recovery_identity_fn = recoveryIdentity,
            .foreground_bounded = self.denseForegroundBounded(),
        };
    }

    pub fn sparseInterface(self: *ManagedEmbedder) db_embedder.SparseEmbedder {
        return .{
            .ptr = self,
            .sparse_embed_fn = embedSparse,
            .sparse_embed_batch_fn = embedSparseBatch,
            .sparse_embed_with_context_fn = embedSparseWithContext,
            .sparse_embed_batch_with_context_fn = embedSparseBatchWithContext,
            .deinit_fn = deinitSparseEmbedder,
            .set_cancellation_fn = setEmbedderCancellation,
            .set_progress_fn = setEmbedderProgress,
            .recovery_identity_fn = recoveryIdentity,
            .foreground_bounded = self.sparseForegroundBounded(),
        };
    }

    fn denseForegroundBounded(self: *const ManagedEmbedder) bool {
        for (self.entries) |*entry| {
            if (entry.sparse) continue;
            if (!entryForegroundBounded(entry, false)) return false;
        }
        return true;
    }

    fn sparseForegroundBounded(self: *const ManagedEmbedder) bool {
        for (self.entries) |*entry| {
            if (!entry.sparse) continue;
            if (!entryForegroundBounded(entry, true)) return false;
        }
        return true;
    }

    pub fn createDenseEmbedder(alloc: std.mem.Allocator, indexes_json: []const u8) !?db_embedder.DenseEmbedder {
        return try createDenseEmbedderWithAntflyProvider(alloc, indexes_json, null);
    }

    pub fn createDenseEmbedderWithAntflyProvider(
        alloc: std.mem.Allocator,
        indexes_json: []const u8,
        antfly_provider: ?AntflyProvider,
    ) !?db_embedder.DenseEmbedder {
        return try createDenseEmbedderWithOptions(alloc, indexes_json, .{ .antfly_provider = antfly_provider });
    }

    pub fn createDenseEmbedderWithOptions(
        alloc: std.mem.Allocator,
        indexes_json: []const u8,
        options: InitOptions,
    ) !?db_embedder.DenseEmbedder {
        var parsed = try std.json.parseFromSlice(std.json.Value, alloc, indexes_json, .{});
        defer parsed.deinit();
        return try createDenseEmbedderFromIndexValueWithOptions(alloc, parsed.value, options);
    }

    pub fn createDenseEmbedderFromIndexValueWithOptions(
        alloc: std.mem.Allocator,
        root: std.json.Value,
        options: InitOptions,
    ) !?db_embedder.DenseEmbedder {
        const owned = try alloc.create(ManagedEmbedder);
        errdefer alloc.destroy(owned);
        owned.* = try initFromIndexValueObjectWithOptionsAndKind(alloc, root, options, false);
        if (!owned.hasDenseEntries()) {
            owned.deinit();
            alloc.destroy(owned);
            return null;
        }
        return owned.denseInterface();
    }

    pub fn createSparseEmbedder(alloc: std.mem.Allocator, indexes_json: []const u8) !?db_embedder.SparseEmbedder {
        return try createSparseEmbedderWithAntflyProvider(alloc, indexes_json, null);
    }

    pub fn createSparseEmbedderWithAntflyProvider(
        alloc: std.mem.Allocator,
        indexes_json: []const u8,
        antfly_provider: ?AntflyProvider,
    ) !?db_embedder.SparseEmbedder {
        return try createSparseEmbedderWithOptions(alloc, indexes_json, .{ .antfly_provider = antfly_provider });
    }

    pub fn createSparseEmbedderWithOptions(
        alloc: std.mem.Allocator,
        indexes_json: []const u8,
        options: InitOptions,
    ) !?db_embedder.SparseEmbedder {
        var parsed = try std.json.parseFromSlice(std.json.Value, alloc, indexes_json, .{});
        defer parsed.deinit();
        return try createSparseEmbedderFromIndexValueWithOptions(alloc, parsed.value, options);
    }

    pub fn createSparseEmbedderFromIndexValueWithOptions(
        alloc: std.mem.Allocator,
        root: std.json.Value,
        options: InitOptions,
    ) !?db_embedder.SparseEmbedder {
        const owned = try alloc.create(ManagedEmbedder);
        errdefer alloc.destroy(owned);
        owned.* = try initFromIndexValueObjectWithOptionsAndKind(alloc, root, options, true);
        if (!owned.hasSparseEntries()) {
            owned.deinit();
            alloc.destroy(owned);
            return null;
        }
        return owned.sparseInterface();
    }

    pub fn embedQuery(self: *const ManagedEmbedder, alloc: std.mem.Allocator, index_name: []const u8, text: []const u8) ![]f32 {
        const entry = self.findQueryEntry(index_name) orelse return error.EmbeddingIndexNotFound;
        return try embedWithEntryForTask(alloc, entry, text, entry.dimensions, .retrieval_query);
    }

    pub fn embedQueryWithCancellation(
        self: *const ManagedEmbedder,
        alloc: std.mem.Allocator,
        index_name: []const u8,
        text: []const u8,
        cancellation: CancellationToken,
    ) ![]f32 {
        const configured_entry = self.findQueryEntry(index_name) orelse return error.EmbeddingIndexNotFound;
        var request_entry = configured_entry.requestOverlay();
        request_entry.owns_bedrock_credentials = false;
        request_entry.auth_header_cache = .{};
        defer request_entry.auth_header_cache.deinit(alloc);
        request_entry.cancellation = cancellation;
        return try embedWithEntryForTask(alloc, &request_entry, text, request_entry.dimensions, .retrieval_query);
    }

    /// Digest the effective dense-text embedding operation. Table and index
    /// names are intentionally excluded so equivalent configurations share
    /// results; the server-derived scope prevents cross-principal reuse.
    pub fn queryCacheKey(
        self: *const ManagedEmbedder,
        index_name: []const u8,
        security_domain: QueryCacheSecurityDomain,
        security_scope: []const u8,
        text: []const u8,
    ) ![32]u8 {
        const entry = self.findQueryEntry(index_name) orelse return error.EmbeddingIndexNotFound;
        if (entry.sparse or entry.multimodal) return error.QueryEmbeddingNotCacheable;
        const endpoint = managedEmbeddingEndpointIdentity(entry);
        // Only effective file-backed credentials depend on this store. Other
        // credential sources must neither refresh it nor invalidate on rotation.
        const secret_store = if (endpoint.credentials.kind == .secret_ref)
            entry.secret_store
        else
            null;
        if (secret_store) |store| {
            _ = try store.refreshIfChangedThrottled(query_cache_secret_refresh_interval_ns);
        }

        var hasher = std.crypto.hash.sha2.Sha256.init(.{});
        hashQueryCacheField(&hasher, "antfly-query-embedding-v3");
        hashQueryCacheField(&hasher, @tagName(security_domain));
        hashQueryCacheField(&hasher, security_scope);
        endpoint.updateHash(&hasher);
        hashQueryCacheField(&hasher, @tagName(entry.bedrock_request_format));
        hashQueryCacheField(&hasher, entry.input_type);
        hashQueryCacheField(&hasher, entry.query_input_type);
        hashQueryCacheField(&hasher, entry.document_input_type);
        hashQueryCacheField(&hasher, entry.query_instruction);
        hashQueryCacheField(&hasher, EmbeddingTaskType.retrieval_query.canonical());
        hashQueryCacheField(&hasher, entry.truncate);
        hashQueryCacheU64(&hasher, entry.dimensions);
        hashQueryCacheU64(&hasher, if (secret_store) |store| store.generationFast() else 0);
        hashQueryCacheField(&hasher, text);
        var digest: [32]u8 = undefined;
        hasher.final(&digest);
        return digest;
    }

    pub fn embedQueryWithTemplate(
        self: *const ManagedEmbedder,
        alloc: std.mem.Allocator,
        index_name: []const u8,
        text: []const u8,
        embedding_template: []const u8,
    ) ![]f32 {
        const entry = self.findQueryEntry(index_name) orelse return error.EmbeddingIndexNotFound;
        const rendered = try renderQueryTemplateWithEntry(alloc, embedding_template, text, entry);
        defer alloc.free(rendered);
        try ensureEntryDeadline(entry);
        try validateRenderedTemplate(alloc, rendered);
        const parts = try template_mod.textToParts(alloc, rendered);
        defer template_mod.freeContentParts(alloc, parts);
        return embedWithEntryPartsForTask(alloc, entry, parts, entry.dimensions, .retrieval_query) catch |err| return err;
    }

    pub fn embedQueryWithTemplateAndCancellation(
        self: *const ManagedEmbedder,
        alloc: std.mem.Allocator,
        index_name: []const u8,
        text: []const u8,
        embedding_template: []const u8,
        cancellation: CancellationToken,
    ) ![]f32 {
        const configured_entry = self.findQueryEntry(index_name) orelse return error.EmbeddingIndexNotFound;
        var request_entry = configured_entry.requestOverlay();
        request_entry.owns_bedrock_credentials = false;
        request_entry.auth_header_cache = .{};
        defer request_entry.auth_header_cache.deinit(alloc);
        request_entry.cancellation = cancellation;
        const rendered = try renderQueryTemplateWithEntry(alloc, embedding_template, text, &request_entry);
        defer alloc.free(rendered);
        try ensureEntryDeadline(&request_entry);
        try validateRenderedTemplate(alloc, rendered);
        const parts = try template_mod.textToParts(alloc, rendered);
        defer template_mod.freeContentParts(alloc, parts);
        return embedWithEntryPartsForTask(alloc, &request_entry, parts, request_entry.dimensions, .retrieval_query) catch |err| return err;
    }

    fn findQueryEntry(self: *const ManagedEmbedder, index_name: []const u8) ?*const ManagedEmbeddingEntry {
        // Query names have a global precedence order. In particular, a public
        // index alias must not lose to an unrelated artifact whose producer
        // happened to be registered earlier in the catalog.
        for (self.entries) |*entry| {
            if (std.mem.eql(u8, entry.index_name, index_name)) return entry;
        }
        for (self.entries) |*entry| {
            for (entry.lookup_aliases) |alias| {
                if (std.mem.eql(u8, alias, index_name)) return entry;
            }
        }
        for (self.entries) |*entry| {
            if (entry.embedding_name.len > 0 and std.mem.eql(u8, entry.embedding_name, index_name)) return entry;
            for (entry.embedding_names) |embedding_name| {
                if (std.mem.eql(u8, embedding_name, index_name)) return entry;
            }
        }
        return null;
    }

    fn findArtifactEntry(self: *const ManagedEmbedder, embedding_name: []const u8) ?*const ManagedEmbeddingEntry {
        // Artifact production has a distinct namespace from public index query
        // lookup. Prefer an explicitly registered artifact producer even when
        // an unrelated public index happens to have the same name.
        for (self.entries) |*entry| {
            if (entry.embedding_name.len > 0 and std.mem.eql(u8, entry.embedding_name, embedding_name)) return entry;
            for (entry.embedding_names) |name| {
                if (std.mem.eql(u8, name, embedding_name)) return entry;
            }
        }
        // Shorthand managed indexes historically name their generated artifact
        // after the index without persisting embedding_name in the public
        // config. Retain that compatibility only after explicit artifacts.
        for (self.entries) |*entry| {
            if (std.mem.eql(u8, entry.index_name, embedding_name)) return entry;
        }
        return null;
    }

    fn findEntry(self: *const ManagedEmbedder, name: []const u8) ?*const ManagedEmbeddingEntry {
        return self.findQueryEntry(name) orelse self.findArtifactEntry(name);
    }

    fn recoveryIdentity(ptr: *anyopaque, embedding_name: []const u8) ?db_embedder.RecoveryIdentity {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return null;
        return .{ .model = entry.model, .backend = @tagName(entry.provider) };
    }

    fn embedDense(ptr: *anyopaque, alloc: std.mem.Allocator, embedding_name: []const u8, text: []const u8, dims: u32) ![]f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (entry.sparse) return error.UnsupportedEmbeddingProvider;
        return try embedWithEntry(alloc, entry, text, dims);
    }

    fn embedDenseWithContext(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        text: []const u8,
        dims: u32,
        context: RequestContext,
    ) ![]f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (configured.sparse) return error.UnsupportedEmbeddingProvider;
        var cancellation = CombinedCancellation.init(configured.cancellation, context.cancellation);
        var entry = configured.*;
        applyRequestContext(&entry, context, &cancellation);
        return try embedWithEntry(alloc, &entry, text, dims);
    }

    fn embedDenseBatch(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        texts: []const []const u8,
        dims: u32,
    ) ![]const []const f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (entry.sparse) return error.UnsupportedEmbeddingProvider;
        return try embedBatchWithEntry(alloc, entry, texts, dims);
    }

    fn embedDenseBatchWithContext(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        texts: []const []const u8,
        dims: u32,
        context: RequestContext,
    ) ![]const []const f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (configured.sparse) return error.UnsupportedEmbeddingProvider;
        var cancellation = CombinedCancellation.init(configured.cancellation, context.cancellation);
        var entry = configured.*;
        applyRequestContext(&entry, context, &cancellation);
        return try embedBatchWithEntry(alloc, &entry, texts, dims);
    }

    fn embedDenseParts(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        parts: []const template_mod.ContentPart,
        dims: u32,
    ) ![]f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (entry.sparse) return error.UnsupportedEmbeddingProvider;
        return try embedWithEntryParts(alloc, entry, parts, dims);
    }

    fn embedDensePartItems(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        items: []const template_mod.ContentPart,
        dims: u32,
    ) ![]const []const f32 {
        return embedDensePartItemsControlled(ptr, alloc, embedding_name, items, dims, null, null);
    }

    fn embedDensePartItemsWithContext(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        items: []const template_mod.ContentPart,
        dims: u32,
        context: RequestContext,
    ) ![]const []const f32 {
        return embedDensePartItemsControlled(ptr, alloc, embedding_name, items, dims, context, null);
    }

    fn embedDensePartItemsPlanned(ptr: *anyopaque, alloc: std.mem.Allocator, name: []const u8, items: []const template_mod.ContentPart, dims: u32, context: ?RequestContext, lease: inference_work.CapabilityLease) ![]const []const f32 {
        return embedDensePartItemsControlled(ptr, alloc, name, items, dims, context, lease);
    }

    fn embedDensePartItemsControlled(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        items: []const template_mod.ContentPart,
        dims: u32,
        context: ?RequestContext,
        resolved_lease: ?inference_work.CapabilityLease,
    ) ![]const []const f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        var local_entry = configured.requestOverlay();
        local_entry.alloc = alloc;
        local_entry.auth_header_cache = .{};
        defer local_entry.auth_header_cache.deinit(alloc);
        var cancellation = CombinedCancellation.init(configured.cancellation, if (context) |value| value.cancellation else null);
        if (context) |value| {
            try value.check();
            applyRequestContext(&local_entry, value, &cancellation);
        }
        const entry = &local_entry;
        if (entry.sparse or !entry.multimodal) return error.UnsupportedEmbeddingProvider;
        const lease = resolved_lease orelse try densePartLeaseForEntry(entry, alloc);
        var capabilities = lease.capabilities orelse return error.EmbeddingCapabilitiesUnavailable;
        capabilities.batch.max_items = try densePartBatchLimit(ptr, embedding_name, dims, lease, capabilities.batch.max_items);
        capabilities.batch.preferred_items = @min(capabilities.batch.preferred_items, capabilities.batch.max_items);
        const attachment_transport: inference_work.AttachmentTransport = if (entry.antfly_provider != null)
            .borrowed_binary
        else if (entry.provider == .antfly and capabilities.framed_attachments)
            .segmented_framed_binary
        else
            .base64_payload;
        if (items.len == 0) return try alloc.alloc([]const f32, 0);

        // The planner normally forms capability-sized windows, but this is
        // also a public executor boundary. Partition here so direct callers
        // cannot accidentally turn a valid large document window into an
        // oversized provider invocation.
        const vectors = try alloc.alloc([]const f32, items.len);
        var initialized: usize = 0;
        errdefer {
            for (vectors[0..initialized]) |vector| alloc.free(vector);
            alloc.free(vectors);
        }
        var offset: usize = 0;
        while (offset < items.len) {
            const end = try densePartBatchEnd(alloc, capabilities, attachment_transport, items, offset, .{
                .model = entry.model,
                .task_type = if (entry.antfly_provider) |local|
                    if (local.embed_dense_parts_with_context != null) EmbeddingTaskType.retrieval_document.canonical() else null
                else
                    null,
            });
            const chunk = items[offset..end];
            try validateDensePartItemInvocation(alloc, capabilities, attachment_transport, chunk);
            const chunk_vectors = try embedPartItemsWithEntry(alloc, entry, chunk, dims, lease);
            defer alloc.free(chunk_vectors);
            for (chunk_vectors) |vector| {
                vectors[initialized] = vector;
                initialized += 1;
            }
            offset = end;
        }
        return vectors;
    }

    fn embedDenseRasterItems(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        items: []const antfly_image.BorrowedRasterAttachment,
        dims: u32,
    ) ![]const []const f32 {
        return embedDenseRasterItemsControlled(ptr, alloc, embedding_name, items, dims, null);
    }

    fn embedDenseRasterItemsWithContext(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        items: []const antfly_image.BorrowedRasterAttachment,
        dims: u32,
        context: RequestContext,
    ) ![]const []const f32 {
        return embedDenseRasterItemsControlled(ptr, alloc, embedding_name, items, dims, context);
    }

    fn embedDenseRasterItemsControlled(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        items: []const antfly_image.BorrowedRasterAttachment,
        dims: u32,
        context: ?RequestContext,
    ) ![]const []const f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        var local_entry = configured.requestOverlay();
        local_entry.alloc = alloc;
        local_entry.auth_header_cache = .{};
        defer local_entry.auth_header_cache.deinit(alloc);
        var cancellation = CombinedCancellation.init(configured.cancellation, if (context) |value| value.cancellation else null);
        if (context) |value| {
            try value.check();
            applyRequestContext(&local_entry, value, &cancellation);
        }
        const entry = &local_entry;
        if (entry.sparse or !entry.multimodal) return error.UnsupportedEmbeddingProvider;
        const local = entry.antfly_provider orelse return error.UnsupportedEmbeddingProvider;
        const embed_rasters = local.embed_dense_rasters orelse return error.UnsupportedEmbeddingProvider;
        if (items.len == 0) return try alloc.alloc([]const f32, 0);
        try checkEntryDispatchDeadline(entry);
        const invocation_context = embeddingRequestContext(entry, .retrieval_document);
        try invocation_context.check();
        const vectors = AntflyProviderBoundary.call(
            "embed_dense_rasters",
            local.boundary_dispatch,
            embed_rasters,
            .{ local.ptr, alloc, entry.model, items, invocation_context },
        ) catch |err| return normalizeLocalEmbeddingError(err);
        errdefer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
        try invocation_context.check();
        try validateDenseBatch(vectors, items.len, dims);
        return vectors;
    }

    fn embedDensePartsWithContext(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        parts: []const template_mod.ContentPart,
        dims: u32,
        context: RequestContext,
    ) ![]f32 {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (configured.sparse) return error.UnsupportedEmbeddingProvider;
        var cancellation = CombinedCancellation.init(configured.cancellation, context.cancellation);
        var entry = configured.*;
        applyRequestContext(&entry, context, &cancellation);
        return try embedWithEntryParts(alloc, &entry, parts, dims);
    }

    fn denseMediaPartLimit(ptr: *anyopaque, embedding_name: []const u8) ?usize {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return null;
        // The local multimodal embedding ABI treats every part as one
        // independently addressable input. The limit is a model/task semantic,
        // not a blanket property of the Antfly provider.
        return if (entry.multimodal and isAntflyProvider(entry.provider)) 1 else null;
    }

    fn jsonStringUpperBound(value: []const u8) !usize {
        const escaped = std.math.mul(usize, value.len, 6) catch return error.InferenceEncodedBytesExceeded;
        return std.math.add(usize, escaped, 2) catch return error.InferenceEncodedBytesExceeded;
    }

    fn densePartInvocationMemory(
        ptr: *anyopaque,
        embedding_name: []const u8,
        shape: db_embedder.DensePartInvocationShape,
        dims: u32,
        resolved_capabilities: ?inference_work.InferenceCapabilities,
    ) !db_embedder.DensePartInvocationMemory {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        const local = entry.antfly_provider;
        const attachment_transport: inference_work.AttachmentTransport = if (local != null)
            .borrowed_binary
        else if (entry.provider == .antfly and
            (resolved_capabilities orelse return error.EmbeddingCapabilitiesUnavailable).framed_attachments)
            .segmented_framed_binary
        else
            .base64_payload;
        const vector_values = std.math.mul(usize, shape.item_count, @as(usize, dims)) catch
            return error.InferenceEncodedBytesExceeded;
        const vector_bytes = std.math.mul(usize, vector_values, @sizeOf(f32)) catch
            return error.InferenceEncodedBytesExceeded;
        const vector_bytes_per_item = std.math.mul(usize, @as(usize, dims), @sizeOf(f32)) catch
            return error.InferenceEncodedBytesExceeded;
        const item_control_bytes = std.math.mul(usize, shape.item_count, 256) catch
            return error.InferenceEncodedBytesExceeded;
        const outer = std.math.add(
            usize,
            "{\"model\":".len + ",\"input\":[".len + "],\"encoding_format\":\"float\"}".len,
            try jsonStringUpperBound(entry.model),
        ) catch return error.InferenceEncodedBytesExceeded;
        const item_envelopes = std.math.add(
            usize,
            shape.item_envelope_json_bytes,
            shape.string_json_bytes,
        ) catch
            return error.InferenceEncodedBytesExceeded;
        const commas = if (shape.item_count == 0) 0 else shape.item_count - 1;
        const request_envelope = std.math.add(
            usize,
            outer,
            std.math.add(usize, item_envelopes, commas) catch return error.InferenceEncodedBytesExceeded,
        ) catch return error.InferenceEncodedBytesExceeded;
        // Legacy HTTP responses are capped at 4 MiB. Typed JSON arrays can be
        // denser than their source text and the arena retains geometric-growth
        // allocations until parsing finishes. Reserve a conservative complete
        // response/parser peak in addition to the expected parsed/final vector
        // copies. A bounded allowance covers URL/header/TLS/client control.
        // Numeric plans require a matching bound lease at execution and cannot
        // discover or parse JSON within this smaller, shape-bound allowance.
        const numeric_response = local == null and entry.provider == .antfly and dims != 0 and resolved_capabilities.?.numeric_responses_v1;
        const response_bytes = if (numeric_response)
            try numericDenseResponseLimit(shape.item_count, dims)
        else
            remote_embedding_max_response_bytes;
        const response_and_parser = if (local != null and local.?.typed_dense_results) 0 else std.math.mul(
            usize,
            response_bytes,
            if (numeric_response) remote_numeric_response_resident_multiplier else remote_embedding_response_resident_multiplier,
        ) catch return error.InferenceEncodedBytesExceeded;
        const vector_copies = std.math.mul(usize, vector_bytes, 2) catch
            return error.InferenceEncodedBytesExceeded;
        var fixed = std.math.add(usize, request_envelope, response_and_parser) catch
            return error.InferenceEncodedBytesExceeded;
        // Segmented framing owns metadata, prefix and segment descriptors, but
        // borrows all page payloads. Keep framing overhead in the fixed plan.
        if (attachment_transport == .segmented_framed_binary) fixed = std.math.add(
            usize,
            fixed,
            request_envelope,
        ) catch return error.InferenceEncodedBytesExceeded;
        fixed = std.math.add(usize, fixed, vector_copies) catch
            return error.InferenceEncodedBytesExceeded;
        fixed = std.math.add(usize, fixed, item_control_bytes) catch
            return error.InferenceEncodedBytesExceeded;
        fixed = std.math.add(usize, fixed, shape.preparation_bytes) catch
            return error.InferenceEncodedBytesExceeded;
        if (local) |provider| {
            if (!provider.owns_invocation_admission)
                return error.InferenceInvocationMemoryUnavailable;
        } else {
            fixed = std.math.add(usize, fixed, remote_embedding_transport_control_bytes) catch
                return error.InferenceEncodedBytesExceeded;
        }
        return .{
            .attachment_transport = attachment_transport,
            .fixed_bytes = fixed,
            .allocator_limit_bytes = fixed,
            .allocator_owner = if (local != null) .executor else .caller,
            .max_result_bytes_per_item = vector_bytes_per_item,
            .max_result_bytes = vector_bytes,
        };
    }

    fn denseCapabilities(ptr: *anyopaque, alloc: std.mem.Allocator, embedding_name: []const u8) !inference_work.InferenceCapabilities {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        return denseCapabilitiesForEntry(entry, alloc);
    }

    fn denseCapabilitiesWithContext(ptr: *anyopaque, alloc: std.mem.Allocator, name: []const u8, context: RequestContext) !inference_work.InferenceCapabilities {
        try context.check();
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(name) orelse return error.EmbeddingIndexNotFound;
        var entry = configured.requestOverlay();
        entry.alloc = alloc;
        entry.auth_header_cache = .{};
        defer entry.auth_header_cache.deinit(alloc);
        var cancellation = CombinedCancellation.init(configured.cancellation, context.cancellation);
        applyRequestContext(&entry, context, &cancellation);
        return denseCapabilitiesForEntry(&entry, alloc);
    }

    fn denseCapabilitiesForEntry(entry: *const ManagedEmbeddingEntry, alloc: std.mem.Allocator) !inference_work.InferenceCapabilities {
        return (try densePartLeaseForEntry(entry, alloc)).capabilities orelse error.EmbeddingCapabilitiesUnavailable;
    }

    fn densePartBatchLimit(ptr: *anyopaque, name: []const u8, dims: u32, lease: inference_work.CapabilityLease, requested: usize) !usize {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(name) orelse return error.EmbeddingIndexNotFound;
        const caps = lease.capabilities orelse return error.EmbeddingCapabilitiesUnavailable;
        if (entry.antfly_provider == null and entry.provider == .antfly and dims != 0 and caps.numeric_responses_v1)
            return @min(requested, try httpx.numeric_response.maxRows(dims));
        return requested;
    }

    fn resolveDensePartLease(ptr: *anyopaque, alloc: std.mem.Allocator, name: []const u8, context: ?RequestContext) !inference_work.CapabilityLease {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(name) orelse return error.EmbeddingIndexNotFound;
        var entry = configured.requestOverlay();
        entry.alloc = alloc;
        entry.auth_header_cache = .{};
        defer entry.auth_header_cache.deinit(alloc);
        var cancellation = CombinedCancellation.init(configured.cancellation, if (context) |value| value.cancellation else null);
        if (context) |value| {
            try value.check();
            applyRequestContext(&entry, value, &cancellation);
        }
        return densePartLeaseForEntry(&entry, alloc);
    }

    fn densePartLeaseForEntry(entry: *const ManagedEmbeddingEntry, alloc: std.mem.Allocator) !inference_work.CapabilityLease {
        if (entry.sparse) return error.UnsupportedEmbeddingProvider;
        if (entry.antfly_provider) |local| {
            if (local.model_capabilities) |resolve| {
                const result = try AntflyProviderBoundary.call(
                    "model_capabilities",
                    local.boundary_dispatch,
                    resolve,
                    .{ local.ptr, alloc, entry.model, inference_work.Task.embed },
                );
                try result.validate();
                if (result.task != .embed) return error.InvalidInferenceCapabilities;
                return .{ .capabilities = result };
            }
        }
        if (entry.provider == .antfly and entry.base_url.len > 0) {
            var fallback_http: ?httpx.Client = null;
            defer if (fallback_http) |*client| client.deinit();
            const http = try entry.httpClient(alloc, &fallback_http);
            var auth_header: ?[]u8 = null;
            defer if (auth_header) |value| alloc.free(value);
            var header_storage: [2][2][]const u8 = undefined;
            var header_count: usize = 0;
            if (entry.api_key) |*api_key_ref| {
                auth_header = try optionalBearerAuthHeaderOwned(@constCast(entry), alloc, api_key_ref);
                if (auth_header) |value| {
                    header_storage[header_count] = .{ "Authorization", value };
                    header_count += 1;
                }
            }
            if (entry.source_table.len > 0) {
                header_storage[header_count] = .{ "X-Antfly-Source-Table", entry.source_table };
                header_count += 1;
            }
            const headers = header_storage[0..header_count];
            const cache = entry.capabilityCache() orelse return error.InferenceCapabilitiesUnavailable;
            const discovered: ?remote_capabilities.CapabilityLease = cache.getOrDiscoverLeaseWithContext(
                http,
                entry.base_url,
                entry.model,
                .embed,
                headers,
                .{
                    .deadline_ns = embeddingOperationDeadline(entry),
                    .cancellation = entry.cancellation orelse .none,
                },
            ) catch |err| switch (err) {
                error.OutOfMemory, error.Canceled, error.Timeout => return err,
                else => null,
            };
            if (discovered) |lease| if (lease.capabilities != null) {
                var owned = lease;
                owned.scope_digest = try remote_capabilities.scopeDigest(alloc, entry.base_url, entry.model, .embed, headers);
                return owned;
            };
        }
        // Unknown remote capability is deliberately conservative. It remains
        // usable, but the document planner cannot assume fused batching or a
        // provider-specific memory ceiling.
        return .{ .capabilities = .{
            .task = .embed,
            .input_modalities = .{ .text = true, .image = entry.multimodal },
            .accepted_mime_types = .{ .text_plain = true, .image_png = entry.multimodal, .image_jpeg = entry.multimodal },
            .input_granularity = if (entry.multimodal) .page else .chunk,
            .batch = .{ .mode = .serial_compatibility, .preferred_items = 1, .max_items = 1, .max_media_parts_per_item = 1 },
            .output = .embedding,
            .borrowed_attachments = false,
        } };
    }

    fn setEmbedderCancellation(ptr: *anyopaque, cancellation: CancellationToken) void {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        for (self.entries) |*entry| entry.cancellation = cancellation;
    }

    fn setEmbedderProgress(ptr: *anyopaque, progress: request_context.ProgressSink) void {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        for (self.entries) |*entry| entry.progress = progress;
    }

    fn deinitDenseEmbedder(ptr: *anyopaque, alloc: std.mem.Allocator) void {
        _ = alloc;
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const owner_alloc = self.alloc;
        self.deinit();
        owner_alloc.destroy(self);
    }

    fn embedSparse(ptr: *anyopaque, alloc: std.mem.Allocator, embedding_name: []const u8, text: []const u8) !db_embedder.SparseEmbedding {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (!entry.sparse) return error.UnsupportedEmbeddingProvider;
        return try embedSparseWithEntry(alloc, entry, text);
    }

    fn embedSparseWithContext(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        text: []const u8,
        context: RequestContext,
    ) !db_embedder.SparseEmbedding {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (!configured.sparse) return error.UnsupportedEmbeddingProvider;
        var cancellation = CombinedCancellation.init(configured.cancellation, context.cancellation);
        var entry = configured.*;
        applyRequestContext(&entry, context, &cancellation);
        return try embedSparseWithEntry(alloc, &entry, text);
    }

    fn embedSparseBatch(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        texts: []const []const u8,
    ) ![]db_embedder.SparseEmbedding {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const entry = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (!entry.sparse) return error.UnsupportedEmbeddingProvider;
        return try embedSparseBatchWithEntry(alloc, entry, texts);
    }

    fn embedSparseBatchWithContext(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        embedding_name: []const u8,
        texts: []const []const u8,
        context: RequestContext,
    ) ![]db_embedder.SparseEmbedding {
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const configured = self.findArtifactEntry(embedding_name) orelse return error.EmbeddingIndexNotFound;
        if (!configured.sparse) return error.UnsupportedEmbeddingProvider;
        var cancellation = CombinedCancellation.init(configured.cancellation, context.cancellation);
        var entry = configured.*;
        applyRequestContext(&entry, context, &cancellation);
        return try embedSparseBatchWithEntry(alloc, &entry, texts);
    }

    fn deinitSparseEmbedder(ptr: *anyopaque, alloc: std.mem.Allocator) void {
        _ = alloc;
        const self: *ManagedEmbedder = @ptrCast(@alignCast(ptr));
        const owner_alloc = self.alloc;
        self.deinit();
        owner_alloc.destroy(self);
    }
};

/// A request-scoped entry preserves the runtime shutdown token while adding
/// the caller's independent cancellation source. The adapter is stack-owned
/// for exactly the synchronous provider invocation that borrows it.
const CombinedCancellation = struct {
    configured: ?CancellationToken,
    request: ?CancellationToken,

    fn init(configured: ?CancellationToken, request: ?CancellationToken) @This() {
        return .{ .configured = configured, .request = request };
    }

    fn isCancelled(raw: *const anyopaque) bool {
        const self: *const @This() = @ptrCast(@alignCast(raw));
        if (self.configured) |source| if (source.isCancelled()) return true;
        if (self.request) |source| if (source.isCancelled()) return true;
        return false;
    }

    fn token(self: *const @This()) ?CancellationToken {
        if (self.configured == null and self.request == null) return null;
        return .{ .ptr = self, .is_cancelled_fn = isCancelled };
    }
};

fn applyRequestContext(
    entry: *ManagedEmbeddingEntry,
    context: RequestContext,
    cancellation: *const CombinedCancellation,
) void {
    entry.io = context.io;
    entry.deadline_ns = if (entry.deadline_ns) |configured|
        if (context.deadline_ns) |request| @min(configured, request) else configured
    else
        context.deadline_ns;
    entry.cancellation = cancellation.token();
    if (context.progress) |progress| entry.progress = progress;
}

pub const QueryCacheSecurityDomain = enum {
    anonymous,
    principal,
    internal,
};

fn hashQueryCacheField(hasher: *std.crypto.hash.sha2.Sha256, value: []const u8) void {
    hashQueryCacheU64(hasher, value.len);
    hasher.update(value);
}

fn hashQueryCacheU64(hasher: *std.crypto.hash.sha2.Sha256, value: anytype) void {
    var encoded = std.mem.nativeToLittle(u64, @intCast(value));
    hasher.update(std.mem.asBytes(&encoded));
}

fn checkEntryDispatchDeadline(entry: *const ManagedEmbeddingEntry) !void {
    try ensureEntryDeadline(entry);
    // Remote attempts acquire at the transport boundary; local execution has
    // no HTTP quota. Keep this checkpoint for the existing cancellation contract.
}

fn embeddingIo(entry: *const ManagedEmbeddingEntry) std.Io {
    return entry.io orelse std.Io.Threaded.global_single_threaded.io();
}

/// Managed constructors always bind remote transports to either the caller's
/// runtime executor or the embedder's lifetime-owned standalone executor.
/// Failing closed here prevents an accidental return to per-request thread
/// pools or the non-concurrent process singleton.
fn embeddingHttpIo(entry: *const ManagedEmbeddingEntry) !std.Io {
    return entry.io orelse error.MissingEmbeddingHttpIo;
}

fn embeddingRequestContext(entry: *const ManagedEmbeddingEntry, task_type: EmbeddingTaskType) EmbeddingRequestContext {
    return .{
        .request = .{
            .io = embeddingIo(entry),
            .deadline_ns = embeddingOperationDeadline(entry),
            .cancellation = entry.cancellation,
            .progress = entry.progress,
        },
        .task_type = task_type,
        .instruction = if (task_type == .retrieval_query and entry.query_instruction.len > 0) entry.query_instruction else null,
    };
}

fn embeddingOperationDeadline(entry: *const ManagedEmbeddingEntry) u64 {
    return entry.deadline_ns orelse monotonicNowNs() +| max_embedding_request_timeout_ns;
}

const remote_embedding_max_response_bytes: usize = 4 << 20;
const remote_embedding_response_resident_multiplier: usize = 8;
// No JSON tree/arena exists on the numeric-only path. Four payload ceilings
// cover HTTP buffer growth, retained compressed input and growing decoded
// output; framing slack lives in transport control. Typed rows are separate.
const remote_numeric_response_resident_multiplier: usize = 4;
const remote_embedding_transport_control_bytes: usize = 256 << 10;

fn numericDenseResponseLimit(items: usize, dims: usize) !usize {
    // Leave room for bounded admission/stale-route error envelopes without
    // allocating a catalog or a success-response JSON parser in this grant.
    return @max(4096, try httpx.numeric_response.frameSize(items, dims));
}

fn ensureEntryDeadline(entry: *const ManagedEmbeddingEntry) !void {
    if (entry.cancellation) |value| if (value.isCancelled()) return error.Cancelled;
    const deadline = entry.deadline_ns orelse return;
    if (monotonicNowNs() >= deadline) return error.Timeout;
}

fn embeddingAttemptObserver(entry: *const ManagedEmbeddingEntry) ?httpx.AttemptObserver {
    return if (entry.quota) |quota| quota.limiter().observer(0) else null;
}

fn embeddingHttpClientConfig(entry: *const ManagedEmbeddingEntry) !httpx.ClientConfig {
    return embeddingHttpClientConfigForDeadline(entry, embeddingOperationDeadline(entry));
}

fn embeddingHttpClientConfigForDeadline(
    entry: *const ManagedEmbeddingEntry,
    deadline: u64,
) !httpx.ClientConfig {
    var config = httpx.ClientConfig{
        .keep_alive = false,
        // Embedding APIs authenticate explicitly. Ambient cookies must never
        // cross provider origins when the lifetime client is shared.
        .cookies_enabled = false,
        .max_response_size = remote_embedding_max_response_bytes,
    };
    const timeout_ms = try embeddingRemainingTimeoutMs(deadline);
    config.timeouts = httpx.Timeouts.uniform(timeout_ms);
    // Both the whole-request and connect watchdogs need an owner-scoped
    // concurrent executor. Managed embedders bind one for their lifetime;
    // catalog validation binds one for the duration of the probe.
    if (entry.bounded_http_request) {
        config.timeouts.request_ms = timeout_ms;
    } else {
        config.timeouts.connect_ms = 0;
    }
    return config;
}

fn embeddingRemainingTimeoutMs(deadline: u64) !u64 {
    const now_ns = monotonicNowNs();
    if (now_ns >= deadline) return error.Timeout;
    const remaining_ns = deadline - now_ns;
    return @min(
        max_embedding_request_timeout_ms,
        @max(@as(u64, 1), (remaining_ns +| std.time.ns_per_ms - 1) / std.time.ns_per_ms),
    );
}

fn createManagedEmbeddingHttpClient(
    alloc: std.mem.Allocator,
    entries: []const ManagedEmbeddingEntry,
) !?*httpx.Client {
    for (entries) |*entry| {
        if (entry.antfly_provider != null or entry.base_url.len == 0) continue;
        var config = try embeddingHttpClientConfigForDeadline(
            entry,
            monotonicNowNs() +| max_embedding_request_timeout_ns,
        );
        config.keep_alive = true;
        const client = try alloc.create(httpx.Client);
        // httpx uses its client allocator for request-local headers, URLs, and
        // responses as well as persistent pool state. Managed embedders are
        // callable concurrently and accept arbitrary owner allocators, so keep
        // all shared-client allocation on the process thread-safe allocator.
        client.* = httpx.Client.initWithConfig(std.heap.smp_allocator, try embeddingHttpIo(entry), config);
        return client;
    }
    return null;
}

fn deinitManagedEmbeddingHttpClient(alloc: std.mem.Allocator, client: ?*httpx.Client) void {
    const owned = client orelse return;
    owned.deinit();
    alloc.destroy(owned);
}

pub fn testManagedEmbedderConstructorAllocationFailureCleanup() !void {
    const Runner = struct {
        fn run(alloc: std.mem.Allocator) !void {
            const initialized = ManagedEmbedder.initFromIndexesJsonWithOptions(alloc,
                \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"openai","model":"text-embedding-3-small"}}}
            , .{ .io = std.Io.Threaded.global_single_threaded.io() });
            var managed = try initialized;
            managed.deinit();
        }
    };
    try std.testing.checkAllAllocationFailures(std.testing.allocator, Runner.run, .{});
}

fn applyAntflyEmbeddingRequestControls(
    entry: *const ManagedEmbeddingEntry,
    provider: *antfly_provider_mod.Provider,
    operation_deadline_ns: u64,
) !void {
    try ensureEntryDeadline(entry);
    const now_ns = monotonicNowNs();
    if (now_ns >= operation_deadline_ns) return error.Timeout;
    const remaining_ns = operation_deadline_ns - now_ns;
    const timeout_ms = @min(
        max_embedding_request_timeout_ms,
        @max(@as(u64, 1), (remaining_ns +| std.time.ns_per_ms - 1) / std.time.ns_per_ms),
    );
    provider.setRequestCancellation(entry.cancellation);
    provider.setRequestTimeoutMs(timeout_ms);
    provider.setMaxResponseBytes(remote_embedding_max_response_bytes);
}
fn entryForegroundBounded(entry: *const ManagedEmbeddingEntry, sparse: bool) bool {
    if (isAntflyProvider(entry.provider)) {
        if (entry.antfly_provider) |local| {
            if (sparse) return local.embed_sparse_texts_with_context != null;
            if (local.embed_dense_texts_with_context == null) return false;
            if (entry.multimodal and local.embed_dense_parts_with_context == null)
                return false;
            return true;
        }
    }
    // Remote providers enforce a whole-request deadline only when the owner
    // supplied an executor capable of running the request and watchdog
    // concurrently.
    return entry.bounded_http_request;
}

pub fn testLocalForegroundEmbeddingAdmissionCapabilities() !void {
    const Stub = struct {
        fn dense(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) ![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn sparse(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return error.TestUnexpectedResult;
        }

        fn sparseWithContext(
            _: *anyopaque,
            _: std.mem.Allocator,
            _: []const u8,
            _: []const []const u8,
            _: EmbeddingRequestContext,
        ) ![]db_embedder.SparseEmbedding {
            return error.TestUnexpectedResult;
        }
    };

    var provider_context: u8 = 0;
    var entry = ManagedEmbeddingEntry{
        .alloc = std.testing.allocator,
        .index_name = @constCast("sparse_idx"),
        .provider = .antfly,
        .model = @constCast("bge-m3"),
        .base_url = @constCast(""),
        .dimensions = 1,
        .sparse = true,
        .antfly_provider = .{
            .ptr = &provider_context,
            .embed_dense_texts = Stub.dense,
            .embed_sparse_texts = Stub.sparse,
        },
    };
    try std.testing.expect(!entryForegroundBounded(&entry, true));

    entry.antfly_provider = .{
        .ptr = &provider_context,
        .embed_dense_texts = Stub.dense,
        .embed_sparse_texts = Stub.sparse,
        .embed_sparse_texts_with_context = Stub.sparseWithContext,
    };
    try std.testing.expect(entryForegroundBounded(&entry, true));
}

pub fn testManagedEmbeddingRequestContextProgress() !void {
    const Capture = struct {
        last: ?request_context.Progress = null,

        fn update(raw: ?*anyopaque, progress: request_context.Progress) void {
            const self: *@This() = @ptrCast(@alignCast(raw.?));
            self.last = progress;
        }
    };

    var capture = Capture{};
    const entry = ManagedEmbeddingEntry{
        .alloc = std.testing.allocator,
        .deadline_ns = std.math.maxInt(u64),
        .progress = .{ .ptr = &capture, .update_fn = Capture.update },
        .index_name = @constCast("semantic_idx"),
        .provider = .antfly,
        .model = @constCast("bge-m3"),
        .base_url = @constCast(""),
        .dimensions = 1,
    };

    const context = embeddingRequestContext(&entry, .retrieval_document);
    try std.testing.expect(context.request.progress != null);
    try context.request.updateDetail(.executing, 2, 3, entry.model, "metal");
    const progress = capture.last.?;
    try std.testing.expectEqual(request_context.Phase.executing, progress.phase);
    try std.testing.expectEqual(@as(u64, 2), progress.completed);
    try std.testing.expectEqual(@as(u64, 3), progress.total);
    try std.testing.expectEqualStrings("bge-m3", progress.model);
    try std.testing.expectEqualStrings("metal", progress.backend);
    try std.testing.expectEqual(entry.deadline_ns, progress.deadline_ns);
}

pub fn testEmbeddingProviderDeadlines() !void {
    const io = std.Io.Threaded.global_single_threaded.io();
    try provider_limits.testCancellationAndDeadline();

    const indexes_json =
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"openai","model":"text-embedding-3-small"}}}
    ;
    const expired_deadline = monotonicNowNs();
    var managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator, indexes_json, .{
        .io = io,
        .bounded_http_request = true,
        .deadline_ns = expired_deadline,
    });
    defer managed.deinit();
    try std.testing.expectEqual(expired_deadline, managed.entries[0].deadline_ns.?);
    try std.testing.expectError(error.Timeout, embeddingHttpClientConfig(&managed.entries[0]));
    const render_config = queryTemplateRenderConfig(&managed.entries[0]);
    if (comptime @hasField(template_remote.RenderConfig, "io")) {
        try std.testing.expect(render_config.io != null);
    }
    if (comptime @hasField(template_remote.RenderConfig, "deadline_ns")) {
        try std.testing.expectEqual(expired_deadline, render_config.deadline_ns.?);
    }
    try std.testing.expectError(
        error.Timeout,
        renderQueryTemplateWithEntry(std.testing.allocator, "{{this}}", "query", &managed.entries[0]),
    );

    managed.entries[0].deadline_ns = monotonicNowNs() + 5 * std.time.ns_per_s;
    const config = try embeddingHttpClientConfig(&managed.entries[0]);
    try std.testing.expectEqual(@as(usize, 4 << 20), config.max_response_size);
    try std.testing.expect(!config.cookies_enabled);
    try std.testing.expect(config.timeouts.request_ms > 0);
    try std.testing.expect(config.timeouts.request_ms <= 5_000);

    var manual = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer manual.deinit();
    try std.testing.expect(manual.owned_http_io != null);
    try std.testing.expect(manual.entries[0].io != null);
    try std.testing.expect(manual.entries[0].bounded_http_request);
    try std.testing.expect(manual.owned_http_client != null);
    try std.testing.expect(manual.owned_http_io.?.allocator.vtable == std.heap.smp_allocator.vtable);
    try std.testing.expect(manual.owned_http_client.?.allocator.vtable == std.heap.smp_allocator.vtable);
    try std.testing.expect(!manual.owned_http_client.?.config.cookies_enabled);
    const manual_config = try embeddingHttpClientConfig(&manual.entries[0]);
    try std.testing.expect(manual_config.timeouts.request_ms > 0);
    try std.testing.expect(manual_config.timeouts.connect_ms > 0);
    try std.testing.expect(manual_config.timeouts.read_ms > 0);
    try std.testing.expect(manual_config.timeouts.write_ms > 0);
    try std.testing.expect(manual.denseInterface().foreground_bounded);

    // The default constructor must remain safe when its owner uses a local,
    // non-thread-safe allocator. The owner allocator owns the stable Threaded
    // shell, while the runtime's concurrent bookkeeping uses smp_allocator.
    var owner_arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer owner_arena.deinit();
    var arena_owned = try ManagedEmbedder.initFromIndexesJson(owner_arena.allocator(), indexes_json);
    defer arena_owned.deinit();
    const threaded = arena_owned.owned_http_io orelse return error.TestUnexpectedResult;
    try std.testing.expect(threaded.allocator.vtable == std.heap.smp_allocator.vtable);

    const Worker = struct {
        fn run(completed: *std.atomic.Value(usize)) std.Io.Cancelable!void {
            _ = completed.fetchAdd(1, .acq_rel);
        }
    };
    const worker_count = 16;
    var completed = std.atomic.Value(usize).init(0);
    var group: std.Io.Group = .init;
    for (0..worker_count) |_| group.async(threaded.io(), Worker.run, .{&completed});
    try group.await(threaded.io());
    try std.testing.expectEqual(@as(usize, worker_count), completed.load(.acquire));

    const Local = struct {
        context_calls: usize = 0,

        fn dense(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) ![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn denseWithContext(ptr: *anyopaque, alloc: std.mem.Allocator, _: []const u8, texts: []const []const u8, context: EmbeddingRequestContext) ![][]f32 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            try context.check();
            try std.testing.expect(context.request.deadline_ns != null);
            self.context_calls += 1;
            const vectors = try alloc.alloc([]f32, texts.len);
            errdefer alloc.free(vectors);
            var initialized: usize = 0;
            errdefer for (vectors[0..initialized]) |vector| alloc.free(vector);
            for (vectors) |*vector| {
                vector.* = try alloc.dupe(f32, &.{ 1, 2, 3 });
                initialized += 1;
            }
            return vectors;
        }

        fn sparse(_: *anyopaque, alloc: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return try alloc.alloc(db_embedder.SparseEmbedding, 0);
        }
    };
    var local = Local{};
    var deadline_aware = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/test"}}}
    , .{
        .antfly_provider = .{
            .ptr = &local,
            .embed_dense_texts = Local.dense,
            .embed_dense_texts_with_context = Local.denseWithContext,
            .embed_sparse_texts = Local.sparse,
        },
        .deadline_ns = monotonicNowNs() + std.time.ns_per_s,
    });
    defer deadline_aware.deinit();
    const local_vector = try deadline_aware.embedQuery(std.testing.allocator, "semantic_idx", "deadline aware");
    defer std.testing.allocator.free(local_vector);
    try std.testing.expectEqual(@as(usize, 1), local.context_calls);
}

pub fn testEmbeddingProviderResultValidation() !void {
    const valid_vector = [_]f32{ 0.25, -0.5 };
    const valid_batch = [_][]const f32{&valid_vector};
    try validateDenseBatch(&valid_batch, 1, 2);
    try std.testing.expectError(error.InvalidEmbeddingResponse, validateDenseBatch(&valid_batch, 2, 2));
    try std.testing.expectError(error.InvalidEmbeddingDimensions, validateDenseBatch(&valid_batch, 1, 3));

    const invalid_vector = [_]f32{ std.math.nan(f32), std.math.inf(f32) };
    try std.testing.expectError(error.InvalidEmbeddingResponse, validateDenseVector(&invalid_vector, 2));

    var sparse_indices = [_]u32{ 1, 3 };
    var sparse_values = [_]f32{ 0.5, std.math.inf(f32) };
    const sparse_batch = [_]db_embedder.SparseEmbedding{.{
        .indices = &sparse_indices,
        .values = &sparse_values,
    }};
    try std.testing.expectError(error.InvalidEmbeddingResponse, validateSparseBatch(&sparse_batch, 1));
    try std.testing.expectError(error.InvalidEmbeddingResponse, validateSparseBatch(&sparse_batch, 2));
    sparse_values[1] = 0.25;
    sparse_indices[1] = 1;
    try std.testing.expectError(error.InvalidEmbeddingResponse, validateSparseBatch(&sparse_batch, 1));
}

pub fn translateEmbeddingsIndexConfigJson(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
) ![]u8 {
    return try translateEmbeddingsIndexConfigJsonWithOptions(alloc, index_name, value, .{});
}

fn validateEmbeddingIndexSources(sources: []const indexes_openapi.ArtifactIndexSource) !void {
    if (sources.len > max_embedding_index_sources) return error.InvalidCreateTableRequest;
    for (sources, 0..) |source, i| {
        if (source.artifact.len == 0) return error.InvalidCreateTableRequest;
        for (sources[0..i]) |previous| {
            if (std.mem.eql(u8, previous.artifact, source.artifact)) return error.InvalidCreateTableRequest;
        }
    }
}

fn appendArtifactIndexSources(
    alloc: std.mem.Allocator,
    out: *std.ArrayListUnmanaged(u8),
    sources: []const indexes_openapi.ArtifactIndexSource,
) !void {
    try out.appendSlice(alloc, ",\"sources\":[");
    for (sources, 0..) |source, i| {
        if (i > 0) try out.append(alloc, ',');
        try out.appendSlice(alloc, "{\"artifact\":");
        try appendJsonString(alloc, out, source.artifact);
        try out.append(alloc, '}');
    }
    try out.append(alloc, ']');
}

pub fn embeddingSemanticProducerJsonAllocWithOptions(
    alloc: std.mem.Allocator,
    value: std.json.Value,
    options: InitOptions,
) ![]u8 {
    var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, value);
    defer parsed_cfg.deinit();
    const cfg = parsed_cfg.value;
    const embedder_value = switch (value) {
        .object => |object| object.get("embedder") orelse return error.InvalidCreateTableRequest,
        else => return error.InvalidCreateTableRequest,
    };
    var embedder_cfg = parseEmbedderConfigFromValue(alloc, embedder_value) catch |err| switch (err) {
        error.OutOfMemory => return err,
        else => return error.InvalidCreateTableRequest,
    };
    defer embedder_cfg.deinit(alloc);
    const provider = try parseEmbedderProvider(embedder_cfg);
    if (embedder_cfg.model.len == 0 and provider != .antfly) return error.InvalidCreateTableRequest;
    const region = if (provider == .bedrock)
        try resolveBedrockRegion(alloc, embedder_cfg)
    else if (provider == .vertex)
        try resolveVertexLocation(alloc, embedder_cfg)
    else
        try alloc.dupe(u8, "");
    defer alloc.free(region);
    const project_id = if (provider == .vertex)
        if (embedder_cfg.project_id.len > 0)
            try alloc.dupe(u8, embedder_cfg.project_id)
        else
            (try vertex_provider.vertexProjectIdFromConfigAlloc(
                alloc,
                if (embedder_cfg.credentials_path.len > 0) embedder_cfg.credentials_path else null,
            )) orelse return error.InvalidCreateTableRequest
    else
        try alloc.dupe(u8, "");
    defer alloc.free(project_id);
    const endpoint = switch (provider) {
        .openai => try resolveOpenAiBaseUrl(alloc, embedder_cfg),
        .ollama => try resolveOllamaBaseUrl(alloc, embedder_cfg),
        .bedrock => try resolveBedrockEndpoint(alloc, embedder_cfg, region),
        .cohere => try resolveCohereBaseUrl(alloc, embedder_cfg),
        .gemini => try resolveGeminiBaseUrl(alloc, embedder_cfg),
        .vertex => try resolveVertexBaseUrl(alloc, embedder_cfg, region),
        .antfly => if (shouldUseAntflyProvider(embedder_cfg, options))
            try alloc.dupe(u8, "antfly:embedded")
        else
            try resolveAntflyInferenceBaseUrl(alloc, embedder_cfg, options),
    };
    defer alloc.free(endpoint);
    if (credential_safety.containsSecretReference(endpoint) or credential_safety.urlContainsCredentials(endpoint))
        return error.InvalidCreateTableRequest;
    const SemanticProducer = struct {
        version: u8 = 2,
        provider: []const u8,
        model: []const u8,
        endpoint: []const u8,
        region: []const u8,
        project_id: ?[]const u8 = null,
        request_format: []const u8,
        sparse: bool,
        multimodal: bool,
        input_type: []const u8,
        truncate: []const u8,
        query_input_type: ?[]const u8 = null,
        document_input_type: ?[]const u8 = null,
        query_instruction: ?[]const u8 = null,
    };
    return try std.json.Stringify.valueAlloc(alloc, SemanticProducer{
        .provider = @tagName(provider),
        .model = embedder_cfg.model,
        .endpoint = endpoint,
        .region = region,
        .project_id = if (project_id.len > 0) project_id else null,
        .request_format = embedder_cfg.request_format,
        .sparse = cfg.sparse orelse false,
        .multimodal = embedder_cfg.multimodal,
        .input_type = embedder_cfg.input_type,
        .truncate = embedder_cfg.truncate,
        .query_input_type = if (embedder_cfg.query_input_type.len > 0) embedder_cfg.query_input_type else null,
        .document_input_type = if (embedder_cfg.document_input_type.len > 0) embedder_cfg.document_input_type else null,
        .query_instruction = if (embedder_cfg.query_instruction.len > 0) embedder_cfg.query_instruction else null,
    }, .{ .emit_null_optional_fields = false });
}

/// Returns the durable, credential-free identity of the producer configured
/// for an embeddings index. Execution policy and declared dimensions are not
/// semantic producer properties and are validated independently.
pub fn embeddingSemanticProducerJsonAlloc(
    alloc: std.mem.Allocator,
    value: std.json.Value,
) ![]u8 {
    return try embeddingSemanticProducerJsonAllocWithOptions(alloc, value, .{});
}

/// Returns the admitted catalog identity when present, otherwise resolves the
/// effective identity for a new owner. Runtime translation and enrichment
/// collection must use this form so a storage node never reinterprets an
/// implicit endpoint using its own process environment.
pub fn embeddingCatalogSemanticProducerJsonAllocWithOptions(
    alloc: std.mem.Allocator,
    value: std.json.Value,
    options: InitOptions,
) ![]u8 {
    const root = switch (value) {
        .object => |object| object,
        else => return error.InvalidEmbeddingArtifactProducer,
    };
    const existing = root.get("semantic_producer") orelse
        return try embeddingSemanticProducerJsonAllocWithOptions(alloc, value, options);
    if (existing != .string or existing.string.len == 0)
        return error.InvalidEmbeddingArtifactProducer;

    var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, value);
    defer parsed_cfg.deinit();
    const sparse = parsed_cfg.value.sparse orelse false;
    try validateCatalogOwnerSemanticIdentity(alloc, .{
        .sparse = sparse,
        .dimensions = null,
        .semantic_producer_json = existing.string,
        .index_value = value,
    });
    return try alloc.dupe(u8, existing.string);
}

fn normalizeEmbeddingCatalogSemanticProducerJsonWithOptions(
    alloc: std.mem.Allocator,
    value: std.json.Value,
    options: InitOptions,
) !?[]u8 {
    const root = switch (value) {
        .object => |object| object,
        else => return null,
    };
    const type_value = root.get("type") orelse return null;
    if (type_value != .string or !std.mem.eql(u8, type_value.string, "embeddings")) return null;
    var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, value);
    defer parsed_cfg.deinit();
    if ((parsed_cfg.value.external orelse false) or root.get("embedder") == null) return null;

    // Once admitted, this is the stable credential-free identity used by
    // context-free metadata validation. Never regenerate an existing identity
    // from a different process's environment or deployment mode.
    if (root.get("semantic_producer") != null) {
        const existing = try embeddingCatalogSemanticProducerJsonAllocWithOptions(alloc, value, options);
        alloc.free(existing);
        return null;
    }

    const semantic_producer = try embeddingCatalogSemanticProducerJsonAllocWithOptions(alloc, value, options);
    defer alloc.free(semantic_producer);
    var out = std.ArrayListUnmanaged(u8).empty;
    defer out.deinit(alloc);
    try out.append(alloc, '{');
    var first = true;
    var it = root.iterator();
    while (it.next()) |entry| {
        if (!first) try out.append(alloc, ',');
        first = false;
        try appendJsonString(alloc, &out, entry.key_ptr.*);
        try out.append(alloc, ':');
        const encoded = try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(entry.value_ptr.*, .{})});
        defer alloc.free(encoded);
        try out.appendSlice(alloc, encoded);
    }
    if (!first) try out.append(alloc, ',');
    try out.appendSlice(alloc, "\"semantic_producer\":");
    try appendJsonString(alloc, &out, semantic_producer);
    try out.append(alloc, '}');
    return try out.toOwnedSlice(alloc);
}

pub fn translateEmbeddingsIndexConfigJsonWithOptions(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
    options: InitOptions,
) ![]u8 {
    var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, value);
    defer parsed_cfg.deinit();
    const cfg = parsed_cfg.value;

    const root = switch (value) {
        .object => |object| object,
        else => return error.InvalidCreateTableRequest,
    };

    const sparse = cfg.sparse orelse false;
    const external = cfg.external orelse false;
    const publication_policy = cfg.publication_policy orelse .progressive;
    if (external and cfg.coverage_policy != null) return error.InvalidCreateTableRequest;
    if (external and cfg.publication_policy != null) return error.InvalidCreateTableRequest;
    const semantic_producer_json = if (!external and root.get("embedder") != null)
        try embeddingCatalogSemanticProducerJsonAllocWithOptions(alloc, value, options)
    else
        null;
    defer if (semantic_producer_json) |raw| alloc.free(raw);

    if (root.get("summarizer") != null) return error.UnsupportedCreateTableRequest;

    const field_name = cfg.field;
    const template_value = cfg.template;
    const artifact_sources = cfg.sources orelse &.{};
    try validateEmbeddingIndexSources(artifact_sources);
    if (artifact_sources.len > 0 and
        (external or field_name != null or template_value != null or root.get("chunker") != null or
            root.get("embedding_name") != null or root.get("source_artifact_name") != null))
    {
        return error.InvalidCreateTableRequest;
    }

    const artifact_embedding_name = if (root.get("embedding_name")) |json_value| blk: {
        if (json_value != .string or json_value.string.len == 0) return error.InvalidCreateTableRequest;
        break :blk json_value.string;
    } else null;
    const artifact_source_name = if (root.get("source_artifact_name")) |json_value| blk: {
        if (json_value != .string or json_value.string.len == 0) return error.InvalidCreateTableRequest;
        break :blk json_value.string;
    } else null;
    if (artifact_embedding_name != null and external) return error.InvalidCreateTableRequest;
    if (artifact_source_name != null and artifact_embedding_name == null) return error.InvalidCreateTableRequest;
    if (artifact_embedding_name != null and (template_value != null or root.get("chunker") != null)) {
        return error.InvalidCreateTableRequest;
    }
    // Artifact-backed indexes consume vectors produced by the authoritative
    // enrichment; that enrichment, rather than the index, owns execution.
    const artifact_backed = artifact_sources.len > 0 or artifact_embedding_name != null;

    if (external) {
        if (field_name != null or template_value != null or root.get("embedder") != null) {
            return error.UnsupportedCreateTableRequest;
        }
    } else if (field_name == null and template_value == null and !artifact_backed) {
        return error.InvalidCreateTableRequest;
    }

    const source_field = if (artifact_sources.len > 0)
        "embedding"
    else if (field_name) |field|
        field
    else if (template_value != null)
        "body"
    else
        "embedding";

    const chunker_json = if (root.get("chunker")) |chunker_value| blk: {
        var chunker_cfg = try chunking_types.parseConfigFromValue(alloc, chunker_value);
        defer chunker_cfg.deinit(alloc);
        break :blk try chunking_types.stringifyAlloc(alloc, chunker_cfg);
    } else null;
    defer if (chunker_json) |raw| alloc.free(raw);

    if (sparse) {
        if (external) {
            var out = std.ArrayListUnmanaged(u8).empty;
            defer out.deinit(alloc);
            try out.appendSlice(alloc, "{\"field\":");
            try appendJsonString(alloc, &out, source_field);
            try appendCoveragePolicyIfPresent(alloc, &out, cfg.coverage_policy);
            try appendExecutionObjectIfPresent(alloc, &out, root);
            try out.append(alloc, '}');
            return try out.toOwnedSlice(alloc);
        }

        const embedder_value = root.get("embedder");
        const embedder_json = if (embedder_value) |embedder| blk: {
            var embedder_cfg = try parseEmbedderConfigFromValue(alloc, embedder);
            defer embedder_cfg.deinit(alloc);
            if (embedder_cfg.model.len == 0) return error.InvalidCreateTableRequest;
            _ = parseEmbedderProvider(embedder_cfg) catch return error.UnsupportedCreateTableRequest;
            break :blk try stringifyManagedEmbedderConfigAlloc(alloc, embedder_cfg, embedder, options.inference_api_key);
        } else null;
        defer if (embedder_json) |raw| alloc.free(raw);
        if (embedder_json == null and !artifact_backed) return error.InvalidCreateTableRequest;

        var out = std.ArrayListUnmanaged(u8).empty;
        defer out.deinit(alloc);

        try out.appendSlice(alloc, "{\"field\":");
        try appendJsonString(alloc, &out, source_field);
        try appendPublicationPolicy(alloc, &out, publication_policy);
        try appendCoveragePolicyIfPresent(alloc, &out, cfg.coverage_policy);
        if (cfg.top_k) |top_k| {
            try out.appendSlice(alloc, ",\"top_k\":");
            const top_k_json = try std.fmt.allocPrint(alloc, "{d}", .{top_k});
            defer alloc.free(top_k_json);
            try out.appendSlice(alloc, top_k_json);
        }
        if (cfg.min_weight) |min_weight| {
            try out.appendSlice(alloc, ",\"min_weight\":");
            const min_weight_json = try std.fmt.allocPrint(alloc, "{d}", .{min_weight});
            defer alloc.free(min_weight_json);
            try out.appendSlice(alloc, min_weight_json);
        }
        if (cfg.chunk_size) |chunk_size| {
            try out.appendSlice(alloc, ",\"chunk_size\":");
            const chunk_size_json = try std.fmt.allocPrint(alloc, "{d}", .{chunk_size});
            defer alloc.free(chunk_size_json);
            try out.appendSlice(alloc, chunk_size_json);
        }
        if (artifact_sources.len > 0) {
            try appendArtifactIndexSources(alloc, &out, artifact_sources);
        } else if (artifact_embedding_name) |embedding_name| {
            try out.appendSlice(alloc, ",\"embedding_name\":");
            try appendJsonString(alloc, &out, embedding_name);
        } else {
            try out.appendSlice(alloc, ",\"generator\":{\"kind\":\"sparse_embedding\",\"source_field\":");
            try appendJsonString(alloc, &out, source_field);
            if (template_value) |source_template| {
                try out.appendSlice(alloc, ",\"source_template\":");
                try appendJsonString(alloc, &out, source_template);
            }
            try out.appendSlice(alloc, ",\"artifact_name\":");
            const artifact_name = try std.fmt.allocPrint(alloc, "{s}_chunks", .{index_name});
            defer alloc.free(artifact_name);
            try appendJsonString(alloc, &out, artifact_name);
            try out.appendSlice(alloc, ",\"embedding_name\":");
            try appendJsonString(alloc, &out, index_name);
            if (chunker_json) |chunker| {
                try out.appendSlice(alloc, ",\"chunker\":");
                try out.appendSlice(alloc, chunker);
            }
            try out.append(alloc, '}');
        }
        if (embedder_json) |embedder| {
            try out.appendSlice(alloc, ",\"embedder\":");
            try out.appendSlice(alloc, embedder);
        }
        if (semantic_producer_json) |producer| {
            try out.appendSlice(alloc, ",\"semantic_producer\":");
            try appendJsonString(alloc, &out, producer);
        }
        try appendExecutionObjectIfPresent(alloc, &out, root);
        try out.append(alloc, '}');
        return try out.toOwnedSlice(alloc);
    }

    const metric = if (cfg.distance_metric) |distance_metric| @tagName(distance_metric) else @tagName(shared_vector.default_distance_metric);

    const embedder_value = root.get("embedder");
    const embedder_json = if (embedder_value) |embedder| blk: {
        var embedder_cfg = try parseEmbedderConfigFromValue(alloc, embedder);
        defer embedder_cfg.deinit(alloc);
        _ = try parseEmbedderProvider(embedder_cfg);
        if (embedder_cfg.model.len == 0) return error.InvalidCreateTableRequest;
        break :blk try stringifyManagedEmbedderConfigAlloc(alloc, embedder_cfg, embedder, options.inference_api_key);
    } else null;
    defer if (embedder_json) |raw| alloc.free(raw);
    if (!external and embedder_json == null and chunker_json == null and !artifact_backed) return error.InvalidCreateTableRequest;

    const dims = if (embedder_value) |embedder|
        try resolveEmbeddingDimensionsForManagedConfig(alloc, index_name, cfg, embedder, options)
    else
        try resolveDeclaredEmbeddingDimensionsRequired(cfg);
    var out = std.ArrayListUnmanaged(u8).empty;
    defer out.deinit(alloc);

    try out.appendSlice(alloc, "{\"field\":");
    try appendJsonString(alloc, &out, source_field);
    try out.appendSlice(alloc, ",\"dims\":");
    const dims_json = try std.fmt.allocPrint(alloc, "{d}", .{dims});
    defer alloc.free(dims_json);
    try out.appendSlice(alloc, dims_json);
    try out.appendSlice(alloc, ",\"metric\":");
    try appendJsonString(alloc, &out, metric);
    if (artifact_sources.len > 0) {
        try appendArtifactIndexSources(alloc, &out, artifact_sources);
    } else {
        try out.appendSlice(alloc, ",\"embedding_name\":");
        try appendJsonString(alloc, &out, artifact_embedding_name orelse index_name);
    }
    if (!external) try appendPublicationPolicy(alloc, &out, publication_policy);
    try appendCoveragePolicyIfPresent(alloc, &out, cfg.coverage_policy);

    if (artifact_sources.len > 0 or artifact_embedding_name != null) {
        // Explicit artifact outputs are generated by their matching enrichment definitions.
    } else if (!external) {
        try out.appendSlice(alloc, ",\"generator\":{\"kind\":\"dense_embedding\",\"source_field\":");
        try appendJsonString(alloc, &out, source_field);
        if (template_value) |source_template| {
            try out.appendSlice(alloc, ",\"source_template\":");
            try appendJsonString(alloc, &out, source_template);
        }
        try out.appendSlice(alloc, ",\"artifact_name\":");
        const artifact_name = try std.fmt.allocPrint(alloc, "{s}_chunks", .{index_name});
        defer alloc.free(artifact_name);
        try appendJsonString(alloc, &out, artifact_name);
        try out.appendSlice(alloc, ",\"embedding_name\":");
        try appendJsonString(alloc, &out, index_name);
        if (chunker_json) |chunker| {
            try out.appendSlice(alloc, ",\"chunker\":");
            try out.appendSlice(alloc, chunker);
        }
        try out.append(alloc, '}');
    } else {
        try out.appendSlice(alloc, ",\"external\":true");
    }

    if (embedder_json) |embedder| {
        try out.appendSlice(alloc, ",\"embedder\":");
        try out.appendSlice(alloc, embedder);
    }
    if (semantic_producer_json) |producer| {
        try out.appendSlice(alloc, ",\"semantic_producer\":");
        try appendJsonString(alloc, &out, producer);
    }

    try appendExecutionObjectIfPresent(alloc, &out, root);
    try out.append(alloc, '}');
    return try out.toOwnedSlice(alloc);
}

fn normalizeAntflyChunkerDefaultModelJson(
    alloc: std.mem.Allocator,
    value: std.json.Value,
) !?[]u8 {
    const root = switch (value) {
        .object => |object| object,
        else => return null,
    };
    const type_value = root.get("type") orelse return null;
    if (type_value != .string or !std.mem.eql(u8, type_value.string, "embeddings")) return null;

    const chunker = switch (root.get("chunker") orelse return null) {
        .object => |object| object,
        else => return null,
    };
    const provider = chunker.get("provider") orelse return null;
    if (provider != .string or !std.mem.eql(u8, provider.string, "antfly")) return null;
    // Preserve explicit values, including null, so validation can reject them
    // instead of silently changing caller intent.
    if (chunker.get("model") != null) return null;

    var out = std.ArrayListUnmanaged(u8).empty;
    defer out.deinit(alloc);
    try out.append(alloc, '{');
    var first_root_field = true;
    var root_it = root.iterator();
    while (root_it.next()) |entry| {
        if (!first_root_field) try out.append(alloc, ',');
        first_root_field = false;
        try appendJsonString(alloc, &out, entry.key_ptr.*);
        try out.append(alloc, ':');
        if (!std.mem.eql(u8, entry.key_ptr.*, "chunker")) {
            const encoded = try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(entry.value_ptr.*, .{})});
            defer alloc.free(encoded);
            try out.appendSlice(alloc, encoded);
            continue;
        }

        try out.append(alloc, '{');
        var first_chunker_field = true;
        var chunker_it = chunker.iterator();
        while (chunker_it.next()) |chunker_entry| {
            if (!first_chunker_field) try out.append(alloc, ',');
            first_chunker_field = false;
            try appendJsonString(alloc, &out, chunker_entry.key_ptr.*);
            try out.append(alloc, ':');
            const encoded = try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(chunker_entry.value_ptr.*, .{})});
            defer alloc.free(encoded);
            try out.appendSlice(alloc, encoded);
        }
        if (!first_chunker_field) try out.append(alloc, ',');
        try out.appendSlice(alloc, "\"model\":\"fixed\"}");
    }
    try out.append(alloc, '}');
    return try out.toOwnedSlice(alloc);
}

fn normalizeEmbeddingsIndexDimensionOnlyJsonWithOptions(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
    catalog_root: std.json.Value,
    options: InitOptions,
    owner_already_admitted: bool,
) !?[]u8 {
    const root = switch (value) {
        .object => |object| object,
        else => return null,
    };
    const type_value = root.get("type") orelse return null;
    if (type_value != .string or !std.mem.eql(u8, type_value.string, "embeddings")) return null;

    var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, value);
    defer parsed_cfg.deinit();
    const cfg = parsed_cfg.value;
    const sparse = cfg.sparse orelse false;
    const validation_value = root.get("validation");
    const validation = try parseDimensionProbeValidation(root);
    const declared_dims = try resolveDeclaredEmbeddingDimensions(cfg);

    // Revalidating an already-admitted catalog must not turn an unrelated
    // mutation into a provider health check. Dense owners with a durable
    // dimension and sparse owners have no missing shape to discover; the
    // semantic-producer pass below can stamp or validate their identity
    // without invoking the provider. New public owners still take the strict
    // path before they are merged into an admitted catalog.
    if (validation_value == null and root.get("embedder") != null and
        (root.get("semantic_producer") != null or owner_already_admitted) and
        (sparse or declared_dims != null))
    {
        return null;
    }

    const external = cfg.external orelse false;
    const embedder_value = root.get("embedder");
    if (external) {
        if (validation_value != null) return error.InvalidCreateTableRequest;
        if (!sparse) _ = try resolveDeclaredEmbeddingDimensionsRequired(cfg);
        return null;
    }
    if (sparse) {
        if (validation_value != null) return error.InvalidCreateTableRequest;
        const has_artifact_sources = if (cfg.sources) |sources|
            sources.len > 0
        else
            false;
        const artifact_backed = has_artifact_sources or cfg.embedding_name != null;
        const embedder = embedder_value orelse {
            if (artifact_backed) return null;
            return error.InvalidCreateTableRequest;
        };
        try validateSparseEmbeddingForManagedConfig(alloc, index_name, cfg, embedder, options);
        return null;
    }

    if (validation == .defer_probe and declared_dims == null) return error.InvalidCreateTableRequest;
    // Chunker-only dense indexes consume caller-supplied chunk embeddings and
    // have no embedding provider to probe. Their declared dimension remains
    // authoritative; the subsequent config translation validates that a
    // chunker is actually present.
    const dims = if (embedder_value) |embedder|
        try resolveEmbeddingDimensionsForManagedConfigWithValidation(alloc, index_name, cfg, embedder, options, validation)
    else blk: {
        if (validation_value != null) return error.InvalidCreateTableRequest;
        if (declared_dims) |_| return null;
        break :blk try resolveArtifactBackedEmbeddingDimensions(value, catalog_root, cfg);
    };
    if (cfg.dimension != null and validation_value == null) return null;

    var out = std.ArrayListUnmanaged(u8).empty;
    defer out.deinit(alloc);
    try out.append(alloc, '{');
    var first = true;
    var it = root.iterator();
    while (it.next()) |entry| {
        if (std.mem.eql(u8, entry.key_ptr.*, "dimension")) continue;
        if (std.mem.eql(u8, entry.key_ptr.*, "validation")) continue;
        if (!first) try out.append(alloc, ',');
        first = false;
        try appendJsonString(alloc, &out, entry.key_ptr.*);
        try out.append(alloc, ':');
        const encoded = try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(entry.value_ptr.*, .{})});
        defer alloc.free(encoded);
        try out.appendSlice(alloc, encoded);
    }
    if (!first) try out.append(alloc, ',');
    try out.appendSlice(alloc, "\"dimension\":");
    const dims_json = try std.fmt.allocPrint(alloc, "{d}", .{dims});
    defer alloc.free(dims_json);
    try out.appendSlice(alloc, dims_json);
    try out.append(alloc, '}');
    return try out.toOwnedSlice(alloc);
}

pub fn normalizeEmbeddingsIndexDimensionJsonWithOptions(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
    options: InitOptions,
) !?[]u8 {
    return try normalizeEmbeddingsIndexDimensionJsonForCatalogWithOptions(alloc, index_name, value, value, options);
}

pub fn normalizeEmbeddingsIndexDimensionJsonForCatalogWithOptions(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
    catalog_root: std.json.Value,
    options: InitOptions,
) !?[]u8 {
    return try normalizeEmbeddingsIndexDimensionJsonForCatalogInternal(
        alloc,
        index_name,
        value,
        catalog_root,
        options,
        false,
    );
}

/// Normalize an owner that has already crossed public admission. This mode
/// migrates durable producer identity and resolves artifact-derived dimensions
/// without re-probing providers whose dense/sparse shape is already durable.
pub fn normalizeAdmittedEmbeddingsIndexDimensionJsonForCatalogWithOptions(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
    catalog_root: std.json.Value,
    options: InitOptions,
) !?[]u8 {
    return try normalizeEmbeddingsIndexDimensionJsonForCatalogInternal(
        alloc,
        index_name,
        value,
        catalog_root,
        options,
        true,
    );
}

fn normalizeEmbeddingsIndexDimensionJsonForCatalogInternal(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
    catalog_root: std.json.Value,
    options: InitOptions,
    owner_already_admitted: bool,
) !?[]u8 {
    if (try normalizeEmbeddingsIndexDimensionOnlyJsonWithOptions(
        alloc,
        index_name,
        value,
        catalog_root,
        options,
        owner_already_admitted,
    )) |normalized_dimension| {
        errdefer alloc.free(normalized_dimension);
        var parsed = try std.json.parseFromSlice(std.json.Value, alloc, normalized_dimension, .{});
        defer parsed.deinit();
        if (try normalizeAntflyChunkerDefaultModelJson(alloc, parsed.value)) |normalized_defaults| {
            alloc.free(normalized_dimension);
            errdefer alloc.free(normalized_defaults);
            var defaults_parsed = try std.json.parseFromSlice(std.json.Value, alloc, normalized_defaults, .{});
            defer defaults_parsed.deinit();
            if (try normalizeEmbeddingCatalogSemanticProducerJsonWithOptions(alloc, defaults_parsed.value, options)) |normalized_semantic| {
                alloc.free(normalized_defaults);
                return normalized_semantic;
            }
            return normalized_defaults;
        }
        if (try normalizeEmbeddingCatalogSemanticProducerJsonWithOptions(alloc, parsed.value, options)) |normalized_semantic| {
            alloc.free(normalized_dimension);
            return normalized_semantic;
        }
        return normalized_dimension;
    }
    if (try normalizeAntflyChunkerDefaultModelJson(alloc, value)) |normalized_defaults| {
        errdefer alloc.free(normalized_defaults);
        var parsed = try std.json.parseFromSlice(std.json.Value, alloc, normalized_defaults, .{});
        defer parsed.deinit();
        if (try normalizeEmbeddingCatalogSemanticProducerJsonWithOptions(alloc, parsed.value, options)) |normalized_semantic| {
            alloc.free(normalized_defaults);
            return normalized_semantic;
        }
        return normalized_defaults;
    }
    return try normalizeEmbeddingCatalogSemanticProducerJsonWithOptions(alloc, value, options);
}

fn managedEntryProvidesLookup(entry: *const ManagedEmbeddingEntry, name: []const u8) bool {
    if (std.mem.eql(u8, entry.index_name, name)) return true;
    if (entry.embedding_name.len > 0 and std.mem.eql(u8, entry.embedding_name, name)) return true;
    for (entry.embedding_names) |embedding_name| {
        if (std.mem.eql(u8, embedding_name, name)) return true;
    }
    for (entry.lookup_aliases) |alias| {
        if (std.mem.eql(u8, alias, name)) return true;
    }
    return false;
}

fn managedEntryIndexForArtifact(entries: []const ManagedEmbeddingEntry, name: []const u8) ?usize {
    for (entries, 0..) |*entry, i| {
        if (entry.embedding_name.len > 0 and std.mem.eql(u8, entry.embedding_name, name)) return i;
        for (entry.embedding_names) |embedding_name| {
            if (std.mem.eql(u8, embedding_name, name)) return i;
        }
    }
    return null;
}

fn appendManagedEntryLookupAlias(
    alloc: std.mem.Allocator,
    entry: *ManagedEmbeddingEntry,
    alias: []const u8,
) !void {
    if (managedEntryProvidesLookup(entry, alias)) return;
    const owned = try alloc.dupe(u8, alias);
    errdefer alloc.free(owned);
    if (entry.lookup_aliases.len == 0) {
        const aliases = try alloc.alloc([]u8, 1);
        aliases[0] = owned;
        entry.lookup_aliases = aliases;
        return;
    }
    entry.lookup_aliases = try alloc.realloc(entry.lookup_aliases, entry.lookup_aliases.len + 1);
    entry.lookup_aliases[entry.lookup_aliases.len - 1] = owned;
}

fn findEmbeddingEnrichmentValue(
    value: std.json.Value,
    artifact_name: []const u8,
    found: *?std.json.Value,
) !void {
    switch (value) {
        .object => |object| {
            if (object.get("enrichments")) |enrichments| {
                if (enrichments != .array) return error.InvalidManagedEmbeddingIndex;
                for (enrichments.array.items) |enrichment| {
                    if (enrichment != .object) return error.InvalidManagedEmbeddingIndex;
                    const kind = enrichment.object.get("kind") orelse continue;
                    if (kind != .string or !std.mem.eql(u8, kind.string, "embedding")) continue;
                    const name = enrichment.object.get("name") orelse return error.InvalidManagedEmbeddingIndex;
                    if (name != .string or name.string.len == 0) return error.InvalidManagedEmbeddingIndex;
                    if (std.mem.eql(u8, name.string, artifact_name) and found.* == null) found.* = enrichment;
                }
            }
            var it = object.iterator();
            while (it.next()) |entry| {
                if (std.mem.eql(u8, entry.key_ptr.*, "enrichments")) continue;
                try findEmbeddingEnrichmentValue(entry.value_ptr.*, artifact_name, found);
            }
        },
        .array => |array| for (array.items) |item| try findEmbeddingEnrichmentValue(item, artifact_name, found),
        else => {},
    }
}

fn embeddingEnrichmentExpectedDimensionsOptional(value: std.json.Value) !?u32 {
    const object = switch (value) {
        .object => |object| object,
        else => return error.InvalidManagedEmbeddingIndex,
    };
    const expected_dims = object.get("expected_dims") orelse return null;
    const raw = switch (expected_dims) {
        .integer => |integer| integer,
        else => return error.EmbeddingArtifactDimensionRequired,
    };
    if (raw <= 0 or raw > std.math.maxInt(u32))
        return error.EmbeddingArtifactDimensionRequired;
    return @intCast(raw);
}

fn embeddingEnrichmentExpectedDimensions(value: std.json.Value) !u32 {
    return (try embeddingEnrichmentExpectedDimensionsOptional(value)) orelse
        error.EmbeddingArtifactDimensionRequired;
}

/// Resolve an omitted dense index dimension from the authoritative embedding
/// enrichment(s). The index may carry its enrichments inline (table create),
/// or they may already live elsewhere in the table catalog (create-index).
fn resolveArtifactBackedEmbeddingDimensions(
    index_value: std.json.Value,
    catalog_root: std.json.Value,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
) !u32 {
    var resolved: ?u32 = null;
    var source_count: usize = 0;

    if (cfg.embedding_name) |artifact_name| {
        source_count += 1;
        var enrichment: ?std.json.Value = null;
        try findEmbeddingEnrichmentValue(index_value, artifact_name, &enrichment);
        if (enrichment == null) try findEmbeddingEnrichmentValue(catalog_root, artifact_name, &enrichment);
        const dims = try embeddingEnrichmentExpectedDimensions(
            enrichment orelse return error.MissingEmbeddingArtifactEnrichment,
        );
        resolved = dims;
    }

    if (cfg.sources) |sources| {
        for (sources) |source| {
            source_count += 1;
            var enrichment: ?std.json.Value = null;
            try findEmbeddingEnrichmentValue(index_value, source.artifact, &enrichment);
            if (enrichment == null) try findEmbeddingEnrichmentValue(catalog_root, source.artifact, &enrichment);
            const dims = try embeddingEnrichmentExpectedDimensions(
                enrichment orelse return error.MissingEmbeddingArtifactEnrichment,
            );
            if (resolved) |expected| {
                if (expected != dims) return error.ConflictingEmbeddingArtifactDimensions;
            } else {
                resolved = dims;
            }
        }
    }

    if (source_count == 0) return error.InvalidCreateTableRequest;
    return resolved orelse error.EmbeddingArtifactDimensionRequired;
}

fn semanticProducerV2Sparse(value: std.json.Value) !?bool {
    const object = switch (value) {
        .object => |object| object,
        else => return null,
    };
    const version = object.get("version") orelse return null;
    if (version != .integer) return error.InvalidEmbeddingArtifactProducer;
    if (version.integer < 2) return null;
    if (version.integer != 2) return error.InvalidEmbeddingArtifactProducer;
    const sparse = object.get("sparse") orelse return error.InvalidEmbeddingArtifactProducer;
    return switch (sparse) {
        .bool => |enabled| enabled,
        else => error.InvalidEmbeddingArtifactProducer,
    };
}

/// Adapt a v2 semantic identity into the parser's configuration shape solely
/// to construct a temporary comparison entry. The result must never be added
/// to the executable registry because it deliberately contains no credentials.
fn semanticProducerComparisonConfigJsonAlloc(
    alloc: std.mem.Allocator,
    value: std.json.Value,
) !?[]u8 {
    const object = switch (value) {
        .object => |object| object,
        else => return null,
    };
    _ = (try semanticProducerV2Sparse(value)) orelse return null;
    var fields = object.iterator();
    while (fields.next()) |field| {
        const allowed = std.mem.eql(u8, field.key_ptr.*, "version") or
            std.mem.eql(u8, field.key_ptr.*, "provider") or
            std.mem.eql(u8, field.key_ptr.*, "model") or
            std.mem.eql(u8, field.key_ptr.*, "endpoint") or
            std.mem.eql(u8, field.key_ptr.*, "region") or
            std.mem.eql(u8, field.key_ptr.*, "project_id") or
            std.mem.eql(u8, field.key_ptr.*, "request_format") or
            std.mem.eql(u8, field.key_ptr.*, "sparse") or
            std.mem.eql(u8, field.key_ptr.*, "multimodal") or
            std.mem.eql(u8, field.key_ptr.*, "input_type") or
            std.mem.eql(u8, field.key_ptr.*, "truncate") or
            std.mem.eql(u8, field.key_ptr.*, "query_input_type") or
            std.mem.eql(u8, field.key_ptr.*, "document_input_type") or
            std.mem.eql(u8, field.key_ptr.*, "query_instruction");
        if (!allowed) return error.InvalidEmbeddingArtifactProducer;
    }
    if (object.get("url") != null or object.get("api_url") != null or object.get("base_url") != null)
        return error.InvalidEmbeddingArtifactProducer;

    const provider = object.get("provider") orelse return error.InvalidEmbeddingArtifactProducer;
    const model = object.get("model") orelse return error.InvalidEmbeddingArtifactProducer;
    const endpoint = object.get("endpoint") orelse return error.InvalidEmbeddingArtifactProducer;
    if (provider != .string or provider.string.len == 0 or
        model != .string or
        endpoint != .string or endpoint.string.len == 0)
    {
        return error.InvalidEmbeddingArtifactProducer;
    }
    if (credential_safety.containsSecretReference(endpoint.string) or credential_safety.urlContainsCredentials(endpoint.string))
        return error.InvalidEmbeddingArtifactProducer;
    const embedded = std.mem.eql(u8, endpoint.string, "antfly:embedded");
    if (embedded and !std.mem.eql(u8, provider.string, "antfly"))
        return error.InvalidEmbeddingArtifactProducer;

    const SemanticExecutionConfig = struct {
        provider: []const u8,
        model: []const u8,
        url: ?[]const u8,
        region: ?[]const u8 = null,
        project_id: ?[]const u8 = null,
        request_format: ?[]const u8 = null,
        input_type: ?[]const u8 = null,
        truncate: ?[]const u8 = null,
        query_input_type: ?[]const u8 = null,
        document_input_type: ?[]const u8 = null,
        query_instruction: ?[]const u8 = null,
        multimodal: ?bool = null,
    };
    const optionalString = struct {
        fn get(source: std.json.ObjectMap, name: []const u8) !?[]const u8 {
            const field = source.get(name) orelse return null;
            if (field != .string) return error.InvalidEmbeddingArtifactProducer;
            return field.string;
        }
    }.get;
    const multimodal = if (object.get("multimodal")) |field| switch (field) {
        .bool => |enabled| enabled,
        else => return error.InvalidEmbeddingArtifactProducer,
    } else null;
    return try std.json.Stringify.valueAlloc(alloc, SemanticExecutionConfig{
        .provider = provider.string,
        .model = model.string,
        .url = if (embedded) null else endpoint.string,
        .region = try optionalString(object, "region"),
        .project_id = try optionalString(object, "project_id"),
        .request_format = try optionalString(object, "request_format"),
        .input_type = try optionalString(object, "input_type"),
        .truncate = try optionalString(object, "truncate"),
        .query_input_type = try optionalString(object, "query_input_type"),
        .document_input_type = try optionalString(object, "document_input_type"),
        .query_instruction = try optionalString(object, "query_instruction"),
        .multimodal = multimodal,
    }, .{ .emit_null_optional_fields = false });
}

const ArtifactManagedEmbeddingEntry = struct {
    entry: ManagedEmbeddingEntry,
    semantic_identity_only: bool,
};

fn buildArtifactManagedEmbeddingEntryFromProducerValue(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    artifact_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    producer: std.json.Value,
    options: InitOptions,
) !ArtifactManagedEmbeddingEntry {
    var artifact_cfg = cfg;
    artifact_cfg.embedding_name = artifact_name;
    artifact_cfg.sources = null;
    const sparse = artifact_cfg.sparse orelse false;
    if (try semanticProducerV2Sparse(producer)) |producer_sparse| {
        if (producer_sparse != sparse) return error.InvalidEmbeddingArtifactProducer;
    }
    const dims = if (sparse) 0 else try resolveDeclaredEmbeddingDimensionsRequired(artifact_cfg);
    const semantic_comparison_json = try semanticProducerComparisonConfigJsonAlloc(alloc, producer);
    defer if (semantic_comparison_json) |raw| alloc.free(raw);
    var semantic_comparison = if (semantic_comparison_json) |raw|
        try std.json.parseFromSlice(std.json.Value, alloc, raw, .{})
    else
        null;
    defer if (semantic_comparison) |*parsed| parsed.deinit();
    const parser_input = if (semantic_comparison) |parsed| parsed.value else producer;
    const entry = buildManagedEmbeddingEntry(alloc, index_name, artifact_cfg, parser_input, options, dims, null) catch |err| switch (err) {
        error.OutOfMemory => return err,
        else => return error.InvalidEmbeddingArtifactProducer,
    };
    return .{
        .entry = entry,
        .semantic_identity_only = semantic_comparison_json != null,
    };
}

fn buildArtifactManagedEmbeddingEntry(
    alloc: std.mem.Allocator,
    root: std.json.Value,
    index_name: []const u8,
    artifact_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    options: InitOptions,
) !ArtifactManagedEmbeddingEntry {
    var enrichment: ?std.json.Value = null;
    try findEmbeddingEnrichmentValue(root, artifact_name, &enrichment);
    const enrichment_value = enrichment orelse return error.MissingEmbeddingArtifactEnrichment;
    const sparse = cfg.sparse orelse false;
    const expected_dims = try embeddingEnrichmentExpectedDimensionsOptional(enrichment_value);
    if (sparse) {
        if (expected_dims != null) return error.ConflictingEmbeddingArtifactDimensions;
    } else {
        const declared_dims = try resolveDeclaredEmbeddingDimensionsRequired(cfg);
        if ((expected_dims orelse return error.EmbeddingArtifactDimensionRequired) != declared_dims)
            return error.ConflictingEmbeddingArtifactDimensions;
    }
    const producer_json = switch (enrichment_value) {
        .object => |object| object.get("producer_json") orelse return error.MissingEmbeddingArtifactProducer,
        else => unreachable,
    };
    return switch (producer_json) {
        .string => |raw| blk: {
            if (raw.len == 0) return error.MissingEmbeddingArtifactProducer;
            var parsed = std.json.parseFromSlice(std.json.Value, alloc, raw, .{}) catch return error.InvalidEmbeddingArtifactProducer;
            defer parsed.deinit();
            break :blk try buildArtifactManagedEmbeddingEntryFromProducerValue(
                alloc,
                index_name,
                artifact_name,
                cfg,
                parsed.value,
                options,
            );
        },
        .object => try buildArtifactManagedEmbeddingEntryFromProducerValue(
            alloc,
            index_name,
            artifact_name,
            cfg,
            producer_json,
            options,
        ),
        else => error.InvalidEmbeddingArtifactProducer,
    };
}

fn validateEmbeddingEnrichmentProducerValue(
    alloc: std.mem.Allocator,
    enrichment_name: []const u8,
    enrichment_dims: ?u32,
    producer: std.json.Value,
    options: InitOptions,
    executable_entries: ?[]const ManagedEmbeddingEntry,
    sparse_kind: ?bool,
) !void {
    const producer_sparse = try semanticProducerV2Sparse(producer);
    if (producer_sparse) |sparse| {
        // Stage factories own only one executable registry. Do not mistake
        // the other stage's deliberately omitted owner for an orphan.
        // Unfiltered catalog admission still validates every producer.
        if (sparse_kind) |selected| if (sparse != selected) return;
        if (sparse and enrichment_dims != null) return error.ConflictingEmbeddingArtifactDimensions;
        if (!sparse and enrichment_dims == null) return error.EmbeddingArtifactDimensionRequired;
    }

    // Legacy producer documents are executable configurations and do not
    // encode dense/sparse shape. The complete consumer validation supplies the
    // actual shape later; one dimension is sufficient for parse-only checks.
    const sparse = producer_sparse orelse false;
    const cfg: indexes_openapi.EmbeddingsIndexConfig = .{
        .dimension = if (sparse) null else enrichment_dims orelse 1,
        .sparse = sparse,
        .embedding_name = enrichment_name,
    };
    var built = try buildArtifactManagedEmbeddingEntryFromProducerValue(
        alloc,
        enrichment_name,
        enrichment_name,
        cfg,
        producer,
        options,
    );
    defer built.entry.deinit(alloc);

    // Single-enrichment admission can validate syntax and shape, but only the
    // merged table catalog can prove ownership. A non-null (possibly empty)
    // registry means this is the authoritative completeness pass.
    const entries = executable_entries orelse return;
    if (managedEntryIndexForArtifact(entries, enrichment_name)) |owner_index| {
        const equivalent = if (built.semantic_identity_only)
            managedEmbeddingEntriesSemanticallyEquivalent(&entries[owner_index], &built.entry)
        else
            managedEmbeddingEntriesEquivalentForLookup(&entries[owner_index], &built.entry);
        if (!equivalent) return error.InvalidEmbeddingArtifactProducer;
        return;
    }
    if (built.semantic_identity_only) return error.InvalidEmbeddingArtifactProducer;
}

fn validateEmbeddingEnrichmentProducer(
    alloc: std.mem.Allocator,
    enrichment: std.json.Value,
    options: InitOptions,
    executable_entries: ?[]const ManagedEmbeddingEntry,
    sparse_kind: ?bool,
) !void {
    const object = switch (enrichment) {
        .object => |object| object,
        else => return error.InvalidEmbeddingArtifactProducer,
    };
    const kind = object.get("kind") orelse return error.InvalidEmbeddingArtifactProducer;
    if (kind != .string) return error.InvalidEmbeddingArtifactProducer;
    if (!std.mem.eql(u8, kind.string, "embedding")) return;

    const name = object.get("name") orelse return error.InvalidEmbeddingArtifactProducer;
    if (name != .string or name.string.len == 0) return error.InvalidEmbeddingArtifactProducer;
    const expected_dims = try embeddingEnrichmentExpectedDimensionsOptional(enrichment);
    const producer_json = object.get("producer_json") orelse {
        // Producer-less enrichments are valid dormant declarations and may be
        // staged before their executable owner. If the complete catalog has an
        // owner, validate its shape here. Otherwise the artifact-backed index
        // registration pass below rejects the configuration only when a
        // non-external index actually consumes the dormant declaration.
        const entries = executable_entries orelse return;
        const owner_index = managedEntryIndexForArtifact(entries, name.string) orelse return;
        const owner = &entries[owner_index];
        if (owner.sparse) {
            if (expected_dims != null) return error.ConflictingEmbeddingArtifactDimensions;
        } else {
            const dims = expected_dims orelse return error.EmbeddingArtifactDimensionRequired;
            if (dims != owner.dimensions) return error.ConflictingEmbeddingArtifactDimensions;
        }
        return;
    };

    switch (producer_json) {
        .string => |raw| {
            if (raw.len == 0) return error.MissingEmbeddingArtifactProducer;
            var producer = std.json.parseFromSlice(std.json.Value, alloc, raw, .{}) catch
                return error.InvalidEmbeddingArtifactProducer;
            defer producer.deinit();
            try validateEmbeddingEnrichmentProducerValue(
                alloc,
                name.string,
                expected_dims,
                producer.value,
                options,
                executable_entries,
                sparse_kind,
            );
        },
        .object => try validateEmbeddingEnrichmentProducerValue(
            alloc,
            name.string,
            expected_dims,
            producer_json,
            options,
            executable_entries,
            sparse_kind,
        ),
        else => return error.InvalidEmbeddingArtifactProducer,
    }
}

fn validateAllEmbeddingEnrichmentProducers(
    alloc: std.mem.Allocator,
    value: std.json.Value,
    options: InitOptions,
    executable_entries: []const ManagedEmbeddingEntry,
    sparse_kind: ?bool,
) !void {
    switch (value) {
        .object => |object| {
            if (object.get("enrichments")) |enrichments| {
                if (enrichments != .array) return error.InvalidManagedEmbeddingIndex;
                for (enrichments.array.items) |enrichment| {
                    try validateEmbeddingEnrichmentProducer(alloc, enrichment, options, executable_entries, sparse_kind);
                }
            }
            var it = object.iterator();
            while (it.next()) |entry| {
                if (std.mem.eql(u8, entry.key_ptr.*, "enrichments")) continue;
                try validateAllEmbeddingEnrichmentProducers(alloc, entry.value_ptr.*, options, executable_entries, sparse_kind);
            }
        },
        .array => |array| for (array.items) |item|
            try validateAllEmbeddingEnrichmentProducers(alloc, item, options, executable_entries, sparse_kind),
        else => {},
    }
}

/// Validate an explicitly registered embedding enrichment even when no index
/// consumes it yet. This keeps invalid producer state out of the catalog; the
/// complete-table validator additionally proves compatibility with consumers.
pub fn validateEmbeddingEnrichmentProducerJsonWithOptions(
    alloc: std.mem.Allocator,
    enrichment_json: []const u8,
    options: InitOptions,
) !void {
    var parsed = try std.json.parseFromSlice(std.json.Value, alloc, enrichment_json, .{});
    defer parsed.deinit();
    try validateEmbeddingEnrichmentProducer(alloc, parsed.value, options, null, null);
}

const CatalogProducerOwner = struct {
    sparse: bool,
    dimensions: ?u32,
    semantic_producer_json: ?[]const u8,
    index_value: std.json.Value,
};

pub const EmbeddingProducerOwnershipOptions = struct {
    /// Extension-owned catalogs describe a managed pipeline, so an embedding
    /// enrichment without durable producer provenance must still have an
    /// executable index owner. General catalog mutations leave this false:
    /// producer-less enrichments are also used for externally materialized
    /// vectors and are structurally valid.
    require_owner_for_missing_producer: bool = false,
    /// Context-free extension admission cannot resolve deployment defaults.
    /// Require packages that install executable artifact owners to carry the
    /// credential-free semantic identity they intend every node to execute.
    require_stable_owner_identity: bool = false,
};

fn semanticIdentityStringField(identity: std.json.Value, name: []const u8) ![]const u8 {
    if (identity != .object) return error.InvalidEmbeddingArtifactProducer;
    const field = identity.object.get(name) orelse return error.InvalidEmbeddingArtifactProducer;
    if (field != .string) return error.InvalidEmbeddingArtifactProducer;
    return field.string;
}

fn semanticIdentityOptionalStringField(identity: std.json.Value, name: []const u8) ![]const u8 {
    if (identity != .object) return error.InvalidEmbeddingArtifactProducer;
    const field = identity.object.get(name) orelse return "";
    if (field != .string) return error.InvalidEmbeddingArtifactProducer;
    return field.string;
}

fn validateCatalogOwnerSemanticIdentity(
    alloc: std.mem.Allocator,
    owner: CatalogProducerOwner,
) !void {
    const raw = owner.semantic_producer_json orelse return;
    var parsed_identity = std.json.parseFromSlice(std.json.Value, alloc, raw, .{}) catch
        return error.InvalidEmbeddingArtifactProducer;
    defer parsed_identity.deinit();
    const comparison = try semanticProducerComparisonConfigJsonAlloc(alloc, parsed_identity.value);
    defer if (comparison) |value| alloc.free(value);
    if (comparison == null) return error.InvalidEmbeddingArtifactProducer;
    const semantic_sparse = (try semanticProducerV2Sparse(parsed_identity.value)) orelse
        return error.InvalidEmbeddingArtifactProducer;
    if (semantic_sparse != owner.sparse) return error.InvalidEmbeddingArtifactProducer;

    const index_object = switch (owner.index_value) {
        .object => |object| object,
        else => return error.InvalidEmbeddingArtifactProducer,
    };
    const embedder_value = index_object.get("embedder") orelse
        return error.InvalidEmbeddingArtifactProducer;
    var embedder_cfg = parseEmbedderConfigFromValue(alloc, embedder_value) catch
        return error.InvalidEmbeddingArtifactProducer;
    defer embedder_cfg.deinit(alloc);
    const provider = parseEmbedderProvider(embedder_cfg) catch
        return error.InvalidEmbeddingArtifactProducer;
    const configured_query_input_type = embedder_cfg.query_input_type;
    const configured_document_input_type = embedder_cfg.document_input_type;
    const configured_query_instruction = embedder_cfg.query_instruction;
    if (!std.mem.eql(u8, try semanticIdentityStringField(parsed_identity.value, "provider"), @tagName(provider)) or
        !std.mem.eql(u8, try semanticIdentityStringField(parsed_identity.value, "model"), embedder_cfg.model) or
        !std.mem.eql(u8, try semanticIdentityStringField(parsed_identity.value, "request_format"), embedder_cfg.request_format) or
        !std.mem.eql(u8, try semanticIdentityStringField(parsed_identity.value, "input_type"), embedder_cfg.input_type) or
        !std.mem.eql(u8, try semanticIdentityStringField(parsed_identity.value, "truncate"), embedder_cfg.truncate) or
        !std.mem.eql(u8, try semanticIdentityOptionalStringField(parsed_identity.value, "query_input_type"), configured_query_input_type) or
        !std.mem.eql(u8, try semanticIdentityOptionalStringField(parsed_identity.value, "document_input_type"), configured_document_input_type) or
        !std.mem.eql(u8, try semanticIdentityOptionalStringField(parsed_identity.value, "query_instruction"), configured_query_instruction))
    {
        return error.InvalidEmbeddingArtifactProducer;
    }
    const semantic_project_id = try semanticIdentityOptionalStringField(parsed_identity.value, "project_id");
    if ((provider == .vertex and semantic_project_id.len == 0) or
        (provider != .vertex and semantic_project_id.len != 0) or
        (embedder_cfg.project_id.len > 0 and !std.mem.eql(u8, semantic_project_id, embedder_cfg.project_id)))
    {
        return error.InvalidEmbeddingArtifactProducer;
    }
    const multimodal = parsed_identity.value.object.get("multimodal") orelse
        return error.InvalidEmbeddingArtifactProducer;
    if (multimodal != .bool or multimodal.bool != embedder_cfg.multimodal)
        return error.InvalidEmbeddingArtifactProducer;
    // Region is part of the canonical v2 identity even for providers where it
    // is empty. Runtime binding must not discover that an extension-installed
    // owner omitted the field only after the catalog has committed.
    const semantic_region = try semanticIdentityStringField(parsed_identity.value, "region");
    if (((provider == .bedrock or provider == .vertex) and semantic_region.len == 0) or
        (provider != .bedrock and provider != .vertex and semantic_region.len != 0) or
        (provider == .bedrock and embedder_cfg.region.len > 0 and !std.mem.eql(u8, semantic_region, embedder_cfg.region)) or
        (provider == .vertex and embedder_cfg.location.len > 0 and !std.mem.eql(u8, semantic_region, embedder_cfg.location)))
    {
        return error.InvalidEmbeddingArtifactProducer;
    }

    // Bind explicit endpoints exactly. When the public config omits one, the
    // persisted identity intentionally captures the API process's effective
    // deployment endpoint and later metadata validation must not re-resolve it.
    if (embedder_cfg.url.len > 0) {
        const endpoint = switch (provider) {
            .openai, .ollama => try appendPathIfMissing(alloc, embedder_cfg.url, "/v1"),
            .cohere => try appendPathIfMissing(alloc, embedder_cfg.url, "/v2"),
            .gemini, .vertex => try alloc.dupe(u8, std.mem.trimEnd(u8, embedder_cfg.url, "/")),
            .bedrock => try alloc.dupe(u8, embedder_cfg.url),
            .antfly => normalizeAntflyInferenceBaseUrl(alloc, embedder_cfg.url) catch
                return error.InvalidEmbeddingArtifactProducer,
        };
        defer alloc.free(endpoint);
        if (!std.mem.eql(u8, try semanticIdentityStringField(parsed_identity.value, "endpoint"), endpoint))
            return error.InvalidEmbeddingArtifactProducer;
    }
}

fn addCatalogProducerOwner(
    alloc: std.mem.Allocator,
    owners: *std.StringHashMapUnmanaged(CatalogProducerOwner),
    name: []const u8,
    owner: CatalogProducerOwner,
) !void {
    const owned_name = try alloc.dupe(u8, name);
    const gop = owners.getOrPut(alloc, owned_name) catch |err| {
        alloc.free(owned_name);
        return err;
    };
    if (!gop.found_existing) {
        gop.value_ptr.* = owner;
        return;
    }
    alloc.free(owned_name);
    // An artifact has one authoritative executable owner. Even equivalent
    // duplicate producers can diverge later through credentials, pacing, or
    // deployment defaults that context-free metadata validation cannot see.
    return error.InvalidEmbeddingArtifactProducer;
}

fn collectCatalogProducerOwners(
    alloc: std.mem.Allocator,
    root: std.json.Value,
    owners: *std.StringHashMapUnmanaged(CatalogProducerOwner),
    options: EmbeddingProducerOwnershipOptions,
) !void {
    if (root != .object) return error.InvalidManagedEmbeddingIndex;
    var it = root.object.iterator();
    while (it.next()) |entry| {
        if (entry.value_ptr.* != .object) continue;
        const object = entry.value_ptr.object;
        const type_value = object.get("type") orelse continue;
        if (type_value != .string or !std.mem.eql(u8, type_value.string, "embeddings")) continue;
        if (object.get("embedder") == null) continue;

        var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, entry.value_ptr.*);
        defer parsed_cfg.deinit();
        const cfg = parsed_cfg.value;
        if (cfg.external orelse false) continue;
        const sparse = cfg.sparse orelse false;
        const owner = CatalogProducerOwner{
            .sparse = sparse,
            .dimensions = if (sparse) null else try resolveDeclaredEmbeddingDimensionsRequired(cfg),
            .semantic_producer_json = if (object.get("semantic_producer")) |semantic| switch (semantic) {
                .string => |raw| if (raw.len > 0) raw else return error.InvalidEmbeddingArtifactProducer,
                else => return error.InvalidEmbeddingArtifactProducer,
            } else null,
            .index_value = entry.value_ptr.*,
        };
        if (options.require_stable_owner_identity and owner.semantic_producer_json == null)
            return error.InvalidEmbeddingArtifactProducer;
        try validateCatalogOwnerSemanticIdentity(alloc, owner);
        if (cfg.embedding_name) |name| try addCatalogProducerOwner(alloc, owners, name, owner);
        if (cfg.sources) |sources| for (sources) |source| {
            try addCatalogProducerOwner(alloc, owners, source.artifact, owner);
        };
    }
}

fn addCatalogEmbeddingConsumer(
    alloc: std.mem.Allocator,
    consumers: *std.StringHashMapUnmanaged(void),
    name: []const u8,
) !void {
    if (consumers.contains(name)) return;
    const owned_name = try alloc.dupe(u8, name);
    errdefer alloc.free(owned_name);
    try consumers.put(alloc, owned_name, {});
}

/// Collect durable artifact streams consumed by non-external embedding
/// indexes. A producer-less enrichment may remain as an externally populated
/// declaration when unconsumed, but every managed consumer needs an executable
/// owner that survives the same catalog mutation.
fn collectCatalogEmbeddingConsumers(
    alloc: std.mem.Allocator,
    root: std.json.Value,
    consumers: *std.StringHashMapUnmanaged(void),
) !void {
    if (root != .object) return error.InvalidManagedEmbeddingIndex;
    var it = root.object.iterator();
    while (it.next()) |entry| {
        if (entry.value_ptr.* != .object) continue;
        const object = entry.value_ptr.object;
        const type_value = object.get("type") orelse continue;
        if (type_value != .string or !std.mem.eql(u8, type_value.string, "embeddings")) continue;

        var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, entry.value_ptr.*);
        defer parsed_cfg.deinit();
        const cfg = parsed_cfg.value;
        if (cfg.external orelse false) continue;
        if (cfg.embedding_name) |name| try addCatalogEmbeddingConsumer(alloc, consumers, name);
        if (cfg.sources) |sources| for (sources) |source| {
            try addCatalogEmbeddingConsumer(alloc, consumers, source.artifact);
        };
    }
}

fn validateCatalogProducerShape(
    owner: CatalogProducerOwner,
    expected_dims: ?u32,
) !void {
    if (owner.sparse) {
        if (expected_dims != null) return error.ConflictingEmbeddingArtifactDimensions;
    } else if ((expected_dims orelse return error.EmbeddingArtifactDimensionRequired) != owner.dimensions.?) {
        return error.ConflictingEmbeddingArtifactDimensions;
    }
}

fn semanticIdentityFieldsEqual(lhs: std.json.Value, rhs: std.json.Value) bool {
    return switch (lhs) {
        .string => |value| rhs == .string and std.mem.eql(u8, value, rhs.string),
        .integer => |value| rhs == .integer and value == rhs.integer,
        .bool => |value| rhs == .bool and value == rhs.bool,
        .null => rhs == .null,
        else => false,
    };
}

fn semanticIdentityDefaultField(name: []const u8) ?std.json.Value {
    if (std.mem.eql(u8, name, "multimodal")) return .{ .bool = false };
    if (std.mem.eql(u8, name, "region") or
        std.mem.eql(u8, name, "project_id") or
        std.mem.eql(u8, name, "request_format") or
        std.mem.eql(u8, name, "input_type") or
        std.mem.eql(u8, name, "truncate") or
        std.mem.eql(u8, name, "query_input_type") or
        std.mem.eql(u8, name, "document_input_type") or
        std.mem.eql(u8, name, "query_instruction"))
    {
        return .{ .string = "" };
    }
    return null;
}

fn validateCatalogSemanticProducerOwner(
    alloc: std.mem.Allocator,
    producer: std.json.Value,
    owner: CatalogProducerOwner,
) !void {
    const owner_identity_json = owner.semantic_producer_json orelse
        return error.InvalidEmbeddingArtifactProducer;
    var owner_identity = std.json.parseFromSlice(std.json.Value, alloc, owner_identity_json, .{}) catch |err| switch (err) {
        error.OutOfMemory => return err,
        else => return error.InvalidEmbeddingArtifactProducer,
    };
    defer owner_identity.deinit();
    if (owner_identity.value != .object or producer != .object)
        return error.InvalidEmbeddingArtifactProducer;
    const owner_sparse = (try semanticProducerV2Sparse(owner_identity.value)) orelse
        return error.InvalidEmbeddingArtifactProducer;
    if (owner_sparse != owner.sparse) return error.InvalidEmbeddingArtifactProducer;
    const comparison = try semanticProducerComparisonConfigJsonAlloc(alloc, owner_identity.value);
    defer if (comparison) |raw| alloc.free(raw);
    if (comparison == null) return error.InvalidEmbeddingArtifactProducer;

    var fields = owner_identity.value.object.iterator();
    while (fields.next()) |field| {
        const producer_field = producer.object.get(field.key_ptr.*) orelse
            semanticIdentityDefaultField(field.key_ptr.*) orelse
            return error.InvalidEmbeddingArtifactProducer;
        if (!semanticIdentityFieldsEqual(field.value_ptr.*, producer_field))
            return error.InvalidEmbeddingArtifactProducer;
    }

    // The owner-to-producer pass above accepts omitted fields at their
    // canonical defaults. Compare in the other direction as well so a
    // producer cannot add a non-default retrieval role or deployment field
    // that was absent from the admitted owner identity.
    var producer_fields = producer.object.iterator();
    while (producer_fields.next()) |field| {
        const owner_field = owner_identity.value.object.get(field.key_ptr.*) orelse
            semanticIdentityDefaultField(field.key_ptr.*) orelse
            return error.InvalidEmbeddingArtifactProducer;
        if (!semanticIdentityFieldsEqual(field.value_ptr.*, owner_field))
            return error.InvalidEmbeddingArtifactProducer;
    }
}

pub fn testCatalogSemanticIdentityRejectsProducerOnlyFields() !void {
    const owner_identity =
        \\{"version":2,"provider":"cohere","model":"embed-v4.0","endpoint":"https://api.cohere.com/v2","region":"","request_format":"","sparse":false,"multimodal":false,"input_type":"","truncate":""}
    ;
    var producer = try std.json.parseFromSlice(
        std.json.Value,
        std.testing.allocator,
        \\{"version":2,"provider":"cohere","model":"embed-v4.0","endpoint":"https://api.cohere.com/v2","region":"","request_format":"","sparse":false,"multimodal":false,"input_type":"","truncate":"","query_input_type":"custom_query"}
    ,
        .{},
    );
    defer producer.deinit();

    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateCatalogSemanticProducerOwner(std.testing.allocator, producer.value, .{
            .sparse = false,
            .dimensions = 1024,
            .semantic_producer_json = owner_identity,
            .index_value = .null,
        }),
    );

    var canonical_default = try std.json.parseFromSlice(
        std.json.Value,
        std.testing.allocator,
        \\{"version":2,"provider":"cohere","model":"embed-v4.0","endpoint":"https://api.cohere.com/v2","region":"","request_format":"","sparse":false,"multimodal":false,"input_type":"","truncate":"","query_input_type":""}
    ,
        .{},
    );
    defer canonical_default.deinit();
    try validateCatalogSemanticProducerOwner(std.testing.allocator, canonical_default.value, .{
        .sparse = false,
        .dimensions = 1024,
        .semantic_producer_json = owner_identity,
        .index_value = .null,
    });
}

test "catalog semantic identity rejects producer-only retrieval fields" {
    try testCatalogSemanticIdentityRejectsProducerOnlyFields();
}

fn validateCatalogEmbeddingProducerOwnership(
    alloc: std.mem.Allocator,
    value: std.json.Value,
    owners: *const std.StringHashMapUnmanaged(CatalogProducerOwner),
    consumers: *const std.StringHashMapUnmanaged(void),
    options: EmbeddingProducerOwnershipOptions,
) !void {
    switch (value) {
        .object => |object| {
            if (object.get("enrichments")) |enrichments| {
                if (enrichments != .array) return error.InvalidManagedEmbeddingIndex;
                for (enrichments.array.items) |enrichment| {
                    if (enrichment != .object) return error.InvalidEmbeddingArtifactProducer;
                    const kind = enrichment.object.get("kind") orelse return error.InvalidEmbeddingArtifactProducer;
                    if (kind != .string) return error.InvalidEmbeddingArtifactProducer;
                    if (!std.mem.eql(u8, kind.string, "embedding")) continue;
                    const name = enrichment.object.get("name") orelse return error.InvalidEmbeddingArtifactProducer;
                    if (name != .string or name.string.len == 0) return error.InvalidEmbeddingArtifactProducer;
                    const expected_dims = try embeddingEnrichmentExpectedDimensionsOptional(enrichment);
                    const owner = owners.get(name.string);
                    const producer_json = enrichment.object.get("producer_json") orelse {
                        if (!options.require_owner_for_missing_producer and !consumers.contains(name.string)) continue;
                        try validateCatalogProducerShape(
                            owner orelse return error.MissingEmbeddingArtifactProducer,
                            expected_dims,
                        );
                        continue;
                    };

                    var parsed_string: ?std.json.Parsed(std.json.Value) = null;
                    defer if (parsed_string) |*parsed| parsed.deinit();
                    const producer = switch (producer_json) {
                        .object => producer_json,
                        .string => |raw| blk: {
                            if (raw.len == 0) return error.MissingEmbeddingArtifactProducer;
                            parsed_string = std.json.parseFromSlice(std.json.Value, alloc, raw, .{}) catch
                                return error.InvalidEmbeddingArtifactProducer;
                            break :blk parsed_string.?.value;
                        },
                        else => return error.InvalidEmbeddingArtifactProducer,
                    };
                    if (try semanticProducerV2Sparse(producer)) |_| {
                        const comparison = try semanticProducerComparisonConfigJsonAlloc(alloc, producer);
                        defer if (comparison) |raw| alloc.free(raw);
                        if (comparison == null) return error.InvalidEmbeddingArtifactProducer;
                        try validateCatalogProducerShape(
                            owner orelse return error.InvalidEmbeddingArtifactProducer,
                            expected_dims,
                        );
                        try validateCatalogSemanticProducerOwner(alloc, producer, owner.?);
                    } else {
                        try validateEmbeddingEnrichmentProducerValue(
                            alloc,
                            name.string,
                            expected_dims,
                            producer,
                            .{},
                            null,
                            null,
                        );
                    }
                }
            }
            var it = object.iterator();
            while (it.next()) |entry| {
                if (std.mem.eql(u8, entry.key_ptr.*, "enrichments")) continue;
                try validateCatalogEmbeddingProducerOwnership(alloc, entry.value_ptr.*, owners, consumers, options);
            }
        },
        .array => |array| for (array.items) |item|
            try validateCatalogEmbeddingProducerOwnership(alloc, item, owners, consumers, options),
        else => {},
    }
}

/// Context-free catalog invariant used at authoritative metadata boundaries.
/// It proves that credential-free v2 provenance retains an executable index
/// owner with the exact stable semantic identity admitted by the API, and that
/// every consumed producer-less embedding enrichment retains an executable
/// owner. Callers admitting a managed extension catalog can additionally
/// require owners for unconsumed inline enrichments. Provider availability
/// remains an API/runtime concern.
pub fn validateEmbeddingProducerOwnershipValue(
    alloc: std.mem.Allocator,
    root: std.json.Value,
) !void {
    return validateEmbeddingProducerOwnershipValueWithOptions(alloc, root, .{});
}

pub fn validateEmbeddingProducerOwnershipValueWithOptions(
    alloc: std.mem.Allocator,
    root: std.json.Value,
    options: EmbeddingProducerOwnershipOptions,
) !void {
    var owners = std.StringHashMapUnmanaged(CatalogProducerOwner).empty;
    defer {
        var keys = owners.keyIterator();
        while (keys.next()) |key| alloc.free(@constCast(key.*));
        owners.deinit(alloc);
    }
    var consumers = std.StringHashMapUnmanaged(void).empty;
    defer {
        var keys = consumers.keyIterator();
        while (keys.next()) |key| alloc.free(@constCast(key.*));
        consumers.deinit(alloc);
    }
    try collectCatalogProducerOwners(alloc, root, &owners, options);
    try collectCatalogEmbeddingConsumers(alloc, root, &consumers);
    try validateCatalogEmbeddingProducerOwnership(alloc, root, &owners, &consumers, options);
}

pub fn validateEmbeddingProducerOwnershipJson(
    alloc: std.mem.Allocator,
    indexes_json: []const u8,
) !void {
    return validateEmbeddingProducerOwnershipJsonWithOptions(alloc, indexes_json, .{});
}

pub fn validateEmbeddingProducerOwnershipJsonWithOptions(
    alloc: std.mem.Allocator,
    indexes_json: []const u8,
    options: EmbeddingProducerOwnershipOptions,
) !void {
    var parsed = try std.json.parseFromSlice(std.json.Value, alloc, indexes_json, .{});
    defer parsed.deinit();
    try validateEmbeddingProducerOwnershipValueWithOptions(alloc, parsed.value, options);
}

test "managed embedder catalog ownership rejects orphaned semantic producers" {
    const owner_identity =
        "{\"version\":2,\"provider\":\"antfly\",\"model\":\"test-model\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}";
    const semantic_enrichment =
        "{\"name\":\"document_dense_v1\",\"kind\":\"embedding\",\"field\":\"body\",\"expected_dims\":3,\"producer_json\":\"{\\\"version\\\":2,\\\"provider\\\":\\\"antfly\\\",\\\"model\\\":\\\"test-model\\\",\\\"endpoint\\\":\\\"antfly:embedded\\\",\\\"sparse\\\":false}\"}";
    const valid = try std.fmt.allocPrint(
        std.testing.allocator,
        "{{\"owner\":{{\"type\":\"embeddings\",\"field\":\"body\",\"dimension\":3,\"embedding_name\":\"document_dense_v1\",\"embedder\":{{\"provider\":\"antfly\",\"model\":\"test-model\"}},\"semantic_producer\":{f}}},\"enrichments\":[{s}]}}",
        .{ std.json.fmt(owner_identity, .{}), semantic_enrichment },
    );
    defer std.testing.allocator.free(valid);
    try validateEmbeddingProducerOwnershipJson(std.testing.allocator, valid);

    const mismatched = try std.fmt.allocPrint(
        std.testing.allocator,
        "{{\"owner\":{{\"type\":\"embeddings\",\"field\":\"body\",\"dimension\":3,\"embedding_name\":\"document_dense_v1\",\"embedder\":{{\"provider\":\"antfly\",\"model\":\"different-model\"}},\"semantic_producer\":\"{{\\\"version\\\":2,\\\"provider\\\":\\\"antfly\\\",\\\"model\\\":\\\"different-model\\\",\\\"endpoint\\\":\\\"antfly:embedded\\\",\\\"region\\\":\\\"\\\",\\\"request_format\\\":\\\"\\\",\\\"sparse\\\":false,\\\"multimodal\\\":false,\\\"input_type\\\":\\\"\\\",\\\"truncate\\\":\\\"\\\"}}\"}},\"enrichments\":[{s}]}}",
        .{semantic_enrichment},
    );
    defer std.testing.allocator.free(mismatched);
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateEmbeddingProducerOwnershipJson(std.testing.allocator, mismatched),
    );

    const orphan = try std.fmt.allocPrint(
        std.testing.allocator,
        "{{\"enrichments\":[{s}]}}",
        .{semantic_enrichment},
    );
    defer std.testing.allocator.free(orphan);
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateEmbeddingProducerOwnershipJson(std.testing.allocator, orphan),
    );

    const externally_materialized =
        "{\"enrichments\":[{\"name\":\"precomputed_dense_v1\",\"kind\":\"embedding\",\"field\":\"embedding\",\"expected_dims\":3}]}";
    try validateEmbeddingProducerOwnershipJson(std.testing.allocator, externally_materialized);
    try std.testing.expectError(
        error.MissingEmbeddingArtifactProducer,
        validateEmbeddingProducerOwnershipJsonWithOptions(
            std.testing.allocator,
            externally_materialized,
            .{ .require_owner_for_missing_producer = true },
        ),
    );

    const consumed_without_owner =
        \\{"consumer":{"type":"embeddings","dimension":3,"sources":[{"artifact":"precomputed_dense_v1"}]},"enrichments":[{"name":"precomputed_dense_v1","kind":"embedding","field":"embedding","expected_dims":3}]}
    ;
    try std.testing.expectError(
        error.MissingEmbeddingArtifactProducer,
        validateEmbeddingProducerOwnershipJson(std.testing.allocator, consumed_without_owner),
    );

    const owner_missing_region =
        \\{"owner":{"type":"embeddings","field":"body","dimension":3,"embedding_name":"document_dense_v1","embedder":{"provider":"antfly","model":"test-model"},"semantic_producer":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"test-model\",\"endpoint\":\"antfly:embedded\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"},"enrichments":[{"name":"document_dense_v1","kind":"embedding","field":"body","expected_dims":3}]}
    ;
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateEmbeddingProducerOwnershipJsonWithOptions(
            std.testing.allocator,
            owner_missing_region,
            .{ .require_stable_owner_identity = true },
        ),
    );
}

test "catalog ownership rejects duplicate executable owners and endpoint mismatches" {
    const duplicate_owners =
        \\{"owner_a":{"type":"embeddings","field":"body","dimension":3,"embedding_name":"dense_v1","embedder":{"provider":"antfly","model":"model-a"}},"owner_b":{"type":"embeddings","field":"body","dimension":3,"embedding_name":"dense_v1","embedder":{"provider":"antfly","model":"model-b"}},"enrichments":[{"name":"dense_v1","kind":"embedding","field":"body","expected_dims":3}]}
    ;
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateEmbeddingProducerOwnershipJsonWithOptions(
            std.testing.allocator,
            duplicate_owners,
            .{ .require_owner_for_missing_producer = true },
        ),
    );

    const mismatched_endpoint =
        \\{"owner":{"type":"embeddings","field":"body","dimension":3,"embedding_name":"dense_v1","embedder":{"provider":"antfly","model":"model-a"},"semantic_producer":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"model-a\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"},"enrichments":[{"name":"dense_v1","kind":"embedding","field":"body","expected_dims":3,"producer_json":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"model-a\",\"endpoint\":\"https://wrong.example/ai/v1\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"}]}
    ;
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateEmbeddingProducerOwnershipJson(std.testing.allocator, mismatched_endpoint),
    );
}

fn registerArtifactManagedEmbeddingLookup(
    alloc: std.mem.Allocator,
    root: std.json.Value,
    index_name: []const u8,
    artifact_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    options: InitOptions,
    entries: *std.ArrayListUnmanaged(ManagedEmbeddingEntry),
) !void {
    // Inline embedding enrichments may omit producer_json when another managed
    // index in the same catalog is the authoritative executable owner. Reuse
    // that entry directly; requiring a duplicated producer document would make
    // the durable owner ambiguous and prevent a second index from consuming the
    // same artifact stream.
    if (managedEntryIndexForArtifact(entries.items, artifact_name)) |entry_index| {
        var enrichment: ?std.json.Value = null;
        try findEmbeddingEnrichmentValue(root, artifact_name, &enrichment);
        const enrichment_value = enrichment orelse return error.MissingEmbeddingArtifactEnrichment;
        const enrichment_object = switch (enrichment_value) {
            .object => |object| object,
            else => return error.InvalidEmbeddingArtifactProducer,
        };
        if (enrichment_object.get("producer_json") == null) {
            const existing = &entries.items[entry_index];
            const sparse = cfg.sparse orelse false;
            if (existing.sparse != sparse) return error.InvalidEmbeddingArtifactProducer;
            const expected_dims = try embeddingEnrichmentExpectedDimensionsOptional(enrichment_value);
            if (sparse) {
                if (expected_dims != null) return error.ConflictingEmbeddingArtifactDimensions;
            } else {
                const declared_dims = try resolveDeclaredEmbeddingDimensionsRequired(cfg);
                if ((expected_dims orelse return error.EmbeddingArtifactDimensionRequired) != declared_dims or
                    existing.dimensions != declared_dims)
                {
                    return error.ConflictingEmbeddingArtifactDimensions;
                }
            }
            try appendManagedEntryLookupAlias(alloc, existing, index_name);
            return;
        }
    }

    var built = try buildArtifactManagedEmbeddingEntry(
        alloc,
        root,
        index_name,
        artifact_name,
        cfg,
        options,
    );
    errdefer built.entry.deinit(alloc);

    if (managedEntryIndexForArtifact(entries.items, artifact_name)) |entry_index| {
        const existing = &entries.items[entry_index];
        const equivalent = if (built.semantic_identity_only)
            managedEmbeddingEntriesSemanticallyEquivalent(existing, &built.entry)
        else
            managedEmbeddingEntriesEquivalentForLookup(existing, &built.entry);
        if (!equivalent) return error.InvalidEmbeddingArtifactProducer;
        try appendManagedEntryLookupAlias(alloc, existing, index_name);
        built.entry.deinit(alloc);
        return;
    }

    // V2 producer documents are credential-free provenance, not executable
    // configuration. They are only valid when an existing managed index owns
    // the matching artifact and supplies its runtime settings.
    if (built.semantic_identity_only) return error.InvalidEmbeddingArtifactProducer;
    try entries.append(alloc, built.entry);
}

fn addArtifactBackedManagedEmbeddingEntries(
    alloc: std.mem.Allocator,
    root: std.json.Value,
    options: InitOptions,
    entries: *std.ArrayListUnmanaged(ManagedEmbeddingEntry),
    sparse_kind: ?bool,
) !void {
    const object = root.object;
    var it = object.iterator();
    while (it.next()) |entry| {
        const index_object = switch (entry.value_ptr.*) {
            .object => |value| value,
            else => continue,
        };
        const type_value = index_object.get("type") orelse continue;
        if (type_value != .string or !std.mem.eql(u8, type_value.string, "embeddings")) continue;
        if (index_object.get("embedder") != null) continue;

        var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, entry.value_ptr.*);
        defer parsed_cfg.deinit();
        const cfg = parsed_cfg.value;
        if (cfg.external orelse false) continue;
        if (sparse_kind) |selected| if ((cfg.sparse orelse false) != selected) continue;

        if (cfg.embedding_name) |artifact_name| {
            try registerArtifactManagedEmbeddingLookup(
                alloc,
                root,
                entry.key_ptr.*,
                artifact_name,
                cfg,
                options,
                entries,
            );
        }
        if (cfg.sources) |sources| {
            for (sources) |source| {
                try registerArtifactManagedEmbeddingLookup(
                    alloc,
                    root,
                    entry.key_ptr.*,
                    source.artifact,
                    cfg,
                    options,
                    entries,
                );
            }
        }
    }
}

fn parseManagedEmbeddingEntry(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    value: std.json.Value,
    options: InitOptions,
    sparse_kind: ?bool,
) !?ManagedEmbeddingEntry {
    const root = switch (value) {
        .object => |object| object,
        else => return null,
    };

    const type_value = root.get("type") orelse return null;
    if (type_value != .string or !std.mem.eql(u8, type_value.string, "embeddings")) return null;

    var parsed_cfg = try parseEmbeddingsIndexConfigFromValue(alloc, value);
    defer parsed_cfg.deinit();
    const cfg = parsed_cfg.value;

    const external = cfg.external orelse false;
    if (external) return null;

    const sparse = cfg.sparse orelse false;
    if (sparse_kind) |expected_sparse| if (sparse != expected_sparse) return null;

    const embedder = root.get("embedder") orelse return null;
    const declared_dims = if (sparse) null else try resolveDeclaredEmbeddingDimensions(cfg);
    var semantic_binding = try catalogSemanticExecutionBindingAlloc(
        alloc,
        value,
        options,
        sparse,
        declared_dims,
    );
    defer if (semantic_binding) |*binding| binding.deinit(alloc);
    const dims = if (sparse)
        0
    else if (declared_dims) |declared|
        declared
    else
        try resolveEmbeddingDimensionsForManagedConfigWithSemanticBinding(
            alloc,
            index_name,
            cfg,
            embedder,
            options,
            semantic_binding,
        );
    return try buildManagedEmbeddingEntry(alloc, index_name, cfg, embedder, options, dims, semantic_binding);
}

const CatalogSemanticExecutionBinding = struct {
    endpoint: []u8,
    region: []u8,
    project_id: []u8,
    embedded: bool,

    fn deinit(self: *CatalogSemanticExecutionBinding, alloc: std.mem.Allocator) void {
        alloc.free(self.endpoint);
        if (self.region.len > 0) alloc.free(self.region);
        if (self.project_id.len > 0) alloc.free(self.project_id);
        self.* = undefined;
    }
};

/// Resolve the durable endpoint and deployment mode before constructing an
/// executable entry. The raw embedder remains authoritative for credentials
/// and pacing, but a runtime loading admitted catalog state must never consult
/// its own endpoint or region defaults first and then overwrite the result.
fn catalogSemanticExecutionBindingAlloc(
    alloc: std.mem.Allocator,
    value: std.json.Value,
    options: InitOptions,
    sparse: bool,
    dimensions: ?u32,
) !?CatalogSemanticExecutionBinding {
    const root = switch (value) {
        .object => |object| object,
        else => return error.InvalidEmbeddingArtifactProducer,
    };
    const semantic = root.get("semantic_producer") orelse return null;
    if (semantic != .string or semantic.string.len == 0)
        return error.InvalidEmbeddingArtifactProducer;

    try validateCatalogOwnerSemanticIdentity(alloc, .{
        .sparse = sparse,
        .dimensions = dimensions,
        .semantic_producer_json = semantic.string,
        .index_value = value,
    });

    var identity = std.json.parseFromSlice(std.json.Value, alloc, semantic.string, .{}) catch
        return error.InvalidEmbeddingArtifactProducer;
    defer identity.deinit();
    if ((try semanticProducerV2Sparse(identity.value)) == null)
        return error.InvalidEmbeddingArtifactProducer;
    const endpoint = try semanticIdentityStringField(identity.value, "endpoint");
    const region = try semanticIdentityStringField(identity.value, "region");
    const project_id = try semanticIdentityOptionalStringField(identity.value, "project_id");
    const embedded = std.mem.eql(u8, endpoint, "antfly:embedded");
    if (embedded and options.antfly_provider == null)
        return error.InvalidEmbeddingArtifactProducer;

    const owned_endpoint = try alloc.dupe(u8, endpoint);
    errdefer alloc.free(owned_endpoint);
    const owned_region: []u8 = if (region.len > 0) try alloc.dupe(u8, region) else @constCast("");
    errdefer if (owned_region.len > 0) alloc.free(owned_region);
    const owned_project_id: []u8 = if (project_id.len > 0) try alloc.dupe(u8, project_id) else @constCast("");
    errdefer if (owned_project_id.len > 0) alloc.free(owned_project_id);
    return .{
        .endpoint = owned_endpoint,
        .region = owned_region,
        .project_id = owned_project_id,
        .embedded = embedded,
    };
}

fn shouldUseAntflyProvider(embedder: embeddings_types.Config, options: InitOptions) bool {
    if (options.antfly_provider == null) return false;
    if (embedder.url.len > 0) return false;
    const env_url = resolveOptionalEnv(std.heap.page_allocator, "ANTFLY_INFERENCE_URL");
    if (env_url) |value| {
        std.heap.page_allocator.free(value);
        return false;
    }
    if (configuredDefaultAntflyInferenceURL(options) != null) return false;
    return true;
}

fn buildManagedEmbeddingEntry(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    embedder: std.json.Value,
    options: InitOptions,
    dimensions: u32,
    semantic_binding: ?CatalogSemanticExecutionBinding,
) !ManagedEmbeddingEntry {
    const sparse = cfg.sparse orelse false;
    var embedder_cfg = parseEmbedderConfigFromValue(alloc, embedder) catch |err| switch (err) {
        error.OutOfMemory => return err,
        else => return error.InvalidManagedEmbeddingIndex,
    };
    defer embedder_cfg.deinit(alloc);

    const provider = try parseEmbedderProvider(embedder_cfg);
    if (embedder_cfg.model.len == 0 and provider != .antfly) return error.InvalidManagedEmbeddingIndex;
    const bedrock_request_format = if (provider == .bedrock)
        try bedrock_provider.resolveRequestFormat(
            embedder_cfg.model,
            try bedrock_provider.parseRequestFormat(embedder_cfg.request_format),
        )
    else
        bedrock_provider.RequestFormat.auto;
    var rate_limit = try provider_limits.Policy.fromConfig(embedder_cfg.rate_limit);
    if (embedder_cfg.rate_limit != null) {
        if (embedder.object.contains("requests_per_minute") or embedder.object.contains("burst"))
            return error.ConflictingRateLimitPolicy;
    } else {
        rate_limit.requests_per_minute = try resolveEmbedderRequestsPerMinute(embedder, provider);
        rate_limit.burst = try resolveEmbedderBurst(embedder, provider);
        // Legacy single-request pacing promised a non-bursting provider path.
        // Anchor it to completion rather than an arbitrary transport margin.
        if (rate_limit.requests_per_minute != 0 and rate_limit.burst == 1)
            rate_limit.pacing = .completion;
    }
    if (rate_limit.tokens_per_minute != 0 and embedder_cfg.multimodal) return error.UnsupportedMediaTokenBudget;
    const requests_per_minute = rate_limit.requests_per_minute;
    const burst = rate_limit.burst;
    const antfly_provider = if (semantic_binding) |binding|
        if (binding.embedded)
            options.antfly_provider orelse return error.InvalidEmbeddingArtifactProducer
        else
            null
    else if (isAntflyProvider(provider) and shouldUseAntflyProvider(embedder_cfg, options))
        options.antfly_provider
    else
        null;
    const owned_index_name = try alloc.dupe(u8, index_name);
    errdefer alloc.free(owned_index_name);
    const owned_embedding_name: []u8 = if (cfg.embedding_name) |embedding_name| try alloc.dupe(u8, embedding_name) else @constCast("");
    errdefer if (owned_embedding_name.len > 0) alloc.free(owned_embedding_name);
    const sources = cfg.sources orelse &.{};
    const owned_embedding_names: [][]u8 = if (sources.len > 0) try alloc.alloc([]u8, sources.len) else &.{};
    var owned_embedding_names_len: usize = 0;
    errdefer {
        for (owned_embedding_names[0..owned_embedding_names_len]) |name| alloc.free(name);
        if (owned_embedding_names.len > 0) alloc.free(owned_embedding_names);
    }
    for (sources, 0..) |source, i| {
        owned_embedding_names[i] = try alloc.dupe(u8, source.artifact);
        owned_embedding_names_len += 1;
    }
    const owned_model = try alloc.dupe(u8, embedder_cfg.model);
    errdefer alloc.free(owned_model);

    const provider_region: []u8 = if (semantic_binding) |binding|
        if (binding.region.len > 0) try alloc.dupe(u8, binding.region) else @constCast("")
    else if (provider == .bedrock)
        try resolveBedrockRegion(alloc, embedder_cfg)
    else if (provider == .vertex)
        try resolveVertexLocation(alloc, embedder_cfg)
    else
        @constCast("");
    errdefer if (provider_region.len > 0) alloc.free(provider_region);
    const project_id: []u8 = if (provider == .vertex and semantic_binding != null)
        try alloc.dupe(u8, semantic_binding.?.project_id)
    else if (provider == .vertex and embedder_cfg.project_id.len > 0)
        try alloc.dupe(u8, embedder_cfg.project_id)
    else
        @constCast("");
    errdefer if (project_id.len > 0) alloc.free(project_id);
    const credentials_path: []u8 = if (provider == .vertex and embedder_cfg.credentials_path.len > 0)
        try alloc.dupe(u8, embedder_cfg.credentials_path)
    else
        @constCast("");
    errdefer if (credentials_path.len > 0) alloc.free(credentials_path);
    const location: []u8 = if (provider == .vertex)
        try alloc.dupe(u8, provider_region)
    else
        @constCast("");
    errdefer if (location.len > 0) alloc.free(location);
    const base_url = if (semantic_binding) |binding|
        try alloc.dupe(u8, if (binding.embedded) "" else binding.endpoint)
    else switch (provider) {
        .openai => try resolveOpenAiBaseUrl(alloc, embedder_cfg),
        .ollama => try resolveOllamaBaseUrl(alloc, embedder_cfg),
        .bedrock => try resolveBedrockEndpoint(alloc, embedder_cfg, provider_region),
        .cohere => try resolveCohereBaseUrl(alloc, embedder_cfg),
        .gemini => try resolveGeminiBaseUrl(alloc, embedder_cfg),
        .vertex => try resolveVertexBaseUrl(alloc, embedder_cfg, provider_region),
        .antfly => if (antfly_provider != null)
            try alloc.dupe(u8, "")
        else
            try resolveAntflyInferenceBaseUrl(alloc, embedder_cfg, options),
    };
    errdefer alloc.free(base_url);
    const input_type = if (embedder_cfg.input_type.len > 0) try alloc.dupe(u8, embedder_cfg.input_type) else @constCast("");
    errdefer if (input_type.len > 0) alloc.free(input_type);
    const query_input_type = if (embedder_cfg.query_input_type.len > 0) try alloc.dupe(u8, embedder_cfg.query_input_type) else @constCast("");
    errdefer if (query_input_type.len > 0) alloc.free(query_input_type);
    const document_input_type = if (embedder_cfg.document_input_type.len > 0) try alloc.dupe(u8, embedder_cfg.document_input_type) else @constCast("");
    errdefer if (document_input_type.len > 0) alloc.free(document_input_type);
    const query_instruction = if (embedder_cfg.query_instruction.len > 0) try alloc.dupe(u8, embedder_cfg.query_instruction) else @constCast("");
    errdefer if (query_instruction.len > 0) alloc.free(query_instruction);
    const truncate = if (embedder_cfg.truncate.len > 0) try alloc.dupe(u8, embedder_cfg.truncate) else @constCast("");
    errdefer if (truncate.len > 0) alloc.free(truncate);
    const source_table: []u8 = if (options.source_table.len > 0)
        try alloc.dupe(u8, options.source_table)
    else
        @constCast("");
    errdefer if (source_table.len > 0) alloc.free(source_table);
    const api_key = switch (provider) {
        .openai => try common_secrets.SecretValue.initConfigOrEnv(alloc, embedder_cfg.api_key, "OPENAI_API_KEY"),
        .cohere => try common_secrets.SecretValue.initConfigOrEnv(alloc, embedder_cfg.api_key, "COHERE_API_KEY"),
        .gemini => try common_secrets.SecretValue.initConfigOrEnv(alloc, embedder_cfg.api_key, "GEMINI_API_KEY"),
        .antfly => try common_secrets.SecretValue.initConfigOrEnv(
            alloc,
            embedder_cfg.api_key orelse options.inference_api_key,
            "ANTFLY_INFERENCE_API_KEY",
        ),
        .ollama, .bedrock, .vertex => null,
    };
    errdefer if (api_key) |*owned_api_key| owned_api_key.deinit(alloc);

    return .{
        .alloc = alloc,
        .io = options.io,
        .bounded_http_request = options.bounded_http_request,
        .deadline_ns = options.deadline_ns,
        .cancellation = options.cancellation,
        .progress = options.progress,
        .index_name = owned_index_name,
        .embedding_name = owned_embedding_name,
        .embedding_names = owned_embedding_names,
        .provider = provider,
        .model = owned_model,
        .base_url = base_url,
        .source_table = source_table,
        .region = provider_region,
        .project_id = project_id,
        .location = location,
        .credentials_path = credentials_path,
        .bedrock_request_format = bedrock_request_format,
        .input_type = input_type,
        .query_input_type = query_input_type,
        .document_input_type = document_input_type,
        .query_instruction = query_instruction,
        .truncate = truncate,
        .api_key = api_key,
        .secret_store = options.secret_store,
        .remote_content = options.remote_content,
        .dimensions = dimensions,
        .sparse = sparse,
        .multimodal = embedder_cfg.multimodal,
        .rate_limit = rate_limit,
        .requests_per_minute = requests_per_minute,
        .burst = burst,
        .antfly_provider = antfly_provider,
        .shared_remote_capability_cache = if (provider == .antfly and antfly_provider == null)
            options.remote_capability_cache
        else
            null,
        .remote_capability_cache = if (provider == .antfly and antfly_provider == null and options.remote_capability_cache == null)
            remote_capabilities.Cache.init(alloc, options.io orelse std.Io.Threaded.global_single_threaded.io())
        else
            null,
    };
}

fn isAntflyProvider(provider: ProviderKind) bool {
    return provider == .antfly;
}

fn resolveDeclaredEmbeddingDimensions(cfg: indexes_openapi.EmbeddingsIndexConfig) !?u32 {
    if (cfg.dimension) |dimension| {
        return std.math.cast(u32, dimension) orelse error.InvalidCreateTableRequest;
    }
    if (cfg.embedder) |embedder| {
        const declared = switch (embedder) {
            .ollama_embedder_config => null,
            .open_ai_embedder_config => |value| value.dimensions,
            .bedrock_embedder_config => |value| value.dimension orelse value.dimensions,
            .cohere_embedder_config => null,
            .google_embedder_config => |value| value.dimension,
            .vertex_embedder_config => |value| value.dimension,
            .antfly_embedder_config => null,
        };
        if (declared) |dimension| {
            return std.math.cast(u32, dimension) orelse error.InvalidCreateTableRequest;
        }
    }
    return null;
}

fn resolveDeclaredEmbeddingDimensionsRequired(cfg: indexes_openapi.EmbeddingsIndexConfig) !u32 {
    return (try resolveDeclaredEmbeddingDimensions(cfg)) orelse error.InvalidCreateTableRequest;
}

fn parseDimensionProbeValidation(root: std.json.ObjectMap) !DimensionProbeValidation {
    const value = root.get("validation") orelse return .strict;
    if (value != .string) return error.InvalidCreateTableRequest;
    if (std.mem.eql(u8, value.string, "strict")) return .strict;
    if (std.mem.eql(u8, value.string, "defer_probe")) return .defer_probe;
    return error.InvalidCreateTableRequest;
}

fn resolveEmbeddingDimensionsForManagedConfig(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    embedder: std.json.Value,
    options: InitOptions,
) !u32 {
    return try resolveEmbeddingDimensionsForManagedConfigWithSemanticBinding(
        alloc,
        index_name,
        cfg,
        embedder,
        options,
        null,
    );
}

fn resolveEmbeddingDimensionsForManagedConfigWithSemanticBinding(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    embedder: std.json.Value,
    supplied_options: InitOptions,
    semantic_binding: ?CatalogSemanticExecutionBinding,
) !u32 {
    if (try resolveDeclaredEmbeddingDimensions(cfg)) |declared| return declared;
    var options = supplied_options;
    const owned_http_io = try bindOwnedHttpIoIfNeeded(alloc, &options);
    defer deinitOwnedHttpIo(alloc, owned_http_io);
    var managed = buildManagedEmbeddingEntry(alloc, index_name, cfg, embedder, options, 0, semantic_binding) catch |err| switch (err) {
        error.InvalidManagedEmbeddingIndex, error.InvalidAntflyInferenceBaseUrl => return error.InvalidCreateTableRequest,
        error.UnsupportedEmbeddingProvider => return error.UnsupportedCreateTableRequest,
        else => return err,
    };
    defer managed.deinit(alloc);
    return try resolveEmbeddingDimensionsForEntry(alloc, cfg, &managed);
}

fn resolveEmbeddingDimensionsForManagedConfigWithValidation(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    embedder: std.json.Value,
    supplied_options: InitOptions,
    validation: DimensionProbeValidation,
) !u32 {
    const declared = try resolveDeclaredEmbeddingDimensions(cfg);
    var options = supplied_options;
    const owned_http_io = try bindOwnedHttpIoIfNeeded(alloc, &options);
    defer deinitOwnedHttpIo(alloc, owned_http_io);
    var managed = buildManagedEmbeddingEntry(alloc, index_name, cfg, embedder, options, declared orelse 0, null) catch |err| switch (err) {
        error.InvalidManagedEmbeddingIndex, error.InvalidAntflyInferenceBaseUrl => return error.InvalidCreateTableRequest,
        error.UnsupportedEmbeddingProvider => return error.UnsupportedCreateTableRequest,
        else => return err,
    };
    defer managed.deinit(alloc);
    try attachProviderQuota(&managed, options);
    return try resolveEmbeddingDimensionsForEntryWithValidation(alloc, &managed, declared, validation);
}

fn validateSparseEmbeddingForManagedConfig(
    alloc: std.mem.Allocator,
    index_name: []const u8,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    embedder: std.json.Value,
    supplied_options: InitOptions,
) !void {
    var options = supplied_options;
    const owned_http_io = try bindOwnedHttpIoIfNeeded(alloc, &options);
    defer deinitOwnedHttpIo(alloc, owned_http_io);
    var managed = buildManagedEmbeddingEntry(alloc, index_name, cfg, embedder, options, 0, null) catch |err| switch (err) {
        error.InvalidManagedEmbeddingIndex, error.InvalidAntflyInferenceBaseUrl => return error.InvalidCreateTableRequest,
        error.UnsupportedEmbeddingProvider => return error.UnsupportedCreateTableRequest,
        else => return err,
    };
    defer managed.deinit(alloc);
    try attachProviderQuota(&managed, options);
    try validateSparseEmbeddingForEntry(alloc, &managed);
}

fn validateSparseEmbeddingForEntry(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
) !void {
    var embedding = embedSparseWithEntry(alloc, entry, dimension_probe_text) catch |err| switch (err) {
        error.EmptyEmbeddingResponse,
        error.InvalidEmbeddingResponse,
        error.EmbedRateLimited,
        error.EmbedTransientFailure,
        error.QueueFull,
        error.EmbedRequestFailed,
        => return error.InvalidCreateTableRequest,
        error.UnsupportedEmbeddingProvider => return error.UnsupportedCreateTableRequest,
        else => return err,
    };
    embedding.deinit(alloc);
}

fn resolveEmbeddingDimensionsForEntry(
    alloc: std.mem.Allocator,
    cfg: indexes_openapi.EmbeddingsIndexConfig,
    entry: *const ManagedEmbeddingEntry,
) !u32 {
    const declared = try resolveDeclaredEmbeddingDimensions(cfg);
    return try resolveEmbeddingDimensionsForEntryWithValidation(alloc, entry, declared, .strict);
}

fn resolveEmbeddingDimensionsForEntryWithValidation(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    declared: ?u32,
    validation: DimensionProbeValidation,
) !u32 {
    const probe_dims = inferEmbeddingDimensionsFromEntry(alloc, entry, declared orelse 0) catch |err| switch (err) {
        error.InvalidEmbeddingDimensions,
        error.EmptyEmbeddingResponse,
        error.InvalidEmbeddingResponse,
        error.EmbedRequestFailed,
        => return error.InvalidCreateTableRequest,
        error.EmbedRateLimited,
        error.EmbedTransientFailure,
        => if (validation == .defer_probe) {
            return declared orelse error.InvalidCreateTableRequest;
        } else {
            return error.EmbeddingProbeUnavailable;
        },
        error.UnsupportedEmbeddingProvider => return error.UnsupportedCreateTableRequest,
        else => {
            if (isOperationalEmbeddingProbeError(err)) {
                if (validation == .defer_probe) return declared orelse error.InvalidCreateTableRequest;
                return error.EmbeddingProbeUnavailable;
            }
            return err;
        },
    };
    if (probe_dims == 0) return error.InvalidCreateTableRequest;
    if (declared) |declared_dims| {
        if (declared_dims != probe_dims) return error.InvalidCreateTableRequest;
        return declared_dims;
    }
    return probe_dims;
}

fn isOperationalEmbeddingProbeError(err: anyerror) bool {
    return switch (err) {
        error.ConnectionRefused,
        error.ConnectionResetByPeer,
        error.ConnectionTimedOut,
        error.Timeout,
        error.NetworkUnreachable,
        error.HostLacksNetworkAddresses,
        error.TemporaryNameServerFailure,
        error.NameServerFailure,
        error.UnexpectedReadFailure,
        error.SendFailed,
        error.RecvFailed,
        // Capability discovery intentionally normalizes transport and retryable
        // HTTP failures so callers do not depend on backend-specific socket
        // errors. Dimension probing must preserve that operational class when
        // `validation: defer_probe` is selected.
        error.RemoteCapabilityDiscoveryTransient,
        // Executor admission is transport capacity, not a malformed index
        // definition. Surface it through the retryable probe-unavailable
        // contract so clients do not turn transient saturation into a
        // permanent configuration failure.
        error.ConcurrencyUnavailable,
        error.QueueFull,
        => true,
        else => false,
    };
}

test "managed embedder treats normalized capability discovery failure as operational" {
    try std.testing.expect(isOperationalEmbeddingProbeError(error.RemoteCapabilityDiscoveryTransient));
    try std.testing.expect(!isOperationalEmbeddingProbeError(error.RemoteCapabilityDiscoveryRejected));
}

fn inferEmbeddingDimensionsFromEntry(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    declared_dims: u32,
) !u32 {
    const vector = try embedWithEntry(alloc, entry, dimension_probe_text, declared_dims);
    defer alloc.free(vector);
    return std.math.cast(u32, vector.len) orelse error.InvalidCreateTableRequest;
}

fn parseEmbeddingsIndexConfigFromValue(
    alloc: std.mem.Allocator,
    value: std.json.Value,
) !std.json.Parsed(indexes_openapi.EmbeddingsIndexConfig) {
    return try std.json.parseFromValue(indexes_openapi.EmbeddingsIndexConfig, alloc, value, .{
        .allocate = .alloc_always,
        .ignore_unknown_fields = true,
    });
}

fn parseEmbedderProvider(embedder: embeddings_types.Config) !ProviderKind {
    return switch (embedder.provider) {
        .openai => .openai,
        .ollama => .ollama,
        .bedrock => .bedrock,
        .cohere => .cohere,
        .gemini => .gemini,
        .vertex => .vertex,
        .antfly => .antfly,
        else => error.UnsupportedEmbeddingProvider,
    };
}

fn parseEmbedderConfigFromValue(
    alloc: std.mem.Allocator,
    value: std.json.Value,
) !embeddings_types.Config {
    const parsed = try std.json.parseFromValue(embeddings_openapi.EmbedderConfig, alloc, value, .{
        .allocate = .alloc_always,
        .ignore_unknown_fields = true,
    });
    defer parsed.deinit();
    return try embeddings_types.configFromOpenApi(alloc, parsed.value);
}

fn resolveEmbedderRequestsPerMinute(value: std.json.Value, provider: ProviderKind) !u32 {
    if (configObjectU32(value, "requests_per_minute")) |rpm| return rpm;
    if (configObjectU32(value, "rpm")) |rpm| return rpm;
    return envOptionalU32(providerRequestsPerMinuteEnv(provider)) orelse envOptionalU32("ANTFLY_EMBED_REQUESTS_PER_MINUTE") orelse 0;
}

fn resolveEmbedderBurst(value: std.json.Value, provider: ProviderKind) !u32 {
    if (configObjectU32(value, "burst")) |burst| return @max(@as(u32, 1), burst);
    return @max(@as(u32, 1), envOptionalU32(providerBurstEnv(provider)) orelse envOptionalU32("ANTFLY_EMBED_BURST") orelse default_pacing_burst);
}

fn configObjectU32(value: std.json.Value, field_name: []const u8) ?u32 {
    const object = switch (value) {
        .object => |object| object,
        else => return null,
    };
    const field = object.get(field_name) orelse return null;
    return switch (field) {
        .integer => |v| std.math.cast(u32, v),
        .float => |v| if (v >= 0 and @round(v) == v) std.math.cast(u32, @as(i64, @intFromFloat(v))) else null,
        .string => |text| std.fmt.parseUnsigned(u32, text, 10) catch null,
        else => null,
    };
}

fn envOptionalU32(name: [:0]const u8) ?u32 {
    const raw_z = getenv(name) orelse return null;
    const raw = std.mem.span(raw_z);
    if (raw.len == 0) return null;
    return std.fmt.parseUnsigned(u32, raw, 10) catch null;
}

fn providerRequestsPerMinuteEnv(provider: ProviderKind) [:0]const u8 {
    return switch (provider) {
        .openai => "ANTFLY_OPENAI_EMBED_REQUESTS_PER_MINUTE",
        .ollama => "ANTFLY_OLLAMA_EMBED_REQUESTS_PER_MINUTE",
        .bedrock => "ANTFLY_BEDROCK_EMBED_REQUESTS_PER_MINUTE",
        .cohere => "ANTFLY_COHERE_EMBED_REQUESTS_PER_MINUTE",
        .gemini => "ANTFLY_GEMINI_EMBED_REQUESTS_PER_MINUTE",
        .vertex => "ANTFLY_VERTEX_EMBED_REQUESTS_PER_MINUTE",
        .antfly => "ANTFLY_INFERENCE_EMBED_REQUESTS_PER_MINUTE",
    };
}

fn providerBurstEnv(provider: ProviderKind) [:0]const u8 {
    return switch (provider) {
        .openai => "ANTFLY_OPENAI_EMBED_BURST",
        .ollama => "ANTFLY_OLLAMA_EMBED_BURST",
        .bedrock => "ANTFLY_BEDROCK_EMBED_BURST",
        .cohere => "ANTFLY_COHERE_EMBED_BURST",
        .gemini => "ANTFLY_GEMINI_EMBED_BURST",
        .vertex => "ANTFLY_VERTEX_EMBED_BURST",
        .antfly => "ANTFLY_INFERENCE_EMBED_BURST",
    };
}

const QueryTemplateRenderContext = struct {
    alloc: std.mem.Allocator,
};

fn renderQueryTemplate(
    alloc: std.mem.Allocator,
    embedding_template: []const u8,
    text: []const u8,
) ![]const u8 {
    const query_json = try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(text, .{})});
    defer alloc.free(query_json);

    var render_ctx = QueryTemplateRenderContext{
        .alloc = alloc,
    };

    var helper_arena_state = std.heap.ArenaAllocator.init(alloc);
    defer helper_arena_state.deinit();
    const helper_arena = helper_arena_state.allocator();

    var extra_helpers: hbs.HelperMap = .{};
    try extra_helpers.put(helper_arena, "remoteMedia", hbs.Helper.withData(&remoteMediaQueryHelper, @ptrCast(&render_ctx)));
    try extra_helpers.put(helper_arena, "remotePDF", hbs.Helper.withData(&remotePdfQueryHelper, @ptrCast(&render_ctx)));
    try extra_helpers.put(helper_arena, "remoteText", hbs.Helper.withData(&remoteTextQueryHelper, @ptrCast(&render_ctx)));

    return try template_mod.renderDocumentWithHelpers(alloc, embedding_template, query_json, &extra_helpers);
}

fn renderQueryTemplateWithEntry(
    alloc: std.mem.Allocator,
    embedding_template: []const u8,
    text: []const u8,
    entry: *const ManagedEmbeddingEntry,
) ![]const u8 {
    try ensureEntryDeadline(entry);
    if (comptime builtin.is_test) {
        return try renderQueryTemplate(alloc, embedding_template, text);
    }

    const config = queryTemplateRenderConfig(entry);
    const query_json = try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(text, .{})});
    defer alloc.free(query_json);
    return try template_remote.renderJsonToTextWithConfig(alloc, embedding_template, query_json, config);
}

fn queryTemplateRenderConfig(entry: *const ManagedEmbeddingEntry) template_remote.RenderConfig {
    var config: template_remote.RenderConfig = .{};
    if (comptime @hasField(template_remote.RenderConfig, "remote_content")) {
        config.remote_content = entry.remote_content;
    }
    if (comptime @hasField(template_remote.RenderConfig, "secret_store")) {
        config.secret_store = entry.secret_store;
    }
    if (comptime @hasField(template_remote.RenderConfig, "io")) {
        // Preserve the distinction between caller-owned request I/O and no
        // request context. The renderer creates and owns its fallback I/O;
        // substituting the process-global single-threaded executor here can
        // make remote helpers fail under the server's concurrent workload.
        config.io = entry.io;
    }
    if (comptime @hasField(template_remote.RenderConfig, "deadline_ns")) {
        config.deadline_ns = entry.deadline_ns;
    }
    if (comptime @hasField(template_remote.RenderConfig, "cancellation")) {
        if (entry.cancellation) |token| {
            config.cancellation = scraping.CancellationToken.fromCallback(
                token.ptr,
                token.is_cancelled_fn,
            );
        }
    }
    if (comptime @hasField(template_remote.RenderConfig, "max_media_parts")) {
        if (isAntflyProvider(entry.provider)) config.max_media_parts = 1;
    }
    return config;
}

fn validateRenderedTemplate(alloc: std.mem.Allocator, rendered: []const u8) !void {
    const directives = try template_mod.parseErrorDirectives(alloc, rendered);
    defer template_mod.freeErrorDirectives(alloc, directives);
    if (directives.len == 0) return;
    if (directives[0].isPermanent()) return QueryTemplateError.PermanentPromptFailure;
    return QueryTemplateError.TransientPromptFailure;
}

fn remoteMediaQueryHelper(ctx: hbs.HelperContext) anyerror!hbs.Value {
    const url = ctx.hash.get("url") orelse return .{ .safe_string = "" };
    const url_str = switch (url) {
        .string => |s| s,
        else => return .{ .safe_string = "" },
    };
    if (url_str.len == 0) return .{ .safe_string = "" };

    const mode = if (ctx.hash.get("mode")) |value| switch (value) {
        .string => |s| s,
        else => "raw",
    } else "raw";
    if (scraping.data_uri.hasScheme(url_str)) {
        const result = try std.fmt.allocPrint(ctx.arena, "<<<dotprompt:media:url {s}>>>", .{url_str});
        return .{ .safe_string = result };
    }

    const render_ctx = queryTemplateRenderContext(ctx) orelse {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, "remoteMedia missing HTTP context");
        return .{ .safe_string = result };
    };

    const fetched = scraping.downloadContentOutcomeAlloc(render_ctx.alloc, url_str, null, null) catch |err| {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, @errorName(err));
        return .{ .safe_string = result };
    };
    if (fetched == .http_error) {
        const result = try template_mod.formatErrorDirective(ctx.arena, fetched.http_error.status, fetched.http_error.message);
        return .{ .safe_string = result };
    }
    var response = fetched.ok;
    defer {
        response.deinit(render_ctx.alloc);
    }

    const is_pdf = std.mem.eql(u8, response.content_type, "application/pdf");
    if (is_pdf and std.mem.eql(u8, mode, "extract")) {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, "remoteMedia extract for PDF is unsupported");
        return .{ .safe_string = result };
    }
    if (is_pdf and std.mem.eql(u8, mode, "render")) {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, "remoteMedia render for PDF is unsupported");
        return .{ .safe_string = result };
    }

    const encoded_len = std.base64.standard.Encoder.calcSize(response.data.len);
    const encoded = try ctx.arena.alloc(u8, encoded_len);
    _ = std.base64.standard.Encoder.encode(encoded, response.data);

    const result = try std.fmt.allocPrint(ctx.arena, "<<<dotprompt:media:url data:{s};base64,{s}>>>", .{
        response.content_type,
        encoded,
    });
    return .{ .safe_string = result };
}

fn remoteTextQueryHelper(ctx: hbs.HelperContext) anyerror!hbs.Value {
    const url = ctx.hash.get("url") orelse return .{ .string = "" };
    const url_str = switch (url) {
        .string => |s| s,
        else => return .{ .string = "" },
    };
    if (url_str.len == 0) return .{ .string = "" };

    const render_ctx = queryTemplateRenderContext(ctx) orelse {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, "remoteText missing HTTP context");
        return .{ .safe_string = result };
    };

    const fetched = scraping.downloadContentOutcomeAlloc(render_ctx.alloc, url_str, null, null) catch |err| {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, @errorName(err));
        return .{ .safe_string = result };
    };
    if (fetched == .http_error) {
        const result = try template_mod.formatErrorDirective(ctx.arena, fetched.http_error.status, fetched.http_error.message);
        return .{ .safe_string = result };
    }
    var response = fetched.ok;
    defer {
        response.deinit(render_ctx.alloc);
    }

    if (!std.mem.startsWith(u8, response.content_type, "text/")) {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, "remoteText requires a text/* response");
        return .{ .safe_string = result };
    }

    const text_copy = try ctx.arena.dupe(u8, response.data);
    return .{ .string = text_copy };
}

/// Deprecated compatibility helper. Prefer document_extraction for durable PDF
/// ingestion or remoteMedia for template-time multimodal inference input.
fn remotePdfQueryHelper(ctx: hbs.HelperContext) anyerror!hbs.Value {
    const url = ctx.hash.get("url") orelse return .{ .safe_string = "" };
    const url_str = switch (url) {
        .string => |s| s,
        else => return .{ .safe_string = "" },
    };
    if (url_str.len == 0) return .{ .safe_string = "" };

    const render_ctx = queryTemplateRenderContext(ctx) orelse {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, "remotePDF missing HTTP context");
        return .{ .safe_string = result };
    };

    const fetched = scraping.downloadContentOutcomeAlloc(render_ctx.alloc, url_str, null, null) catch |err| {
        const result = try template_mod.formatErrorDirective(ctx.arena, 0, @errorName(err));
        return .{ .safe_string = result };
    };
    if (fetched == .http_error) {
        const result = try template_mod.formatErrorDirective(ctx.arena, fetched.http_error.status, fetched.http_error.message);
        return .{ .safe_string = result };
    }
    var response = fetched.ok;
    defer {
        response.deinit(render_ctx.alloc);
    }

    if (std.mem.startsWith(u8, response.content_type, "text/")) {
        const text_copy = try ctx.arena.dupe(u8, response.data);
        return .{ .string = text_copy };
    }

    const result = try template_mod.formatErrorDirective(ctx.arena, 0, "remotePDF extraction is unsupported");
    return .{ .safe_string = result };
}

fn queryTemplateRenderContext(ctx: hbs.HelperContext) ?*QueryTemplateRenderContext {
    const userdata = ctx.userdata orelse return null;
    return @ptrCast(@alignCast(userdata));
}

fn flattenContentPartsToText(
    alloc: std.mem.Allocator,
    parts: []const template_mod.ContentPart,
) ![]u8 {
    var out = std.ArrayListUnmanaged(u8).empty;
    defer out.deinit(alloc);

    var saw_text = false;
    for (parts) |part| {
        if (part != .text) continue;
        if (saw_text) try out.append(alloc, ' ');
        try out.appendSlice(alloc, part.text);
        saw_text = true;
    }
    if (!saw_text) {
        for (parts) |part| {
            if (part == .media_url) {
                try out.appendSlice(alloc, part.media_url);
                break;
            }
        }
    }
    return try out.toOwnedSlice(alloc);
}

fn validateDenseVector(vector: []const f32, dims: u32) !void {
    if (vector.len == 0) return error.InvalidEmbeddingResponse;
    if (dims > 0 and vector.len != dims) return error.InvalidEmbeddingDimensions;
    for (vector) |value| {
        if (!std.math.isFinite(value)) return error.InvalidEmbeddingResponse;
    }
}

fn validateDenseBatch(vectors: []const []const f32, expected_count: usize, dims: u32) !void {
    if (vectors.len == 0) return error.EmptyEmbeddingResponse;
    if (vectors.len != expected_count) return error.InvalidEmbeddingResponse;
    for (vectors) |vector| try validateDenseVector(vector, dims);
}

fn validateSparseBatch(embeddings: []const db_embedder.SparseEmbedding, expected_count: usize) !void {
    if (embeddings.len == 0) return error.EmptyEmbeddingResponse;
    if (embeddings.len != expected_count) return error.InvalidEmbeddingResponse;
    for (embeddings) |embedding| {
        if (embedding.indices.len != embedding.values.len) return error.InvalidEmbeddingResponse;
        for (embedding.indices, embedding.values, 0..) |index, value, i| {
            if (i > 0 and embedding.indices[i - 1] >= index) return error.InvalidEmbeddingResponse;
            if (!std.math.isFinite(value)) return error.InvalidEmbeddingResponse;
        }
    }
}

fn normalizeLocalEmbeddingError(err: anyerror) anyerror {
    return switch (err) {
        error.ResourceTemporarilyUnavailable,
        => error.EmbedTransientFailure,
        else => err,
    };
}

fn remoteEmbeddingHeaders(
    entry: *const ManagedEmbeddingEntry,
    authorization_header: ?[]const u8,
    storage: *[2][2][]const u8,
) []const [2][]const u8 {
    var count: usize = 0;
    if (authorization_header) |value| {
        storage[count] = .{ "Authorization", value };
        count += 1;
    }
    if (entry.source_table.len > 0) {
        storage[count] = .{ "X-Antfly-Source-Table", entry.source_table };
        count += 1;
    }
    return storage[0..count];
}

fn textEmbeddingInvocationShape(texts: []const []const u8) !inference_work.InvocationShape {
    var shape = inference_work.InvocationShape{
        .item_count = texts.len,
        .modalities = .{ .text = true },
    };
    for (texts) |text| {
        shape.text_bytes = std.math.add(usize, shape.text_bytes, text.len) catch
            return error.InferenceTextBytesExceeded;
        shape.max_text_bytes_per_item = @max(shape.max_text_bytes_per_item, text.len);
    }
    return shape;
}

fn densePartsInvocationShape(
    alloc: std.mem.Allocator,
    parts: []const template_mod.ContentPart,
    attachment_transport: inference_work.AttachmentTransport,
) !inference_work.InvocationShape {
    var shape = inference_work.InvocationShape{ .item_count = 1 };
    var media_parts: usize = 0;
    for (parts) |part| switch (part) {
        .text => |text| {
            mergeModalities(&shape.modalities, .{ .text = true });
            shape.text_bytes = std.math.add(usize, shape.text_bytes, text.len) catch
                return error.InferenceTextBytesExceeded;
            shape.max_text_bytes_per_item = shape.text_bytes;
        },
        .media_url => |url| {
            mergeModalities(&shape.modalities, .{ .image = true });
            media_parts = std.math.add(usize, media_parts, 1) catch
                return error.InferenceMediaPartLimitExceeded;
            if (try inference_work.parseInlineDataUri(url)) |parsed| {
                const essence = inference_work.mimeTypeEssence(parsed.mime_type) catch
                    return error.UnsupportedInferenceMimeType;
                if (!std.ascii.startsWithIgnoreCase(essence, "image/"))
                    return error.UnsupportedInferenceMimeType;
                shape.encoded_media_bytes = std.math.add(usize, shape.encoded_media_bytes, url.len) catch
                    return error.InferenceEncodedBytesExceeded;
                const pixels = try denseMediaUrlPixelsAlloc(alloc, url);
                shape.decoded_pixels = std.math.add(u64, shape.decoded_pixels, pixels) catch
                    return error.InferenceDecodedPixelsExceeded;
            }
        },
        .binary => |media| {
            if (media.data.len == 0) return error.InvalidInferenceMedia;
            mergeModalities(&shape.modalities, try modalityForContentType(media.mime_type));
            media_parts = std.math.add(usize, media_parts, 1) catch
                return error.InferenceMediaPartLimitExceeded;
            const wire_bytes = try attachment_transport.wireSize(
                media.data.len,
                media.mime_type.len,
            );
            shape.encoded_media_bytes = std.math.add(usize, shape.encoded_media_bytes, wire_bytes) catch
                return error.InferenceEncodedBytesExceeded;
            if ((try modalityForContentType(media.mime_type)).image) {
                const pixels = try inference_work.encodedImagePixels(media.mime_type, media.data);
                shape.decoded_pixels = std.math.add(u64, shape.decoded_pixels, pixels) catch
                    return error.InferenceDecodedPixelsExceeded;
            }
        },
    };
    shape.max_media_parts_per_item = media_parts;
    return shape;
}

fn validateDensePartsMimeTypes(
    capabilities: inference_work.InferenceCapabilities,
    parts: []const template_mod.ContentPart,
) !void {
    for (parts) |part| switch (part) {
        .text => try capabilities.validateMimeType("text/plain"),
        .media_url => |url| {
            // Network URLs intentionally leave MIME resolution to the remote
            // provider. Inline data URIs have a declared MIME and therefore
            // must satisfy the same descriptor contract as borrowed bytes.
            if (try inference_work.parseInlineDataUri(url)) |parsed|
                try capabilities.validateMimeType(parsed.mime_type);
        },
        .binary => |media| try capabilities.validateMimeType(media.mime_type),
    };
}

pub fn testSingleMultimodalEmbeddingAdmission() !void {
    const image_url = "data:image/png;base64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD";
    const parts = [_]template_mod.ContentPart{
        .{ .text = "ocr" },
        .{ .media_url = image_url },
    };
    const shape = try densePartsInvocationShape(std.testing.allocator, &parts, .base64_payload);
    try std.testing.expectEqual(@as(usize, 1), shape.item_count);
    try std.testing.expect(shape.modalities.text);
    try std.testing.expect(shape.modalities.image);
    try std.testing.expectEqual(@as(usize, 3), shape.text_bytes);
    try std.testing.expectEqual(image_url.len, shape.encoded_media_bytes);
    try std.testing.expectEqual(@as(u64, 6), shape.decoded_pixels);
    try std.testing.expectEqual(@as(usize, 1), shape.max_media_parts_per_item);

    var png = [_]u8{0} ** 24;
    @memcpy(png[0..8], "\x89PNG\r\n\x1a\n");
    std.mem.writeInt(u32, png[16..20], 2, .big);
    std.mem.writeInt(u32, png[20..24], 3, .big);
    const binary_parts = [_]template_mod.ContentPart{.{
        .binary = .{ .mime_type = "image/png", .data = &png },
    }};
    const framed_shape = try densePartsInvocationShape(std.testing.allocator, &binary_parts, .framed_binary);
    const base64_shape = try densePartsInvocationShape(std.testing.allocator, &binary_parts, .base64_payload);
    try std.testing.expectEqual(@as(usize, 24), framed_shape.encoded_media_bytes);
    try std.testing.expectEqual(@as(usize, 32), base64_shape.encoded_media_bytes);

    const invalid = [_]template_mod.ContentPart{
        .{ .media_url = "data:audio/wav;base64,YWFh" },
    };
    try std.testing.expectError(
        error.UnsupportedInferenceMimeType,
        densePartsInvocationShape(std.testing.allocator, &invalid, .base64_payload),
    );
}

fn bindRemoteEmbeddingLease(
    entry: *const ManagedEmbeddingEntry,
    http: *httpx.Client,
    provider: *antfly_provider_mod.Provider,
    headers: []const [2][]const u8,
    operation_deadline_ns: u64,
    shape: inference_work.InvocationShape,
) !remote_capabilities.CapabilityLease {
    const lease = try acquireRemoteEmbeddingLease(
        entry,
        http,
        provider,
        headers,
        operation_deadline_ns,
    );
    if (lease.capabilities) |capabilities| try capabilities.validateInvocation(.embed, shape);
    return lease;
}

fn acquireRemoteEmbeddingLease(
    entry: *const ManagedEmbeddingEntry,
    http: *httpx.Client,
    provider: *antfly_provider_mod.Provider,
    headers: []const [2][]const u8,
    operation_deadline_ns: u64,
) !remote_capabilities.CapabilityLease {
    const cache = entry.capabilityCache() orelse return error.InferenceCapabilitiesUnavailable;
    const lease = try cache.getOrDiscoverLeaseWithContext(
        http,
        entry.base_url,
        entry.model,
        .embed,
        headers,
        .{
            .deadline_ns = operation_deadline_ns,
            .cancellation = entry.cancellation orelse .none,
        },
    );
    // Providers are reused across invocations, but transport support belongs
    // to this concrete lease. Reset first so a legacy or downgraded route can
    // never inherit framed mode from an earlier endpoint.
    provider.setFramedAttachments(false);
    if (lease.capabilities) |capabilities| {
        provider.setFramedAttachments(capabilities.framed_attachments);
    }
    if (lease.routing_token) |token| try provider.setCapabilityToken(token.slice());
    if (lease.descriptor_revision) |revision| try provider.setCapabilityRevision(revision.slice());
    return lease;
}

fn bindRemoteEmbeddingPartsLease(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    http: *httpx.Client,
    provider: *antfly_provider_mod.Provider,
    headers: []const [2][]const u8,
    operation_deadline_ns: u64,
    parts: []const template_mod.ContentPart,
    independently_addressable: bool,
) !remote_capabilities.CapabilityLease {
    const lease = try acquireRemoteEmbeddingLease(
        entry,
        http,
        provider,
        headers,
        operation_deadline_ns,
    );
    if (lease.capabilities) |capabilities| {
        const transport: inference_work.AttachmentTransport = if (capabilities.framed_attachments)
            .segmented_framed_binary
        else
            .base64_payload;
        if (independently_addressable) {
            try validateDensePartItemInvocation(alloc, capabilities, transport, parts);
        } else {
            try capabilities.validateInvocation(
                .embed,
                try densePartsInvocationShape(alloc, parts, transport),
            );
            try validateDensePartsMimeTypes(capabilities, parts);
        }
    }
    return lease;
}

fn invalidateRemoteEmbeddingLease(
    entry: *const ManagedEmbeddingEntry,
    headers: []const [2][]const u8,
) !void {
    const cache = entry.capabilityCache() orelse return;
    try cache.invalidate(entry.base_url, entry.model, .embed, headers);
}

fn embedWithEntryParts(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    parts: []const template_mod.ContentPart,
    dims: u32,
) ![]f32 {
    return embedWithEntryPartsForTask(alloc, entry, parts, dims, .retrieval_document);
}

fn embedWithEntryPartsForTask(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    parts: []const template_mod.ContentPart,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]f32 {
    try embeddingRequestContext(entry, task_type).request.updateDetail(
        if (entry.provider == .antfly) .loading_model else .executing,
        0,
        1,
        entry.model,
        @tagName(entry.provider),
    );
    if (entry.rate_limit.tokens_per_minute != 0 and partsContainMedia(parts))
        return error.UnsupportedMediaTokenBudget;
    if (entry.provider == .bedrock and (entry.multimodal or partsContainMedia(parts))) {
        try checkEntryDispatchDeadline(entry);
        var fallback_http: ?httpx.Client = null;
        defer if (fallback_http) |*client| client.deinit();
        const http = try entry.httpClient(alloc, &fallback_http);

        var provider = bedrock_provider.Provider.initWithCredentialCache(alloc, http, .{
            .region = entry.region,
            .endpoint = entry.base_url,
            .request_format = entry.bedrock_request_format,
            .attempt_observer = embeddingAttemptObserver(entry),
            .input_type = effectiveInputType(entry, task_type),
            .truncate = entry.truncate,
            .dimension = dims,
            .cancellation = entry.cancellation,
            .timeout_ms = try embeddingRemainingTimeoutMs(embeddingOperationDeadline(entry)),
        }, entry.bedrock_credentials orelse return error.MissingBedrockCredentialCache);
        defer provider.deinit();

        var result = try provider.embedParts(alloc, entry.model, parts);
        defer result.deinit();
        if (result.vectors.len == 0) return error.EmptyEmbeddingResponse;
        if (result.vectors.len != 1) return error.InvalidEmbeddingResponse;
        try validateDenseVector(result.vectors[0], dims);
        return try alloc.dupe(f32, result.vectors[0]);
    }

    if (isAntflyProvider(entry.provider) and (entry.multimodal or partsContainMedia(parts))) {
        if (parts.len == 0) return error.EmptyEmbeddingResponse;
        if (entry.antfly_provider) |local| {
            if (local.embed_dense_parts) |embed_parts| {
                try checkEntryDispatchDeadline(entry);
                const context = embeddingRequestContext(entry, task_type);
                try context.check();
                const vectors = (if (local.embed_dense_parts_with_context) |embed_parts_with_context|
                    AntflyProviderBoundary.call("embed_dense_parts_with_context", local.boundary_dispatch, embed_parts_with_context, .{ local.ptr, alloc, entry.model, parts, context })
                else
                    AntflyProviderBoundary.call("embed_dense_parts", local.boundary_dispatch, embed_parts, .{ local.ptr, alloc, entry.model, parts })) catch |err|
                    return normalizeLocalEmbeddingError(err);
                defer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
                try context.check();
                if (vectors.len == 0) return error.EmptyEmbeddingResponse;
                if (vectors.len != 1) return error.InvalidEmbeddingResponse;
                try validateDenseVector(vectors[0], dims);
                return try alloc.dupe(f32, vectors[0]);
            }
            return error.UnsupportedEmbeddingProvider;
        }
        try checkEntryDispatchDeadline(entry);
        const operation_deadline_ns = embeddingOperationDeadline(entry);
        var fallback_http: ?httpx.Client = null;
        defer if (fallback_http) |*client| client.deinit();
        const http = try entry.httpClient(alloc, &fallback_http);

        var provider = antfly_provider_mod.Provider.init(alloc, http, entry.base_url);
        defer provider.deinit();
        provider.attempt_observer = embeddingAttemptObserver(entry);
        provider.setRequestCancellation(entry.cancellation);
        try provider.setSourceTable(entry.source_table);
        var auth_header_owned: ?[]u8 = null;
        defer if (auth_header_owned) |value| alloc.free(value);
        if (entry.api_key) |*api_key_ref| {
            if (try optionalBearerAuthHeaderOwned(@constCast(entry), alloc, api_key_ref)) |auth_header| {
                auth_header_owned = auth_header;
                try provider.setAuthorizationHeader(auth_header);
            }
        }
        var capability_header_storage: [2][2][]const u8 = undefined;
        const capability_headers = remoteEmbeddingHeaders(entry, auth_header_owned, &capability_header_storage);
        _ = try bindRemoteEmbeddingPartsLease(
            alloc,
            entry,
            http,
            &provider,
            capability_headers,
            operation_deadline_ns,
            parts,
            false,
        );
        try applyAntflyEmbeddingRequestControls(entry, &provider, operation_deadline_ns);

        var result = provider.embedPartsWithTask(
            alloc,
            entry.model,
            parts,
            task_type.canonical(),
            effectiveInstruction(entry, task_type),
        ) catch |err| switch (err) {
            error.EmptyResponse => return error.EmptyEmbeddingResponse,
            error.InferenceCapabilitiesStale => {
                try invalidateRemoteEmbeddingLease(entry, capability_headers);
                return err;
            },
            else => return err,
        };
        defer result.deinit();
        if (result.vectors.len == 0) return error.EmptyEmbeddingResponse;
        if (result.vectors.len != 1) return error.InvalidEmbeddingResponse;
        try validateDenseVector(result.vectors[0], dims);
        return try alloc.dupe(f32, result.vectors[0]);
    }

    // Text-only provider adapters must never turn media into an empty string
    // or URL-shaped text. Bedrock and Antfly return above through adapters
    // that preserve binary parts; the remaining providers currently expose
    // only their text embedding contract through Antfly.
    if (partsContainMedia(parts)) return error.UnsupportedEmbeddingProvider;

    const flattened = try flattenContentPartsToText(alloc, parts);
    defer alloc.free(flattened);
    return try embedWithEntryForTask(alloc, entry, flattened, dims, task_type);
}

fn modalityForContentType(content_type: []const u8) !inference_work.Modalities {
    const essence = inference_work.mimeTypeEssence(content_type) catch
        return error.UnsupportedInferenceMimeType;
    if (std.ascii.startsWithIgnoreCase(essence, "image/")) return .{ .image = true };
    if (std.ascii.startsWithIgnoreCase(essence, "audio/")) return .{ .audio = true };
    if (std.ascii.eqlIgnoreCase(essence, "application/pdf")) return .{ .document = true };
    if (std.ascii.eqlIgnoreCase(essence, "text/plain")) return .{ .text = true };
    return error.UnsupportedInferenceMimeType;
}

fn mergeModalities(target: *inference_work.Modalities, value: inference_work.Modalities) void {
    const target_bits: u8 = @bitCast(target.*);
    const value_bits: u8 = @bitCast(value);
    target.* = @bitCast(target_bits | value_bits);
}

fn denseMediaUrlWireBytes(
    capabilities: inference_work.InferenceCapabilities,
    url: []const u8,
) !usize {
    const parsed_uri = (try inference_work.parseInlineDataUri(url)) orelse return 0;
    if (parsed_uri.decoded_size == 0) return error.InvalidDataURI;
    const essence = inference_work.mimeTypeEssence(parsed_uri.mime_type) catch
        return error.UnsupportedInferenceMimeType;
    if (!std.ascii.startsWithIgnoreCase(essence, "image/"))
        return error.UnsupportedInferenceMimeType;
    try capabilities.validateMimeType(parsed_uri.mime_type);
    return url.len;
}

fn denseMediaUrlPixelsAlloc(alloc: std.mem.Allocator, url: []const u8) !u64 {
    const parsed_uri = (try inference_work.parseInlineDataUri(url)) orelse return 0;
    var decoded = try inference_work.decodeInlineDataUriAlloc(alloc, url);
    defer decoded.deinit(alloc);
    return try inference_work.encodedImagePixels(parsed_uri.mime_type, decoded.data);
}

fn validateDensePartItemInvocation(
    alloc: std.mem.Allocator,
    capabilities: inference_work.InferenceCapabilities,
    attachment_transport: inference_work.AttachmentTransport,
    items: []const template_mod.ContentPart,
) !void {
    var shape = inference_work.InvocationShape{ .item_count = items.len };
    for (items) |item| switch (item) {
        .text => |text| {
            mergeModalities(&shape.modalities, .{ .text = true });
            shape.text_bytes = std.math.add(usize, shape.text_bytes, text.len) catch
                return error.InferenceTextBytesExceeded;
            shape.max_text_bytes_per_item = @max(shape.max_text_bytes_per_item, text.len);
            try capabilities.validateMimeType("text/plain");
        },
        .media_url => |url| {
            // The embedding transport currently defines media_url as an image
            // URL. Inline data URIs are fully known and therefore admitted here;
            // network URL MIME and bytes remain provider-owned until download.
            mergeModalities(&shape.modalities, .{ .image = true });
            const wire_bytes = try denseMediaUrlWireBytes(capabilities, url);
            shape.encoded_media_bytes = std.math.add(usize, shape.encoded_media_bytes, wire_bytes) catch
                return error.InferenceEncodedBytesExceeded;
            if (capabilities.batch.max_encoded_media_bytes) |limit| {
                if (shape.encoded_media_bytes > limit) return error.InferenceEncodedBytesExceeded;
            }
            const pixels = try denseMediaUrlPixelsAlloc(alloc, url);
            shape.decoded_pixels = std.math.add(u64, shape.decoded_pixels, pixels) catch
                return error.InferenceDecodedPixelsExceeded;
            shape.max_media_parts_per_item = @max(shape.max_media_parts_per_item, 1);
        },
        .binary => |media| {
            if (media.data.len == 0) return error.InvalidInferenceMedia;
            mergeModalities(&shape.modalities, try modalityForContentType(media.mime_type));
            try capabilities.validateMimeType(media.mime_type);
            const resident = try attachment_transport.wireSize(media.data.len, media.mime_type.len);
            shape.encoded_media_bytes = std.math.add(usize, shape.encoded_media_bytes, resident) catch
                return error.InferenceEncodedBytesExceeded;
            const modality = try modalityForContentType(media.mime_type);
            if (modality.image) {
                const pixels = try inference_work.encodedImagePixels(media.mime_type, media.data);
                shape.decoded_pixels = std.math.add(u64, shape.decoded_pixels, pixels) catch
                    return error.InferenceDecodedPixelsExceeded;
            }
            shape.max_media_parts_per_item = @max(shape.max_media_parts_per_item, 1);
        },
    };
    try capabilities.validateInvocation(.embed, shape);
}

fn densePartBatchEnd(
    alloc: std.mem.Allocator,
    capabilities: inference_work.InferenceCapabilities,
    attachment_transport: inference_work.AttachmentTransport,
    items: []const template_mod.ContentPart,
    start: usize,
    metadata_options: embedding_wire.Options,
) !usize {
    if (start >= items.len) return start;
    // This descriptor describes the linked-worker ABI, not remote HTTP JSON.
    const metadata_limit = if (attachment_transport == .borrowed_binary) capabilities.attachment_metadata_max_bytes else null;
    const envelope_limit = if (attachment_transport == .borrowed_binary) capabilities.attachment_envelope_max_bytes else null;
    var metadata_sizer: ?embedding_wire.Sizer = if (metadata_limit != null or envelope_limit != null)
        try embedding_wire.Sizer.init(template_mod.ContentPart, metadata_options)
    else
        null;
    const max_items = capabilities.batch.max_items;
    var encoded_media_bytes: usize = 0;
    var decoded_pixels: u64 = 0;
    var end = start;
    while (end < items.len and end - start < max_items) : (end += 1) {
        const item_bytes: usize = switch (items[end]) {
            .text => 0,
            .binary => |value| if (value.data.len == 0)
                return error.InvalidInferenceMedia
            else
                try attachment_transport.wireSize(value.data.len, value.mime_type.len),
            .media_url => |url| try denseMediaUrlWireBytes(capabilities, url),
        };
        const next_bytes = std.math.add(usize, encoded_media_bytes, item_bytes) catch
            return error.InferenceEncodedBytesExceeded;
        if (capabilities.batch.max_encoded_media_bytes) |limit| {
            if (next_bytes > limit) {
                if (end == start) return error.InferenceEncodedBytesExceeded;
                break;
            }
        }
        const item_pixels: u64 = switch (items[end]) {
            .text => 0,
            .binary => |value| if ((try modalityForContentType(value.mime_type)).image)
                try inference_work.encodedImagePixels(value.mime_type, value.data)
            else
                0,
            .media_url => |url| try denseMediaUrlPixelsAlloc(alloc, url),
        };
        const next_pixels = std.math.add(u64, decoded_pixels, item_pixels) catch
            return error.InferenceDecodedPixelsExceeded;
        if (capabilities.batch.max_decoded_pixels) |limit| {
            if (next_pixels > limit) {
                if (end == start) return error.InferenceDecodedPixelsExceeded;
                break;
            }
        }
        if (metadata_sizer) |*sizer| {
            const metadata_bytes = try sizer.append(items[end]);
            // The smaller metadata ceiling applies only to physical attachments.
            // A text-only prefix can still fit when adding an image cannot.
            const metadata_exceeded = if (metadata_limit) |limit| sizer.attachment_count > 0 and metadata_bytes > limit else false;
            const envelope_exceeded = if (envelope_limit) |limit| try sizer.envelopeSize(metadata_bytes) > limit else false;
            if (metadata_exceeded or envelope_exceeded) {
                if (end == start) return error.BodyTooLarge;
                break;
            }
        }
        encoded_media_bytes = next_bytes;
        decoded_pixels = next_pixels;
    }
    return end;
}

test "managed embedder metadata sizing matches wire JSON at every prefix" {
    const alloc = std.testing.allocator;
    const options = embedding_wire.Options{ .model = "model\"\\\nλ", .task_type = "RETRIEVAL_DOCUMENT", .instruction = "\x00instruction" };
    var parts: [105]template_mod.ContentPart = undefined;
    var wire_parts: [105]template_mod.ContentPart = undefined;
    var payloads: [105]httpx.attachment_envelope.Attachment = undefined;
    var sizer = try embedding_wire.Sizer.init(template_mod.ContentPart, options);
    var attachments: usize = 0;
    for (&parts, &wire_parts, 0..) |*part, *wire_part, i| {
        part.* = if (i == 0) .{ .text = "\x00\n\"\\λ" } else if (i == 1) .{ .media_url = "https://example.test/\"λ" } else .{ .binary = .{ .mime_type = "image/png", .data = "\x00\xffpayload stays borrowed" } };
        wire_part.* = embedding_wire.metadataPart(part.*);
        if (part.* == .binary) {
            payloads[attachments] = .{ .mime_type = part.binary.mime_type, .data = part.binary.data };
            attachments += 1;
        }
        const measured = try sizer.append(part.*);
        // Independent literal catches drift in both fields and JSON encoding,
        // including attachment-count transitions through 9/10 and 99/100.
        const json = try std.json.Stringify.valueAlloc(alloc, .{
            .model = options.model,
            .parts = wire_parts[0 .. i + 1],
            .attachment_count = attachments,
            .task_type = options.task_type,
            .instruction = options.instruction,
        }, .{});
        defer alloc.free(json);
        try std.testing.expectEqual(json.len, measured);
        const body = try httpx.attachment_envelope.encodeAlloc(alloc, json, payloads[0..attachments]);
        defer alloc.free(body);
        try std.testing.expectEqual(body.len, try sizer.envelopeSize(measured));
    }
}

test "managed embedder metadata text-only windows respect the complete envelope limit" {
    const alloc = std.testing.allocator;
    const text = try alloc.alloc(u8, 6 * 1024 * 1024);
    defer alloc.free(text);
    @memset(text, 0);
    const items = [_]template_mod.ContentPart{ .{ .text = text }, .{ .text = text } };
    var caps = inference_work.InferenceCapabilities{
        .task = .embed,
        .input_modalities = .{ .text = true, .image = true },
        .input_granularity = .page,
        .batch = .{ .mode = .native, .max_items = 8, .preferred_items = 8, .max_media_parts_per_item = 1 },
        .output = .embedding,
        .attachment_metadata_max_bytes = 1024 * 1024,
        .attachment_envelope_max_bytes = 64 * 1024 * 1024,
    };
    const options = embedding_wire.Options{ .model = "model", .task_type = "RETRIEVAL_DOCUMENT" };
    // Each escaped item is ~36 MiB, but their combined JSON is ~72 MiB.
    // The complete envelope limit applies even with zero attachments.
    try std.testing.expectEqual(@as(usize, 1), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    try std.testing.expectEqual(@as(usize, 2), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 1, options));
    caps.attachment_metadata_max_bytes = null;
    try std.testing.expectEqual(@as(usize, 1), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    // An unrelated HTTP transport must not inherit the linked-worker ceiling.
    try std.testing.expectEqual(@as(usize, 2), try densePartBatchEnd(alloc, caps, .segmented_framed_binary, &items, 0, options));
    @memset(text, 'a');
    caps.attachment_metadata_max_bytes = 1024 * 1024;
    try std.testing.expectEqual(@as(usize, 2), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    var sizer = try embedding_wire.Sizer.init(template_mod.ContentPart, options);
    const single_metadata = try sizer.append(items[0]);
    caps.attachment_envelope_max_bytes = try sizer.envelopeSize(single_metadata);
    try std.testing.expectEqual(@as(usize, 1), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    caps.attachment_envelope_max_bytes.? -= 1;
    try std.testing.expectError(error.BodyTooLarge, densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    caps.attachment_envelope_max_bytes = null;
    try std.testing.expectEqual(@as(usize, 2), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
}

test "managed embedder metadata envelope sizing includes binary framing and payload" {
    const alloc = std.testing.allocator;
    var png = [_]u8{0} ** 24;
    @memcpy(png[0..8], "\x89PNG\r\n\x1a\n");
    std.mem.writeInt(u32, png[16..20], 2, .big);
    std.mem.writeInt(u32, png[20..24], 3, .big);
    const part = template_mod.ContentPart{ .binary = .{ .mime_type = "image/png", .data = &png } };
    const items = [_]template_mod.ContentPart{ part, part };
    const options = embedding_wire.Options{ .model = "model" };
    var sizer = try embedding_wire.Sizer.init(template_mod.ContentPart, options);
    const metadata_bytes = try sizer.append(part);
    var caps = inference_work.InferenceCapabilities{
        .task = .embed,
        .input_modalities = .{ .image = true },
        .input_granularity = .page,
        .batch = .{ .mode = .native, .max_items = 8, .preferred_items = 8, .max_media_parts_per_item = 1 },
        .output = .embedding,
        .attachment_metadata_max_bytes = 1024 * 1024,
        .attachment_envelope_max_bytes = try sizer.envelopeSize(metadata_bytes),
    };
    try std.testing.expectEqual(@as(usize, 1), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    caps.attachment_envelope_max_bytes.? -= 1;
    try std.testing.expectError(error.BodyTooLarge, densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
}

test "managed embedder metadata ceiling splits mixed batches before dispatch" {
    const alloc = std.testing.allocator;
    var png = [_]u8{0} ** 24;
    @memcpy(png[0..8], "\x89PNG\r\n\x1a\n");
    std.mem.writeInt(u32, png[16..20], 2, .big);
    std.mem.writeInt(u32, png[20..24], 3, .big);
    const binary = template_mod.ContentPart{ .binary = .{ .mime_type = "image/png", .data = &png } };
    const text = try alloc.alloc(u8, 600 * 1024);
    defer alloc.free(text);
    @memset(text, 'a');
    const items = [_]template_mod.ContentPart{ binary, .{ .text = text }, .{ .text = text } };
    var caps = inference_work.InferenceCapabilities{
        .task = .embed,
        .input_modalities = .{ .text = true, .image = true },
        .input_granularity = .page,
        .batch = .{ .mode = .native, .max_items = 8, .preferred_items = 8, .max_media_parts_per_item = 1 },
        .output = .embedding,
        .attachment_metadata_max_bytes = 1024 * 1024,
    };
    const options = embedding_wire.Options{ .model = "local-model", .task_type = "RETRIEVAL_DOCUMENT" };
    try std.testing.expectEqual(@as(usize, 2), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    try std.testing.expectEqual(@as(usize, 3), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 2, options));
    // A text-only prefix remains legal even above 1 MiB. Adding its first
    // physical attachment must start a new invocation, regardless of order.
    const reversed = [_]template_mod.ContentPart{ items[1], items[2], binary };
    try std.testing.expectEqual(@as(usize, 2), try densePartBatchEnd(alloc, caps, .borrowed_binary, &reversed, 0, options));
    try std.testing.expectEqual(@as(usize, 3), try densePartBatchEnd(alloc, caps, .borrowed_binary, &reversed, 2, options));
    @memset(text, 0);
    try std.testing.expectEqual(@as(usize, 1), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    // Exact boundary includes the model, task, instruction, and JSON syntax.
    var sizer = try embedding_wire.Sizer.init(template_mod.ContentPart, options);
    caps.attachment_metadata_max_bytes = try sizer.append(binary);
    try std.testing.expectEqual(@as(usize, 1), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    caps.attachment_metadata_max_bytes.? -= 1;
    try std.testing.expectError(error.BodyTooLarge, densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
    // A linked-worker ceiling must not be applied to a different wire format.
    try std.testing.expectEqual(@as(usize, 3), try densePartBatchEnd(alloc, caps, .segmented_framed_binary, &items, 0, options));
    caps.attachment_metadata_max_bytes = null;
    try std.testing.expectEqual(@as(usize, 3), try densePartBatchEnd(alloc, caps, .borrowed_binary, &items, 0, options));
}

test "managed embedder admission follows the selected attachment transport" {
    var bytes = [_]u8{0} ** 24;
    @memcpy(bytes[0..8], "\x89PNG\r\n\x1a\n");
    std.mem.writeInt(u32, bytes[16..20], 2, .big);
    std.mem.writeInt(u32, bytes[20..24], 3, .big);
    const items = [_]template_mod.ContentPart{
        .{ .binary = .{ .mime_type = "image/png", .data = &bytes } },
        .{ .binary = .{ .mime_type = "image/png", .data = &bytes } },
    };
    const capabilities = inference_work.InferenceCapabilities{
        .task = .embed,
        .input_modalities = .{ .image = true },
        .accepted_mime_types = .{ .image_png = true },
        .input_granularity = .page,
        .batch = .{
            .mode = .native,
            .preferred_items = 2,
            .max_items = 2,
            .max_encoded_media_bytes = 63,
            .max_media_parts_per_item = 1,
        },
        .output = .embedding,
    };
    try std.testing.expectEqual(
        @as(usize, 2),
        try densePartBatchEnd(std.testing.allocator, capabilities, .borrowed_binary, &items, 0, .{}),
    );
    try std.testing.expectEqual(
        @as(usize, 1),
        try densePartBatchEnd(std.testing.allocator, capabilities, .base64_payload, &items, 0, .{}),
    );
    try validateDensePartItemInvocation(std.testing.allocator, capabilities, .base64_payload, items[0..1]);
    try std.testing.expectError(
        error.InferenceEncodedBytesExceeded,
        validateDensePartItemInvocation(std.testing.allocator, capabilities, .base64_payload, &items),
    );
    var pixel_limited = capabilities;
    pixel_limited.batch.max_encoded_media_bytes = null;
    pixel_limited.batch.max_decoded_pixels = 6;
    try std.testing.expectEqual(
        @as(usize, 1),
        try densePartBatchEnd(std.testing.allocator, pixel_limited, .borrowed_binary, &items, 0, .{}),
    );
    try std.testing.expectError(
        error.InferenceDecodedPixelsExceeded,
        validateDensePartItemInvocation(std.testing.allocator, pixel_limited, .borrowed_binary, &items),
    );
}

test "managed embedder partitions and validates inline image data URIs" {
    const first = "data:image/png;base64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD";
    // The second payload is valid base64 but not an image. The encoded-byte
    // window closes before it, proving partitioning does not materialize or
    // inspect a payload that cannot fit the current invocation.
    const second = "data:image/png;base64,bm90IGFuIGltYWdl";
    const items = [_]template_mod.ContentPart{
        .{ .media_url = first },
        .{ .media_url = second },
    };
    const capabilities = inference_work.InferenceCapabilities{
        .task = .embed,
        .input_modalities = .{ .image = true },
        .accepted_mime_types = .{ .image_png = true },
        .input_granularity = .page,
        .batch = .{
            .mode = .native,
            .preferred_items = 2,
            .max_items = 2,
            .max_encoded_media_bytes = first.len + 1,
            .max_media_parts_per_item = 1,
        },
        .output = .embedding,
    };
    try std.testing.expectEqual(@as(usize, 1), try densePartBatchEnd(std.testing.allocator, capabilities, .base64_payload, &items, 0, .{}));
    try validateDensePartItemInvocation(std.testing.allocator, capabilities, .base64_payload, items[0..1]);
    try std.testing.expectError(
        error.InferenceEncodedBytesExceeded,
        validateDensePartItemInvocation(std.testing.allocator, capabilities, .base64_payload, &items),
    );
    try std.testing.expectError(
        error.InvalidDataURI,
        denseMediaUrlWireBytes(capabilities, "data:image/png;base64,"),
    );
    try std.testing.expectError(
        error.UnsupportedInferenceMimeType,
        denseMediaUrlWireBytes(capabilities, "data:audio/wav;base64,AQID"),
    );
    try std.testing.expectError(
        error.InvalidInferenceMedia,
        validateDensePartItemInvocation(std.testing.allocator, capabilities, .borrowed_binary, &.{.{
            .binary = .{ .mime_type = "image/png", .data = &.{} },
        }}),
    );
}

/// Embed a bounded window of independently addressable document assets. Each
/// part is one input and must produce exactly one vector; callers retain page
/// identity by position and never receive an implicit document-level pool.
fn embedPartItemsWithEntry(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    items: []const template_mod.ContentPart,
    dims: u32,
    planned_lease: inference_work.CapabilityLease,
) ![]const []const f32 {
    if (items.len == 0) return try alloc.alloc([]const f32, 0);
    if (!isAntflyProvider(entry.provider)) return error.UnsupportedEmbeddingProvider;

    if (entry.antfly_provider) |local| {
        const embed_parts = local.embed_dense_parts orelse return error.UnsupportedEmbeddingProvider;
        try checkEntryDispatchDeadline(entry);
        const context = embeddingRequestContext(entry, .retrieval_document);
        try context.check();
        const vectors = (if (local.embed_dense_parts_with_context) |with_context|
            AntflyProviderBoundary.call("embed_dense_parts_with_context", local.boundary_dispatch, with_context, .{ local.ptr, alloc, entry.model, items, context })
        else
            AntflyProviderBoundary.call("embed_dense_parts", local.boundary_dispatch, embed_parts, .{ local.ptr, alloc, entry.model, items })) catch |err|
            return normalizeLocalEmbeddingError(err);
        errdefer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
        try context.check();
        try validateDenseBatch(vectors, items.len, dims);
        return vectors;
    }

    try checkEntryDispatchDeadline(entry);
    const operation_deadline_ns = embeddingOperationDeadline(entry);
    var fallback_http: ?httpx.Client = null;
    defer if (fallback_http) |*client| client.deinit();
    const http = try entry.httpClient(alloc, &fallback_http);
    var provider = antfly_provider_mod.Provider.init(alloc, http, entry.base_url);
    defer provider.deinit();
    provider.attempt_observer = embeddingAttemptObserver(entry);
    provider.setRequestCancellation(entry.cancellation);
    try provider.setSourceTable(entry.source_table);
    var auth_header_owned: ?[]u8 = null;
    defer if (auth_header_owned) |value| alloc.free(value);
    var capability_header_storage: [2][2][]const u8 = undefined;
    var capability_header_count: usize = 0;
    if (entry.api_key) |*api_key_ref| {
        if (try optionalBearerAuthHeaderOwned(@constCast(entry), alloc, api_key_ref)) |auth_header| {
            auth_header_owned = auth_header;
            try provider.setAuthorizationHeader(auth_header);
            capability_header_storage[capability_header_count] = .{ "Authorization", auth_header };
            capability_header_count += 1;
        }
    }
    if (entry.source_table.len > 0) {
        capability_header_storage[capability_header_count] = .{ "X-Antfly-Source-Table", entry.source_table };
        capability_header_count += 1;
    }
    const capability_headers = capability_header_storage[0..capability_header_count];
    const capability_cache = entry.capabilityCache() orelse return error.InferenceCapabilitiesUnavailable;
    const planned_capabilities = planned_lease.capabilities orelse return error.EmbeddingCapabilitiesUnavailable;
    if (planned_lease.scope_digest) |scope| {
        const current_scope = try remote_capabilities.scopeDigest(alloc, entry.base_url, entry.model, .embed, capability_headers);
        if (!std.mem.eql(u8, &scope, &current_scope)) return error.InferenceCapabilitiesStale;
        const lease = planned_lease;
        const live = lease.capabilities orelse return error.InferenceCapabilitiesStale;
        try validateDensePartItemInvocation(alloc, live, if (live.framed_attachments) .segmented_framed_binary else .base64_payload, items);
        provider.setFramedAttachments(live.framed_attachments);
        if (lease.routing_token) |token| try provider.setCapabilityToken(token.slice());
        if (lease.descriptor_revision) |revision| try provider.setCapabilityRevision(revision.slice());
        if (live.numeric_responses_v1 and dims != 0) provider.numeric_dense_dimensions = dims;
    } else if (planned_capabilities.numeric_responses_v1 and dims != 0) {
        return error.InferenceCapabilitiesStale;
    } else _ = try bindRemoteEmbeddingPartsLease(
        alloc,
        entry,
        http,
        &provider,
        capability_headers,
        operation_deadline_ns,
        items,
        true,
    );
    try applyAntflyEmbeddingRequestControls(entry, &provider, operation_deadline_ns);
    if (provider.numeric_dense_dimensions != null) provider.setMaxResponseBytes(try numericDenseResponseLimit(items.len, dims));

    var result = provider.embedParts(alloc, entry.model, items) catch |err| switch (err) {
        error.EmptyResponse => return error.EmptyEmbeddingResponse,
        error.InferenceCapabilitiesStale => {
            try capability_cache.invalidate(entry.base_url, entry.model, .embed, capability_headers);
            return err;
        },
        else => return err,
    };
    errdefer result.deinit();
    try validateDenseBatch(result.vectors, items.len, dims);
    return try adoptDenseBatchResult(alloc, &result);
}

fn embedSparseWithEntry(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    text: []const u8,
) !db_embedder.SparseEmbedding {
    var batch = try embedSparseBatchWithEntry(alloc, entry, &.{text});
    errdefer db_embedder.freeSparseEmbeddingBatch(alloc, batch);
    if (batch.len == 0) return error.EmptyEmbeddingResponse;

    const embedding = batch[0];
    if (batch.len > 1) {
        for (batch[1..]) |*item| item.deinit(alloc);
    }
    alloc.free(batch);
    return embedding;
}

fn embedSparseBatchWithEntry(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
) ![]db_embedder.SparseEmbedding {
    try embeddingRequestContext(entry, .retrieval_document).request.updateDetail(
        if (entry.provider == .antfly) .loading_model else .executing,
        0,
        1,
        entry.model,
        @tagName(entry.provider),
    );
    switch (entry.provider) {
        .antfly => {
            if (entry.antfly_provider) |local| {
                try checkEntryDispatchDeadline(entry);
                const context = embeddingRequestContext(entry, .retrieval_document);
                try context.check();
                const embeddings = (if (local.embed_sparse_texts_with_context) |embed_with_context|
                    AntflyProviderBoundary.call("embed_sparse_texts_with_context", local.boundary_dispatch, embed_with_context, .{ local.ptr, alloc, entry.model, texts, context })
                else
                    AntflyProviderBoundary.call("embed_sparse_texts", local.boundary_dispatch, local.embed_sparse_texts, .{ local.ptr, alloc, entry.model, texts })) catch |err|
                    return normalizeLocalEmbeddingError(err);
                errdefer db_embedder.freeSparseEmbeddingBatch(alloc, embeddings);
                try context.check();
                try validateSparseBatch(embeddings, texts.len);
                return embeddings;
            }
            try checkEntryDispatchDeadline(entry);
            const operation_deadline_ns = embeddingOperationDeadline(entry);
            var fallback_http: ?httpx.Client = null;
            defer if (fallback_http) |*client| client.deinit();
            const http = try entry.httpClient(alloc, &fallback_http);

            var provider = antfly_provider_mod.Provider.init(alloc, http, entry.base_url);
            defer provider.deinit();
            provider.attempt_observer = embeddingAttemptObserver(entry);
            provider.setRequestCancellation(entry.cancellation);
            try provider.setSourceTable(entry.source_table);
            var auth_header_owned: ?[]u8 = null;
            defer if (auth_header_owned) |value| alloc.free(value);
            if (entry.api_key) |*api_key_ref| {
                if (try optionalBearerAuthHeaderOwned(@constCast(entry), alloc, api_key_ref)) |auth_header| {
                    auth_header_owned = auth_header;
                    try provider.setAuthorizationHeader(auth_header);
                }
            }
            var capability_header_storage: [2][2][]const u8 = undefined;
            const capability_headers = remoteEmbeddingHeaders(entry, auth_header_owned, &capability_header_storage);
            _ = try bindRemoteEmbeddingLease(
                entry,
                http,
                &provider,
                capability_headers,
                operation_deadline_ns,
                try textEmbeddingInvocationShape(texts),
            );
            try applyAntflyEmbeddingRequestControls(entry, &provider, operation_deadline_ns);

            var result = provider.embedSparse(alloc, entry.model, texts) catch |err| {
                if (err == error.InferenceCapabilitiesStale)
                    try invalidateRemoteEmbeddingLease(entry, capability_headers);
                return err;
            };
            defer result.deinit();
            if (result.indices.len == 0) return error.EmptyEmbeddingResponse;
            if (result.indices.len != texts.len or result.values.len != texts.len) return error.InvalidEmbeddingResponse;

            const embeddings = try alloc.alloc(db_embedder.SparseEmbedding, result.indices.len);
            var initialized: usize = 0;
            errdefer {
                for (embeddings[0..initialized]) |*embedding| embedding.deinit(alloc);
                alloc.free(embeddings);
            }

            for (result.indices, result.values, 0..) |src_indices, src_values, i| {
                if (src_indices.len != src_values.len) return error.InvalidEmbeddingResponse;
                for (src_values) |value| {
                    if (!std.math.isFinite(value)) return error.InvalidEmbeddingResponse;
                }
                const indices = try alloc.alloc(u32, src_indices.len);
                errdefer alloc.free(indices);
                for (src_indices, 0..) |value, j| {
                    if (value < 0) return error.InvalidEmbeddingResponse;
                    indices[j] = @intCast(value);
                }
                embeddings[i] = .{
                    .indices = indices,
                    .values = try alloc.dupe(f32, src_values),
                };
                initialized += 1;
            }
            try validateSparseBatch(embeddings, texts.len);
            return embeddings;
        },
        .openai, .ollama, .bedrock, .cohere, .gemini, .vertex => return error.UnsupportedEmbeddingProvider,
    }
}

fn partsContainMedia(parts: []const template_mod.ContentPart) bool {
    for (parts) |part| {
        switch (part) {
            .media_url, .binary => return true,
            .text => {},
        }
    }
    return false;
}

pub fn testTextOnlyManagedProvidersRejectMedia() !void {
    var managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"vertex_idx":{"type":"embeddings","field":"body","dimension":3072,"embedder":{"provider":"vertex","model":"gemini-embedding-001","project_id":"test-project","location":"us-central1"}}}
    );
    defer managed.deinit();

    const parts = [_]template_mod.ContentPart{
        .{ .text = "caption" },
        .{ .binary = .{ .mime_type = "image/png", .data = &.{ 1, 2, 3 } } },
    };
    try std.testing.expectError(
        error.UnsupportedEmbeddingProvider,
        embedWithEntryParts(std.testing.allocator, &managed.entries[0], &parts, 3072),
    );
}

fn resolveOpenAiBaseUrl(alloc: std.mem.Allocator, embedder: embeddings_types.Config) ![]u8 {
    const raw = try resolveConfigString(
        alloc,
        if (embedder.url.len > 0) embedder.url else null,
        "OPENAI_BASE_URL",
        provider_defaults.openai_origin,
    );
    defer alloc.free(raw);
    return try appendPathIfMissing(alloc, raw, "/v1");
}

fn resolveOllamaBaseUrl(alloc: std.mem.Allocator, embedder: embeddings_types.Config) ![]u8 {
    const raw = try resolveConfigString(
        alloc,
        if (embedder.url.len > 0) embedder.url else null,
        "OLLAMA_HOST",
        provider_defaults.ollama_origin,
    );
    defer alloc.free(raw);
    return try appendPathIfMissing(alloc, raw, "/v1");
}

fn resolveCohereBaseUrl(alloc: std.mem.Allocator, embedder: embeddings_types.Config) ![]u8 {
    const raw = try resolveConfigString(
        alloc,
        if (embedder.url.len > 0) embedder.url else null,
        "COHERE_BASE_URL",
        provider_defaults.cohere_origin,
    );
    defer alloc.free(raw);
    return try appendPathIfMissing(alloc, raw, "/v2");
}

fn resolveGeminiBaseUrl(alloc: std.mem.Allocator, embedder: embeddings_types.Config) ![]u8 {
    const raw = try resolveConfigString(
        alloc,
        if (embedder.url.len > 0) embedder.url else null,
        "GEMINI_BASE_URL",
        provider_defaults.gemini_v1beta_base,
    );
    defer alloc.free(raw);
    return try alloc.dupe(u8, std.mem.trimEnd(u8, raw, "/"));
}

fn resolveVertexLocation(alloc: std.mem.Allocator, embedder: embeddings_types.Config) ![]u8 {
    return try resolveConfigString(
        alloc,
        if (embedder.location.len > 0) embedder.location else null,
        "GOOGLE_CLOUD_LOCATION",
        provider_defaults.default_google_location,
    );
}

fn resolveVertexBaseUrl(alloc: std.mem.Allocator, embedder: embeddings_types.Config, location: []const u8) ![]u8 {
    return try provider_defaults.vertexRegionalV1BaseAlloc(alloc, embedder.url, location);
}

fn resolveAntflyInferenceBaseUrl(alloc: std.mem.Allocator, embedder: embeddings_types.Config, options: InitOptions) ![]u8 {
    const raw = if (embedder.url.len > 0)
        try alloc.dupe(u8, embedder.url)
    else if (resolveOptionalEnv(alloc, "ANTFLY_INFERENCE_URL")) |value|
        value
    else if (configuredDefaultAntflyInferenceURL(options)) |value|
        try alloc.dupe(u8, value)
    else
        try alloc.dupe(u8, "http://localhost:8082");
    defer alloc.free(raw);
    return try normalizeAntflyInferenceBaseUrl(alloc, raw);
}

fn configuredDefaultAntflyInferenceURL(options: InitOptions) ?[]const u8 {
    const value = options.inference_api_url orelse return null;
    const trimmed = std.mem.trim(u8, value, " \t\r\n");
    if (trimmed.len == 0) return null;
    return trimmed;
}

fn normalizeAntflyInferenceBaseUrl(alloc: std.mem.Allocator, raw: []const u8) ![]u8 {
    const trimmed = std.mem.trimEnd(u8, raw, "/");
    if (std.mem.endsWith(u8, trimmed, "/ai/v1")) return try alloc.dupe(u8, trimmed);

    const scheme_pos = std.mem.indexOf(u8, trimmed, "://");
    const host_start = if (scheme_pos) |pos| pos + 3 else 0;
    const path_pos = std.mem.indexOfPos(u8, trimmed, host_start, "/");
    if (path_pos == null) return try std.fmt.allocPrint(alloc, "{s}/ai/v1", .{trimmed});

    return error.InvalidAntflyInferenceBaseUrl;
}

fn resolveBedrockRegion(alloc: std.mem.Allocator, embedder: embeddings_types.Config) ![]u8 {
    if (embedder.region.len > 0) return try alloc.dupe(u8, embedder.region);
    if (resolveOptionalEnv(alloc, "AWS_REGION")) |value| return value;
    if (resolveOptionalEnv(alloc, "AWS_DEFAULT_REGION")) |value| return value;
    return try alloc.dupe(u8, provider_defaults.default_aws_region);
}

fn resolveBedrockEndpoint(alloc: std.mem.Allocator, embedder: embeddings_types.Config, region: []const u8) ![]u8 {
    return try provider_defaults.bedrockRuntimeEndpointAlloc(alloc, embedder.url, region);
}

fn resolveConfigString(
    alloc: std.mem.Allocator,
    configured_value: ?[]const u8,
    env_name: []const u8,
    default_value: []const u8,
) ![]u8 {
    if (configured_value) |value| return try alloc.dupe(u8, value);
    if (resolveOptionalEnv(alloc, env_name)) |value| return value;
    return try alloc.dupe(u8, default_value);
}

fn resolveOptionalConfigString(
    alloc: std.mem.Allocator,
    configured_value: ?[]const u8,
    env_name: []const u8,
) !?[]u8 {
    if (configured_value) |value| return try alloc.dupe(u8, value);
    return resolveOptionalEnv(alloc, env_name);
}

fn resolveOptionalEnv(alloc: std.mem.Allocator, env_name: []const u8) ?[]u8 {
    const name_z = alloc.dupeZ(u8, env_name) catch return null;
    defer alloc.free(name_z);
    const value_z = getenv(name_z.ptr) orelse return null;
    return alloc.dupe(u8, std.mem.span(value_z)) catch null;
}

fn appendPathIfMissing(alloc: std.mem.Allocator, raw: []const u8, suffix: []const u8) ![]u8 {
    if (std.mem.endsWith(u8, raw, suffix)) return try alloc.dupe(u8, raw);

    const scheme_pos = std.mem.indexOf(u8, raw, "://");
    const host_start = if (scheme_pos) |pos| pos + 3 else 0;
    const path_pos = std.mem.indexOfPos(u8, raw, host_start, "/");
    if (path_pos == null) return try std.fmt.allocPrint(alloc, "{s}{s}", .{ raw, suffix });
    if (path_pos.? == raw.len - 1) {
        return try std.fmt.allocPrint(alloc, "{s}{s}", .{ raw[0 .. raw.len - 1], suffix });
    }
    return try alloc.dupe(u8, raw);
}

fn embedWithEntry(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    text: []const u8,
    dims: u32,
) ![]f32 {
    return embedWithEntryForTask(alloc, entry, text, dims, .retrieval_document);
}

fn embedWithEntryForTask(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    text: []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]f32 {
    const vectors = try embedBatchWithEntryForTask(alloc, entry, &.{text}, dims, task_type);
    errdefer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
    if (vectors.len == 0) return error.EmptyEmbeddingResponse;

    const vector = try alloc.dupe(f32, vectors[0]);
    db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
    return vector;
}

fn embedBatchWithEntry(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
) ![]const []const f32 {
    return embedBatchWithEntryForTask(alloc, entry, texts, dims, .retrieval_document);
}

fn effectiveInputType(entry: *const ManagedEmbeddingEntry, task_type: EmbeddingTaskType) []const u8 {
    const role_override = switch (task_type) {
        .retrieval_query => entry.query_input_type,
        .retrieval_document => entry.document_input_type,
    };
    if (role_override.len > 0) return role_override;
    // Backward-compatible expert override: the legacy field applies to both
    // roles. New configurations should prefer the role-specific fields.
    if (entry.input_type.len > 0) return entry.input_type;
    return task_type.cohereInputType();
}

fn effectiveInstruction(entry: *const ManagedEmbeddingEntry, task_type: EmbeddingTaskType) ?[]const u8 {
    if (task_type != .retrieval_query or entry.query_instruction.len == 0) return null;
    return entry.query_instruction;
}

pub fn testEmbeddingTaskRouting() !void {
    const entry = ManagedEmbeddingEntry{
        .alloc = std.testing.allocator,
        .index_name = @constCast("semantic"),
        .provider = .bedrock,
        .model = @constCast("cohere.embed-v4:0"),
        .base_url = @constCast("https://bedrock.example"),
        .dimensions = 1024,
        .query_input_type = @constCast("custom_query"),
    };
    try std.testing.expectEqualStrings("custom_query", effectiveInputType(&entry, .retrieval_query));
    try std.testing.expectEqualStrings("search_document", effectiveInputType(&entry, .retrieval_document));
    try std.testing.expect(effectiveInstruction(&entry, .retrieval_query) == null);
    try std.testing.expect(effectiveInstruction(&entry, .retrieval_document) == null);

    const query_context = embeddingRequestContext(&entry, .retrieval_query);
    try std.testing.expectEqual(EmbeddingTaskType.retrieval_query, query_context.task_type);
    try std.testing.expect(query_context.instruction == null);
    const document_context = embeddingRequestContext(&entry, .retrieval_document);
    try std.testing.expectEqual(EmbeddingTaskType.retrieval_document, document_context.task_type);
    try std.testing.expect(document_context.instruction == null);

    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"provider":"bedrock","model":"cohere.embed-v4:0","region":"us-east-1","retrieval":{"query_input_type":"search_query","document_input_type":"search_document"}}
    , .{});
    defer parsed.deinit();
    var config = try parseEmbedderConfigFromValue(std.testing.allocator, parsed.value);
    defer config.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("search_query", config.query_input_type);
    try std.testing.expectEqualStrings("search_document", config.document_input_type);
    try std.testing.expectEqualStrings("", config.query_instruction);

    var antfly_parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"provider":"antfly","model":"Qwen/Qwen3-Embedding-0.6B-GGUF","retrieval":{"query_instruction":"retrieve passages"}}
    , .{});
    defer antfly_parsed.deinit();
    var antfly_config = try parseEmbedderConfigFromValue(std.testing.allocator, antfly_parsed.value);
    defer antfly_config.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("retrieve passages", antfly_config.query_instruction);
}

test "managed embeddings derive provider task types from query and document operations" {
    try testEmbeddingTaskRouting();
}

fn embedBatchWithEntryForTask(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    const trace_batch = entry.provider == .antfly and entry.antfly_provider != null and
        getenv("ANTFLY_EMBED_TRACE_DIR") != null and traced_local_batches.fetchAdd(1, .monotonic) < 64;
    const trace_started = if (trace_batch) monotonicNowNs() else 0;
    var provider_started: u64 = 0;
    var provider_finished: u64 = 0;
    var trace_success = false;
    defer if (trace_batch) {
        const finished = monotonicNowNs();
        std.log.info("managed embed trace items={d} started_ns={d} provider_started_ns={d} provider_finished_ns={d} total_ns={d} pacer_context_ns={d} success={}", .{
            texts.len,                 trace_started,                                                                              provider_started, provider_finished,
            finished -| trace_started, if (provider_started > 0) provider_started -| trace_started else finished -| trace_started, trace_success,
        });
    };
    try embeddingRequestContext(entry, task_type).request.updateDetail(
        if (entry.provider == .antfly) .loading_model else .executing,
        0,
        1,
        entry.model,
        @tagName(entry.provider),
    );
    switch (entry.provider) {
        .openai, .ollama => {
            if (entry.requests_per_minute > 0 and texts.len > entry.burst) {
                return try embedBatchWithOpenAiCompatiblePacedChunks(alloc, entry, texts, dims, task_type);
            }
            return try embedBatchWithOpenAiCompatible(alloc, entry, texts, dims, task_type);
        },
        .bedrock => {
            return try embedBatchWithBedrock(alloc, entry, texts, dims, task_type);
        },
        .cohere => return try embedBatchWithCohere(alloc, entry, texts, dims, task_type),
        .gemini => return try embedBatchWithGemini(alloc, entry, texts, dims, task_type),
        .vertex => return try embedBatchWithVertex(alloc, entry, texts, dims, task_type),
        .antfly => {
            if (entry.antfly_provider) |local| {
                try checkEntryDispatchDeadline(entry);
                const context = embeddingRequestContext(entry, task_type);
                try context.check();
                if (trace_batch) provider_started = monotonicNowNs();
                const vectors = (if (local.embed_dense_texts_with_context) |embed_with_context|
                    AntflyProviderBoundary.call("embed_dense_texts_with_context", local.boundary_dispatch, embed_with_context, .{ local.ptr, alloc, entry.model, texts, context })
                else
                    AntflyProviderBoundary.call("embed_dense_texts", local.boundary_dispatch, local.embed_dense_texts, .{ local.ptr, alloc, entry.model, texts })) catch |err|
                    return normalizeLocalEmbeddingError(err);
                if (trace_batch) provider_finished = monotonicNowNs();
                errdefer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
                // The errdefer is the sole owner of failure cleanup. Native
                // kernels may return successfully after lifecycle cancellation;
                // manually freeing here as well would double-release their
                // allocator-owned result while unwinding the cancellation.
                try context.check();
                try validateDenseBatch(vectors, texts.len, dims);
                trace_success = true;
                return vectors;
            }
            try checkEntryDispatchDeadline(entry);
            const operation_deadline_ns = embeddingOperationDeadline(entry);
            var fallback_http: ?httpx.Client = null;
            defer if (fallback_http) |*client| client.deinit();
            const http = try entry.httpClient(alloc, &fallback_http);

            var provider = antfly_provider_mod.Provider.init(alloc, http, entry.base_url);
            defer provider.deinit();
            provider.attempt_observer = embeddingAttemptObserver(entry);
            provider.setRequestCancellation(entry.cancellation);
            try provider.setSourceTable(entry.source_table);
            var auth_header_owned: ?[]u8 = null;
            defer if (auth_header_owned) |value| alloc.free(value);
            if (entry.api_key) |*api_key_ref| {
                if (try optionalBearerAuthHeaderOwned(@constCast(entry), alloc, api_key_ref)) |auth_header| {
                    auth_header_owned = auth_header;
                    try provider.setAuthorizationHeader(auth_header);
                }
            }
            var capability_header_storage: [2][2][]const u8 = undefined;
            const capability_headers = remoteEmbeddingHeaders(entry, auth_header_owned, &capability_header_storage);
            _ = try bindRemoteEmbeddingLease(
                entry,
                http,
                &provider,
                capability_headers,
                operation_deadline_ns,
                try textEmbeddingInvocationShape(texts),
            );
            try applyAntflyEmbeddingRequestControls(entry, &provider, operation_deadline_ns);

            var result = provider.embedWithTask(
                alloc,
                entry.model,
                texts,
                task_type.canonical(),
                effectiveInstruction(entry, task_type),
            ) catch |err| {
                if (err == error.InferenceCapabilitiesStale)
                    try invalidateRemoteEmbeddingLease(entry, capability_headers);
                return err;
            };
            errdefer result.deinit();
            try validateDenseBatch(result.vectors, texts.len, dims);
            return try adoptDenseBatchResult(alloc, &result);
        },
    }
}

fn embeddingHttpCancellation(entry: *const ManagedEmbeddingEntry) ?httpx.CancellationToken {
    return if (entry.cancellation) |token|
        httpx.CancellationToken.fromCallback(token.ptr, token.is_cancelled_fn)
    else
        null;
}

fn effectiveProviderTaskType(entry: *const ManagedEmbeddingEntry, task_type: EmbeddingTaskType) []const u8 {
    const role_override = switch (task_type) {
        .retrieval_query => entry.query_input_type,
        .retrieval_document => entry.document_input_type,
    };
    return if (role_override.len > 0) role_override else task_type.canonical();
}

fn embedBatchWithGemini(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    try checkEntryDispatchDeadline(entry);
    var fallback_http: ?httpx.Client = null;
    defer if (fallback_http) |*client| client.deinit();
    const http = try entry.httpClient(alloc, &fallback_http);
    const api_key_ref = if (entry.api_key) |*value| value else return error.MissingEmbeddingApiKey;
    const api_key = (try api_key_ref.resolveOwned(alloc, entry.secret_store)) orelse return error.MissingEmbeddingApiKey;
    defer alloc.free(api_key);
    var provider = try vertex_provider.GeminiProvider.init(alloc, http, .{
        .base_url = entry.base_url,
        .api_key = api_key,
    });
    defer provider.deinit();
    provider.attempt_observer = embeddingAttemptObserver(entry);
    var result = try provider.embedText(alloc, entry.model, texts, .{
        .task_type = effectiveProviderTaskType(entry, task_type),
        .dimensions = if (dims > 0) dims else null,
        .cancellation = embeddingHttpCancellation(entry),
    });
    errdefer result.deinit();
    try validateDenseBatch(result.vectors, texts.len, dims);
    return try adoptDenseBatchResult(alloc, &result);
}

fn embedBatchWithVertex(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    if (texts.len == 0) return error.EmptyEmbeddingResponse;
    const auth_control = @import("antfly_google").RequestControl{
        .deadline_ns = embeddingOperationDeadline(entry),
        .cancellation = embeddingHttpCancellation(entry),
    };
    try auth_control.check();
    var fallback_http: ?httpx.Client = null;
    defer if (fallback_http) |*client| client.deinit();
    const http = try entry.httpClient(alloc, &fallback_http);
    const token_source = if (entry.google_credentials) |manager|
        manager.tokenSourceWithControl(
            if (entry.credentials_path.len > 0) entry.credentials_path else null,
            vertex_provider.vertex_auth_scope,
            auth_control,
        ) catch |err| switch (err) {
            error.OutOfMemory, error.Cancelled, error.Timeout => return err,
            else => return error.MissingVertexCredentials,
        }
    else
        null;
    var provider = try vertex_provider.Provider.init(alloc, http, .{
        .base_url = entry.base_url,
        .project_id = if (entry.project_id.len > 0) entry.project_id else null,
        .location = entry.location,
        .credentials_path = if (entry.credentials_path.len > 0) entry.credentials_path else null,
        .token_source = token_source,
        .request_control = auth_control,
    });
    defer provider.deinit();
    provider.attempt_observer = embeddingAttemptObserver(entry);

    var out = std.ArrayListUnmanaged([]const f32).empty;
    errdefer {
        for (out.items) |vector| alloc.free(vector);
        out.deinit(alloc);
    }

    const max_batch = provider_defaults.vertexMaxEmbeddingBatchSize(entry.model);
    var offset: usize = 0;
    while (offset < texts.len) {
        const end = cappedEmbeddingBatchEnd(offset, texts.len, max_batch);
        try checkEntryDispatchDeadline(entry);
        var result = try provider.embedTextRequest(alloc, entry.model, texts[offset..end], .{
            .task_type = effectiveProviderTaskType(entry, task_type),
            .dimensions = if (dims > 0) dims else null,
            .timeout_ms = if (entry.bounded_http_request) try embeddingRemainingTimeoutMs(embeddingOperationDeadline(entry)) else null,
            .cancellation = embeddingHttpCancellation(entry),
        });
        errdefer result.deinit();
        try validateDenseBatch(result.vectors, end - offset, dims);
        try out.ensureUnusedCapacity(alloc, result.vectors.len);
        const vectors = try adoptDenseBatchResult(alloc, &result);
        for (vectors) |vector| out.appendAssumeCapacity(vector);
        alloc.free(vectors);
        offset = end;
    }
    return try out.toOwnedSlice(alloc);
}

fn embedBatchWithCohere(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    var out = std.ArrayListUnmanaged([]const f32).empty;
    errdefer {
        for (out.items) |vector| alloc.free(vector);
        out.deinit(alloc);
    }

    var offset: usize = 0;
    while (offset < texts.len) {
        const end = cappedEmbeddingBatchEnd(
            offset,
            texts.len,
            provider_defaults.cohere_max_embedding_batch_size,
        );
        const vectors = try embedBatchWithCohereRequest(alloc, entry, texts[offset..end], dims, task_type);
        errdefer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
        try out.ensureUnusedCapacity(alloc, vectors.len);
        for (vectors) |vector| out.appendAssumeCapacity(vector);
        alloc.free(vectors);
        offset = end;
    }
    return try out.toOwnedSlice(alloc);
}

fn embedBatchWithCohereRequest(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    const Request = struct {
        model: []const u8,
        texts: []const []const u8,
        input_type: []const u8,
        embedding_types: []const []const u8 = &.{"float"},
        output_dimension: ?u32 = null,
        truncate: ?[]const u8 = null,
    };
    const Response = struct {
        embeddings: struct { float: []const []const f32 = &.{} },
    };
    const body = try std.json.Stringify.valueAlloc(alloc, Request{
        .model = entry.model,
        .texts = texts,
        .input_type = effectiveInputType(entry, task_type),
        .output_dimension = if (dims > 0 and std.mem.indexOf(u8, entry.model, "v4") != null) dims else null,
        .truncate = if (entry.truncate.len > 0) entry.truncate else null,
    }, .{ .emit_null_optional_fields = false });
    defer alloc.free(body);
    const url = try std.fmt.allocPrint(alloc, "{s}/embed", .{entry.base_url});
    defer alloc.free(url);
    const api_key_ref = if (entry.api_key) |*value| value else return error.MissingEmbeddingApiKey;
    const auth_header = (try optionalBearerAuthHeaderOwned(@constCast(entry), alloc, api_key_ref)) orelse
        return error.MissingEmbeddingApiKey;
    defer alloc.free(auth_header);
    const headers = [_][2][]const u8{
        .{ "content-type", "application/json" },
        .{ "authorization", auth_header },
    };
    try checkEntryDispatchDeadline(entry);
    var fallback_http: ?httpx.Client = null;
    defer if (fallback_http) |*client| client.deinit();
    const client = try entry.httpClient(alloc, &fallback_http);
    var response = try client.post(url, .{
        .attempt_observer = embeddingAttemptObserver(entry),
        .json = body,
        .headers = &headers,
        .cancellation = embeddingHttpCancellation(entry),
    });
    defer response.deinit();
    if (!response.ok()) return mapEmbedStatus(response.status.code);
    var parsed = try std.json.parseFromSlice(Response, alloc, response.body orelse return error.EmptyEmbeddingResponse, .{ .ignore_unknown_fields = true });
    defer parsed.deinit();
    try validateDenseBatch(parsed.value.embeddings.float, texts.len, dims);
    const vectors = try alloc.alloc([]const f32, texts.len);
    var initialized: usize = 0;
    errdefer {
        for (vectors[0..initialized]) |vector| alloc.free(vector);
        alloc.free(vectors);
    }
    for (parsed.value.embeddings.float, 0..) |vector, i| {
        vectors[i] = try alloc.dupe(f32, vector);
        initialized += 1;
    }
    return vectors;
}

fn embedBatchWithBedrock(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    const max_batch = bedrock_provider.maxBatchSizeForFormat(entry.bedrock_request_format);
    var out = std.ArrayListUnmanaged([]const f32).empty;
    errdefer {
        for (out.items) |vector| alloc.free(vector);
        out.deinit(alloc);
    }

    var offset: usize = 0;
    while (offset < texts.len) {
        const end = @min(texts.len, offset + max_batch);
        const vectors = try embedBatchWithBedrockRequest(alloc, entry, texts[offset..end], dims, task_type);
        errdefer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
        try out.ensureUnusedCapacity(alloc, vectors.len);
        for (vectors) |vector| out.appendAssumeCapacity(vector);
        alloc.free(vectors);
        offset = end;
    }
    return try out.toOwnedSlice(alloc);
}

fn embedBatchWithBedrockRequest(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    try checkEntryDispatchDeadline(entry);
    var fallback_http: ?httpx.Client = null;
    defer if (fallback_http) |*client| client.deinit();
    const http = try entry.httpClient(alloc, &fallback_http);
    var provider = bedrock_provider.Provider.initWithCredentialCache(alloc, http, .{
        .region = entry.region,
        .endpoint = entry.base_url,
        .request_format = entry.bedrock_request_format,
        .attempt_observer = embeddingAttemptObserver(entry),
        .input_type = effectiveInputType(entry, task_type),
        .truncate = entry.truncate,
        .dimension = dims,
        .cancellation = entry.cancellation,
        .timeout_ms = try embeddingRemainingTimeoutMs(embeddingOperationDeadline(entry)),
    }, entry.bedrock_credentials orelse return error.MissingBedrockCredentialCache);
    defer provider.deinit();
    var result = try provider.embedText(alloc, entry.model, texts);
    errdefer result.deinit();
    try validateDenseBatch(result.vectors, texts.len, dims);
    return try adoptDenseBatchResult(alloc, &result);
}

fn embedBatchWithOpenAiCompatiblePacedChunks(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    const chunk_size = @max(@as(usize, 1), @as(usize, @intCast(entry.burst)));
    var out = std.ArrayListUnmanaged([]const f32).empty;
    errdefer {
        for (out.items) |vector| alloc.free(vector);
        out.deinit(alloc);
    }

    var offset: usize = 0;
    while (offset < texts.len) {
        const end = @min(texts.len, offset + chunk_size);
        const vectors = try embedBatchWithOpenAiCompatible(alloc, entry, texts[offset..end], dims, task_type);
        errdefer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
        try out.ensureUnusedCapacity(alloc, vectors.len);
        for (vectors) |vector| out.appendAssumeCapacity(vector);
        alloc.free(vectors);
        offset = end;
    }
    return try out.toOwnedSlice(alloc);
}

fn embedBatchWithOpenAiCompatible(
    alloc: std.mem.Allocator,
    entry: *const ManagedEmbeddingEntry,
    texts: []const []const u8,
    dims: u32,
    task_type: EmbeddingTaskType,
) ![]const []const f32 {
    _ = task_type;
    const Request = openai_api.types.CreateEmbeddingRequest;
    const Response = struct {
        data: []const struct {
            embedding: []const f32,
        },
    };

    var input_array = std.json.Array.init(alloc);
    defer input_array.deinit();
    for (texts) |text| try input_array.append(.{ .string = text });

    const url = try std.fmt.allocPrint(alloc, "{s}/embeddings", .{entry.base_url});
    defer alloc.free(url);
    const json_body = try httpx.json.Json.stringify(alloc, Request{
        .model = .{ .string = entry.model },
        .input = .{ .array = input_array },
        .dimensions = if (dims > 0) dims else null,
    });
    defer alloc.free(json_body);

    const auth_header = if (entry.api_key) |*api_key_ref|
        try optionalBearerAuthHeaderOwned(@constCast(entry), alloc, api_key_ref)
    else
        null;
    defer if (auth_header) |value| alloc.free(value);

    var headers_buf: [2][2][]const u8 = undefined;
    headers_buf[0] = .{ "content-type", "application/json" };
    const header_count: usize = if (auth_header != null) 2 else 1;
    if (auth_header) |value| {
        headers_buf[1] = .{ "authorization", value };
    }

    try checkEntryDispatchDeadline(entry);

    var fallback_http: ?httpx.Client = null;
    defer if (fallback_http) |*client| client.deinit();
    const client = try entry.httpClient(alloc, &fallback_http);

    var response = try client.post(url, .{
        .attempt_observer = embeddingAttemptObserver(entry),
        .json = json_body,
        .headers = headers_buf[0..header_count],
        .timeout_ms = try embeddingRemainingTimeoutMs(embeddingOperationDeadline(entry)),
        .cancellation = if (entry.cancellation) |token|
            httpx.CancellationToken.fromCallback(token.ptr, token.is_cancelled_fn)
        else
            null,
    });
    defer response.deinit();
    if (!response.ok()) return mapEmbedStatus(response.status.code);
    const response_body = response.body orelse return error.EmptyEmbeddingResponse;

    var parsed = std.json.parseFromSlice(Response, alloc, response_body, .{ .ignore_unknown_fields = true }) catch |err| return err;
    defer parsed.deinit();

    if (parsed.value.data.len == 0) return error.EmptyEmbeddingResponse;
    if (parsed.value.data.len != texts.len) return error.InvalidEmbeddingResponse;

    const vectors = try alloc.alloc([]const f32, parsed.value.data.len);
    var initialized: usize = 0;
    errdefer {
        for (vectors[0..initialized]) |vector| alloc.free(@constCast(vector));
        alloc.free(vectors);
    }
    for (parsed.value.data, 0..) |item, i| {
        try validateDenseVector(item.embedding, dims);
        vectors[i] = try alloc.dupe(f32, item.embedding);
        initialized += 1;
    }
    return vectors;
}

fn optionalBearerAuthHeaderOwned(
    entry: *ManagedEmbeddingEntry,
    alloc: std.mem.Allocator,
    api_key_ref: *const common_secrets.SecretValue,
) !?[]u8 {
    return entry.auth_header_cache.getOwned(entry.alloc, alloc, api_key_ref, entry.secret_store) catch |err| switch (err) {
        error.SecretNotFound => switch (api_key_ref.*) {
            .env_var => return null,
            else => return err,
        },
        else => return err,
    };
}

fn mapEmbedStatus(status: u16) anyerror {
    return switch (status) {
        429 => error.EmbedRateLimited,
        408,
        502,
        503,
        504,
        => error.EmbedTransientFailure,
        else => if (status >= 500 and status < 600) error.EmbedTransientFailure else error.EmbedRequestFailed,
    };
}

fn cappedEmbeddingBatchEnd(offset: usize, total: usize, maximum: usize) usize {
    std.debug.assert(offset <= total);
    std.debug.assert(maximum > 0);
    return offset + @min(total - offset, maximum);
}

pub fn testCohereBatchLimit() !void {
    const maximum = provider_defaults.cohere_max_embedding_batch_size;
    try std.testing.expectEqual(@as(usize, 96), cappedEmbeddingBatchEnd(0, 97, maximum));
    try std.testing.expectEqual(@as(usize, 97), cappedEmbeddingBatchEnd(96, 97, maximum));
    try std.testing.expectEqual(@as(usize, 12), cappedEmbeddingBatchEnd(0, 12, maximum));
}

test "Cohere embedding batches respect the provider request limit" {
    try testCohereBatchLimit();
}

pub fn testVertexEmbeddingRequestPlanning() !void {
    try std.testing.expectEqual(
        @as(usize, 2),
        vertexEmbeddingRequestCount("gemini-embedding-001", 2),
    );
    try std.testing.expectEqual(
        @as(usize, 2),
        vertexEmbeddingRequestCount("text-embedding-005", 251),
    );
    try std.testing.expectEqual(
        @as(usize, 0),
        vertexEmbeddingRequestCount("gemini-embedding-001", 0),
    );
}

test "Vertex embedding request planning matches model-specific wire limits" {
    try testVertexEmbeddingRequestPlanning();
}

pub fn testManagedVertexCredentialManagerLifetime() !void {
    const indexes_json =
        \\{"semantic":{"type":"embeddings","field":"body","dimension":3072,"embedder":{"provider":"vertex","model":"gemini-embedding-001","project_id":"test-project","location":"us-central1"}}}
    ;
    var provider_runtime = ProviderRuntime.init(
        std.testing.allocator,
        std.Io.Threaded.global_single_threaded.io(),
    );
    defer provider_runtime.deinit();

    var first_request = try ManagedEmbedder.initFromIndexesJsonWithOptions(
        std.testing.allocator,
        indexes_json,
        .{ .provider_runtime = &provider_runtime },
    );
    defer first_request.deinit();
    var second_request = try ManagedEmbedder.initFromIndexesJsonWithOptions(
        std.testing.allocator,
        indexes_json,
        .{ .provider_runtime = &provider_runtime },
    );
    defer second_request.deinit();

    try std.testing.expect(first_request.owned_google_credentials == null);
    try std.testing.expect(second_request.owned_google_credentials == null);
    try std.testing.expect(first_request.owned_http_client == null);
    try std.testing.expect(second_request.owned_http_client == null);
    try std.testing.expect(first_request.entries[0].google_credentials == &provider_runtime.google_credentials);
    try std.testing.expect(second_request.entries[0].google_credentials == &provider_runtime.google_credentials);
    var first_fallback: ?httpx.Client = null;
    var second_fallback: ?httpx.Client = null;
    const first_client = try first_request.entries[0].httpClient(std.testing.allocator, &first_fallback);
    const second_client = try second_request.entries[0].httpClient(std.testing.allocator, &second_fallback);
    try std.testing.expect(first_client == second_client);
    try std.testing.expect(first_fallback == null);
    try std.testing.expect(second_fallback == null);
}

test "request-scoped managed Vertex embedders borrow the service credential manager" {
    try testManagedVertexCredentialManagerLifetime();
}

test "request-scoped managed Bedrock embedders borrow region-scoped credential caches" {
    const indexes_json =
        \\{"semantic":{"type":"embeddings","field":"body","dimension":1024,"embedder":{"provider":"bedrock","model":"cohere.embed-v4:0","request_format":"cohere_v4","region":"us-east-1"}}}
    ;
    var provider_runtime = ProviderRuntime.init(
        std.testing.allocator,
        std.Io.Threaded.global_single_threaded.io(),
    );
    defer provider_runtime.deinit();

    var first_request = try ManagedEmbedder.initFromIndexesJsonWithOptions(
        std.testing.allocator,
        indexes_json,
        .{ .provider_runtime = &provider_runtime },
    );
    defer first_request.deinit();
    var second_request = try ManagedEmbedder.initFromIndexesJsonWithOptions(
        std.testing.allocator,
        indexes_json,
        .{ .provider_runtime = &provider_runtime },
    );
    defer second_request.deinit();

    const east_cache = first_request.entries[0].bedrock_credentials.?;
    try std.testing.expect(east_cache == second_request.entries[0].bedrock_credentials.?);
    try std.testing.expect(!first_request.entries[0].owns_bedrock_credentials);
    try std.testing.expect(!second_request.entries[0].owns_bedrock_credentials);
    const west_cache = try provider_runtime.bedrock_credentials.cacheForRegion("us-west-2");
    try std.testing.expect(east_cache != west_cache);
}

test "standalone managed Bedrock embedders own their credential cache" {
    const indexes_json =
        \\{"semantic":{"type":"embeddings","field":"body","dimension":1024,"embedder":{"provider":"bedrock","model":"cohere.embed-v4:0","request_format":"cohere_v4","region":"us-east-1"}}}
    ;
    var embedder = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer embedder.deinit();

    try std.testing.expect(embedder.entries[0].bedrock_credentials != null);
    try std.testing.expect(embedder.entries[0].owns_bedrock_credentials);
}

fn vertexEmbeddingRequestCount(model: []const u8, input_count: usize) usize {
    if (input_count == 0) return 0;
    const maximum = provider_defaults.vertexMaxEmbeddingBatchSize(model);
    return input_count / maximum + @intFromBool(input_count % maximum != 0);
}

fn adoptDenseBatchResult(
    alloc: std.mem.Allocator,
    result: *inference_types.EmbedResult,
) ![]const []const f32 {
    const vectors = try alloc.alloc([]const f32, result.vectors.len);
    for (result.vectors, 0..) |vector, i| vectors[i] = vector;
    result.allocator.free(result.vectors);
    result.vectors = &.{};
    return vectors;
}

fn appendJsonString(alloc: std.mem.Allocator, out: *std.ArrayListUnmanaged(u8), value: []const u8) !void {
    const encoded = try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(value, .{})});
    defer alloc.free(encoded);
    try out.appendSlice(alloc, encoded);
}

fn appendCoveragePolicyIfPresent(
    alloc: std.mem.Allocator,
    out: *std.ArrayListUnmanaged(u8),
    policy: ?indexes_openapi.DerivedCoveragePolicy,
) !void {
    const value = policy orelse return;
    try out.appendSlice(alloc, ",\"coverage_policy\":");
    try appendJsonString(alloc, out, @tagName(value));
}

fn appendPublicationPolicy(
    alloc: std.mem.Allocator,
    out: *std.ArrayListUnmanaged(u8),
    policy: indexes_openapi.IndexPublicationPolicy,
) !void {
    try out.appendSlice(alloc, ",\"publication_policy\":");
    try appendJsonString(alloc, out, @tagName(policy));
}

fn appendExecutionObjectIfPresent(
    alloc: std.mem.Allocator,
    out: *std.ArrayListUnmanaged(u8),
    root: std.json.ObjectMap,
) !void {
    const execution = root.get("execution") orelse return;
    if (execution != .object) return error.InvalidCreateTableRequest;
    // Reject unsupported namespaces and policy fields with the public domain
    // error before the generated parser can expose an incidental JSON error.
    try validateIndexExecutionObjectForCreateTable(execution);
    var parsed = try std.json.parseFromValue(indexes_openapi.IndexExecutionConfig, alloc, execution, .{ .allocate = .alloc_always });
    defer parsed.deinit();
    const encoded = try std.json.Stringify.valueAlloc(alloc, execution, .{});
    defer alloc.free(encoded);
    try out.appendSlice(alloc, ",\"execution\":");
    try out.appendSlice(alloc, encoded);
}

fn validateIndexExecutionObjectForCreateTable(execution: std.json.Value) !void {
    if (execution != .object) return error.InvalidCreateTableRequest;
    var iter = execution.object.iterator();
    while (iter.next()) |entry| {
        if (!isCreateTableIndexExecutionNamespace(entry.key_ptr.*)) return error.InvalidCreateTableRequest;
        _ = enrichment_types.parseExecutionPolicyValue(entry.value_ptr.*) catch return error.InvalidCreateTableRequest;
    }
}

fn isCreateTableIndexExecutionNamespace(name: []const u8) bool {
    return std.mem.eql(u8, name, "chunking") or
        std.mem.eql(u8, name, "embedding");
}

fn stringifyManagedEmbedderConfigAlloc(
    alloc: std.mem.Allocator,
    cfg: embeddings_types.Config,
    raw_value: std.json.Value,
    inference_api_key: ?[]const u8,
) ![]u8 {
    const base_json = try embeddings_types.stringifyAlloc(alloc, cfg);
    defer alloc.free(base_json);

    const requests_per_minute = configObjectU32(raw_value, "requests_per_minute");
    const burst = configObjectU32(raw_value, "burst");
    const default_inference_api_key = if (cfg.api_key == null and isAntflyProvider(try parseEmbedderProvider(cfg)))
        inference_api_key
    else
        null;
    if (requests_per_minute == null and burst == null and default_inference_api_key == null) return try alloc.dupe(u8, base_json);

    var out = std.ArrayListUnmanaged(u8).empty;
    defer out.deinit(alloc);
    try out.appendSlice(alloc, base_json[0 .. base_json.len - 1]);
    if (default_inference_api_key) |api_key| {
        try out.appendSlice(alloc, ",\"api_key\":");
        try appendJsonString(alloc, &out, api_key);
    }
    if (requests_per_minute) |rpm| {
        try out.appendSlice(alloc, ",\"requests_per_minute\":");
        const rpm_json = try std.fmt.allocPrint(alloc, "{d}", .{rpm});
        defer alloc.free(rpm_json);
        try out.appendSlice(alloc, rpm_json);
    }
    if (burst) |burst_value| {
        try out.appendSlice(alloc, ",\"burst\":");
        const burst_json = try std.fmt.allocPrint(alloc, "{d}", .{burst_value});
        defer alloc.free(burst_json);
        try out.appendSlice(alloc, burst_json);
    }
    try out.append(alloc, '}');
    return try out.toOwnedSlice(alloc);
}

const TestLocalDenseProvider = struct {
    dimensions: u32,
    calls: usize = 0,
    sparse_calls: usize = 0,

    fn provider(self: *@This()) AntflyProvider {
        return .{
            .ptr = self,
            .embed_dense_texts = dense,
            .embed_sparse_texts = sparse,
            .owns_invocation_admission = true,
        };
    }

    fn dense(ptr: *anyopaque, alloc: std.mem.Allocator, _: []const u8, texts: []const []const u8) ![][]f32 {
        const self: *@This() = @ptrCast(@alignCast(ptr));
        self.calls += 1;
        const vectors = try alloc.alloc([]f32, texts.len);
        errdefer alloc.free(vectors);
        var initialized: usize = 0;
        errdefer {
            for (vectors[0..initialized]) |vector| alloc.free(vector);
        }
        for (texts, 0..) |_, i| {
            vectors[i] = try alloc.alloc(f32, self.dimensions);
            @memset(vectors[i], 0.25);
            initialized += 1;
        }
        return vectors;
    }

    fn sparse(ptr: *anyopaque, alloc: std.mem.Allocator, _: []const u8, texts: []const []const u8) ![]db_embedder.SparseEmbedding {
        const self: *@This() = @ptrCast(@alignCast(ptr));
        self.sparse_calls += 1;
        const embeddings = try alloc.alloc(db_embedder.SparseEmbedding, texts.len);
        errdefer alloc.free(embeddings);
        var initialized: usize = 0;
        errdefer {
            for (embeddings[0..initialized]) |*embedding| embedding.deinit(alloc);
        }
        for (texts, 0..) |_, i| {
            embeddings[i] = .{
                .indices = try alloc.dupe(u32, &.{0}),
                .values = try alloc.dupe(f32, &.{1.0}),
            };
            initialized += 1;
        }
        return embeddings;
    }
};

test "managed embedder owned numeric lease executes without cache residency and rejects scope rotation" {
    const alloc = std.testing.allocator;
    const App = struct {
        calls: std.atomic.Value(usize) = .init(0),
        fn execute(ptr: *anyopaque, a: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            _ = self.calls.fetchAdd(1, .monotonic);
            // Any accidental catalog discovery fails this regression.
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expectEqualStrings("owned-route", req.header(remote_capabilities.capability_token_header) orelse return error.MissingLease);
            try std.testing.expectEqualStrings(httpx.numeric_response.content_type, req.header("Accept") orelse return error.MissingAccept);
            const frame = try httpx.numeric_response.allocFrame(a, .dense, 1, 2);
            errdefer a.free(frame);
            try httpx.numeric_response.setValue(frame, 0, 0.25);
            try httpx.numeric_response.setValue(frame, 1, 0.75);
            return .{ .status = 200, .content_type = try a.dupe(u8, httpx.numeric_response.content_type), .body = frame };
        }
    };
    var app = App{};
    var listener = std_http_listener.StdHttpListener.init(alloc, .{}, .{ .ptr = &app, .vtable = &.{ .execute = App.execute } });
    defer listener.deinit();
    try listener.start();
    const url = try listener.baseUri(alloc);
    defer alloc.free(url);
    var cache = remote_capabilities.Cache.init(alloc, std.testing.io);
    defer cache.deinit();
    var entries = [_]ManagedEmbeddingEntry{.{
        .alloc = alloc,
        .index_name = @constCast("visual"),
        .provider = .antfly,
        .model = @constCast("clipclap"),
        .base_url = url,
        .dimensions = 2,
        .multimodal = true,
        .io = std.testing.io,
        .shared_remote_capability_cache = &cache,
    }};
    var managed = ManagedEmbedder{ .alloc = alloc, .entries = &entries };
    const lease = inference_work.CapabilityLease{
        .capabilities = .{
            .task = .embed,
            .input_modalities = .{ .image = true },
            .accepted_mime_types = .{ .image_png = true },
            .input_granularity = .page,
            .output = .embedding,
            .framed_attachments = true,
            .numeric_responses_v1 = true,
            .batch = .{ .max_media_parts_per_item = 1 },
        },
        .routing_token = try remote_capabilities.RoutingToken.init("owned-route"),
        .descriptor_revision = try remote_capabilities.CapabilityRevision.init("0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef"),
        .scope_digest = try remote_capabilities.scopeDigest(alloc, url, "clipclap", .embed, &.{}),
    };
    const planned = managed.denseInterface().withPartLease(lease);
    const items = [_]template_mod.ContentPart{.{ .binary = .{
        .mime_type = "image/png",
        .data = &.{ 0x89, 'P', 'N', 'G', 13, 10, 26, 10, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 2, 0, 0, 0, 3 },
    } }};
    const vectors = try planned.embedDensePartItems(alloc, "visual", &items, 2);
    defer db_embedder.freeDenseEmbeddingBatch(alloc, vectors);
    try std.testing.expectEqualSlices(f32, &.{ 0.25, 0.75 }, vectors[0]);
    entries[0].source_table = @constCast("rotated-scope");
    try std.testing.expectError(error.InferenceCapabilitiesStale, planned.embedDensePartItems(alloc, "visual", &items, 2));
    try std.testing.expectEqual(@as(usize, 1), app.calls.load(.acquire));
}

test "managed embedder media planning is pure and typed results avoid JSON reservations" {
    const alloc = std.testing.allocator;
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var entry = [_]ManagedEmbeddingEntry{.{
        .alloc = alloc,
        .index_name = @constCast("visual"),
        .provider = .antfly,
        .model = @constCast("clipclap"),
        .base_url = @constCast("http://unreachable.invalid"),
        .dimensions = 384,
    }};
    // No HTTP executor, cache, or discovery allocator exists. Memory planning
    // is arithmetic over the coordinator's immutable capability snapshot.
    var managed = ManagedEmbedder{ .alloc = alloc, .entries = &entry };
    const caps = inference_work.InferenceCapabilities{
        .task = .embed,
        .input_modalities = .{ .image = true },
        .input_granularity = .page,
        .output = .embedding,
        .framed_attachments = true,
    };
    const shape = db_embedder.DensePartInvocationShape{ .item_count = 4 };
    const remote = try ManagedEmbedder.densePartInvocationMemory(&managed, "visual", shape, 384, caps);
    try std.testing.expectEqual(inference_work.AttachmentTransport.segmented_framed_binary, remote.attachment_transport);
    const page_bytes = 64 << 20;
    try std.testing.expectEqual(@as(usize, page_bytes), try remote.attachment_transport.batchPeakResidentSize(page_bytes, 9, 4));
    try std.testing.expectEqual(@as(usize, page_bytes * 2), try inference_work.AttachmentTransport.framed_binary.batchPeakResidentSize(page_bytes, 9, 4));
    var numeric_caps = caps;
    numeric_caps.numeric_responses_v1 = true;
    const numeric = try ManagedEmbedder.densePartInvocationMemory(&managed, "visual", shape, 384, numeric_caps);
    try std.testing.expectEqual(remote_embedding_max_response_bytes * remote_embedding_response_resident_multiplier - try numericDenseResponseLimit(4, 384) * remote_numeric_response_resident_multiplier, remote.allocator_limit_bytes - numeric.allocator_limit_bytes);
    try std.testing.expectEqual(@as(usize, 127), try httpx.numeric_response.maxRows(8192));
    var large_caps = numeric_caps;
    large_caps.batch.max_items = 128;
    large_caps.batch.preferred_items = 128;
    const large = managed.denseInterface().withPartLease(.{ .capabilities = large_caps });
    try std.testing.expectEqual(@as(usize, 127), try large.partBatchLimit("visual", 8192, 128));
    _ = try large.partInvocationMemoryForMime("visual", 127, "image/png", 8192);
    try std.testing.expectError(error.NumericResponseTooLarge, large.partInvocationMemoryForMime("visual", 128, "image/png", 8192));
    try std.testing.expect(numeric.allocator_limit_bytes < 384 * 1024);
    try std.testing.expectEqual(@as(usize, 4096), try numericDenseResponseLimit(1, 384));
    try std.testing.expectError(error.NumericResponseTooLarge, numericDenseResponseLimit(1024, 4096));
    const unknown = try ManagedEmbedder.densePartInvocationMemory(&managed, "visual", shape, 0, numeric_caps);
    const unknown_json = try ManagedEmbedder.densePartInvocationMemory(&managed, "visual", shape, 0, caps);
    try std.testing.expectEqual(unknown_json.allocator_limit_bytes, unknown.allocator_limit_bytes);
    try std.testing.expectError(error.EmbeddingCapabilitiesUnavailable, ManagedEmbedder.densePartInvocationMemory(&managed, "visual", shape, 384, null));
    entry[0].antfly_provider = local.provider();
    // A local typed executor does not inherit the HTTP frame ceiling.
    try std.testing.expectEqual(@as(usize, 128), try large.partBatchLimit("visual", 8192, 128));
    const json = try ManagedEmbedder.densePartInvocationMemory(&managed, "visual", shape, 384, caps);
    entry[0].antfly_provider.?.typed_dense_results = true;
    const typed = try ManagedEmbedder.densePartInvocationMemory(&managed, "visual", shape, 384, caps);
    try std.testing.expectEqual(@as(usize, 32 << 20), json.allocator_limit_bytes - typed.allocator_limit_bytes);
    try std.testing.expectEqual(@as(usize, 4 * 384 * @sizeOf(f32)), typed.max_result_bytes);
    try std.testing.expect(typed.allocator_limit_bytes < 64 * 1024);
}

test "managed embedder numeric response budget covers non-resizable HTTP buffer growth" {
    const NoResize = struct {
        fn allocate(_: *anyopaque, len: usize, alignment: std.mem.Alignment, ret_addr: usize) ?[*]u8 {
            return std.testing.allocator.rawAlloc(len, alignment, ret_addr);
        }
        fn free(_: *anyopaque, bytes: []u8, alignment: std.mem.Alignment, ret_addr: usize) void {
            std.testing.allocator.rawFree(bytes, alignment, ret_addr);
        }
    };
    var context: u8 = 0;
    const backing = std.mem.Allocator{ .ptr = &context, .vtable = &.{ .alloc = NoResize.allocate, .free = NoResize.free, .resize = std.mem.Allocator.noResize, .remap = std.mem.Allocator.noRemap } };
    // Match HTTP's 16-KiB append loop, with both encoded and decoded bodies
    // reaching their ceiling. Refusing resize/remap forces old+new buffers to
    // coexist; this must not rely on a favorable backing allocator.
    for ([_]usize{ 4096, try numericDenseResponseLimit(4, 384), try numericDenseResponseLimit(127, 8192) }) |body_limit| {
        var bounded = inference_work.BoundedInvocationAllocator.init(backing, body_limit * remote_numeric_response_resident_multiplier + remote_embedding_transport_control_bytes);
        const a = bounded.allocator();
        {
            var compressed = std.ArrayListUnmanaged(u8).empty;
            defer compressed.deinit(a);
            var decoded = std.ArrayListUnmanaged(u8).empty;
            defer decoded.deinit(a);
            for ([_]*std.ArrayListUnmanaged(u8){ &compressed, &decoded }) |buffer| {
                while (buffer.items.len < body_limit) {
                    const bytes = try buffer.addManyAsSlice(a, @min(16 * 1024, body_limit - buffer.items.len));
                    @memset(bytes, 0);
                }
            }
            compressed.deinit(a);
            compressed = .empty;
            const body = try decoded.toOwnedSlice(a);
            a.free(body);
        }
        try std.testing.expect(!bounded.limit_exceeded);
        try std.testing.expectEqual(@as(usize, 0), bounded.live_bytes);
    }
}

test "managed embedder parses local antfly and antfly entries from indexes metadata" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "full_text_idx":{"type":"full_text"},
        \\  "semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}},
        \\  "chunk_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
        \\}
    , local.provider());
    defer managed.deinit();

    try std.testing.expectEqual(@as(usize, 2), managed.entries.len);
    try std.testing.expectEqual(ProviderKind.antfly, managed.entries[0].provider);
    try std.testing.expectEqualStrings("", managed.entries[0].base_url);
    try std.testing.expectEqual(ProviderKind.antfly, managed.entries[1].provider);
    try std.testing.expectEqualStrings("", managed.entries[1].base_url);
}

test "managed embedder registers every multi-source embedding artifact name" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{"document_vectors":{"type":"embeddings","dimension":3,"sources":[{"artifact":"document_dense_v1"},{"artifact":"document_chunk_dense_v1"}],"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}}
    , local.provider());
    defer managed.deinit();

    try std.testing.expectEqual(@as(usize, 1), managed.entries.len);
    try std.testing.expectEqual(@as(usize, 2), managed.entries[0].embedding_names.len);
    try std.testing.expect(managed.findEntry("document_dense_v1") != null);
    try std.testing.expect(managed.findEntry("document_chunk_dense_v1") != null);

    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"dimension":3,"publication_policy":"atomic","sources":[{"artifact":"document_dense_v1"},{"artifact":"document_chunk_dense_v1"}],"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();
    const translated = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "document_vectors", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(translated);
    try std.testing.expect(std.mem.indexOf(u8, translated, "\"sources\":[{\"artifact\":\"document_dense_v1\"},{\"artifact\":\"document_chunk_dense_v1\"}]") != null);
    try std.testing.expect(std.mem.indexOf(u8, translated, "\"generator\"") == null);
    try std.testing.expect(std.mem.indexOf(u8, translated, "\"semantic_producer\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, translated, "\"publication_policy\":\"atomic\"") != null);

    var sparse_parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"sparse":true,"sources":[{"artifact":"title_sparse_v1"},{"artifact":"body_sparse_v1"}],"embedder":{"provider":"antfly","model":"antflydb/sparse"}}
    , .{});
    defer sparse_parsed.deinit();
    const sparse_translated = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "document_sparse", sparse_parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(sparse_translated);
    try std.testing.expect(std.mem.indexOf(u8, sparse_translated, "\"sources\":[{\"artifact\":\"title_sparse_v1\"},{\"artifact\":\"body_sparse_v1\"}]") != null);
    try std.testing.expect(std.mem.indexOf(u8, sparse_translated, "\"generator\"") == null);
}

test "managed embedder binds execution to catalog semantic producer identity" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var remote = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{"semantic":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"model-a"},"semantic_producer":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"model-a\",\"endpoint\":\"http://identity.example/ai/v1\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"}}
    , .{
        .antfly_provider = local.provider(),
        // Catalog loading must not parse this node-local default before it
        // selects the already-admitted durable endpoint.
        .inference_api_url = "http://runtime-default.example/wrong-path",
    });
    defer remote.deinit();
    try std.testing.expectEqualStrings("http://identity.example/ai/v1", remote.entries[0].base_url);
    try std.testing.expect(remote.entries[0].antfly_provider == null);

    const embedded_catalog =
        \\{"semantic":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"model-a"},"semantic_producer":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"model-a\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"}}
    ;
    var embedded = try ManagedEmbedder.initFromIndexesJsonWithOptions(
        std.testing.allocator,
        embedded_catalog,
        .{
            .antfly_provider = local.provider(),
            .inference_api_url = "http://runtime-default.example/wrong-path",
        },
    );
    defer embedded.deinit();
    try std.testing.expectEqualStrings("", embedded.entries[0].base_url);
    try std.testing.expect(embedded.entries[0].antfly_provider != null);

    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        ManagedEmbedder.initFromIndexesJsonWithOptions(
            std.testing.allocator,
            embedded_catalog,
            .{ .inference_api_url = "http://runtime-default.example/wrong-path" },
        ),
    );
}

test "managed embedder reuses an executable owner for producerless artifact consumers" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    const catalog =
        \\{"consumer":{"type":"embeddings","dimension":3,"sources":[{"artifact":"dense_v1"}]},"owner":{"type":"embeddings","field":"body","dimension":3,"embedding_name":"dense_v1","embedder":{"provider":"antfly","model":"model-a"},"semantic_producer":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"model-a\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"},"enrichments":[{"name":"dense_v1","kind":"embedding","field":"body","expected_dims":3}]}
    ;

    try validateEmbeddingProducerOwnershipJson(std.testing.allocator, catalog);
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(
        std.testing.allocator,
        catalog,
        local.provider(),
    );
    defer managed.deinit();
    try std.testing.expectEqual(@as(usize, 1), managed.entries.len);
    try std.testing.expect(managed.findEntry("owner") != null);
    try std.testing.expect(managed.findEntry("consumer") != null);
    try std.testing.expect(managed.findEntry("dense_v1") != null);
}

pub fn testMultiSourceEmbeddingContracts() !void {
    var local = TestLocalDenseProvider{ .dimensions = 3 };

    var duplicate = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","sources":[{"artifact":"body_dense_v1"},{"artifact":"body_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer duplicate.deinit();
    try std.testing.expectError(error.InvalidCreateTableRequest, translateEmbeddingsIndexConfigJsonWithOptions(
        std.testing.allocator,
        "document_vectors",
        duplicate.value,
        .{ .antfly_provider = local.provider() },
    ));
    var mixed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","sources":[{"artifact":"body_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer mixed.deinit();
    try std.testing.expectError(error.InvalidCreateTableRequest, translateEmbeddingsIndexConfigJsonWithOptions(
        std.testing.allocator,
        "document_vectors",
        mixed.value,
        .{ .antfly_provider = local.provider() },
    ));

    var equivalent = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "primary":{"type":"embeddings","sources":[{"artifact":"shared_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-a"}},
        \\  "secondary":{"type":"embeddings","sources":[{"artifact":"shared_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-a"}}
        \\}
    , local.provider());
    defer equivalent.deinit();
    try std.testing.expect(equivalent.findEntry("shared_dense_v1") != null);

    try std.testing.expectError(error.InvalidManagedEmbeddingIndex, ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "primary":{"type":"embeddings","sources":[{"artifact":"implicit_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-a"}},
        \\  "secondary":{"type":"embeddings","sources":[{"artifact":"implicit_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-b"}}
        \\}
    , local.provider()));

    var explicit = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "primary":{"type":"embeddings","sources":[{"artifact":"shared_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-a"},"enrichments":[{"name":"shared_dense_v1","kind":"embedding","field":"body","expected_dims":3,"vector_space":"acme:dense-v1"}]},
        \\  "secondary":{"type":"embeddings","sources":[{"artifact":"shared_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-b"},"enrichments":[{"name":"shared_dense_v1","kind":"embedding","field":"body","expected_dims":3,"vector_space":"acme:dense-v1"}]}
        \\}
    , local.provider());
    defer explicit.deinit();
    try std.testing.expect(explicit.findEntry("shared_dense_v1") != null);

    try std.testing.expectError(error.ConflictingEmbeddingArtifactDimensions, ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "primary":{"type":"embeddings","sources":[{"artifact":"shared_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-a"},"enrichments":[{"name":"shared_dense_v1","kind":"embedding","field":"body","expected_dims":3,"vector_space":"acme:dense-v1"}]},
        \\  "secondary":{"type":"embeddings","sources":[{"artifact":"shared_dense_v1"}],"dimension":4,"embedder":{"provider":"antfly","model":"antflydb/model-b"},"enrichments":[{"name":"shared_dense_v1","kind":"embedding","field":"body","expected_dims":4,"vector_space":"acme:dense-v1"}]}
        \\}
    , local.provider()));

    try std.testing.expectError(error.InvalidManagedEmbeddingIndex, ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{"combined":{"type":"embeddings","sources":[{"artifact":"title_dense_v1"},{"artifact":"body_dense_v1"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-a"},"enrichments":[{"name":"title_dense_v1","kind":"embedding","field":"title","expected_dims":3,"vector_space":"acme:dense-v1"},{"name":"body_dense_v1","kind":"embedding","field":"body","expected_dims":3}]}}
    , local.provider()));

    var aliased = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "aliased_vectors":{"type":"embeddings","sources":[{"artifact":"document_vectors"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-a"}},
        \\  "document_vectors":{"type":"embeddings","sources":[{"artifact":"document_vectors_v2"}],"dimension":3,"embedder":{"provider":"antfly","model":"antflydb/model-b"}}
        \\}
    , local.provider());
    defer aliased.deinit();
    try std.testing.expectEqualStrings("antflydb/model-b", aliased.findQueryEntry("document_vectors").?.model);
    try std.testing.expectEqualStrings("antflydb/model-a", aliased.findArtifactEntry("document_vectors").?.model);

    var producer = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","dimension":3,"embedder":{"provider":"openai","model":"embed-v1","url":"https://models.example/v1","api_key":"secret","requests_per_minute":10}}
    , .{});
    defer producer.deinit();
    const identity = try embeddingSemanticProducerJsonAlloc(std.testing.allocator, producer.value);
    defer std.testing.allocator.free(identity);
    try std.testing.expect(std.mem.indexOf(u8, identity, "https://models.example/v1") != null);
    try std.testing.expect(std.mem.indexOf(u8, identity, "secret") == null);
    try std.testing.expect(std.mem.indexOf(u8, identity, "requests_per_minute") == null);

    const credential_endpoints = [_][]const u8{
        "https://alice:password@models.example/v1",
        "https://models.example/v1?api_key=secret",
        "https://models.example/v1#access_token=secret",
        "${secret:openai.endpoint}",
    };
    for (credential_endpoints) |endpoint| {
        const raw = try std.fmt.allocPrint(
            std.testing.allocator,
            "{{\"type\":\"embeddings\",\"dimension\":3,\"embedder\":{{\"provider\":\"openai\",\"model\":\"embed-v1\",\"url\":\"{s}\"}}}}",
            .{endpoint},
        );
        defer std.testing.allocator.free(raw);
        var credential_endpoint = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, raw, .{});
        defer credential_endpoint.deinit();
        try std.testing.expectError(
            error.InvalidCreateTableRequest,
            embeddingSemanticProducerJsonAlloc(std.testing.allocator, credential_endpoint.value),
        );
    }
}

test "managed embedder enforces multi-source producer and vector-space contracts" {
    try testMultiSourceEmbeddingContracts();
}

pub fn testQueryEmbeddingCacheKeys() !void {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    const test_io = std.Io.Threaded.global_single_threaded.io();
    var managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{
        \\  "first":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}},
        \\  "second":{"type":"embeddings","field":"title","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
        \\}
    , .{ .antfly_provider = local.provider(), .io = test_io });
    defer managed.deinit();

    try std.testing.expect(managed.entries[0].io.?.userdata == test_io.userdata);
    try std.testing.expect(managed.entries[0].io.?.vtable == test_io.vtable);

    const first = try managed.queryCacheKey("first", .principal, "alice", "exact input");
    const equivalent = try managed.queryCacheKey("second", .principal, "alice", "exact input");
    const other_principal = try managed.queryCacheKey("second", .principal, "bob", "exact input");
    const anonymous = try managed.queryCacheKey("second", .anonymous, "alice", "exact input");
    const changed_text = try managed.queryCacheKey("second", .principal, "alice", "exact input ");

    var first_credentials = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"dense":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"openai","model":"text-embedding-3-small","api_key":"credential-a"}}}
    );
    defer first_credentials.deinit();
    var second_credentials = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"dense":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"openai","model":"text-embedding-3-small","api_key":"credential-b"}}}
    );
    defer second_credentials.deinit();
    const credential_a = try first_credentials.queryCacheKey("dense", .principal, "alice", "exact input");
    const credential_b = try second_credentials.queryCacheKey("dense", .principal, "alice", "exact input");

    var vertex_project_a = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"dense":{"type":"embeddings","field":"body","dimension":3072,"embedder":{"provider":"vertex","model":"gemini-embedding-001","project_id":"project-a","location":"us-central1","credentials_path":"credentials-a.json"}}}
    );
    defer vertex_project_a.deinit();
    var vertex_project_b = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"dense":{"type":"embeddings","field":"body","dimension":3072,"embedder":{"provider":"vertex","model":"gemini-embedding-001","project_id":"project-b","location":"us-central1","credentials_path":"credentials-a.json"}}}
    );
    defer vertex_project_b.deinit();
    var vertex_credentials_b = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"dense":{"type":"embeddings","field":"body","dimension":3072,"embedder":{"provider":"vertex","model":"gemini-embedding-001","project_id":"project-a","location":"us-central1","credentials_path":"credentials-b.json"}}}
    );
    defer vertex_credentials_b.deinit();
    var vertex_default_credentials = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"dense":{"type":"embeddings","field":"body","dimension":3072,"embedder":{"provider":"vertex","model":"gemini-embedding-001","project_id":"project-a","location":"us-central1"}}}
    );
    defer vertex_default_credentials.deinit();
    var vertex_sentinel_credentials = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"dense":{"type":"embeddings","field":"body","dimension":3072,"embedder":{"provider":"vertex","model":"gemini-embedding-001","project_id":"project-a","location":"us-central1","credentials_path":"<default-adc>"}}}
    );
    defer vertex_sentinel_credentials.deinit();
    const vertex_a = try vertex_project_a.queryCacheKey("dense", .principal, "alice", "exact input");
    const vertex_b = try vertex_project_b.queryCacheKey("dense", .principal, "alice", "exact input");
    const vertex_credential_b = try vertex_credentials_b.queryCacheKey("dense", .principal, "alice", "exact input");
    const vertex_default = try vertex_default_credentials.queryCacheKey("dense", .principal, "alice", "exact input");
    const vertex_sentinel = try vertex_sentinel_credentials.queryCacheKey("dense", .principal, "alice", "exact input");

    const bedrock_profile = "arn:aws:bedrock:us-east-1:123456789012:application-inference-profile/team-embeddings";
    const bedrock_v3_json = try std.fmt.allocPrint(
        std.testing.allocator,
        "{{\"dense\":{{\"type\":\"embeddings\",\"field\":\"body\",\"dimension\":1024,\"embedder\":{{\"provider\":\"bedrock\",\"model\":\"{s}\",\"request_format\":\"cohere_v3\",\"region\":\"us-east-1\"}}}}}}",
        .{bedrock_profile},
    );
    defer std.testing.allocator.free(bedrock_v3_json);
    const bedrock_v4_json = try std.fmt.allocPrint(
        std.testing.allocator,
        "{{\"dense\":{{\"type\":\"embeddings\",\"field\":\"body\",\"dimension\":1024,\"embedder\":{{\"provider\":\"bedrock\",\"model\":\"{s}\",\"request_format\":\"cohere_v4\",\"region\":\"us-east-1\"}}}}}}",
        .{bedrock_profile},
    );
    defer std.testing.allocator.free(bedrock_v4_json);
    var bedrock_v3 = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, bedrock_v3_json);
    defer bedrock_v3.deinit();
    var bedrock_v4 = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, bedrock_v4_json);
    defer bedrock_v4.deinit();
    const bedrock_format_v3 = try bedrock_v3.queryCacheKey("dense", .principal, "alice", "exact input");
    const bedrock_format_v4 = try bedrock_v4.queryCacheKey("dense", .principal, "alice", "exact input");

    try std.testing.expectEqual(first, equivalent);
    try std.testing.expect(!std.mem.eql(u8, &first, &other_principal));
    try std.testing.expect(!std.mem.eql(u8, &first, &anonymous));
    try std.testing.expect(!std.mem.eql(u8, &first, &changed_text));
    try std.testing.expect(!std.mem.eql(u8, &credential_a, &credential_b));
    try std.testing.expect(!std.mem.eql(u8, &vertex_a, &vertex_b));
    try std.testing.expect(!std.mem.eql(u8, &vertex_a, &vertex_credential_b));
    try std.testing.expect(!std.mem.eql(u8, &vertex_default, &vertex_sentinel));
    try std.testing.expect(!std.mem.eql(u8, &bedrock_format_v3, &bedrock_format_v4));
}

test "query embedding cache keys share equivalent indexes and isolate security domains" {
    try testQueryEmbeddingCacheKeys();
}

test "managed embedder rejects legacy antfly api path" {
    try std.testing.expectError(error.InvalidAntflyInferenceBaseUrl, ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":768,"embedder":{"provider":"antfly","model":"bge-base-en-v1.5","api_url":"http://localhost:8082/api"}}}
    ));
}

test "managed embedder interface deinit uses owner allocator" {
    if (builtin.os.tag == .freestanding) return;

    var local = TestLocalDenseProvider{ .dimensions = 384 };
    const dense = (try ManagedEmbedder.createDenseEmbedderWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "semantic_idx":{"type":"embeddings","field":"body","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
        \\}
    , local.provider())) orelse return error.TestUnexpectedResult;
    dense.deinit(std.heap.page_allocator);
}

test "serverless managed embedder stage factories ignore unrelated provider construction" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };

    var sparse_parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{
        \\  "sparse_idx":{"type":"embeddings","field":"body","sparse":true,"embedder":{"provider":"antfly","model":"antflydb/splade"}},
        \\  "broken_dense":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"openai","model":""}}
        \\}
    , .{});
    defer sparse_parsed.deinit();
    const sparse = (try ManagedEmbedder.createSparseEmbedderFromIndexValueWithOptions(
        std.testing.allocator,
        sparse_parsed.value,
        .{ .antfly_provider = local.provider() },
    )) orelse return error.TestUnexpectedResult;
    sparse.deinit(std.testing.allocator);

    var dense_parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{
        \\  "dense_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}},
        \\  "broken_sparse":{"type":"embeddings","field":"body","sparse":true,"embedder":{"provider":"openai","model":""}}
        \\}
    , .{});
    defer dense_parsed.deinit();
    const dense = (try ManagedEmbedder.createDenseEmbedderFromIndexValueWithOptions(
        std.testing.allocator,
        dense_parsed.value,
        .{ .antfly_provider = local.provider() },
    )) orelse return error.TestUnexpectedResult;
    dense.deinit(std.testing.allocator);
}

test "serverless managed embedder stage selection includes producer validation and artifact consumers" {
    const alloc = std.testing.allocator;
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var parsed = try std.json.parseFromSlice(std.json.Value, alloc,
        \\{
        \\  "dense_owner":{"type":"embeddings","field":"body","dimension":3,"embedding_name":"dense_v1","embedder":{"provider":"antfly","model":"test-model"}},
        \\  "sparse_owner":{"type":"embeddings","field":"body","sparse":true,"embedding_name":"sparse_v1","embedder":{"provider":"antfly","model":"test-model"}},
        \\  "dense_consumer":{"type":"embeddings","dimension":3,"sources":[{"artifact":"dense_v1"}]},
        \\  "sparse_consumer":{"type":"embeddings","sparse":true,"sources":[{"artifact":"sparse_v1"}]},
        \\  "enrichments":[
        \\    {"name":"dense_v1","kind":"embedding","field":"body","expected_dims":3,"producer_json":{"version":2,"provider":"antfly","model":"test-model","endpoint":"antfly:embedded","sparse":false}},
        \\    {"name":"sparse_v1","kind":"embedding","field":"body","producer_json":{"version":2,"provider":"antfly","model":"test-model","endpoint":"antfly:embedded","sparse":true}}
        \\  ]
        \\}
    , .{});
    defer parsed.deinit();
    for ([_]?bool{ null, false, true }) |kind| {
        var managed = try ManagedEmbedder.initFromIndexValueObjectWithOptionsAndKind(alloc, parsed.value, .{ .antfly_provider = local.provider() }, kind);
        defer managed.deinit();
        try std.testing.expectEqual(@as(usize, if (kind == null) 2 else 1), managed.entries.len);
        try std.testing.expectEqual(kind == null or !kind.?, managed.hasDenseEntries());
        try std.testing.expectEqual(kind == null or kind.?, managed.hasSparseEntries());
    }
    // In-stage validation must still reject an orphan; selecting a factory
    // must not disable ownership validation altogether.
    _ = parsed.value.object.swapRemove("dense_owner");
    try std.testing.expectError(error.InvalidEmbeddingArtifactProducer, ManagedEmbedder.initDenseFromIndexValueObjectWithOptions(alloc, parsed.value, .{ .antfly_provider = local.provider() }));
}

test "managed embedder uses embedder dimensions metadata at runtime" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "semantic_idx":{"type":"embeddings","field":"body","embedder":{"provider":"antfly","model":"antflydb/clipclap","dimensions":3}}
        \\}
    , local.provider());
    defer managed.deinit();

    try std.testing.expectEqual(@as(usize, 1), managed.entries.len);
    try std.testing.expectEqual(@as(u32, 3), managed.entries[0].dimensions);
}

test "managed embedder translates managed embeddings config into db generator config" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"},"execution":{"chunking":{"batch_items":1024},"embedding":{"batch_items":16,"batch_bytes":262144}}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"field\":\"body\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"dims\":384") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"embedding_name\":\"semantic_idx\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"publication_policy\":\"progressive\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"generator\":{\"kind\":\"dense_embedding\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"execution\":") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"embedding\":{\"batch_items\":16,\"batch_bytes\":262144}") != null);
}

test "managed embedder preserves atomic publication policy" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","publication_policy":"atomic","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"publication_policy\":\"atomic\"") != null);
}

test "managed embedder rejects invalid execution batch policy" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"},"execution":{"embedding":{"batch_items":0}}}
    , .{});
    defer parsed.deinit();

    try std.testing.expectError(error.InvalidCreateTableRequest, translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() }));
}

test "managed embedder preserves coverage policy in storage config" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","coverage_policy":"partial","template":"{{#if image_url}}{{remoteMedia url=image_url}}{{/if}}","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(
        std.testing.allocator,
        "thumbnail",
        parsed.value,
        .{ .antfly_provider = local.provider() },
    );
    defer std.testing.allocator.free(config_json);

    try ant_json.testing.expectSubsetJsonText(
        std.testing.allocator,
        \\{"field":"body","dims":384,"metric":"l2_squared","embedding_name":"thumbnail","coverage_policy":"partial"}
    ,
        config_json,
    );
}

test "managed embedder rejects unsupported execution namespaces" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    for ([_][]const u8{
        \\{"indexing":{"batch_items":8}}
        ,
        \\{"embedding":{"batch_items":8},"indexing":{}}
        ,
        \\{"embedding":{"unknown_option":8}}
        ,
    }) |execution| {
        const json = try std.fmt.allocPrint(std.testing.allocator,
            \\{{"type":"embeddings","field":"body","dimension":384,"embedder":{{"provider":"antfly","model":"antflydb/clipclap"}},"execution":{s}}}
        , .{execution});
        defer std.testing.allocator.free(json);
        var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, json, .{});
        defer parsed.deinit();
        try std.testing.expectError(error.InvalidCreateTableRequest, translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() }));
    }
}

pub fn testArtifactBackedEmbeddingRequestsWithoutIndexEmbedder() !void {
    const cases = [_]struct {
        name: []const u8,
        request: []const u8,
        expected_source: []const u8,
    }{
        .{
            .name = "document_vectors",
            .request =
            \\{"type":"embeddings","dimension":384,"sources":[{"artifact":"document_chunk_dense_v1"}]}
            ,
            .expected_source = "\"sources\":[{\"artifact\":\"document_chunk_dense_v1\"}]",
        },
        .{
            .name = "document_vectors_compat",
            .request =
            \\{"type":"embeddings","embedding_name":"document_chunk_dense_v1","dimension":384,"distance_metric":"cosine"}
            ,
            .expected_source = "\"embedding_name\":\"document_chunk_dense_v1\"",
        },
    };

    for (cases) |case| {
        var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, case.request, .{});
        defer parsed.deinit();

        const config_json = try translateEmbeddingsIndexConfigJson(std.testing.allocator, case.name, parsed.value);
        defer std.testing.allocator.free(config_json);

        try std.testing.expect(std.mem.indexOf(u8, config_json, "\"field\":\"embedding\"") != null);
        try std.testing.expect(std.mem.indexOf(u8, config_json, "\"dims\":384") != null);
        try std.testing.expect(std.mem.indexOf(u8, config_json, case.expected_source) != null);
        try std.testing.expect(std.mem.indexOf(u8, config_json, "\"embedder\"") == null);
        try std.testing.expect(std.mem.indexOf(u8, config_json, "\"generator\"") == null);
    }

    var catalog = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{
        \\  "enrichments":[{"name":"document_chunk_dense_v1","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"provider\":\"antfly\",\"model\":\"BAAI/bge-small-en-v1.5\"}"}],
        \\  "existing":{"type":"full_text","field":"body"}
        \\}
    , .{});
    defer catalog.deinit();
    var dimensionless = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","sources":[{"artifact":"document_chunk_dense_v1"}]}
    , .{});
    defer dimensionless.deinit();
    const inferred = (try normalizeEmbeddingsIndexDimensionJsonForCatalogWithOptions(
        std.testing.allocator,
        "document_vectors_inferred",
        dimensionless.value,
        catalog.value,
        .{},
    )) orelse return error.TestUnexpectedResult;
    defer std.testing.allocator.free(inferred);
    try std.testing.expect(std.mem.indexOf(u8, inferred, "\"dimension\":384") != null);

    var sparse = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","sparse":true,"embedding_name":"document_chunk_sparse_v1"}
    , .{});
    defer sparse.deinit();
    try std.testing.expect((try normalizeEmbeddingsIndexDimensionJsonWithOptions(
        std.testing.allocator,
        "document_sparse",
        sparse.value,
        .{},
    )) == null);
    const sparse_config_json = try translateEmbeddingsIndexConfigJson(std.testing.allocator, "document_sparse", sparse.value);
    defer std.testing.allocator.free(sparse_config_json);
    try std.testing.expect(std.mem.indexOf(u8, sparse_config_json, "\"embedding_name\":\"document_chunk_sparse_v1\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, sparse_config_json, "\"embedder\"") == null);
    try std.testing.expect(std.mem.indexOf(u8, sparse_config_json, "\"generator\"") == null);

    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{
        \\  "enrichments":[
        \\    {"name":"document_chunks_v1","kind":"chunk","field":"body","chunk_size":128},
        \\    {"name":"document_chunk_dense_v1","kind":"embedding","field":"body","source_artifact_name":"document_chunks_v1","expected_dims":384,"producer_json":"{\"provider\":\"antfly\",\"model\":\"BAAI/bge-small-en-v1.5\"}"}
        \\  ],
        \\  "document_vectors":{"type":"embeddings","dimension":384,"sources":[{"artifact":"document_chunk_dense_v1"}]},
        \\  "document_vectors_compat":{"type":"embeddings","dimension":384,"embedding_name":"document_chunk_dense_v1"}
        \\}
    , .{ .antfly_provider = local.provider() });
    defer managed.deinit();
    try std.testing.expectEqual(@as(usize, 1), managed.entries.len);
    try std.testing.expectEqualStrings("document_vectors", managed.entries[0].index_name);
    try std.testing.expectEqualStrings("document_chunk_dense_v1", managed.entries[0].embedding_name);
    try std.testing.expectEqual(@as(usize, 1), managed.entries[0].lookup_aliases.len);
    try std.testing.expectEqualStrings("document_vectors_compat", managed.entries[0].lookup_aliases[0]);

    const query_vector = try managed.embedQuery(std.testing.allocator, "document_vectors_compat", "hello");
    defer std.testing.allocator.free(query_vector);
    try std.testing.expectEqual(@as(usize, 384), query_vector.len);
    try std.testing.expectEqual(@as(usize, 1), local.calls);

    // Different artifact producers may serve one query index when the catalog
    // explicitly asserts that their vector spaces are compatible.
    var multi_source = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{
        \\  "enrichments":[
        \\    {"name":"title_dense_v1","kind":"embedding","field":"title","expected_dims":384,"vector_space":"acme:dense-v1","producer_json":"{\"provider\":\"antfly\",\"model\":\"title-model\"}"},
        \\    {"name":"body_dense_v1","kind":"embedding","field":"body","expected_dims":384,"vector_space":"acme:dense-v1","producer_json":"{\"provider\":\"antfly\",\"model\":\"body-model\"}"}
        \\  ],
        \\  "combined_vectors":{"type":"embeddings","dimension":384,"sources":[{"artifact":"title_dense_v1"},{"artifact":"body_dense_v1"}]}
        \\}
    , .{ .antfly_provider = local.provider() });
    defer multi_source.deinit();
    try std.testing.expectEqual(@as(usize, 2), multi_source.entries.len);
    try std.testing.expectEqualStrings("title-model", multi_source.findQueryEntry("combined_vectors").?.model);
    try std.testing.expectEqualStrings("title-model", multi_source.findArtifactEntry("title_dense_v1").?.model);
    try std.testing.expectEqualStrings("body-model", multi_source.findArtifactEntry("body_dense_v1").?.model);

    // Query index names and durable artifact names are separate namespaces.
    // A direct index may share a name with an artifact without hijacking the
    // producer selected for that artifact's enrichment runtime.
    var colliding = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{
        \\  "shared_name":{"type":"embeddings","field":"body","dimension":384,"embedder":{"provider":"antfly","model":"direct-model"}},
        \\  "enrichments":[
        \\    {"name":"shared_name","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"provider\":\"antfly\",\"model\":\"artifact-model\",\"multimodal\":true}"},
        \\    {"name":"reference_artifact","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"provider\":\"antfly\",\"model\":\"artifact-model\",\"multimodal\":true}"}
        \\  ],
        \\  "artifact_consumer":{"type":"embeddings","dimension":384,"sources":[{"artifact":"shared_name"}]},
        \\  "reference_consumer":{"type":"embeddings","dimension":384,"sources":[{"artifact":"reference_artifact"}]}
        \\}
    , .{ .antfly_provider = local.provider() });
    defer colliding.deinit();
    try std.testing.expectEqual(@as(usize, 3), colliding.entries.len);
    try std.testing.expectEqualStrings("direct-model", colliding.findQueryEntry("shared_name").?.model);
    try std.testing.expectEqualStrings("artifact-model", colliding.findArtifactEntry("shared_name").?.model);
    try std.testing.expectEqual(@as(?usize, 1), ManagedEmbedder.denseMediaPartLimit(&colliding, "shared_name"));
    const colliding_plan = try ManagedEmbedder.densePartInvocationMemory(&colliding, "shared_name", .{ .item_count = 1 }, 384, null);
    const reference_plan = try ManagedEmbedder.densePartInvocationMemory(&colliding, "reference_artifact", .{ .item_count = 1 }, 384, null);
    try std.testing.expectEqual(reference_plan, colliding_plan);

    // Public query aliases outrank every legacy artifact name globally, not
    // merely within whichever registry entry is encountered first.
    var alias_collision = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{
        \\  "other_owner":{"type":"embeddings","dimension":384,"embedding_name":"artifact_consumer","embedder":{"provider":"antfly","model":"wrong-model"}},
        \\  "actual_owner":{"type":"embeddings","dimension":384,"embedding_name":"actual_artifact","embedder":{"provider":"antfly","model":"right-model"}},
        \\  "enrichments":[
        \\    {"name":"artifact_consumer","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"wrong-model\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"},
        \\    {"name":"actual_artifact","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"right-model\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"}
        \\  ],
        \\  "artifact_consumer":{"type":"embeddings","dimension":384,"sources":[{"artifact":"actual_artifact"}]}
        \\}
    , .{ .antfly_provider = local.provider() });
    defer alias_collision.deinit();
    try std.testing.expectEqualStrings("right-model", alias_collision.findQueryEntry("artifact_consumer").?.model);
    try std.testing.expectEqualStrings("wrong-model", alias_collision.findArtifactEntry("artifact_consumer").?.model);

    var semantic_identity = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{
        \\  "enrichments":[{"name":"semantic_artifact","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"semantic-model\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"}],
        \\  "semantic_owner":{"type":"embeddings","dimension":384,"embedding_name":"semantic_artifact","embedder":{"provider":"antfly","model":"semantic-model"}},
        \\  "semantic_consumer":{"type":"embeddings","dimension":384,"sources":[{"artifact":"semantic_artifact"}]}
        \\}
    , .{ .antfly_provider = local.provider() });
    defer semantic_identity.deinit();
    try std.testing.expectEqualStrings("semantic-model", semantic_identity.findArtifactEntry("semantic_artifact").?.model);

    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
            \\{
            \\  "enrichments":[{"name":"orphan_identity","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"semantic-model\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"}]
            \\}
        , .{ .antfly_provider = local.provider() }),
    );
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
            \\{
            \\  "enrichments":[{"name":"claimed_artifact","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"authoritative-model\",\"endpoint\":\"antfly:embedded\",\"region\":\"\",\"request_format\":\"\",\"sparse\":false,\"multimodal\":false,\"input_type\":\"\",\"truncate\":\"\"}"}],
            \\  "legacy_claim":{"type":"embeddings","dimension":384,"embedding_name":"claimed_artifact","embedder":{"provider":"antfly","model":"different-model"}}
            \\}
        , .{ .antfly_provider = local.provider() }),
    );
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
            \\{"enrichments":[{"name":"unused_invalid","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"provider\":false}"}]}
        , .{ .antfly_provider = local.provider() }),
    );

    try std.testing.expectError(
        error.EmbeddingArtifactDimensionRequired,
        ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
            \\{
            \\  "enrichments":[{"name":"wrong_shape","kind":"embedding","field":"body","producer_json":"{\"version\":2,\"provider\":\"antfly\",\"model\":\"dense-model\",\"endpoint\":\"antfly:embedded\",\"sparse\":false}"}],
            \\  "sparse_consumer":{"type":"embeddings","sparse":true,"sources":[{"artifact":"wrong_shape"}]}
            \\}
        , .{ .antfly_provider = local.provider() }),
    );
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
            \\{
            \\  "enrichments":[{"name":"future_producer","kind":"embedding","field":"body","expected_dims":384,"producer_json":"{\"version\":3,\"provider\":\"antfly\",\"model\":\"future-model\",\"endpoint\":\"antfly:embedded\",\"sparse\":false}"}],
            \\  "future_consumer":{"type":"embeddings","dimension":384,"sources":[{"artifact":"future_producer"}]}
            \\}
        , .{ .antfly_provider = local.provider() }),
    );
    try std.testing.expectError(
        error.EmbeddingArtifactDimensionRequired,
        validateEmbeddingEnrichmentProducerJsonWithOptions(
            std.testing.allocator,
            "{\"name\":\"dense_without_dims\",\"kind\":\"embedding\",\"field\":\"body\",\"producer_json\":\"{\\\"version\\\":2,\\\"provider\\\":\\\"antfly\\\",\\\"model\\\":\\\"dense-model\\\",\\\"endpoint\\\":\\\"antfly:embedded\\\",\\\"sparse\\\":false}\"}",
            .{ .antfly_provider = local.provider() },
        ),
    );
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateEmbeddingEnrichmentProducerJsonWithOptions(
            std.testing.allocator,
            "{\"name\":\"credential_bearing_identity\",\"kind\":\"embedding\",\"field\":\"body\",\"expected_dims\":384,\"producer_json\":\"{\\\"version\\\":2,\\\"provider\\\":\\\"antfly\\\",\\\"model\\\":\\\"dense-model\\\",\\\"endpoint\\\":\\\"antfly:embedded\\\",\\\"sparse\\\":false,\\\"api_key\\\":\\\"must-not-be-provenance\\\"}\"}",
            .{ .antfly_provider = local.provider() },
        ),
    );
    try std.testing.expectError(
        error.InvalidEmbeddingArtifactProducer,
        validateEmbeddingEnrichmentProducerJsonWithOptions(
            std.testing.allocator,
            "{\"name\":\"credential_endpoint_identity\",\"kind\":\"embedding\",\"field\":\"body\",\"expected_dims\":384,\"producer_json\":\"{\\\"version\\\":2,\\\"provider\\\":\\\"openai\\\",\\\"model\\\":\\\"dense-model\\\",\\\"endpoint\\\":\\\"https://models.example/v1?api_key=secret\\\",\\\"sparse\\\":false}\"}",
            .{ .antfly_provider = local.provider() },
        ),
    );

    var dormant = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{"enrichments":[{"name":"staged_producer","kind":"embedding","field":"body","expected_dims":384}]}
    , .{ .antfly_provider = local.provider() });
    defer dormant.deinit();
    try std.testing.expect(!dormant.hasEntries());

    try std.testing.expectError(
        error.MissingEmbeddingArtifactProducer,
        ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
            \\{"enrichments":[{"name":"missing_producer","kind":"embedding","field":"body","expected_dims":384}],"vectors":{"type":"embeddings","dimension":384,"embedding_name":"missing_producer"}}
        , .{ .antfly_provider = local.provider() }),
    );

    var owned = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator,
        \\{"vectors":{"type":"embeddings","dimension":384,"embedding_name":"owned_producer","embedder":{"provider":"antfly","model":"dense-model"},"enrichments":[{"name":"owned_producer","kind":"embedding","field":"body","expected_dims":384}]}}
    , .{ .antfly_provider = local.provider() });
    defer owned.deinit();
    try std.testing.expect(owned.findEntry("owned_producer") != null);
}

pub fn testArtifactBackedEmbeddingTranslation() !void {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"embedding","embedding_name":"document_chunk_dense_v1","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "document_vectors", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"field\":\"embedding\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"dims\":384") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"embedding_name\":\"document_chunk_dense_v1\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"embedder\":") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"generator\"") == null);

    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{"document_vectors":{"type":"embeddings","field":"embedding","embedding_name":"document_chunk_dense_v1","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}}
    , local.provider());
    defer managed.deinit();
    try std.testing.expect(managed.findEntry("document_vectors") != null);
    try std.testing.expect(managed.findEntry("document_chunk_dense_v1") != null);
}

pub fn testArtifactBackedSparseEmbeddingTranslation() !void {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","sparse":true,"field":"embedding","embedding_name":"document_chunk_sparse_v1","source_artifact_name":"document_chunks_v1","embedder":{"provider":"antfly","model":"antflydb/sparse"}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "document_sparse", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"field\":\"embedding\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"embedding_name\":\"document_chunk_sparse_v1\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"embedder\":") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"generator\"") == null);

    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{"document_sparse":{"type":"embeddings","sparse":true,"field":"embedding","embedding_name":"document_chunk_sparse_v1","source_artifact_name":"document_chunks_v1","embedder":{"provider":"antfly","model":"antflydb/sparse"}}}
    , local.provider());
    defer managed.deinit();
    try std.testing.expect(managed.findEntry("document_sparse") != null);
    try std.testing.expect(managed.findEntry("document_chunk_sparse_v1") != null);

    var missing_output = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","sparse":true,"field":"embedding","source_artifact_name":"document_chunks_v1","embedder":{"provider":"antfly","model":"antflydb/sparse"}}
    , .{});
    defer missing_output.deinit();
    try std.testing.expectError(error.InvalidCreateTableRequest, translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "document_sparse", missing_output.value, .{ .antfly_provider = local.provider() }));

    var conflicting_generator = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","sparse":true,"template":"{{body}}","embedding_name":"document_sparse_v1","embedder":{"provider":"antfly","model":"antflydb/sparse"}}
    , .{});
    defer conflicting_generator.deinit();
    try std.testing.expectError(error.InvalidCreateTableRequest, translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "document_sparse", conflicting_generator.value, .{ .antfly_provider = local.provider() }));
}

test "managed embedder translates artifact backed embeddings config without generator" {
    try testArtifactBackedEmbeddingTranslation();
}

test "managed embedder translates artifact backed sparse embeddings config without generator" {
    try testArtifactBackedSparseEmbeddingTranslation();
}

test "managed embedder allows equivalent embedding name aliases" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "document_vectors_primary":{"type":"embeddings","field":"embedding","embedding_name":"document_chunk_dense_v1","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}},
        \\  "document_vectors_secondary":{"type":"embeddings","field":"embedding","embedding_name":"document_chunk_dense_v1","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
        \\}
    , local.provider());
    defer managed.deinit();

    try std.testing.expect(managed.findEntry("document_vectors_primary") != null);
    try std.testing.expect(managed.findEntry("document_vectors_secondary") != null);
    try std.testing.expect(managed.findEntry("document_chunk_dense_v1") != null);
}

test "managed embedder rejects conflicting embedding name aliases" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    try std.testing.expectError(error.InvalidManagedEmbeddingIndex, ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "document_vectors_primary":{"type":"embeddings","field":"embedding","embedding_name":"document_chunk_dense_v1","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}},
        \\  "document_vectors_secondary":{"type":"embeddings","field":"embedding","embedding_name":"document_chunk_dense_v1","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/other"}}
        \\}
    , local.provider()));
}

test "managed embedder separates index and artifact lookup namespaces with different configs" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "aliased_vectors":{"type":"embeddings","field":"embedding","embedding_name":"document_vectors","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}},
        \\  "document_vectors":{"type":"embeddings","field":"embedding","embedding_name":"document_vectors_v2","source_artifact_name":"document_chunks_v1","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/other"}}
        \\}
    , local.provider());
    defer managed.deinit();

    // Neither namespace may depend on the catalog's insertion order.
    for (0..2) |_| {
        try std.testing.expectEqualStrings("antflydb/other", managed.findQueryEntry("document_vectors").?.model);
        try std.testing.expectEqualStrings("antflydb/clipclap", managed.findArtifactEntry("document_vectors").?.model);
        try std.testing.expectEqualStrings("antflydb/clipclap", managed.findQueryEntry("aliased_vectors").?.model);
        try std.testing.expectEqualStrings("antflydb/other", managed.findArtifactEntry("document_vectors_v2").?.model);
        std.mem.reverse(ManagedEmbeddingEntry, managed.entries);
    }
}

test "managed embedder translates managed embeddings config with probed dimension" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expectEqual(@as(usize, 1), local.calls);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"dims\":3") != null);
}

test "managed embedder normalizes missing dimension from probe result" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();

    const normalized = (try normalizeEmbeddingsIndexDimensionJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() })) orelse return error.TestUnexpectedResult;
    defer std.testing.allocator.free(normalized);

    try std.testing.expectEqual(@as(usize, 1), local.calls);
    try std.testing.expect(std.mem.indexOf(u8, normalized, "\"dimension\":3") != null);

    var normalized_parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, normalized, .{});
    defer normalized_parsed.deinit();
    const config_json = try translateEmbeddingsIndexConfigJson(std.testing.allocator, "semantic_idx", normalized_parsed.value);
    defer std.testing.allocator.free(config_json);
    try std.testing.expectEqual(@as(usize, 1), local.calls);
}

test "managed embedder validates sparse config with probe during normalization" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","sparse":true,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();

    const normalized = try normalizeEmbeddingsIndexDimensionJsonWithOptions(std.testing.allocator, "sparse_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer if (normalized) |value| std.testing.allocator.free(value);
    try std.testing.expect(normalized != null);
    try std.testing.expect(std.mem.indexOf(u8, normalized.?, "\"semantic_producer\"") != null);
    try std.testing.expectEqual(@as(usize, 1), local.sparse_calls);
    var normalized_parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, normalized.?, .{});
    defer normalized_parsed.deinit();
    try std.testing.expect(normalized_parsed.value.object.get("dimension") == null);
    const identity = normalized_parsed.value.object.get("semantic_producer") orelse return error.TestUnexpectedResult;
    try ant_json.testing.expectSubsetJsonText(std.testing.allocator,
        \\{"provider":"antfly","model":"antflydb/clipclap","sparse":true,"endpoint":"antfly:embedded"}
    , identity.string);
    // A normalized catalog entry preserves its admitted identity on replay.
    const replay = try normalizeEmbeddingsIndexDimensionJsonWithOptions(std.testing.allocator, "sparse_idx", normalized_parsed.value, .{ .antfly_provider = local.provider() });
    defer if (replay) |owned| std.testing.allocator.free(owned);
    try std.testing.expect(replay == null);
}

test "managed embedder translates typed distance metric and embedder dimensions" {
    var local = TestLocalDenseProvider{ .dimensions = 3 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","distance_metric":"l2_squared","embedder":{"provider":"antfly","model":"antflydb/clipclap","dimensions":3}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"dims\":3") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"metric\":\"l2_squared\"") != null);
}

test "managed embedder translates template-based embeddings config into db generator config" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","template":"{{title}} {{body}}","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"source_template\":\"{{title}} {{body}}\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"source_field\":\"body\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"generator\":{\"kind\":\"dense_embedding\"") != null);
}

test "managed embedder translates external sparse embeddings config" {
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","external":true,"sparse":true}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJson(std.testing.allocator, "semantic_idx", parsed.value);
    defer std.testing.allocator.free(config_json);

    try std.testing.expectEqualStrings("{\"field\":\"embedding\"}", config_json);
}

test "managed embedder translates chunker config into db generator config" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"},"chunker":{"provider":"antfly","model":"fixed-bert-tokenizer","text":{"target_tokens":128,"overlap_tokens":16,"separator":"\n\n"}}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"artifact_name\":\"semantic_idx_chunks\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"chunker\":") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"provider\":\"antfly\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"model\":\"fixed-bert-tokenizer\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"target_tokens\":128") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"overlap_tokens\":16") != null);
}

test "managed embedder preserves chunker full text config" {
    var local = TestLocalDenseProvider{ .dimensions = 384 };
    var parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator,
        \\{"type":"embeddings","field":"body","dimension":384,"embedder":{"provider":"antfly","model":"antflydb/clipclap"},"chunker":{"provider":"antfly","store_chunks":false,"full_text_index":{},"text":{"target_tokens":128,"overlap_tokens":16}}}
    , .{});
    defer parsed.deinit();

    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{ .antfly_provider = local.provider() });
    defer std.testing.allocator.free(config_json);

    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"chunker\":") != null);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"full_text_index\":{}") != null);
}

test "managed embedder calls openai compatible embeddings endpoint" {
    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expect(std.mem.endsWith(u8, req.uri, "/v1/embeddings"));
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"model\":\"text-embedding-3-small\"") != null);
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"dimensions\":3") != null);
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"object":"list","data":[{"object":"embedding","index":0,"embedding":[0.125,0.25,0.5]}],"model":"text-embedding-3-small","usage":{"prompt_tokens":1,"total_tokens":1}}
                ),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const indexes_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}"}}}}}}
    , .{base_uri});
    defer std.testing.allocator.free(indexes_json);

    var managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer managed.deinit();

    const vector = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(vector);

    try std.testing.expectEqual(@as(usize, 3), vector.len);
    try std.testing.expectEqual(@as(f32, 0.125), vector[0]);
    try std.testing.expectEqual(@as(f32, 0.5), vector[2]);
}

pub fn testRemoteEmbeddingCancellation() !void {
    const alloc = std.testing.allocator;
    const DelayedApp = struct {
        entered: std.atomic.Value(bool) = .init(false),
        release: std.atomic.Value(bool) = .init(false),
        completed: std.atomic.Value(bool) = .init(false),

        fn executor(self: *@This()) http_common.RequestExecutor {
            return .{ .ptr = self, .vtable = &.{ .execute = execute } };
        }

        fn execute(ptr: *anyopaque, response_alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            if (req.method == .GET) return .{
                .status = 200,
                .content_type = try response_alloc.dupe(u8, "application/json"),
                .body = try response_alloc.dupe(u8, "{\"embedders\":{\"antflydb/clipclap\":{}}}"),
            };
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expect(
                std.mem.endsWith(u8, req.uri, "/v1/embeddings") or
                    std.mem.endsWith(u8, req.uri, "/embed"),
            );
            self.entered.store(true, .release);
            while (!self.release.load(.acquire)) std.atomic.spinLoopHint();
            self.completed.store(true, .release);
            return .{
                .status = 200,
                .content_type = try response_alloc.dupe(u8, "application/json"),
                .body = try response_alloc.dupe(u8,
                    \\{"object":"list","data":[{"object":"embedding","index":0,"embedding":[0.125,0.25,0.5]}]}
                ),
            };
        }
    };

    var app = DelayedApp{};
    var listener = std_http_listener.StdHttpListener.init(alloc, .{}, app.executor());
    defer {
        app.release.store(true, .release);
        listener.deinit();
    }
    try listener.start();
    const base_uri = try listener.baseUri(alloc);
    defer alloc.free(base_uri);

    const indexes_json = try std.fmt.allocPrint(alloc,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}"}}}}}}
    , .{base_uri});
    defer alloc.free(indexes_json);

    var io_impl = std.Io.Threaded.init(std.heap.page_allocator, .{});
    defer io_impl.deinit();
    var cancellation = std.atomic.Value(bool).init(false);
    var managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(alloc, indexes_json, .{
        .io = io_impl.io(),
        .cancellation = CancellationToken.fromAtomic(&cancellation),
    });
    defer managed.deinit();

    const Worker = struct {
        fn run(target: *ManagedEmbedder, err_out: *?anyerror) void {
            const vector = target.embedQuery(alloc, "semantic_idx", "alpha concept") catch |err| {
                err_out.* = err;
                return;
            };
            alloc.free(vector);
            err_out.* = error.TestUnexpectedResult;
        }
    };
    var err_out: ?anyerror = null;
    var worker = try std.testing.io.concurrent(Worker.run, .{ &managed, &err_out });
    while (!app.entered.load(.acquire)) std.atomic.spinLoopHint();

    const started_ns = monotonicNowNs();
    cancellation.store(true, .release);
    worker.await(std.testing.io);
    const elapsed_ns = monotonicNowNs() - started_ns;
    app.release.store(true, .release);
    while (!app.completed.load(.acquire)) std.atomic.spinLoopHint();

    try std.testing.expectEqual(error.Cancelled, err_out.?);
    try std.testing.expect(elapsed_ns < 250 * std.time.ns_per_ms);

    // Multimodal Antfly requests use a distinct provider path from dense text
    // batches. It must carry the same runtime lifecycle cancellation or a
    // ClipClap invocation can pin synchronous index activation until its full
    // transport deadline.
    app.entered.store(false, .release);
    app.release.store(false, .release);
    app.completed.store(false, .release);
    const antfly_indexes_json = try std.fmt.allocPrint(alloc,
        \\{{"visual_idx":{{"type":"embeddings","field":"image","dimension":3,"embedder":{{"provider":"antfly","model":"antflydb/clipclap","api_url":"{s}"}}}}}}
    , .{base_uri});
    defer alloc.free(antfly_indexes_json);
    var parts_cancellation = std.atomic.Value(bool).init(false);
    var multimodal = try ManagedEmbedder.initFromIndexesJsonWithOptions(alloc, antfly_indexes_json, .{
        .io = io_impl.io(),
        .cancellation = CancellationToken.fromAtomic(&parts_cancellation),
    });
    defer multimodal.deinit();

    const PartsWorker = struct {
        fn run(target: *ManagedEmbedder, err_out_ptr: *?anyerror) void {
            const parts = [_]template_mod.ContentPart{.{
                .media_url = "data:image/png;base64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD",
            }};
            const vector = target.denseInterface().embedDenseParts(
                alloc,
                "visual_idx",
                &parts,
                3,
            ) catch |err| {
                err_out_ptr.* = err;
                return;
            };
            alloc.free(vector);
            err_out_ptr.* = error.TestUnexpectedResult;
        }
    };
    err_out = null;
    var parts_worker = try std.testing.io.concurrent(PartsWorker.run, .{ &multimodal, &err_out });
    while (!app.entered.load(.acquire)) std.atomic.spinLoopHint();

    const parts_started_ns = monotonicNowNs();
    parts_cancellation.store(true, .release);
    parts_worker.await(std.testing.io);
    const parts_elapsed_ns = monotonicNowNs() - parts_started_ns;
    app.release.store(true, .release);
    while (!app.completed.load(.acquire)) std.atomic.spinLoopHint();

    try std.testing.expectEqual(error.Cancelled, err_out.?);
    try std.testing.expect(parts_elapsed_ns < 250 * std.time.ns_per_ms);
}

pub fn testFileBackedApiKeyRotation() !void {
    const alloc = std.testing.allocator;
    const AuthCaptureApp = struct {
        alloc: std.mem.Allocator,
        mutex: std.atomic.Mutex = .unlocked,
        headers: [2]?[]u8 = .{ null, null },
        count: usize = 0,

        fn deinit(self: *@This()) void {
            for (&self.headers) |*header| {
                if (header.*) |value| self.alloc.free(value);
                header.* = null;
            }
        }

        fn executor(self: *@This()) http_common.RequestExecutor {
            return .{
                .ptr = self,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(ptr: *anyopaque, response_alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            const auth = req.authorization orelse req.header("authorization") orelse "";
            platform_sync.lockYielding(&self.mutex);
            defer self.mutex.unlock();
            const index = self.count;
            if (index < self.headers.len) {
                if (self.headers[index]) |value| self.alloc.free(value);
                self.headers[index] = try self.alloc.dupe(u8, auth);
            }
            self.count += 1;

            return .{
                .status = 200,
                .content_type = try response_alloc.dupe(u8, "application/json"),
                .body = try response_alloc.dupe(u8,
                    \\{"object":"list","data":[{"object":"embedding","index":0,"embedding":[0.125,0.25,0.5]}],"model":"text-embedding-3-small","usage":{"prompt_tokens":1,"total_tokens":1}}
                ),
            };
        }

        fn expectHeader(self: *@This(), index: usize, expected: []const u8) !void {
            platform_sync.lockYielding(&self.mutex);
            defer self.mutex.unlock();
            try std.testing.expect(index < self.count);
            try std.testing.expectEqualStrings(expected, self.headers[index] orelse return error.TestUnexpectedResult);
        }
    };

    const store_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-managed-embedder-secret-rotation-{d}.json", .{monotonicNowNs()});
    defer alloc.free(store_path);
    var io_impl = std.Io.Threaded.init(std.heap.page_allocator, .{});
    defer io_impl.deinit();
    defer std.Io.Dir.cwd().deleteFile(io_impl.io(), store_path) catch {};

    try std.Io.Dir.cwd().writeFile(io_impl.io(), .{
        .sub_path = store_path,
        .data = "{\"secrets\":[{\"key\":\"openai.api_key\",\"value\":\"first-key\",\"created_at_ns\":1,\"updated_at_ns\":1}]}",
    });

    var secret_store = try common_secrets.FileStore.initWithIo(alloc, io_impl.io(), store_path);
    defer secret_store.deinit();

    var app = AuthCaptureApp{ .alloc = alloc };
    defer app.deinit();
    var listener = std_http_listener.StdHttpListener.init(alloc, .{}, app.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(alloc);
    defer alloc.free(base_uri);

    const indexes_json = try std.fmt.allocPrint(alloc,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}","api_key":"${{secret:openai.api_key}}"}}}}}}
    , .{base_uri});
    defer alloc.free(indexes_json);

    var managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(alloc, indexes_json, .{
        .secret_store = &secret_store,
        .deadline_ns = monotonicNowNs() + 30 * std.time.ns_per_s,
    });
    defer managed.deinit();
    const first_cache_key = try managed.queryCacheKey("semantic_idx", .principal, "alice", "same query");

    const env_indexes_json = try std.fmt.allocPrint(alloc,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}","api_key":"${{env:OPENAI_API_KEY}}"}}}}}}
    , .{base_uri});
    defer alloc.free(env_indexes_json);
    var env_managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(alloc, env_indexes_json, .{
        .secret_store = &secret_store,
    });
    defer env_managed.deinit();
    const env_cache_key = try env_managed.queryCacheKey("semantic_idx", .principal, "alice", "same query");

    // Store rotation must not invalidate sources that do not use the store,
    // even if an unused api_key reference is configured for a cloud provider.
    const independent_sources = [_]struct {
        provider: ProviderKind,
        api_key: ?common_secrets.SecretValue,
        credentials_path: []const u8 = "",
    }{
        .{ .provider = .openai, .api_key = .{ .literal = @constCast("literal-key") } },
        .{ .provider = .openai, .api_key = null },
        .{ .provider = .vertex, .api_key = .{ .secret_ref = @constCast("unused") } },
        .{ .provider = .vertex, .api_key = null, .credentials_path = "credentials.json" },
        .{ .provider = .bedrock, .api_key = .{ .secret_ref = @constCast("unused") } },
        .{ .provider = .ollama, .api_key = .{ .secret_ref = @constCast("unused") } },
    };
    // These are borrowed, stack-owned entries: only query identity is tested.
    var independent_entries: [independent_sources.len]ManagedEmbeddingEntry = undefined;
    var independent_keys: [independent_sources.len][32]u8 = undefined;
    for (independent_sources, &independent_entries, &independent_keys) |source, *entry, *key| {
        entry.* = .{
            .alloc = alloc,
            .index_name = @constCast("semantic_idx"),
            .provider = source.provider,
            .model = @constCast("model"),
            .base_url = @constCast(base_uri),
            .dimensions = 3,
            .api_key = source.api_key,
            .credentials_path = @constCast(source.credentials_path),
            .secret_store = &secret_store,
        };
        const independent = ManagedEmbedder{ .alloc = alloc, .entries = entry[0..1] };
        key.* = try independent.queryCacheKey("semantic_idx", .principal, "alice", "same query");
    }

    const first = try managed.embedQuery(alloc, "semantic_idx", "alpha concept");
    defer alloc.free(first);
    try app.expectHeader(0, "Bearer first-key");

    try std.Io.Dir.cwd().writeFile(io_impl.io(), .{
        .sub_path = store_path,
        .data = "{\"secrets\":[{\"key\":\"openai.api_key\",\"value\":\"second-key-longer\",\"created_at_ns\":1,\"updated_at_ns\":2}]}",
    });

    _ = try secret_store.refreshIfChanged();
    const rotated_cache_key = try managed.queryCacheKey("semantic_idx", .principal, "alice", "same query");
    try std.testing.expect(!std.mem.eql(u8, &first_cache_key, &rotated_cache_key));
    const rotated_env_cache_key = try env_managed.queryCacheKey("semantic_idx", .principal, "alice", "same query");
    try std.testing.expectEqualSlices(u8, &env_cache_key, &rotated_env_cache_key);
    for (&independent_entries, &independent_keys) |*entry, *key| {
        const independent = ManagedEmbedder{ .alloc = alloc, .entries = entry[0..1] };
        const rotated = try independent.queryCacheKey("semantic_idx", .principal, "alice", "same query");
        try std.testing.expectEqualSlices(u8, key, &rotated);
    }

    const second = try managed.embedQuery(alloc, "semantic_idx", "beta concept");
    defer alloc.free(second);
    try app.expectHeader(1, "Bearer second-key-longer");
}

test "managed embedder surfaces rate-limited openai compatible responses as retryable" {
    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expect(std.mem.endsWith(u8, req.uri, "/v1/embeddings"));
            return .{
                .status = 429,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8, "{\"error\":{\"message\":\"rate limited\"}}"),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const indexes_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}"}}}}}}
    , .{base_uri});
    defer std.testing.allocator.free(indexes_json);

    var managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer managed.deinit();

    try std.testing.expectError(error.EmbedRateLimited, managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept"));
}

test "managed embedder paces repeated openai compatible requests" {
    const PaceState = struct {
        var mutex: std.atomic.Mutex = .unlocked;
        var count: usize = 0;
        var times_ns: [4]u64 = .{ 0, 0, 0, 0 };

        fn reset() void {
            lockAtomic(&mutex);
            defer mutex.unlock();
            count = 0;
            times_ns = .{ 0, 0, 0, 0 };
        }

        fn record() void {
            lockAtomic(&mutex);
            defer mutex.unlock();
            if (count < times_ns.len) {
                times_ns[count] = monotonicNowNs();
                count += 1;
            }
        }
    };

    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            PaceState.record();
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"object":"list","data":[{"object":"embedding","index":0,"embedding":[0.125,0.25,0.5]}],"model":"text-embedding-3-small","usage":{"prompt_tokens":1,"total_tokens":1}}
                ),
            };
        }
    };

    PaceState.reset();
    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const indexes_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}","requests_per_minute":6000,"burst":1}}}}}}
    , .{base_uri});
    defer std.testing.allocator.free(indexes_json);

    var managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer managed.deinit();

    const first = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(first);
    const second = try managed.embedQuery(std.testing.allocator, "semantic_idx", "beta architecture");
    defer std.testing.allocator.free(second);

    try std.testing.expectEqual(@as(usize, 2), PaceState.count);
    try std.testing.expect(PaceState.times_ns[1] >= PaceState.times_ns[0]);
    try std.testing.expect(PaceState.times_ns[1] - PaceState.times_ns[0] >= 8 * std.time.ns_per_ms);
}

test "managed embedder shares pacing across instances" {
    const PaceState = struct {
        var mutex: std.atomic.Mutex = .unlocked;
        var count: usize = 0;
        var times_ns: [4]u64 = .{ 0, 0, 0, 0 };

        fn reset() void {
            lockAtomic(&mutex);
            defer mutex.unlock();
            count = 0;
            times_ns = .{ 0, 0, 0, 0 };
        }

        fn record() void {
            lockAtomic(&mutex);
            defer mutex.unlock();
            if (count < times_ns.len) {
                times_ns[count] = monotonicNowNs();
                count += 1;
            }
        }
    };

    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            PaceState.record();
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"object":"list","data":[{"object":"embedding","index":0,"embedding":[0.125,0.25,0.5]}],"model":"text-embedding-3-small","usage":{"prompt_tokens":1,"total_tokens":1}}
                ),
            };
        }
    };

    PaceState.reset();
    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const indexes_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}","requests_per_minute":6000,"burst":1}}}}}}
    , .{base_uri});
    defer std.testing.allocator.free(indexes_json);

    var first_managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer first_managed.deinit();
    var second_managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer second_managed.deinit();

    const first = try first_managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(first);
    const second = try second_managed.embedQuery(std.testing.allocator, "semantic_idx", "beta architecture");
    defer std.testing.allocator.free(second);

    try std.testing.expectEqual(@as(usize, 2), PaceState.count);
    try std.testing.expect(PaceState.times_ns[1] >= PaceState.times_ns[0]);
    try std.testing.expect(PaceState.times_ns[1] - PaceState.times_ns[0] >= 8 * std.time.ns_per_ms);
}

test "managed embedder calls ollama compatible embeddings endpoint" {
    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expect(std.mem.endsWith(u8, req.uri, "/v1/embeddings"));
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"model\":\"all-minilm\"") != null);
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"object":"list","data":[{"object":"embedding","index":0,"embedding":[0.2,0.4,0.8]}],"model":"all-minilm","usage":{"prompt_tokens":1,"total_tokens":1}}
                ),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const indexes_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"ollama","model":"all-minilm","url":"{s}"}}}}}}
    , .{base_uri});
    defer std.testing.allocator.free(indexes_json);

    var managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator, indexes_json);
    defer managed.deinit();

    const vector = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(vector);

    try std.testing.expectEqual(@as(usize, 3), vector.len);
    try std.testing.expectEqual(@as(f32, 0.2), vector[0]);
    try std.testing.expectEqual(@as(f32, 0.8), vector[2]);
}

test "managed embedder rejects embedding dimension mismatch" {
    try testManagedEmbedderRejectsEmbeddingDimensionMismatch();
}

fn testManagedEmbedderRejectsEmbeddingDimensionMismatch() !void {
    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, _: http_common.HttpRequest) !http_common.HttpResponse {
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"object":"list","data":[{"object":"embedding","index":0,"embedding":[0.125,0.25]}],"model":"text-embedding-3-small","usage":{"prompt_tokens":1,"total_tokens":1}}
                ),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const index_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}"}}}}
    , .{base_uri});
    defer std.testing.allocator.free(index_json);
    var parsed = try std.json.parseFromSlice(
        std.json.Value,
        std.testing.allocator,
        index_json,
        .{},
    );
    defer parsed.deinit();

    try std.testing.expectError(error.InvalidCreateTableRequest, normalizeEmbeddingsIndexDimensionJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{}));
}

test "managed embedder defers operational dimension probe failure with explicit dimension" {
    try testManagedEmbedderDefersOperationalDimensionProbeFailure();
}

fn testChunkerOnlyDenseIndexPreservesDeclaredDimensions() !void {
    const alloc = std.testing.allocator;
    const index_json =
        \\{"type":"embeddings","field":"body","dimension":3,"chunker":{"provider":"antfly","store_chunks":false,"text":{"target_tokens":4,"separator":" "}}}
    ;
    var parsed = try std.json.parseFromSlice(std.json.Value, alloc, index_json, .{});
    defer parsed.deinit();

    const normalized = (try normalizeEmbeddingsIndexDimensionJsonWithOptions(
        alloc,
        "semantic_chunked_idx",
        parsed.value,
        .{},
    )) orelse return error.TestUnexpectedResult;
    defer alloc.free(normalized);
    try ant_json.testing.expectEqualJsonText(
        alloc,
        \\{"type":"embeddings","field":"body","dimension":3,"chunker":{"provider":"antfly","model":"fixed","store_chunks":false,"text":{"target_tokens":4,"separator":" "}}}
    ,
        normalized,
    );

    var normalized_parsed = try std.json.parseFromSlice(std.json.Value, alloc, normalized, .{});
    defer normalized_parsed.deinit();

    const translated = try translateEmbeddingsIndexConfigJsonWithOptions(
        alloc,
        "semantic_chunked_idx",
        normalized_parsed.value,
        .{},
    );
    defer alloc.free(translated);
    try std.testing.expect(std.mem.indexOf(u8, translated, "\"dims\":3") != null);
    try std.testing.expect(std.mem.indexOf(u8, translated, "\"chunker\":") != null);
}

test "managed embedder strict dimension probe failure is retryable" {
    try testManagedEmbedderStrictDimensionProbeFailureIsRetryable();
}

test "managed embedder treats executor saturation as an operational probe failure" {
    try std.testing.expect(isOperationalEmbeddingProbeError(error.ConcurrencyUnavailable));
}

pub fn testDimensionProbeValidationModes() !void {
    try testManagedEmbedderRejectsEmbeddingDimensionMismatch();
    try testManagedEmbedderDefersOperationalDimensionProbeFailure();
    try testManagedEmbedderDeferProbeRequiresDeclaredDimension();
    try testManagedEmbedderStrictDimensionProbeFailureIsRetryable();
    try testChunkerOnlyDenseIndexPreservesDeclaredDimensions();
}

fn testManagedEmbedderDefersOperationalDimensionProbeFailure() !void {
    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, _: http_common.HttpRequest) !http_common.HttpResponse {
            return .{
                .status = 429,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8, "{}"),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const index_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"type":"embeddings","field":"body","dimension":3,"validation":"defer_probe","embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}"}}}}
    , .{base_uri});
    defer std.testing.allocator.free(index_json);
    var parsed = try std.json.parseFromSlice(
        std.json.Value,
        std.testing.allocator,
        index_json,
        .{},
    );
    defer parsed.deinit();

    const normalized = (try normalizeEmbeddingsIndexDimensionJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{})).?;
    defer std.testing.allocator.free(normalized);
    try std.testing.expect(std.mem.indexOf(u8, normalized, "\"dimension\":3") != null);
    try std.testing.expect(std.mem.indexOf(u8, normalized, "\"validation\"") == null);

    var normalized_parsed = try std.json.parseFromSlice(std.json.Value, std.testing.allocator, normalized, .{});
    defer normalized_parsed.deinit();
    const config_json = try translateEmbeddingsIndexConfigJsonWithOptions(std.testing.allocator, "semantic_idx", normalized_parsed.value, .{});
    defer std.testing.allocator.free(config_json);
    try std.testing.expect(std.mem.indexOf(u8, config_json, "\"dims\":3") != null);
}

fn testManagedEmbedderDeferProbeRequiresDeclaredDimension() !void {
    const index_json =
        \\{"type":"embeddings","field":"body","validation":"defer_probe","embedder":{"provider":"openai","model":"text-embedding-3-small","url":"http://127.0.0.1:9"}}
    ;
    var parsed = try std.json.parseFromSlice(
        std.json.Value,
        std.testing.allocator,
        index_json,
        .{},
    );
    defer parsed.deinit();

    try std.testing.expectError(error.InvalidCreateTableRequest, normalizeEmbeddingsIndexDimensionJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{}));
}

fn testManagedEmbedderStrictDimensionProbeFailureIsRetryable() !void {
    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, _: http_common.HttpRequest) !http_common.HttpResponse {
            return .{
                .status = 429,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8, "{}"),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const index_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"openai","model":"text-embedding-3-small","url":"{s}"}}}}
    , .{base_uri});
    defer std.testing.allocator.free(index_json);
    var parsed = try std.json.parseFromSlice(
        std.json.Value,
        std.testing.allocator,
        index_json,
        .{},
    );
    defer parsed.deinit();

    try std.testing.expectError(error.EmbeddingProbeUnavailable, normalizeEmbeddingsIndexDimensionJsonWithOptions(std.testing.allocator, "semantic_idx", parsed.value, .{}));
}

test "managed embedder routes antfly model to local provider" {
    const Local = struct {
        calls: usize = 0,

        fn dense(ptr: *anyopaque, alloc: std.mem.Allocator, _: []const u8, texts: []const []const u8) ![][]f32 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.calls += 1;
            const vectors = try alloc.alloc([]f32, texts.len);
            errdefer alloc.free(vectors);
            for (texts, 0..) |_, i| {
                vectors[i] = try alloc.dupe(f32, &.{ 0.25, 0.5, 0.75 });
            }
            return vectors;
        }

        fn sparse(_: *anyopaque, alloc: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return try alloc.alloc(db_embedder.SparseEmbedding, 0);
        }
    };

    var local = Local{};
    const provider = AntflyProvider{
        .ptr = &local,
        .owns_invocation_admission = true,
        .embed_dense_texts = Local.dense,
        .embed_sparse_texts = Local.sparse,
    };

    const indexes_json =
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"antflydb/clipclap"}}}
    ;
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator, indexes_json, provider);
    defer managed.deinit();

    try std.testing.expectEqualStrings("", managed.entries[0].base_url);
    const vector = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(vector);
    try std.testing.expectEqual(@as(usize, 1), local.calls);
    try std.testing.expectEqualSlices(f32, &.{ 0.25, 0.5, 0.75 }, vector);
}

pub fn testLocalAdmissionOverloadNormalization() !void {
    const Local = struct {
        failure: anyerror = error.QueueFull,

        fn dense(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) anyerror![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn denseWithContext(
            ptr: *anyopaque,
            _: std.mem.Allocator,
            _: []const u8,
            _: []const []const u8,
            _: EmbeddingRequestContext,
        ) anyerror![][]f32 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.failure;
        }

        fn sparse(ptr: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) anyerror![]db_embedder.SparseEmbedding {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.failure;
        }

        fn parts(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const template_mod.ContentPart) anyerror![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn partsWithContext(
            ptr: *anyopaque,
            _: std.mem.Allocator,
            _: []const u8,
            _: []const template_mod.ContentPart,
            _: EmbeddingRequestContext,
        ) anyerror![][]f32 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.failure;
        }
    };

    var local = Local{};
    const provider = AntflyProvider{
        .ptr = &local,
        .owns_invocation_admission = true,
        .embed_dense_texts = Local.dense,
        .embed_dense_texts_with_context = Local.denseWithContext,
        .embed_sparse_texts = Local.sparse,
        .embed_dense_parts = Local.parts,
        .embed_dense_parts_with_context = Local.partsWithContext,
    };
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{
        \\  "dense_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"local-model"}},
        \\  "sparse_idx":{"type":"embeddings","field":"body","sparse":true,"embedder":{"provider":"antfly","model":"local-model"}},
        \\  "multimodal_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"local-model","multimodal":true}}
        \\}
    , provider);
    defer managed.deinit();

    const media_parts = [_]template_mod.ContentPart{.{ .media_url = "data:image/png;base64,YWFh" }};
    const sparse_entry = managed.findEntry("sparse_idx").?;
    const multimodal_entry = managed.findEntry("multimodal_idx").?;

    try std.testing.expectError(error.QueueFull, managed.embedQuery(std.testing.allocator, "dense_idx", "query"));
    try std.testing.expectError(error.QueueFull, embedSparseWithEntry(std.testing.allocator, sparse_entry, "query"));
    try std.testing.expectError(error.QueueFull, embedWithEntryParts(std.testing.allocator, multimodal_entry, &media_parts, 3));

    local.failure = error.ResourceTemporarilyUnavailable;
    try std.testing.expectError(error.EmbedTransientFailure, managed.embedQuery(std.testing.allocator, "dense_idx", "query"));
    try std.testing.expectError(error.EmbedTransientFailure, embedSparseWithEntry(std.testing.allocator, sparse_entry, "query"));
    try std.testing.expectError(error.EmbedTransientFailure, embedWithEntryParts(std.testing.allocator, multimodal_entry, &media_parts, 3));

    local.failure = error.ResourceLimitExceeded;
    try std.testing.expectError(error.ResourceLimitExceeded, managed.embedQuery(std.testing.allocator, "dense_idx", "query"));
    try std.testing.expectError(error.ResourceLimitExceeded, embedSparseWithEntry(std.testing.allocator, sparse_entry, "query"));
    try std.testing.expectError(error.ResourceLimitExceeded, embedWithEntryParts(std.testing.allocator, multimodal_entry, &media_parts, 3));

    local.failure = error.TestUnexpectedResult;
    // Default providers created and consumed in one runtime unit keep normal
    // Zig error semantics. Explicit foreign dispatchers still use the stable
    // status ABI, as covered by runtime_callback_abi's boundary tests.
    try std.testing.expectError(error.TestUnexpectedResult, managed.embedQuery(std.testing.allocator, "dense_idx", "query"));
    try std.testing.expectError(error.TestUnexpectedResult, embedSparseWithEntry(std.testing.allocator, sparse_entry, "query"));
    try std.testing.expectError(error.TestUnexpectedResult, embedWithEntryParts(std.testing.allocator, multimodal_entry, &media_parts, 3));
}

test "managed embedder routes antfly without api_url to local provider" {
    const Local = struct {
        calls: usize = 0,

        fn dense(ptr: *anyopaque, alloc: std.mem.Allocator, model: []const u8, texts: []const []const u8) ![][]f32 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.calls += 1;
            try std.testing.expectEqualStrings("local-model", model);
            const vectors = try alloc.alloc([]f32, texts.len);
            errdefer alloc.free(vectors);
            for (texts, 0..) |_, i| {
                vectors[i] = try alloc.dupe(f32, &.{ 0.5, 0.25, 0.125 });
            }
            return vectors;
        }

        fn sparse(_: *anyopaque, alloc: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return try alloc.alloc(db_embedder.SparseEmbedding, 0);
        }
    };

    var local = Local{};
    const provider = AntflyProvider{
        .ptr = &local,
        .embed_dense_texts = Local.dense,
        .embed_sparse_texts = Local.sparse,
    };

    const indexes_json =
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"local-model"}}}
    ;
    // An omitted URL selects the embedded provider, but does not relax the
    // schema's independently required model field.
    try std.testing.expectError(error.MissingField, ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator,
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly"}}}
    , provider));
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator, indexes_json, provider);
    defer managed.deinit();

    try std.testing.expectEqualStrings("", managed.entries[0].base_url);
    const vector = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(vector);
    try std.testing.expectEqual(@as(usize, 1), local.calls);
    try std.testing.expectEqualSlices(f32, &.{ 0.5, 0.25, 0.125 }, vector);
}

test "managed embedder routes antfly with api_url to antfly endpoint" {
    const Local = struct {
        fn dense(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) ![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn sparse(_: *anyopaque, alloc: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return try alloc.alloc(db_embedder.SparseEmbedding, 0);
        }
    };

    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            if (req.method == .GET) return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8, "{\"embedders\":{\"remote-model\":{}}}"),
            };
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expect(std.mem.endsWith(u8, req.uri, "/ai/v1/embed"));
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"model\":\"remote-model\"") != null);
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"input\":[\"alpha concept\"]") != null);
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"data":[{"embedding":[0.125,0.25,0.5]}]}
                ),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    var local = Local{};
    const provider = AntflyProvider{
        .ptr = &local,
        .embed_dense_texts = Local.dense,
        .embed_sparse_texts = Local.sparse,
    };

    const indexes_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"antfly","model":"remote-model","api_url":"{s}"}}}}}}
    , .{base_uri});
    defer std.testing.allocator.free(indexes_json);

    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator, indexes_json, provider);
    defer managed.deinit();

    const expected_base_url = try std.fmt.allocPrint(std.testing.allocator, "{s}/ai/v1", .{base_uri});
    defer std.testing.allocator.free(expected_base_url);
    try std.testing.expectEqualStrings(expected_base_url, managed.entries[0].base_url);

    const vector = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(vector);
    try std.testing.expectEqualSlices(f32, &.{ 0.125, 0.25, 0.5 }, vector);
}

pub fn testConfiguredInferenceAPIURLPrecedence() !void {
    const Local = struct {
        fn dense(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) ![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn sparse(_: *anyopaque, alloc: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return try alloc.alloc(db_embedder.SparseEmbedding, 0);
        }
    };

    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            if (req.method == .GET) return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8, "{\"embedders\":{\"remote-model\":{}}}"),
            };
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expect(std.mem.endsWith(u8, req.uri, "/ai/v1/embed"));
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"model\":\"remote-model\"") != null);
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"input\":[\"alpha concept\"]") != null);
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"data":[{"embedding":[0.125,0.25,0.5]}]}
                ),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    var local = Local{};
    const provider = AntflyProvider{
        .ptr = &local,
        .embed_dense_texts = Local.dense,
        .embed_sparse_texts = Local.sparse,
    };

    const indexes_json =
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"remote-model"}}}
    ;

    var managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(std.testing.allocator, indexes_json, .{
        .antfly_provider = provider,
        .inference_api_url = base_uri,
    });
    defer managed.deinit();

    const expected_base_url = try std.fmt.allocPrint(std.testing.allocator, "{s}/ai/v1", .{base_uri});
    defer std.testing.allocator.free(expected_base_url);
    try std.testing.expectEqualStrings(expected_base_url, managed.entries[0].base_url);

    const vector = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(vector);
    try std.testing.expectEqualSlices(f32, &.{ 0.125, 0.25, 0.5 }, vector);
}

test "managed embedder routes antfly with configured inference api url to antfly endpoint" {
    try testConfiguredInferenceAPIURLPrecedence();
}

pub fn testAntflyEmbedPartSelectionAndCardinality() !void {
    const Local = struct {
        saw_parts: bool = false,
        response_count: usize = 1,
        capability_calls: usize = 0,

        fn dense(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) ![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn sparse(_: *anyopaque, alloc: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return try alloc.alloc(db_embedder.SparseEmbedding, 0);
        }

        fn capabilities(raw: *anyopaque, _: std.mem.Allocator, model: []const u8, task: inference_work.Task) !inference_work.InferenceCapabilities {
            const self: *@This() = @ptrCast(@alignCast(raw));
            self.capability_calls += 1;
            try std.testing.expectEqualStrings("local-model", model);
            try std.testing.expectEqual(inference_work.Task.embed, task);
            return .{
                .task = .embed,
                .input_modalities = .{ .text = true, .image = true },
                .accepted_mime_types = .{ .text_plain = true, .image_png = true, .image_jpeg = true },
                .input_granularity = .page,
                .batch = .{ .mode = .native, .preferred_items = 3, .max_items = 3, .max_media_parts_per_item = 1 },
                .output = .embedding,
                .borrowed_attachments = true,
            };
        }

        fn parts(ptr: *anyopaque, alloc: std.mem.Allocator, model: []const u8, parts_slice: []const template_mod.ContentPart) ![][]f32 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            try std.testing.expectEqualStrings("local-model", model);
            try std.testing.expectEqual(@as(usize, 3), parts_slice.len);
            try std.testing.expectEqualStrings("caption", parts_slice[0].text);
            try std.testing.expectEqualStrings("data:image/png;base64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD", parts_slice[1].media_url);
            try std.testing.expectEqualStrings("image/png", parts_slice[2].binary.mime_type);
            self.saw_parts = true;

            const vectors = try alloc.alloc([]f32, self.response_count);
            errdefer alloc.free(vectors);
            var initialized: usize = 0;
            errdefer for (vectors[0..initialized]) |vector| alloc.free(vector);
            for (vectors) |*vector| {
                vector.* = try alloc.dupe(f32, &.{ 0.25, 0.5, 0.75 });
                initialized += 1;
            }
            return vectors;
        }
    };

    var local = Local{};
    const provider = AntflyProvider{
        .ptr = &local,
        .embed_dense_texts = Local.dense,
        .embed_sparse_texts = Local.sparse,
        .embed_dense_parts = Local.parts,
        .model_capabilities = Local.capabilities,
        .owns_invocation_admission = true,
    };

    const indexes_json =
        \\{"semantic_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"antfly","model":"local-model","multimodal":true}}}
    ;
    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator, indexes_json, provider);
    defer managed.deinit();
    const dense_interface = managed.denseInterface();
    try std.testing.expectEqual(@as(?usize, 1), dense_interface.mediaPartLimit("semantic_idx"));
    try std.testing.expectEqual(@as(?usize, null), dense_interface.mediaPartLimit("missing"));

    var bedrock_managed = try ManagedEmbedder.initFromIndexesJson(std.testing.allocator,
        \\{"bedrock_idx":{"type":"embeddings","field":"body","dimension":3,"embedder":{"provider":"bedrock","model":"amazon.titan-embed-image-v1","region":"us-east-1","multimodal":true}}}
    );
    defer bedrock_managed.deinit();
    try std.testing.expectEqual(@as(?usize, null), bedrock_managed.denseInterface().mediaPartLimit("bedrock_idx"));

    const png_header = "\x89PNG\r\n\x1a\n\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x02\x00\x00\x00\x03";
    const parts = [_]template_mod.ContentPart{
        .{ .text = "caption" },
        .{ .media_url = "data:image/png;base64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD" },
        .{ .binary = .{ .mime_type = "image/png", .data = png_header } },
    };
    const vector = try embedWithEntryParts(std.testing.allocator, &managed.entries[0], &parts, 3);
    defer std.testing.allocator.free(vector);
    try std.testing.expectEqualSlices(f32, &.{ 0.25, 0.5, 0.75 }, vector);
    try std.testing.expect(local.saw_parts);

    local.response_count = parts.len;
    const discoveries_before = local.capability_calls;
    const page_vectors = try dense_interface.embedDensePartItems(std.testing.allocator, "semantic_idx", &parts, 3);
    defer db_embedder.freeDenseEmbeddingBatch(std.testing.allocator, page_vectors);
    try std.testing.expectEqual(@as(usize, 3), page_vectors.len);
    for (page_vectors) |page_vector| {
        try std.testing.expectEqualSlices(f32, &.{ 0.25, 0.5, 0.75 }, page_vector);
    }
    try std.testing.expectEqual(discoveries_before + 1, local.capability_calls);
    const resolved = try dense_interface.resolvePartLease(std.testing.allocator, "semantic_idx");
    const discoveries_after_planning = local.capability_calls;
    var planned = dense_interface.withPartLease(resolved);
    planned.part_request_context = .{ .io = std.testing.io, .deadline_ns = null };
    const planned_vectors = try planned.embedDensePartItems(std.testing.allocator, "semantic_idx", &parts, 3);
    defer db_embedder.freeDenseEmbeddingBatch(std.testing.allocator, planned_vectors);
    try std.testing.expectEqual(discoveries_after_planning, local.capability_calls);
    planned.part_request_context.?.deadline_ns = 0;
    try std.testing.expectError(error.Timeout, planned.embedDensePartItems(std.testing.allocator, "semantic_idx", &parts, 3));
    try std.testing.expectEqual(discoveries_after_planning, local.capability_calls);

    local.response_count = 0;
    try std.testing.expectError(error.EmptyEmbeddingResponse, embedWithEntryParts(std.testing.allocator, &managed.entries[0], &parts, 3));
    local.response_count = 2;
    try std.testing.expectError(error.InvalidEmbeddingResponse, embedWithEntryParts(std.testing.allocator, &managed.entries[0], &parts, 3));

    try std.testing.expectError(error.EmptyEmbeddingResponse, embedWithEntryParts(std.testing.allocator, &managed.entries[0], &.{}, 3));
}

pub fn testBedrockCredentialTrafficBypassesModelQuota() !void {
    const alloc = std.testing.allocator;
    const io = std.testing.io;
    var registry = provider_limits.Registry.init(alloc);
    defer registry.deinit();
    var runtime = ProviderRuntime.init(alloc, io);
    runtime.limits = &registry;
    defer runtime.deinit();
    var server = try httpx.TestServer.start(alloc, io, &.{
        .{ .method = .POST, .path = "/sts", .respond = .{ .body = "<Credentials><AccessKeyId>test-access</AccessKeyId><SecretAccessKey>test-secret</SecretAccessKey><SessionToken>test-session</SessionToken></Credentials>" } },
        .{ .method = .POST, .path = "/model/amazon.titan-embed-text-v2%3A0/invoke", .respond = .{ .body = "{\"embedding\":[0.1,0.2]}" } },
    });
    defer server.deinit();
    const raw = try std.fmt.allocPrint(alloc,
        \\{{"i":{{"type":"embeddings","field":"body","dimension":2,"embedder":{{"provider":"bedrock","model":"amazon.titan-embed-text-v2:0","region":"us-east-1","url":{f},"rate_limit":{{"requests_per_minute":1}}}}}}}}
    , .{std.json.fmt(server.baseUrl(), .{})});
    defer alloc.free(raw);
    var managed = try ManagedEmbedder.initFromIndexesJsonWithOptions(alloc, raw, .{ .io = io, .provider_runtime = &runtime });
    defer managed.deinit();
    const entry = &managed.entries[0];
    var config = try embeddingHttpClientConfig(entry);
    try std.testing.expect(config.attempt_observer == null);
    config.timeouts = httpx.Timeouts.uniform(500);
    config.timeouts.request_ms = 500;
    var client = httpx.Client.initWithConfig(alloc, io, config);
    defer client.deinit();
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    try tmp.dir.writeFile(io, .{ .sub_path = "token", .data = "test-web-identity" });
    const token_path = try tmp.dir.realPathFileAlloc(io, "token", alloc);
    defer alloc.free(token_path);
    const sts = try std.fmt.allocPrint(alloc, "{s}/sts", .{server.baseUrl()});
    defer alloc.free(sts);
    var provider = bedrock_provider.Provider.init(alloc, &client, .{
        .region = "us-east-1",
        .endpoint = server.baseUrl(),
        .attempt_observer = embeddingAttemptObserver(entry),
        .credential_source = .{ .web_identity = .{ .role_arn = "test-role", .token_file = token_path, .sts_endpoint = sts } },
    });
    defer provider.deinit();
    const Run = struct {
        fn run(a: std.mem.Allocator, p: *bedrock_provider.Provider, err_out: *?anyerror) !void {
            runInner(a, p) catch |err| {
                err_out.* = err;
            };
        }
        fn runInner(a: std.mem.Allocator, p: *bedrock_provider.Provider) !void {
            var result = try p.embedText(a, "amazon.titan-embed-text-v2:0", &.{"hello"});
            defer result.deinit();
            try std.testing.expectEqual(@as(usize, 1), result.vectors.len);
            try std.testing.expectError(error.Timeout, p.embedText(a, "amazon.titan-embed-text-v2:0", &.{"again"}));
        }
    };
    var failure: ?anyerror = null;
    var group = std.Io.Group.init;
    defer group.cancel(io);
    try group.concurrent(io, Run.run, .{ alloc, &provider, &failure });
    try server.handleOne();
    try server.handleOne();
    try group.await(io);
    if (failure) |err| return err;
    try std.testing.expectEqual(@as(u32, 0), entry.quota.?.limiter().in_flight);
}

pub fn testBedrockRequestFormatConfiguration() !void {
    const alloc = std.testing.allocator;

    var system_profile = try ManagedEmbedder.initFromIndexesJson(alloc,
        \\{"bedrock_idx":{"type":"embeddings","field":"body","dimension":1024,"embedder":{"provider":"bedrock","model":"us.amazon.titan-embed-image-v1:0","region":"us-east-1"}}}
    );
    defer system_profile.deinit();
    try std.testing.expectEqual(bedrock_provider.RequestFormat.titan_multimodal, system_profile.entries[0].bedrock_request_format);

    var application_profile = try ManagedEmbedder.initFromIndexesJson(alloc,
        \\{"bedrock_idx":{"type":"embeddings","field":"body","dimension":1024,"embedder":{"provider":"bedrock","model":"arn:aws:bedrock:us-east-1:123456789012:application-inference-profile/team-embeddings","request_format":"titan_multimodal","region":"us-east-1"}}}
    );
    defer application_profile.deinit();
    try std.testing.expectEqual(bedrock_provider.RequestFormat.titan_multimodal, application_profile.entries[0].bedrock_request_format);

    try std.testing.expectError(
        error.BedrockRequestFormatRequired,
        ManagedEmbedder.initFromIndexesJson(alloc,
            \\{"bedrock_idx":{"type":"embeddings","field":"body","dimension":1024,"embedder":{"provider":"bedrock","model":"arn:aws:bedrock:us-east-1:123456789012:application-inference-profile/team-embeddings","region":"us-east-1"}}}
        ),
    );
}

test "managed embedder preserves antfly api_url path for shared antfly endpoint" {
    const Local = struct {
        fn dense(_: *anyopaque, _: std.mem.Allocator, _: []const u8, _: []const []const u8) ![][]f32 {
            return error.TestUnexpectedResult;
        }

        fn sparse(_: *anyopaque, alloc: std.mem.Allocator, _: []const u8, _: []const []const u8) ![]db_embedder.SparseEmbedding {
            return try alloc.alloc(db_embedder.SparseEmbedding, 0);
        }
    };

    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            if (req.method == .GET) return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8, "{\"embedders\":{\"remote-model\":{}}}"),
            };
            try std.testing.expectEqual(http_common.Method.POST, req.method);
            try std.testing.expect(std.mem.endsWith(u8, req.uri, "/ai/v1/embed"));
            try std.testing.expect(std.mem.indexOf(u8, req.body, "\"model\":\"remote-model\"") != null);
            return .{
                .status = 200,
                .content_type = try alloc.dupe(u8, "application/json"),
                .body = try alloc.dupe(u8,
                    \\{"data":[{"embedding":[0.75,0.5,0.25]}]}
                ),
            };
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);
    const shared_antfly_uri = try std.fmt.allocPrint(std.testing.allocator, "{s}/ai/v1", .{base_uri});
    defer std.testing.allocator.free(shared_antfly_uri);

    var local = Local{};
    const provider = AntflyProvider{
        .ptr = &local,
        .embed_dense_texts = Local.dense,
        .embed_sparse_texts = Local.sparse,
    };

    const indexes_json = try std.fmt.allocPrint(std.testing.allocator,
        \\{{"semantic_idx":{{"type":"embeddings","field":"body","dimension":3,"embedder":{{"provider":"antfly","model":"remote-model","api_url":"{s}"}}}}}}
    , .{shared_antfly_uri});
    defer std.testing.allocator.free(indexes_json);

    var managed = try ManagedEmbedder.initFromIndexesJsonWithAntflyProvider(std.testing.allocator, indexes_json, provider);
    defer managed.deinit();

    try std.testing.expectEqualStrings(shared_antfly_uri, managed.entries[0].base_url);

    const vector = try managed.embedQuery(std.testing.allocator, "semantic_idx", "alpha concept");
    defer std.testing.allocator.free(vector);
    try std.testing.expectEqualSlices(f32, &.{ 0.75, 0.5, 0.25 }, vector);
}

test "managed embedder query template supports remoteText and surfaces permanent helper failures" {
    const FakeApp = struct {
        fn executor() http_common.RequestExecutor {
            return .{
                .ptr = undefined,
                .vtable = &.{
                    .execute = execute,
                },
            };
        }

        fn execute(_: *anyopaque, alloc: std.mem.Allocator, req: http_common.HttpRequest) !http_common.HttpResponse {
            try std.testing.expectEqual(http_common.Method.GET, req.method);
            if (std.mem.endsWith(u8, req.uri, "/doc.txt")) {
                return .{
                    .status = 200,
                    .content_type = try alloc.dupe(u8, "text/plain"),
                    .body = try alloc.dupe(u8, "alpha concept"),
                };
            }
            if (std.mem.endsWith(u8, req.uri, "/missing.pdf")) {
                return .{
                    .status = 404,
                    .content_type = try alloc.dupe(u8, "application/pdf"),
                    .body = try alloc.dupe(u8, ""),
                };
            }
            return error.TestUnexpectedResult;
        }
    };

    var listener = std_http_listener.StdHttpListener.init(std.testing.allocator, .{}, FakeApp.executor());
    defer listener.deinit();
    try listener.start();

    const base_uri = try listener.baseUri(std.testing.allocator);
    defer std.testing.allocator.free(base_uri);

    const text_url = try std.fmt.allocPrint(std.testing.allocator, "{s}/doc.txt", .{base_uri});
    defer std.testing.allocator.free(text_url);
    const rendered_text = try renderQueryTemplate(std.testing.allocator, "{{remoteText url=this}}", text_url);
    defer std.testing.allocator.free(rendered_text);
    try validateRenderedTemplate(std.testing.allocator, rendered_text);
    try std.testing.expectEqualStrings("alpha concept", std.mem.trim(u8, rendered_text, &std.ascii.whitespace));

    const pdf_url = try std.fmt.allocPrint(std.testing.allocator, "{s}/missing.pdf", .{base_uri});
    defer std.testing.allocator.free(pdf_url);
    const rendered_pdf = try renderQueryTemplate(std.testing.allocator, "{{remotePDF url=this}}", pdf_url);
    defer std.testing.allocator.free(rendered_pdf);
    try std.testing.expectError(QueryTemplateError.PermanentPromptFailure, validateRenderedTemplate(std.testing.allocator, rendered_pdf));
}
