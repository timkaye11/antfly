// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const system_catalog = @import("../system_catalog/domain.zig");
const ha_wal = @import("../storage/wal_runtime.zig");
const inference_provider = @import("inference_provider.zig");
const lease_executor = @import("lease_executor.zig");
const builtin = @import("builtin");
const platform_sync = @import("antfly_platform").sync;
const platform_clock = @import("antfly_platform").clock;
const httpx = @import("httpx");
const antfly = @import("runtime_root.zig");
const group_ids = @import("../common/group_ids.zig");
const threaded_io_limits = @import("../common/threaded_io_limits.zig");
const fs_paths = @import("../common/fs_paths.zig");
const process_memory_budget = @import("../common/process_memory_budget.zig");
const preload_model_spec = @import("../common/preload_model_spec.zig");
const platform_time = @import("antfly_platform").time;
const platform = @import("antfly_platform");
const inference_bridge = @import("inference_bridge.zig");
const inference_connection_abi = @import("../inference_connection_abi.zig");
const internal_service_auth = @import("../api/internal_service_auth.zig");
const runtime_http_abi = @import("../runtime_http_abi.zig");
const kernel_owner_client = @import("../storage/kernel_owner_client.zig");
const storage_source_options = @import("storage_source_options");
const control_only_storage_sources = storage_source_options.control_only;
const LegacyLiteHandle = if (control_only_storage_sources) struct {} else antfly.lite.backend.Handle;
const LegacyAuthBackend = if (control_only_storage_sources) struct {} else antfly.lsm_backend.BackendHandle;
const CancellationToken = @import("../common/cancellation.zig").CancellationToken;
const inline_inference_codegen = builtin.is_test;
const inference_host = if (inline_inference_codegen) @import("inference_host.zig") else struct {};
const inference_chunker = @import("inference_chunker");
const chunking_types = @import("../chunking/types.zig");

const ApiHttpServer = antfly.public_api.ApiHttpServer;
const ApiKernelHandler = antfly.public_api.kernel_bridge.HttpxHandler;
const http_common = antfly.common.http;
const public_api_max_requests_per_connection: u32 = 64;
const public_api_max_body_size: usize = antfly.common.http.default_max_request_bytes;
const local_schema_migration_finalize_interval_ms: u64 = std.time.ms_per_s;

const LocalInferenceConnectionContext = inference_provider.LocalInferenceConnectionContext;

const LocalSchemaProgressProvider = struct {
    ptr: *anyopaque,
    shard_db_adapter: ?antfly.metadata.ShardDbAdapter = null,
    collect: *const fn (
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        tables: []const antfly.metadata.TableRecord,
        ranges: []const antfly.metadata.RangeRecord,
    ) anyerror!antfly.data.runtime.DataServer.LocalSchemaProgressSnapshot,
};
const default_public_port: u16 = 8080;
const cors_default_methods = [_][]const u8{ "GET", "POST", "PUT", "DELETE", "OPTIONS", "PATCH" };
const cors_default_headers = [_][]const u8{ "Content-Type", "Authorization", "X-Requested-With", "Accept", "Origin" };
const cors_default_exposed_headers = [_][]const u8{
    "X-Request-ID",
    "Retry-After",
    "Deprecation",
    "X-RateLimit-Limit",
    "X-RateLimit-Remaining",
    "X-RateLimit-Reset",
    "X-Antfly-Next-Cursor",
};
const cors_default_max_age: u32 = 3600;
const antfarm_max_file_bytes: usize = 64 * 1024 * 1024;
const standalone_session_ttl_ns: u64 = std.time.ns_per_hour;
const standalone_session_cleanup_interval_ns: u64 = std.time.ns_per_min;
const standalone_session_max_count: usize = 1024;
const standalone_session_max_record_bytes: usize = 16 * 1024 * 1024;
const standalone_session_savepoint_limit: usize = 64;
const antfarm_installed_asset_roots = [_][]const u8{
    "../share/antfly/antfarm", // Prefix installation: bin/antfly.
    "share/antfly/antfarm", // Release archive: antfly at the archive root.
};
const antfarm_asset_roots = [_][]const u8{
    "zig/pkg/antfly/antfarm",
    "pkg/antfly/antfarm",
    "/usr/share/antfly/antfarm",
    "antfarm",
};
const ha_lease_poll_interval_ns: u64 = 2 * std.time.ns_per_s;
const ha_lease_request_timeout_ms: u32 = 1_000;
const ha_lease_timing_jitter_ns: u64 = std.time.ns_per_s;
const ha_lease_min_grace_ms: u64 = 10_000;
const ha_lease_api_host_env = "ANTFLY_HA_LEASE_API_HOST";
const ha_lease_default_api_host = "kubernetes.default.svc";
const ha_lease_max_response_bytes: usize = 256 * 1024;
const internal_service_secret_key = "antfly.internal_service.secret";
const internal_service_issuer_key = "antfly.internal_service.issuer";

const StandaloneHttpContext = struct {
    api_server: ?*ApiHttpServer,
    cors_config: ?*const antfly.common.config.Config.CorsConfig = null,
};

const HALeaseAPIEndpoint = struct {
    host: []const u8,
    port: []const u8,
};

fn haLeaseAPIEndpoint(env: *const std.process.Environ.Map) !HALeaseAPIEndpoint {
    return .{
        .host = env.get(ha_lease_api_host_env) orelse ha_lease_default_api_host,
        .port = env.get("KUBERNETES_SERVICE_PORT_HTTPS") orelse env.get("KUBERNETES_SERVICE_PORT") orelse return error.HALeaseAPIPortMissing,
    };
}

const CliConfig = struct {
    config_path: ?[]const u8 = null,
    experimental: bool = false,
    bind_host: ?[]const u8 = null,
    bind_port: ?u16 = null,
    health_enabled: ?bool = null,
    health_port: ?u16 = null,
    control_tick_ms: u64 = antfly.raft.RuntimeCadence.default_control_tick_ms,
    local_node_id: ?u64 = null,
    auth_enabled: ?bool = null,
    ard_base_url: ?[]const u8 = null,
    ard_publisher_domain: ?[]const u8 = null,
    ard_display_name: ?[]const u8 = null,
    ard_public_catalog_enabled: bool = false,
    inference_models_dir: ?[]const u8 = null,
    inference_ml_dir: ?[]const u8 = null,
    inference_host_budget_mb: usize = 0,
    inference_backend_budget_mb: usize = 0,
    inference_combined_budget_mb: usize = 0,
    inference_kv_budget_mb: usize = 0,
    inference_scratch_budget_mb: usize = 0,
    inference_process_memory_budget_mb: ?usize = null,
    inference_kernel_jit_mode: ?antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode = null,
    inference_preload_models: std.ArrayListUnmanaged(inference_bridge.WarmModel) = .empty,
    data_dir: ?[]const u8 = null,
    storage_engine: ?antfly.common.config.StorageEngine = null,
    storage_path: ?[]const u8 = null,
    storage_fsync: ?bool = null,
    replica_root_dir: ?[]const u8 = null,
    replica_catalog_path: ?[]const u8 = null,
    snapshot_root_dir: ?[]const u8 = null,
    extension_package_store_dir: ?[]const u8 = null,
    secret_store_paths: std.ArrayListUnmanaged([]const u8) = .empty,
    ha_primary_log: ?[]const u8 = null,
    ha_primary_slots: ?[]const u8 = null,
    ha_primary_node_id: ?[]const u8 = null,
    ha_seed_capture_root: ?[]const u8 = null,
    ha_fence_wal: ?[]const u8 = null,
    ha_former_primary_log: ?[]const u8 = null,
    admin_token_env: ?[]const u8 = null,
    ha_retention_max_lag_lsn: ?u64 = null,
    ha_retention_max_retained_bytes: ?u64 = null,
    ha_retention_max_retained_age_ns: ?u64 = null,
    ha_sync_mode: ?antfly.hot_standby.primary.DurabilityMode = null,
    ha_sync_selection: ?antfly.hot_standby.primary.StandbySelection = null,
    ha_sync_required: ?usize = null,
    ha_sync_failure_policy: ?antfly.hot_standby.primary.FailurePolicy = null,
    ha_sync_standby_names: std.ArrayListUnmanaged([]const u8) = .empty,
    ha_standby_log: ?[]const u8 = null,
    ha_standby_progress: ?[]const u8 = null,
    ha_standby_node_id: ?[]const u8 = null,
    ha_standby_upstream_url: ?[]const u8 = null,
    ha_standby_slot: ?[]const u8 = null,
    ha_startup_target_root: ?[]const u8 = null,
    ha_startup_topology_id: ?[]const u8 = null,
    ha_startup_topology_generation: ?u64 = null,
    ha_startup_generation: ?[]const u8 = null,
    ha_startup_slot_name: ?[]const u8 = null,
    ha_startup_timeline_id: ?u64 = null,
    ha_startup_epoch: ?u64 = null,
    ha_startup_target_pvc_name: ?[]const u8 = null,
    ha_startup_target_pvc_uid: ?[]const u8 = null,
    ha_startup_manifest_sha256: ?[]const u8 = null,
    ha_startup_aggregate_sha256: ?[]const u8 = null,
    ha_startup_seed_receipt_sha256: ?[]const u8 = null,
    ha_startup_capture_receipt_sha256: ?[]const u8 = null,
    ha_startup_materialized_receipt_sha256: ?[]const u8 = null,
    ha_startup_materialized_aggregate_sha256: ?[]const u8 = null,
    ha_startup_target_local_node_id: ?u64 = null,
    ha_startup_target_replica_id: ?u64 = null,
    ha_cluster_id: ?u64 = null,
    ha_shard_id: ?u64 = null,
    ha_table_id: ?u64 = null,
    ha_timeline_id: ?u64 = null,
    ha_epoch: ?u64 = null,
    help: bool = false,

    fn deinit(self: *CliConfig, alloc: std.mem.Allocator) void {
        self.secret_store_paths.deinit(alloc);
        self.ha_sync_standby_names.deinit(alloc);
        self.inference_preload_models.deinit(alloc);
        self.* = undefined;
    }

    fn primarySecretStorePath(self: *const CliConfig) ?[]const u8 {
        if (self.secret_store_paths.items.len == 0) return null;
        return self.secret_store_paths.items[0];
    }
};

// JSON is used only as a versioned, language-neutral payload inside the
// inference CreateContext. The distributed unit owns config parsing; the
// inference unit owns translation into inference runtime types.
const InferenceRuntimeConfigWire = struct {
    const KernelJit = struct {
        mode: antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode = .off,
        cache_dir: ?[]const u8 = null,
        max_cache_bytes_mb: usize = 1024,
        preload_budget_ms: u64 = 300_000,
    };
    const PromptCache = struct {
        enabled: bool = false,
        mode: antfly.common.config.Config.InferenceConfig.PromptCacheConfig.Mode = .block_hash,
        max_bytes_mb: usize = 512,
        min_tokens: usize = 64,
        ttl_ms: u64 = 300_000,
    };

    embedded_enabled: bool = true,
    worker_environment: []const struct { name: []const u8, value: []const u8 } = &.{},
    max_concurrent_requests: ?usize = null,
    kernel_jit: KernelJit = .{},
    prompt_cache: PromptCache = .{},
};

fn resolveKernelJitMode(
    configured: antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode,
    environment: ?[]const u8,
    cli: ?antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode,
) !antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode {
    if (cli) |mode| return mode;
    if (environment) |raw|
        return std.meta.stringToEnum(
            antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode,
            raw,
        ) orelse error.InvalidArguments;
    return configured;
}

const RuntimeLeaseWatchdog = struct {
    const ObservationFailureStage = enum { fetch, validation };

    watchdog: antfly.hot_standby.kubernetes_lease_watchdog.Watchdog,
    io: std.Io,
    executor: lease_executor.LeaseExecutor,
    uri: []u8,
    token_path: []const u8,
    lease_name: []const u8,
    lease_namespace: []const u8,
    stable_topology_id: []const u8,
    node_id: []const u8,
    pod_uid: []const u8,
    process_boot_id: [64]u8,
    owned_data_generation: ?[]u8 = null,
    proof_active: std.atomic.Value(bool) = .init(false),
    proof_capability_deadline_ns: std.atomic.Value(u64) = .init(0),
    proof_authority_deadline_ns: std.atomic.Value(u64) = .init(0),
    proof_transitions: std.atomic.Value(u64) = .init(0),
    proof_mutex: std.atomic.Mutex = .unlocked,
    sentinel_persisted: bool = false,
    next_poll_ns: u64 = 0,
    fetch_failure_logged: bool = false,
    validation_failure_logged: bool = false,

    fn initFromEnv(
        alloc: std.mem.Allocator,
        io: std.Io,
        env: *const std.process.Environ.Map,
        cli: CliConfig,
        pod_uid: ?[]const u8,
    ) !?RuntimeLeaseWatchdog {
        const lease_name = env.get("ANTFLY_HA_LEASE_NAME") orelse return null;
        const namespace = env.get("ANTFLY_HA_LEASE_NAMESPACE") orelse return error.HALeaseNamespaceMissing;
        const api_endpoint = try haLeaseAPIEndpoint(env);
        const grace_raw = env.get("ANTFLY_HA_LEASE_GRACE_MS") orelse return error.HALeaseGraceMissing;
        const sentinel_path = env.get("ANTFLY_HA_LEASE_SENTINEL_PATH") orelse return error.HALeaseSentinelMissing;
        const topology_id = env.get("ANTFLY_HA_LEASE_TOPOLOGY_ID") orelse return error.HALeaseTopologyIDMissing;
        const resolved_pod_uid = pod_uid orelse return error.HALeasePodUIDMissing;
        const node_id = cli.ha_primary_node_id orelse cli.ha_standby_node_id orelse return error.HALeaseNodeIDMissing;
        const grace_ms = std.fmt.parseInt(u64, grace_raw, 10) catch return error.HALeaseGraceInvalid;
        if (grace_ms < ha_lease_min_grace_ms or grace_ms >= 30_000) return error.HALeaseGraceInvalid;
        const requested_generation = cli.ha_startup_generation orelse "initial";
        const sentinel_generation = try antfly.hot_standby.kubernetes_lease_watchdog.loadSentinelGenerationAlloc(alloc, io, sentinel_path);
        defer if (sentinel_generation) |generation| alloc.free(generation);
        const repaired_generation = if (sentinel_generation != null)
            try antfly.hot_standby.kubernetes_lease_watchdog.loadValidatedRepairGenerationAlloc(alloc, io, sentinel_path, topology_id, node_id)
        else
            null;
        errdefer if (repaired_generation) |generation| alloc.free(generation);
        if (repaired_generation) |generation| {
            try antfly.hot_standby.kubernetes_lease_watchdog.rotateSentinelAfterValidatedRepair(
                alloc,
                io,
                sentinel_path,
                topology_id,
                node_id,
                generation,
            );
        }
        const data_generation = repaired_generation orelse requested_generation;
        var entropy: [32]u8 = undefined;
        try io.randomSecure(&entropy);
        const process_boot_id = std.fmt.bytesToHex(entropy, .lower);
        const scope = antfly.hot_standby.kubernetes_lease_watchdog.Scope{
            .topology_id = topology_id,
            .node_id = node_id,
            .data_generation = data_generation,
            .process_boot_id = &process_boot_id,
        };
        var executor = try lease_executor.LeaseExecutor.init(
            alloc,
            io,
            env.get("ANTFLY_HA_LEASE_CA_PATH") orelse antfly.hot_standby.kubernetes_lease_watchdog.service_account_ca_path,
            ha_lease_max_response_bytes,
        );
        errdefer executor.deinit();
        return .{
            .watchdog = try .init(.{
                .scope = scope,
                .grace_ns = grace_ms * std.time.ns_per_ms,
                .sentinel_path = sentinel_path,
            }, sentinel_generation, repaired_generation),
            .io = io,
            .executor = executor,
            .uri = try antfly.hot_standby.kubernetes_lease_watchdog.leaseURLAlloc(alloc, api_endpoint.host, api_endpoint.port, namespace, lease_name),
            .token_path = env.get("ANTFLY_HA_LEASE_TOKEN_PATH") orelse antfly.hot_standby.kubernetes_lease_watchdog.service_account_token_path,
            .lease_name = lease_name,
            .lease_namespace = namespace,
            .stable_topology_id = topology_id,
            .node_id = node_id,
            .pod_uid = resolved_pod_uid,
            .process_boot_id = process_boot_id,
            .owned_data_generation = repaired_generation,
            .sentinel_persisted = sentinel_generation != null and std.mem.eql(u8, sentinel_generation.?, data_generation),
        };
    }

    fn proofSource(self: *const RuntimeLeaseWatchdog) antfly.hot_standby.http_admin.Server.AuthOptions.LeaseWatchdogProofSource {
        return .{ .ptr = self, .snapshot_fn = proofSnapshot };
    }

    /// `Watchdog.Config` borrows its scope strings. `initFromEnv` necessarily
    /// constructs the return value through a temporary, so its initial slice
    /// cannot safely point at the temporary process_boot_id array after the
    /// value is moved into the caller's final storage. Rebind exactly once at
    /// that final address before the watchdog can be observed by another
    /// thread.
    fn bindOwnedProcessBootID(self: *RuntimeLeaseWatchdog) void {
        self.watchdog.cfg.scope.process_boot_id = &self.process_boot_id;
    }

    fn repairReceiptSink(self: *RuntimeLeaseWatchdog) antfly.hot_standby.http_admin.Server.AuthOptions.RepairReceiptSink {
        return .{ .ptr = self, .record_fn = recordRepairReceipt };
    }

    fn recordRepairReceipt(ptr: *anyopaque, result: antfly.hot_standby.rejoin.RewindResult) !void {
        const self: *RuntimeLeaseWatchdog = @ptrCast(@alignCast(ptr));
        _ = try antfly.hot_standby.kubernetes_lease_watchdog.persistRepairReceipt(
            self.executor.alloc,
            self.io,
            self.watchdog.cfg.sentinel_path,
            self.stable_topology_id,
            self.node_id,
            result.target_timeline_id,
            result.target_epoch,
            result.current_last_lsn,
            "",
        );
    }

    fn proofSnapshot(ptr: *const anyopaque, alloc: std.mem.Allocator) !?antfly.admin.HALeaseWatchdogProof {
        const self: *RuntimeLeaseWatchdog = @ptrCast(@alignCast(@constCast(ptr)));
        platform_sync.lockYielding(&self.proof_mutex);
        defer self.proof_mutex.unlock();
        const deadline = self.proof_authority_deadline_ns.load(.acquire);
        const capability_deadline = self.proof_capability_deadline_ns.load(.acquire);
        const now = platform_time.authorityNs();
        const authority_remaining_ms: u64 = if (deadline > now)
            @intCast(@min(
                (deadline - now) / std.time.ns_per_ms,
                self.watchdog.cfg.grace_ns / std.time.ns_per_ms,
            ))
        else
            0;
        return .{
            .capability_version = 1,
            .active = self.proof_active.load(.acquire) and capability_deadline != 0 and now < capability_deadline,
            // Rounding down makes the proof conservative at the sub-ms edge.
            .authority_granted = authority_remaining_ms > 0,
            .authority_remaining_ms = authority_remaining_ms,
            .lease_name = self.lease_name,
            .lease_namespace = self.lease_namespace,
            .stable_topology_id = self.stable_topology_id,
            .local_node_id = self.node_id,
            // The parsed JSON buffer is released after every poll. Return an
            // owned copy of the fixed watchdog snapshot so response encoding
            // can never race a later Lease observation.
            .observed_holder_node_id = try alloc.dupe(u8, self.watchdog.observedHolder()),
            .pod_uid = self.pod_uid,
            .process_boot_id = &self.process_boot_id,
            .observed_lease_transitions = @intCast(self.proof_transitions.load(.acquire)),
            .max_fence_latency_ms = @intCast(self.watchdog.cfg.grace_ns / std.time.ns_per_ms),
        };
    }

    fn deinit(self: *RuntimeLeaseWatchdog, alloc: std.mem.Allocator) void {
        self.executor.deinit();
        alloc.free(self.uri);
        if (self.owned_data_generation) |generation| alloc.free(generation);
        self.* = undefined;
    }

    fn poll(
        self: *RuntimeLeaseWatchdog,
        alloc: std.mem.Allocator,
        data_server: *antfly.data.runtime.DataServer,
    ) !void {
        const io = self.executor.io;
        const poll_started_ns = platform_time.authorityNs();
        if (poll_started_ns < self.next_poll_ns) return;
        self.next_poll_ns = poll_started_ns +| ha_lease_poll_interval_ns;
        const body = antfly.hot_standby.kubernetes_lease_watchdog.fetchLeaseAlloc(
            alloc,
            io,
            self.executor.executor(),
            self.uri,
            self.token_path,
            ha_lease_request_timeout_ms,
        ) catch |err| {
            platform_sync.lockYielding(&self.proof_mutex);
            const failure = self.noteObservationFailureLocked(.fetch, err, platform_time.authorityNs());
            self.proof_mutex.unlock();
            return try self.applyDecision(alloc, io, data_server, failure);
        };
        defer alloc.free(body);
        const observed_monotonic_ns = platform_time.authorityNs();
        platform_sync.lockYielding(&self.proof_mutex);
        const decision = self.watchdog.observe(
            alloc,
            body,
            platform_time.realtimeNs(),
            observed_monotonic_ns,
        ) catch |err| {
            // A syntactically valid HTTP response that cannot prove the exact
            // topology/generation is not current capability evidence.
            const failure = self.noteObservationFailureLocked(.validation, err, platform_time.authorityNs());
            self.proof_mutex.unlock();
            return try self.applyDecision(alloc, io, data_server, failure);
        };
        self.publishValidatedObservationLocked(decision, observed_monotonic_ns);
        self.proof_mutex.unlock();
        try self.applyDecision(alloc, io, data_server, decision);
    }

    // Called only after `Watchdog.observe` has validated the Lease response.
    // `active` proves that this exact process is still monitoring and enforcing
    // the authority gate; it is deliberately independent from whether the
    // Lease currently grants authority. An expired pre-transfer Lease is thus
    // fresh capability evidence for a self-fenced standby, while a latched
    // process remains inactive.
    fn publishValidatedObservationLocked(
        self: *RuntimeLeaseWatchdog,
        decision: antfly.hot_standby.kubernetes_lease_watchdog.Decision,
        observed_monotonic_ns: u64,
    ) void {
        switch (decision) {
            .waiting, .observed, .pending_authority, .authorized, .grace => {
                self.proof_transitions.store(self.watchdog.last_generation, .release);
                self.proof_active.store(true, .release);
                self.proof_capability_deadline_ns.store(observed_monotonic_ns +| self.watchdog.cfg.grace_ns, .release);
            },
            .fence => {
                self.proof_active.store(false, .release);
                self.proof_capability_deadline_ns.store(0, .release);
            },
        }
    }

    const ObservationFailureTransition = struct {
        decision: antfly.hot_standby.kubernetes_lease_watchdog.Decision,
        should_log: bool,
    };

    // Called with proof_mutex held. Separate the deterministic fail-closed
    // state transition from logging so tests can exercise expected failure
    // paths without emitting a real production error. The wrapper below
    // remains the sole logging boundary.
    fn transitionObservationFailureLocked(
        self: *RuntimeLeaseWatchdog,
        stage: ObservationFailureStage,
        now_ns: u64,
    ) ObservationFailureTransition {
        const should_log = switch (stage) {
            .fetch => first: {
                const first_failure = !self.fetch_failure_logged;
                self.fetch_failure_logged = true;
                break :first first_failure;
            },
            .validation => first: {
                self.proof_active.store(false, .release);
                self.proof_capability_deadline_ns.store(0, .release);
                const first_failure = !self.validation_failure_logged;
                self.validation_failure_logged = true;
                break :first first_failure;
            },
        };
        return .{
            .decision = self.watchdog.noteAPIFailure(now_ns),
            .should_log = should_log,
        };
    }

    // Called with proof_mutex held. Each failure stage logs at most once per
    // runtime process and includes only the Zig error name: bearer tokens,
    // request headers, and response bodies are never rendered.
    fn noteObservationFailureLocked(
        self: *RuntimeLeaseWatchdog,
        stage: ObservationFailureStage,
        err: anyerror,
        now_ns: u64,
    ) antfly.hot_standby.kubernetes_lease_watchdog.Decision {
        const transition = self.transitionObservationFailureLocked(stage, now_ns);
        if (transition.should_log) switch (stage) {
            .fetch => std.log.err("Hot-standby lease watchdog Kubernetes Lease fetch failed err={s}", .{@errorName(err)}),
            .validation => std.log.err("Hot-standby lease watchdog Lease response validation failed err={s}", .{@errorName(err)}),
        };
        return transition.decision;
    }

    fn runIndependent(
        self: *RuntimeLeaseWatchdog,
        alloc: std.mem.Allocator,
        io: std.Io,
        data_server: *antfly.data.runtime.DataServer,
        stop: *const std.atomic.Value(bool),
        failed: *std.atomic.Value(bool),
    ) void {
        while (!stop.load(.acquire)) {
            self.poll(alloc, data_server) catch {
                failed.store(true, .release);
                return;
            };
            io.sleep(std.Io.Duration.fromMilliseconds(50), .awake) catch {
                failed.store(true, .release);
                return;
            };
        }
    }

    fn applyDecision(
        self: *RuntimeLeaseWatchdog,
        alloc: std.mem.Allocator,
        io: std.Io,
        data_server: *antfly.data.runtime.DataServer,
        decision: antfly.hot_standby.kubernetes_lease_watchdog.Decision,
    ) !void {
        switch (decision) {
            .waiting, .observed, .pending_authority, .grace => {},
            .authorized => {
                self.proof_transitions.store(self.watchdog.last_generation, .release);
                self.proof_active.store(true, .release);
                self.proof_authority_deadline_ns.store(self.watchdog.local_deadline_ns, .release);
                data_server.ha_public_gate_state.publishExternalAuthorityUntil(true, self.watchdog.local_deadline_ns);
            },
            .fence => {
                // Fence transitions wait for every mutation that passed the
                // preflight authority gate to finish its local commit and HA
                // append before freezing the durable tail.
                var mutation_lease = data_server.ha_mutation_barrier.acquireExclusive();
                defer mutation_lease.release();
                platform_sync.lockYielding(&data_server.ha_state_mutex);
                defer data_server.ha_state_mutex.unlock();
                self.proof_active.store(false, .release);
                self.proof_capability_deadline_ns.store(0, .release);
                self.proof_authority_deadline_ns.store(0, .release);
                data_server.ha_public_gate_state.publishExternalAuthority(false);
                data_server.ha_public_gate_state.publishPrimaryFence(true);
                if (!self.sentinel_persisted) {
                    try self.watchdog.persistFence(alloc, io);
                    self.sentinel_persisted = true;
                }
            },
        }
    }
};

const ResolvedPaths = struct {
    replica_root_dir: []u8,
    replica_catalog_path: []u8,
    local_metadata_catalog_path: []u8,
    snapshot_root_dir: []u8,
    extension_package_store_dir: []u8,
    secret_store_path: []u8,
    auth_store_root_dir: []u8,

    fn deinit(self: ResolvedPaths, alloc: std.mem.Allocator) void {
        alloc.free(self.replica_root_dir);
        alloc.free(self.replica_catalog_path);
        alloc.free(self.local_metadata_catalog_path);
        alloc.free(self.snapshot_root_dir);
        alloc.free(self.extension_package_store_dir);
        alloc.free(self.secret_store_path);
        alloc.free(self.auth_store_root_dir);
    }
};

const StandaloneHealthSource = struct {
    data_server: *antfly.data.runtime.DataServer,
    unified_api_ready: *const std.atomic.Value(bool),
    supervisor: *const antfly.common.runtime_lifecycle.RuntimeSupervisor,
    startup_checkpoint_lsn: ?u64 = null,
    handler: *const ApiKernelHandler,
    unified_lifecycle: *UnifiedServerLifecycle,

    fn readiness(self: *StandaloneHealthSource) antfly.common.health_server.ReadinessChecker {
        return .{
            .ptr = self,
            .vtable = &.{ .check = checkReady },
        };
    }

    fn metricsWriter(self: *StandaloneHealthSource) antfly.common.health_server.MetricsWriter {
        return .{
            .ptr = self,
            .vtable = &.{ .write_metrics = writeMetrics },
        };
    }

    fn checkReady(ptr: *anyopaque) bool {
        const self: *StandaloneHealthSource = @ptrCast(@alignCast(ptr));
        if (self.supervisor.currentState() != .ready) return false;
        switch (self.data_server.ha_public_gate_state.currentRole()) {
            .transitioning, .fenced_primary => return false,
            .disabled, .standby, .primary => {},
        }
        if (self.data_server.http_server) |*api_server| {
            if (api_server.storageMaintenanceExclusiveActive()) return false;
        }
        if (self.startup_checkpoint_lsn) |checkpoint_lsn| {
            self.data_server.ha_public_gate_state.checkRead(.{
                .consistency = .at_least_lsn,
                .required_lsn = checkpoint_lsn,
            }) catch return false;
        }
        if (self.unified_lifecycle.httpRuntimeStats()) |http_runtime| {
            if (!http_runtime.healthy) return false;
        }
        return standaloneReadyFromState(
            self.data_server.http_server != null,
            self.unified_api_ready.load(.acquire),
        );
    }

    fn writeMetrics(ptr: *anyopaque, writer: *std.Io.Writer) anyerror!void {
        const self: *StandaloneHealthSource = @ptrCast(@alignCast(ptr));
        var data_health = antfly.data.runtime.HealthSource{ .data_server = self.data_server };
        try data_health.metricsWriter().writeMetrics(writer);
        try antfly.common.health_server.appendPromMetric(writer, "antfly_runtime_supervisor_state", "gauge", "Runtime supervisor phase (0 starting, 1 ready, 2 quiescing, 3 failed, 4 stopped)", @intFromEnum(self.supervisor.currentState()));
        try antfly.common.health_server.appendPromMetric(writer, "antfly_runtime_supervisor_cancelled", "gauge", "Whether process-level runtime cancellation has been requested", @intFromBool(self.supervisor.token().isCancelled()));

        const handler = antfly.public_api.kernel_bridge.handlerStats(self.handler);
        try antfly.common.request_admission.appendPrometheusMetrics(writer, .query, .{
            .capacity = handler.query_capacity,
            .in_flight = handler.query_in_flight,
            .peak_in_flight = handler.query_peak_in_flight,
            .rejected_total = handler.query_rejected_total,
        });
        try antfly.common.request_admission.appendPrometheusMetrics(writer, .write, .{
            .capacity = handler.write_capacity,
            .in_flight = handler.write_in_flight,
            .peak_in_flight = handler.write_peak_in_flight,
            .rejected_total = handler.write_rejected_total,
        });
        try antfly.common.request_admission.appendPrometheusMetrics(writer, .inference, .{
            .capacity = handler.inference_capacity,
            .in_flight = handler.inference_in_flight,
            .peak_in_flight = handler.inference_peak_in_flight,
            .rejected_total = handler.inference_rejected_total,
        });
        try antfly.common.health_server.appendPromMetric(writer, "antfly_query_body_capacity", "gauge", "Maximum concurrent streaming H2 query bodies", handler.query_body_capacity);
        try antfly.common.health_server.appendPromMetric(writer, "antfly_query_bodies_in_flight", "gauge", "Streaming H2 query bodies currently admitted", handler.query_body_in_flight);
        try antfly.common.health_server.appendPromMetric(writer, "antfly_query_body_peak_in_flight", "gauge", "Peak concurrent streaming H2 query bodies since process start", handler.query_body_peak_in_flight);
        try antfly.common.health_server.appendPromMetric(writer, "antfly_query_body_rejected_total", "counter", "Streaming H2 query bodies rejected by admission control", handler.query_body_rejected_total);
        if (self.unified_lifecycle.runtimeStats()) |http| {
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_connection_limit", "gauge", "Maximum concurrent public HTTP connections", http.max_connections);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_active_connections", "gauge", "Currently active public HTTP connections", http.active_connections);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_active_requests", "gauge", "Currently active public HTTP requests", http.active_requests);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_accept_errors_total", "counter", "Public HTTP listener accept failures", http.accept_errors_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_connection_dispatch_rejections_total", "counter", "Accepted public HTTP connections closed because concurrent execution was unavailable", http.connection_dispatch_rejections_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_request_dispatch_rejections_total", "counter", "HTTP requests rejected before application execution because listener or runtime request capacity was unavailable", http.request_dispatch_rejections_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_h2_stream_dispatch_rejections_total", "counter", "HTTP/2 streams reset before application execution because bounded handler execution was unavailable", http.h2_stream_dispatch_rejections_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_request_cancellations_total", "counter", "Public HTTP requests terminated by application cancellation", http.request_cancellations_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_body_buffer_capacity_bytes", "gauge", "Aggregate HTTP request-body buffer capacity", http.body_buffer_capacity_bytes);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_body_buffer_in_use_bytes", "gauge", "HTTP request-body bytes admitted across HTTP/1 and HTTP/2", http.body_buffer_in_use_bytes);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_body_buffer_peak_bytes", "gauge", "Peak admitted HTTP request-body bytes since process start", http.body_buffer_peak_bytes);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_body_buffer_rejected_total", "counter", "HTTP request bodies rejected by aggregate memory admission", http.body_buffer_rejected_total);
        }
        if (self.unified_lifecycle.httpRuntimeStats()) |http_runtime| {
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_listener_capacity", "gauge", "Maximum concurrent long-lived HTTP listeners", http_runtime.listener_capacity);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_active_listener_leases", "gauge", "Long-lived HTTP listeners currently owned by the shared runtime", http_runtime.active_listener_leases);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_transport_connection_capacity", "gauge", "Shared HTTP transport connection-task capacity", http_runtime.connection_capacity);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_transport_reserved_connections", "gauge", "HTTP transport connection-task capacity reserved by live listeners", http_runtime.reserved_connection_capacity);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_request_task_capacity", "gauge", "Shared HTTP application request-task capacity", http_runtime.request_capacity);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_request_task_reserved", "gauge", "HTTP request-task capacity reserved by live listeners", http_runtime.reserved_request_capacity);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_cancellation_watcher_start_failures_total", "counter", "Public requests rejected because transport cancellation observation could not be registered", http_runtime.h1_cancellation_registration_failures_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_hard_disconnect_cancellations_total", "counter", "Public requests cancelled after a hard transport failure", http_runtime.h1_hard_disconnect_cancellations_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_peer_observer_failures_total", "counter", "Public requests cancelled after transport cancellation observation failed", http_runtime.h1_cancellation_observer_failures_total);
            try antfly.common.health_server.appendPromMetric(writer, "antfly_http_active_peer_observers", "gauge", "HTTP/1 request sockets currently registered for hard-disconnect observation", http_runtime.active_h1_cancellation_observers);
        }
    }
};

fn standaloneReadyFromState(api_server_initialized: bool, unified_api_ready: bool) bool {
    return api_server_initialized and unified_api_ready;
}

fn startupCheckpointSatisfied(progress: antfly.hot_standby.standby.Progress, checkpoint_lsn: u64) bool {
    return progress.applied_lsn >= checkpoint_lsn and progress.safe_read_lsn >= checkpoint_lsn;
}

const UnifiedServerLifecycle = antfly.common.runtime_lifecycle.HttpServerLifecycle;

const LocalStandaloneMetadata = struct {
    alloc: std.mem.Allocator,
    mutex: std.atomic.Mutex = .unlocked,
    vector_migration_commands: @import("../common/vector_migration.zig").CommandAdmissions = .{},
    manager: antfly.metadata.TableManager,
    extension_catalog: antfly.extensions.ExtensionCatalog,
    local_node_id: u64,
    store_id: u64,
    api_url: []const u8,
    replica_root_dir: []const u8,
    catalog_path: []const u8,
    operator_lock: ?std.Io.File = null,
    catalog_store: ?*antfly.storage_backend_erased.Store,
    owned_catalog_backend: ?antfly.lsm_backend.BackendHandle = null,
    owned_catalog_cache: ?*antfly.lsm_backend.Cache = null,
    owned_catalog_store: ?antfly.storage_backend_erased.Store = null,
    catalog_rows_initialized: bool = false,
    catalog_listing_indexes_initialized: bool = false,
    catalog_durability_failed: bool = false,
    backend_runtime: *antfly.db.background_runtime.BackendRuntime,
    storage_engine: antfly.common.config.StorageEngine = .local,
    vector_source_storage_allowed: bool = true,
    ha_catalog_server: ?*antfly.data.runtime.DataServer = null,

    epoch: u64 = 1,
    last_schema_migration_finalize_at_ms: u64 = 0,
    local_schema_progress_provider: ?LocalSchemaProgressProvider = null,

    system_catalog_state: ?system_catalog.MutableState = null,
    routing_generation: ?*antfly.public_api.table_catalog.RoutingGeneration = null,
    join_planning_generation: ?*antfly.public_api.join_planning.Generation = null,
    join_planning_epoch: u64 = 0,
    const CatalogCreate = struct {
        schema_version: u32 = 4,
        kind: enum { table_create } = .table_create,
        table: antfly.metadata.TableRecord,
        ranges: []const antfly.metadata.RangeRecord,
        binding: ?struct {
            previous_revision: u64,
            delta: system_catalog.Delta,
        } = null,
    };

    const PersistedCatalog = struct {
        // Current HA logical seeds carry this state. Checkpoints written by
        // main omit it. This is an active restore contract, not a migration
        // promise for catalog layouts from earlier revisions of this PR.
        system_catalog: system_catalog.State = .{},
        epoch: u64 = 1,
        tables: []const antfly.metadata.TableRecord = &.{},
        ranges: []const antfly.metadata.RangeRecord = &.{},
        extension_packages: []const antfly.extensions.PackageManifest = &.{},
        installed_extensions: []const antfly.extensions.InstalledExtension = &.{},
        extension_members: []const antfly.extensions.ExtensionMember = &.{},
        extension_dependencies: []const antfly.extensions.ExtensionDependency = &.{},
    };

    const CatalogMutation = struct {
        previous_tables: std.AutoHashMapUnmanaged(u64, ?antfly.metadata.TableRecord) = .empty,
        previous_ranges: std.AutoHashMapUnmanaged(u64, ?antfly.metadata.RangeRecord) = .empty,
        previous_extensions: ?antfly.extensions.ExtensionCatalog = null,
        catalog_change: ?system_catalog.MutableState.Change = null,
        previous_epoch: u64,
        committed: bool = false,

        fn upsertTable(self: *CatalogMutation, metadata: *LocalStandaloneMetadata, table: antfly.metadata.TableRecord) !void {
            try self.captureTable(metadata, table.table_id);
            try metadata.manager.upsertTable(table);
        }
        fn captureTable(self: *CatalogMutation, metadata: *LocalStandaloneMetadata, id: u64) !void {
            if (self.previous_tables.contains(id)) return;
            const owned = if (metadata.manager.tables.get(id)) |row| try antfly.metadata.table_manager.cloneTable(metadata.alloc, row) else null;
            errdefer if (owned) |row| antfly.metadata.table_manager.freeTable(metadata.alloc, row);
            try self.previous_tables.put(metadata.alloc, id, owned);
        }
        fn upsertRange(self: *CatalogMutation, metadata: *LocalStandaloneMetadata, range: antfly.metadata.RangeRecord) !void {
            try self.captureRange(metadata, range.group_id);
            try metadata.manager.upsertRange(range);
        }
        fn captureRange(self: *CatalogMutation, metadata: *LocalStandaloneMetadata, id: u64) !void {
            if (self.previous_ranges.contains(id)) return;
            const owned = if (metadata.manager.ranges.get(id)) |row| try antfly.metadata.table_manager.cloneRange(metadata.alloc, row) else null;
            errdefer if (owned) |row| antfly.metadata.table_manager.freeRange(metadata.alloc, row);
            try self.previous_ranges.put(metadata.alloc, id, owned);
        }
        fn removeTable(self: *CatalogMutation, metadata: *LocalStandaloneMetadata, id: u64) !void {
            try self.captureTable(metadata, id);
            var it = metadata.manager.ranges.iterator();
            while (it.next()) |entry| if (entry.value_ptr.table_id == id) {
                try self.captureRange(metadata, entry.key_ptr.*);
            };
            var removed = self.previous_ranges.keyIterator();
            while (removed.next()) |key| {
                _ = metadata.manager.removeRange(key.*);
            }
            _ = metadata.manager.removeTable(id);
        }
        fn extensions(self: *CatalogMutation, metadata: *LocalStandaloneMetadata) !void {
            if (self.previous_extensions == null) self.previous_extensions = try metadata.cloneExtensionCatalogLocked();
        }
        fn applyCatalog(self: *CatalogMutation, metadata: *LocalStandaloneMetadata, delta: system_catalog.Delta) !void {
            std.debug.assert(self.catalog_change == null);
            if (metadata.system_catalog_state == null) metadata.system_catalog_state = try system_catalog.MutableState.clone(metadata.alloc, .{});
            self.catalog_change = try metadata.system_catalog_state.?.apply(delta);
        }
        fn commit(self: *CatalogMutation, metadata: *LocalStandaloneMetadata) !void {
            try metadata.persistMutationLocked(self);
        }
        fn deinit(self: *CatalogMutation, metadata: *LocalStandaloneMetadata) void {
            if (self.catalog_change) |*change| change.finish(&metadata.system_catalog_state.?, self.committed);
            if (self.previous_extensions) |*previous| {
                if (self.committed) previous.deinit() else {
                    metadata.extension_catalog.deinit();
                    metadata.extension_catalog = previous.*;
                }
            }
            var ranges = self.previous_ranges.iterator();
            while (ranges.next()) |entry| {
                if (!self.committed) {
                    _ = metadata.manager.removeRange(entry.key_ptr.*);
                    if (entry.value_ptr.*) |row| metadata.manager.ranges.putAssumeCapacity(entry.key_ptr.*, row);
                } else if (entry.value_ptr.*) |row| antfly.metadata.table_manager.freeRange(metadata.alloc, row);
            }
            var tables = self.previous_tables.iterator();
            while (tables.next()) |entry| {
                if (!self.committed) {
                    _ = metadata.manager.removeTable(entry.key_ptr.*);
                    if (entry.value_ptr.*) |row| {
                        metadata.manager.tables.putAssumeCapacity(entry.key_ptr.*, row);
                        metadata.manager.table_names.putAssumeCapacity(row.name, row.table_id);
                    }
                } else if (entry.value_ptr.*) |row| antfly.metadata.table_manager.freeTable(metadata.alloc, row);
            }
            if (!self.committed) metadata.epoch = self.previous_epoch;
            self.previous_ranges.deinit(metadata.alloc);
            self.previous_tables.deinit(metadata.alloc);
        }
    };

    fn beginCatalogMutationLocked(self: *LocalStandaloneMetadata) !CatalogMutation {
        if (self.catalog_durability_failed) return error.MetadataMutationOutcomeUnknown;
        return .{ .previous_epoch = self.epoch };
    }

    fn init(
        alloc: std.mem.Allocator,
        local_node_id: u64,
        store_id: u64,
        api_url: []const u8,
        replica_root_dir: []const u8,
        catalog_path: []const u8,
        backend_runtime: *antfly.db.background_runtime.BackendRuntime,
        catalog_store: ?*antfly.storage_backend_erased.Store,
        storage_engine: antfly.common.config.StorageEngine,
    ) !LocalStandaloneMetadata {
        var owned_api_url: ?[]u8 = try alloc.dupe(u8, api_url);
        errdefer if (owned_api_url) |value| alloc.free(value);
        var owned_replica_root_dir: ?[]u8 = try alloc.dupe(u8, replica_root_dir);
        errdefer if (owned_replica_root_dir) |value| alloc.free(value);
        var owned_catalog_path: ?[]u8 = try alloc.dupe(u8, catalog_path);
        errdefer if (owned_catalog_path) |value| alloc.free(value);
        var self = LocalStandaloneMetadata{
            .alloc = alloc,
            .manager = antfly.metadata.TableManager.init(alloc),
            .extension_catalog = antfly.extensions.ExtensionCatalog.init(alloc),
            .local_node_id = local_node_id,
            .store_id = store_id,
            .api_url = owned_api_url.?,
            .replica_root_dir = owned_replica_root_dir.?,
            .catalog_path = owned_catalog_path.?,
            .catalog_store = catalog_store,
            .backend_runtime = backend_runtime,
            .storage_engine = storage_engine,
        };
        owned_api_url = null;
        owned_replica_root_dir = null;
        owned_catalog_path = null;
        errdefer self.deinit();
        if (catalog_store == null) self.operator_lock = try @import("../common/migration_files.zig").lockCatalog(
            alloc,
            backend_runtime.filesystemIo() orelse return error.MissingBackendRuntimeIo,
            catalog_path,
        );
        if (catalog_store == null) {
            const root = try std.fmt.allocPrint(alloc, "{s}.store", .{catalog_path});
            defer alloc.free(root);
            // Ordered listing indexes are small, repeatedly read metadata.
            // Reuse immutable blocks instead of reopening them for every table.
            const cache = try alloc.create(antfly.lsm_backend.Cache);
            cache.* = antfly.lsm_backend.Cache.init(alloc, 8 * 1024 * 1024);
            self.owned_catalog_cache = cache;
            self.owned_catalog_backend = try antfly.lsm_backend.BackendHandle.open(alloc, root, .{ .wal_sync_on_commit = true, .cache = cache });
            self.owned_catalog_store = try self.owned_catalog_backend.?.backend.runtimeStore(alloc, .{ .name = "system/metadata" });
        }
        try self.loadPersistedCatalog();
        var admitted_tables = self.manager.tables.valueIterator();
        while (admitted_tables.next()) |table| if (table.storage_migration) |admission| {
            if (admission.request.mode == .offline) return error.VectorMigrationOfflineAdmission;
        };
        if (self.system_catalog_state == null) self.system_catalog_state = try system_catalog.MutableState.clone(self.alloc, .{});
        return self;
    }

    fn deinit(self: *LocalStandaloneMetadata) void {
        if (self.owned_catalog_store) |*store| store.deinit();
        if (self.owned_catalog_backend) |*backend| backend.close();
        if (self.owned_catalog_cache) |cache| {
            cache.deinit();
            self.alloc.destroy(cache);
        }
        if (self.routing_generation) |generation| generation.release();
        if (self.join_planning_generation) |generation| generation.release();
        if (self.system_catalog_state) |*state| state.deinit();
        self.vector_migration_commands.deinit(self.alloc);
        if (self.operator_lock) |file| file.close(self.backend_runtime.filesystemIo().?);
        self.extension_catalog.deinit();
        self.manager.deinit();
        self.alloc.free(self.catalog_path);
        self.alloc.free(self.replica_root_dir);
        self.alloc.free(self.api_url);
        self.* = undefined;
    }

    fn setApiUrl(self: *LocalStandaloneMetadata, api_url: []const u8) !void {
        const owned_api_url = try self.alloc.dupe(u8, api_url);
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        self.alloc.free(self.api_url);
        self.api_url = owned_api_url;
    }

    fn catalogSource(self: *LocalStandaloneMetadata) antfly.public_api.table_catalog.CatalogSource {
        return .{
            .ptr = self,
            .vtable = &.{
                .admin_snapshot = catalogAdminSnapshot,
                .export_catalog = exportCatalog,
                .free_admin_snapshot = catalogFreeAdminSnapshot,
                .acquire_routing_generation = acquireRoutingGeneration,
                .routing_snapshot = catalogRoutingSnapshot,
                .linearizable_routing_snapshot = catalogRoutingSnapshot,
                .table_routing_snapshot = catalogTableRoutingSnapshot,
                .linearizable_table_routing_snapshot = catalogTableRoutingSnapshot,
                .free_routing_snapshot = catalogFreeRoutingSnapshot,
                .wait_for_routing_change = catalogWaitForRoutingChange,
            },
        };
    }

    fn statusSource(self: *LocalStandaloneMetadata) antfly.public_api.http_server.StatusSource {
        return .{
            .ptr = self,
            .routing = self.catalogSource().routingSource() catch unreachable,
            .vtable = &.{
                .status = status,
                .system_catalog = systemCatalog,
                .supports_query_definitions = true,
                .acquire_join_planning = acquireJoinPlanning,
                .admin_snapshot = catalogAdminSnapshot,
                .cached_admin_snapshot = cachedAdminSnapshot,
                .linearizable_snapshot = linearizableSnapshot,
                .free_admin_snapshot = catalogFreeAdminSnapshot,
                .routing_snapshot = catalogRoutingSnapshot,
                .linearizable_routing_snapshot = catalogRoutingSnapshot,
                .free_routing_snapshot = catalogFreeRoutingSnapshot,
                .create_table = createTable,
                .replace_table_definition = replaceTableDefinition,
                .publish_vector_migration_table = publishVectorMigrationTable,
                .begin_vector_migration_command = beginVectorMigrationCommand,
                .end_vector_migration_command = endVectorMigrationCommand,
                .restore_table = restoreTable,
                .drop_table = dropTable,
                .drop_table_exact = dropTableExact,
                .update_schema = updateSchema,
                .update_schema_versioned = updateSchemaVersioned,
                .update_schema_versioned_expected = updateSchemaVersionedExpected,
                .mutate_schema = mutateSchema,
                .create_index = createIndex,
                .drop_index = dropIndex,
                .put_artifact_enrichment = putArtifactEnrichment,
                .delete_artifact_enrichment = deleteArtifactEnrichment,
                .wait_table_lifecycle = waitTableLifecycle,
                .wait_table_projection = waitTableProjection,
                .run_round = runRound,
                .install_extension = installExtension,
                .update_extension = updateExtension,
                .drop_extension = dropExtension,
                .enable_extension = enableExtension,
                .disable_extension = disableExtension,
                .configure_extension = configureExtension,
                .restore_extensions = restoreExtensions,
            },
        };
    }

    fn status(ptr: *anyopaque) !antfly.metadata_api.MetadataStatus {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        return .{
            .metadata_group_id = group_ids.main_metadata_group_id,
            .metadata_epoch = self.epoch,
            .metadata_raft_role = "disabled",
            .projected_tables = self.manager.tables.count(),
            .projected_extension_packages = self.extension_catalog.packages.items.len,
            .projected_installed_extensions = self.extension_catalog.installed.items.len,
            .projected_extension_members = self.extension_catalog.members.items.len,
            .projected_extension_dependencies = self.extension_catalog.dependencies.items.len,
            .projected_ranges = self.manager.ranges.count(),
            .projected_stores = 1,
            .projected_placement_intents = self.manager.ranges.count(),
            .metrics = .{},
        };
    }

    fn cachedAdminSnapshot(ptr: *anyopaque) !?antfly.metadata_api.AdminSnapshot {
        return try catalogAdminSnapshot(ptr);
    }

    fn linearizableSnapshot(
        ptr: *anyopaque,
        request: antfly.public_api.operation.RequestContext,
    ) !?antfly.metadata_api.AdminSnapshot {
        // Standalone catalog mutations and snapshots share the same mutex, so
        // the locked clone itself is the linearization point. Preserve the
        // request lifecycle contract on both sides of the potentially large
        // allocation just like the Raft-backed coherent snapshot path does.
        try request.ensureActive();
        var snapshot = try catalogAdminSnapshot(ptr);
        errdefer catalogFreeAdminSnapshot(ptr, &snapshot);
        try request.ensureActive();
        return snapshot;
    }

    fn catalogAdminSnapshot(ptr: *anyopaque) !antfly.metadata_api.AdminSnapshot {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (self.catalog_durability_failed) return error.MetadataMutationOutcomeUnknown;

        const tables = try self.manager.listTables(self.alloc);
        errdefer self.manager.freeTables(self.alloc, tables);
        const ranges = try self.manager.listRanges(self.alloc);
        errdefer self.manager.freeRanges(self.alloc, ranges);
        const extension_packages = try self.extension_catalog.listPackages(self.alloc);
        errdefer self.extension_catalog.freePackages(self.alloc, extension_packages);
        const installed_extensions = try self.extension_catalog.listInstalled(self.alloc);
        errdefer self.extension_catalog.freeInstalled(self.alloc, installed_extensions);
        const extension_members = try self.extension_catalog.listMembers(self.alloc);
        errdefer self.extension_catalog.freeMembers(self.alloc, extension_members);
        const extension_dependencies = try self.extension_catalog.listDependencies(self.alloc);
        errdefer self.extension_catalog.freeDependencies(self.alloc, extension_dependencies);

        const stores = try self.alloc.alloc(antfly.metadata.StoreRecord, 1);
        errdefer self.alloc.free(stores);
        stores[0] = try antfly.metadata.table_manager.cloneStore(self.alloc, .{
            .store_id = self.store_id,
            .node_id = self.local_node_id,
            .api_url = self.api_url,
            .role = "data",
            .health_class = "healthy",
            .live = true,
        });
        errdefer antfly.metadata.table_manager.freeStore(self.alloc, stores[0]);

        const placement_intents = try self.alloc.alloc(antfly.raft.PlacementIntent, ranges.len);
        errdefer self.alloc.free(placement_intents);
        for (ranges, 0..) |range, i| {
            placement_intents[i] = .{
                .record = .{
                    .group_id = range.group_id,
                    .replica_id = 1,
                    .local_node_id = self.local_node_id,
                    .bootstrap_mode = .persisted,
                    .metadata_version = self.epoch,
                },
                .store_id = self.store_id,
                .peer_node_ids = &.{},
            };
        }

        return .{
            .status = .{
                .metadata_group_id = group_ids.main_metadata_group_id,
                .metadata_epoch = self.epoch,
                .metadata_raft_role = "disabled",
                .projected_tables = tables.len,
                .projected_extension_packages = extension_packages.len,
                .projected_installed_extensions = installed_extensions.len,
                .projected_extension_members = extension_members.len,
                .projected_extension_dependencies = extension_dependencies.len,
                .projected_ranges = ranges.len,
                .projected_stores = stores.len,
                .projected_placement_intents = placement_intents.len,
                .metrics = .{},
            },
            .tables = tables,
            .ranges = ranges,
            .stores = stores,
            .placement_intents = placement_intents,
            .extension_packages = extension_packages,
            .installed_extensions = installed_extensions,
            .extension_members = extension_members,
            .extension_dependencies = extension_dependencies,
            .split_transitions = try self.alloc.alloc(antfly.metadata.SplitTransitionRecord, 0),
            .merge_transitions = try self.alloc.alloc(antfly.metadata.MergeTransitionRecord, 0),
        };
    }

    fn acquireRoutingGeneration(ptr: *anyopaque, deadline_ns: ?u64, _: bool) !*antfly.public_api.table_catalog.RoutingGeneration {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        if (!lockAtomicUntil(&self.mutex, deadline_ns)) return error.CatalogRoutingSnapshotTimeout;
        if (self.catalog_durability_failed) {
            self.mutex.unlock();
            return error.MetadataMutationOutcomeUnknown;
        }
        // The metadata mutex is the standalone linearizable-read barrier.
        if (self.routing_generation) |generation| {
            if (generation.indexed.snapshot.value.catalog_revision == self.epoch) {
                generation.retain();
                self.mutex.unlock();
                return generation;
            }
        }
        var snapshot = self.routingSnapshotLocked(deadline_ns) catch |err| {
            self.mutex.unlock();
            return err;
        };
        self.mutex.unlock();
        defer catalogFreeRoutingSnapshot(ptr, &snapshot);
        // Build indexes outside the publication lock. The captured generation
        // is still a valid linearizable observation if a writer races it, but
        // it must not replace a newer cached generation.
        const next = try antfly.public_api.table_catalog.RoutingGeneration.create(self.alloc, snapshot, .{ .deadline_ns = deadline_ns });
        errdefer next.release();
        if (!lockAtomicUntil(&self.mutex, deadline_ns)) return error.CatalogRoutingSnapshotTimeout;
        if (self.catalog_durability_failed) {
            self.mutex.unlock();
            return error.MetadataMutationOutcomeUnknown;
        }
        var previous: ?*antfly.public_api.table_catalog.RoutingGeneration = null;
        if (self.epoch == snapshot.catalog_revision) {
            previous = self.routing_generation;
            self.routing_generation = next;
            next.retain();
        }
        self.mutex.unlock();
        if (previous) |generation| generation.release();
        return next;
    }

    fn acquireJoinPlanning(ptr: *anyopaque, budget: antfly.public_api.table_router.RouteBudget) !?*antfly.public_api.join_planning.Generation {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        try budget.check();
        const deadline_ns = (antfly.public_api.table_catalog.RoutingBudget{}).deadlineFrom(budget.clock);
        if (!lockAtomicUntil(&self.mutex, deadline_ns)) return error.CatalogRoutingSnapshotTimeout;
        if (self.catalog_durability_failed) {
            self.mutex.unlock();
            return error.MetadataMutationOutcomeUnknown;
        }
        if (self.join_planning_generation) |generation| {
            if (self.join_planning_epoch == self.epoch) {
                const retained = generation.retain();
                self.mutex.unlock();
                errdefer retained.release();
                try budget.check();
                return retained;
            }
        }
        self.mutex.unlock();
        // Reuse the compact, coherently captured tables and ranges. Building
        // planning indexes never requires an administrative snapshot.
        const routing = try acquireRoutingGeneration(ptr, deadline_ns, false);
        defer routing.release();
        const snapshot = routing.indexed.snapshot.value;
        const next = try antfly.public_api.join_planning.Generation.create(self.alloc, .{
            .tables = snapshot.tables,
            .ranges = snapshot.ranges,
            .merged_group_statuses = @as([]const antfly.metadata.reconciler.MergedGroupStatus, &.{}),
        }, budget);
        errdefer next.release();
        if (!lockAtomicUntil(&self.mutex, deadline_ns)) return error.CatalogRoutingSnapshotTimeout;
        var previous: ?*antfly.public_api.join_planning.Generation = null;
        if (!self.catalog_durability_failed and self.epoch == snapshot.catalog_revision) {
            previous = self.join_planning_generation;
            self.join_planning_generation = next.retain();
            self.join_planning_epoch = snapshot.catalog_revision;
        }
        const durability_failed = self.catalog_durability_failed;
        self.mutex.unlock();
        if (previous) |generation| generation.release();
        if (durability_failed) return error.MetadataMutationOutcomeUnknown;
        try budget.check();
        return next;
    }

    fn catalogRoutingSnapshot(ptr: *anyopaque, deadline_ns: ?u64) !antfly.metadata_api.CatalogRoutingSnapshot {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        if (!lockAtomicUntil(&self.mutex, deadline_ns)) return error.CatalogRoutingSnapshotTimeout;
        defer self.mutex.unlock();
        if (self.catalog_durability_failed) return error.MetadataMutationOutcomeUnknown;

        return self.routingSnapshotLocked(deadline_ns);
    }

    fn catalogTableRoutingSnapshot(ptr: *anyopaque, table_name: []const u8, deadline_ns: ?u64) !antfly.metadata_api.CatalogRoutingSnapshot {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        const budget = antfly.public_api.table_catalog.RoutingBudget{ .deadline_ns = deadline_ns };
        while (true) {
            try budget.checkpoint();
            const generation = try acquireRoutingGeneration(ptr, deadline_ns, true);
            defer generation.release();
            if (!lockAtomicUntil(&self.mutex, deadline_ns)) return error.CatalogRoutingSnapshotTimeout;
            defer self.mutex.unlock();
            if (self.catalog_durability_failed) return error.MetadataMutationOutcomeUnknown;
            if (generation.indexed.snapshot.value.catalog_revision != self.epoch) continue;
            const table = self.manager.findTableByName(table_name);
            const tables = try self.alloc.alloc(antfly.metadata.TableRecord, if (table != null) 1 else 0);
            var copied_table = false;
            errdefer {
                if (copied_table) antfly.metadata.table_manager.freeTable(self.alloc, tables[0]);
                self.alloc.free(tables);
            }
            if (table) |value| {
                tables[0] = try antfly.metadata.table_manager.cloneTable(self.alloc, value.*);
                copied_table = true;
            }
            const refs = if (table) |value| generation.indexed.table_range_refs.get(value.table_id) orelse &.{} else &.{};
            const ranges = try self.alloc.alloc(antfly.metadata.RangeRecord, refs.len);
            var copied_ranges: usize = 0;
            errdefer {
                for (ranges[0..copied_ranges]) |range| antfly.metadata.table_manager.freeRange(self.alloc, range);
                self.alloc.free(ranges);
            }
            for (refs, 0..) |range, i| {
                try budget.checkpointIndex(i);
                ranges[i] = try antfly.metadata.table_manager.cloneRoutingRange(self.alloc, range.*);
                copied_ranges += 1;
            }
            try budget.checkpoint();
            return .{
                .metadata_group_id = group_ids.main_metadata_group_id,
                .catalog_revision = self.epoch,
                .change_token = .{ .metadata_group_id = group_ids.main_metadata_group_id, .revision = self.epoch },
                .tables = tables,
                .ranges = ranges,
            };
        }
    }

    fn routingSnapshotLocked(self: *LocalStandaloneMetadata, deadline_ns: ?u64) !antfly.metadata_api.CatalogRoutingSnapshot {
        const budget = antfly.public_api.table_catalog.RoutingBudget{ .deadline_ns = deadline_ns };
        const tables = try self.alloc.alloc(antfly.metadata.TableRecord, self.manager.tables.count());
        var table_count: usize = 0;
        errdefer {
            for (tables[0..table_count]) |table| antfly.metadata.table_manager.freeTable(self.alloc, table);
            self.alloc.free(tables);
        }
        var table_it = self.manager.tables.valueIterator();
        while (table_it.next()) |table| {
            try budget.checkpointIndex(table_count);
            tables[table_count] = try antfly.metadata.table_manager.cloneRoutingTable(self.alloc, table.*);
            table_count += 1;
        }
        const ranges = try self.alloc.alloc(antfly.metadata.RangeRecord, self.manager.ranges.count());
        var range_count: usize = 0;
        errdefer {
            for (ranges[0..range_count]) |range| antfly.metadata.table_manager.freeRange(self.alloc, range);
            self.alloc.free(ranges);
        }
        var range_it = self.manager.ranges.valueIterator();
        while (range_it.next()) |range| {
            try budget.checkpointIndex(range_count);
            ranges[range_count] = try antfly.metadata.table_manager.cloneRoutingRange(self.alloc, range.*);
            range_count += 1;
        }
        try budget.checkpoint();
        return .{
            .metadata_group_id = group_ids.main_metadata_group_id,
            .catalog_revision = self.epoch,
            .change_token = .{
                .metadata_group_id = group_ids.main_metadata_group_id,
                .revision = self.epoch,
            },
            .tables = tables,
            .ranges = ranges,
        };
    }

    fn catalogWaitForRoutingChange(
        ptr: *anyopaque,
        observed_token: antfly.metadata_api.CatalogRoutingChangeToken,
        deadline_ns: u64,
        probe_interval_ns: u64,
    ) !antfly.public_api.table_catalog.CatalogChangeWaitResult {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        return self.catalogWaitForRoutingChangeWithClock(observed_token, deadline_ns, probe_interval_ns, StandaloneWaitClock{});
    }

    fn catalogWaitForRoutingChangeWithClock(
        self: *LocalStandaloneMetadata,
        observed_token: antfly.metadata_api.CatalogRoutingChangeToken,
        deadline_ns: u64,
        probe_interval_ns: u64,
        clock: anytype,
    ) !antfly.public_api.table_catalog.CatalogChangeWaitResult {
        if (!lockAtomicUntilWithClock(&self.mutex, deadline_ns, clock)) return .retry;
        if (standaloneCatalogTokenChanged(self, observed_token)) {
            self.mutex.unlock();
            return .changed;
        }
        self.mutex.unlock();

        var now_ns = clock.nowNs();
        if (now_ns < deadline_ns) {
            // Finish the passive watch before the outer deadline and reserve
            // bounded time for the authoritative mutex confirmation. Waiting
            // all the way to the deadline makes lockAtomicUntil reject the
            // final read and turns a stable absence into a false timeout.
            const remaining_ns = deadline_ns - now_ns;
            const confirmation_budget_ns = @min(
                10 * std.time.ns_per_ms,
                @max(std.time.ns_per_ms, remaining_ns / 4),
            );
            const watch_deadline_ns = deadline_ns -| confirmation_budget_ns;
            while (now_ns < watch_deadline_ns) {
                const wait_ns = @min(
                    watch_deadline_ns - now_ns,
                    @max(probe_interval_ns, std.time.ns_per_ms),
                );
                clock.sleepMs(@max(@as(u64, 1), wait_ns / std.time.ns_per_ms));
                if (!lockAtomicUntilWithClock(&self.mutex, deadline_ns, clock)) return .retry;
                const changed = standaloneCatalogTokenChanged(self, observed_token);
                self.mutex.unlock();
                if (changed) return .changed;
                now_ns = clock.nowNs();
            }
        }
        if (!lockAtomicUntilWithClock(&self.mutex, deadline_ns, clock)) return .retry;
        defer self.mutex.unlock();
        if (standaloneCatalogTokenChanged(self, observed_token)) return .changed;
        return .authoritative_absence;
    }

    fn standaloneCatalogTokenChanged(
        self: *const LocalStandaloneMetadata,
        observed_token: antfly.metadata_api.CatalogRoutingChangeToken,
    ) bool {
        if (observed_token.metadata_group_id != 0 and
            observed_token.metadata_group_id != group_ids.main_metadata_group_id)
        {
            return true;
        }
        return observed_token.revision != self.epoch;
    }

    fn catalogFreeRoutingSnapshot(ptr: *anyopaque, snapshot: *antfly.metadata_api.CatalogRoutingSnapshot) void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        self.manager.freeTables(self.alloc, snapshot.tables);
        self.manager.freeRanges(self.alloc, snapshot.ranges);
        snapshot.* = undefined;
    }

    fn catalogFreeAdminSnapshot(ptr: *anyopaque, snapshot: *antfly.metadata_api.AdminSnapshot) void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        self.manager.freeTables(self.alloc, snapshot.tables);
        self.manager.freeRanges(self.alloc, snapshot.ranges);
        for (snapshot.stores) |store| antfly.metadata.table_manager.freeStore(self.alloc, store);
        self.alloc.free(snapshot.stores);
        self.alloc.free(snapshot.placement_intents);
        self.extension_catalog.freePackages(self.alloc, snapshot.extension_packages);
        self.extension_catalog.freeInstalled(self.alloc, snapshot.installed_extensions);
        self.extension_catalog.freeMembers(self.alloc, snapshot.extension_members);
        self.extension_catalog.freeDependencies(self.alloc, snapshot.extension_dependencies);
        self.alloc.free(snapshot.split_transitions);
        self.alloc.free(snapshot.merge_transitions);
        snapshot.* = undefined;
    }

    fn systemCatalogState(self: *const LocalStandaloneMetadata) system_catalog.State {
        return if (self.system_catalog_state) |state| state.value else .{};
    }

    const CatalogReader = struct {
        owner: *LocalStandaloneMetadata,
        alloc: std.mem.Allocator,
        index: *const system_catalog.StateIndex,
        pub fn lookup(self: @This(), kind: system_catalog.Kind, parent: u64, name: []const u8) !?system_catalog.Resource {
            return self.index.find(kind, parent, name);
        }
        pub fn byId(self: @This(), kind: system_catalog.Kind, id: u64) !?system_catalog.Resource {
            return self.index.byId(kind, id);
        }
        pub fn namespaceFor(self: @This(), database: []const u8, namespace: []const u8) !system_catalog.Resource {
            return self.index.namespaceFor(database, namespace);
        }
        pub fn children(self: @This(), kind: system_catalog.Kind, parent: u64, limit: usize) ![]const system_catalog.Resource {
            const rows = self.index.list(kind, parent);
            return if (limit == 0) rows else rows[0..@min(rows.len, limit)];
        }
        pub fn bindingForStorage(self: @This(), name: []const u8) !?system_catalog.Resource {
            return self.index.storage_names.get(name);
        }
        pub fn tablespaceInUse(self: @This(), id: u64) !bool {
            return self.index.tablespace_users.contains(id);
        }
        pub fn physicalByName(self: @This(), name: []const u8) !?system_catalog.PhysicalTable {
            const table = self.owner.manager.findTableByName(name) orelse return null;
            // Legacy adoption can replace the physical table before the delta
            // is published. Retain its name in the mutation arena.
            return .{ .id = table.table_id, .name = try self.alloc.dupe(u8, table.name) };
        }
        pub fn physicalById(self: @This(), id: u64) !?system_catalog.PhysicalTable {
            const table = self.owner.manager.tables.get(id) orelse return null;
            return .{ .id = table.table_id, .name = try self.alloc.dupe(u8, table.name) };
        }
    };
    fn planCatalogLocked(self: *LocalStandaloneMetadata, alloc: std.mem.Allocator, command: system_catalog.Mutation) !system_catalog.Delta {
        const empty: system_catalog.StateIndex = .{};
        const reader: CatalogReader = .{ .owner = self, .alloc = alloc, .index = if (self.system_catalog_state) |*state| &state.index else &empty };
        return system_catalog.planWithReader(alloc, reader, self.systemCatalogState().next_id, command);
    }

    fn resolveSystemCatalogLocked(self: *LocalStandaloneMetadata, target: system_catalog.Target) !?antfly.metadata.TableRecord {
        try target.validate();
        const empty: system_catalog.StateIndex = .{};
        const index = if (self.system_catalog_state) |*state| &state.index else &empty;
        const namespace = index.namespaceFor(target.database, target.namespace) catch return null;
        if (index.find(.table, namespace.id, target.table)) |binding| {
            const table = self.manager.tables.getPtr(binding.id) orelse return error.InvalidCatalogRecord;
            if (!std.mem.eql(u8, table.name, binding.storage_name)) return error.InvalidCatalogRecord;
            return table.*;
        }
        if (namespace.id != system_catalog.default_namespace_id) return null;
        const table = self.findTableByNameLocked(target.table) orelse return null;
        if (index.byId(.table, table.table_id) != null) return null;
        return table.*;
    }

    fn exportCatalog(ptr: *anyopaque, alloc: std.mem.Allocator) ![]u8 {
        return systemCatalog(ptr, alloc, .{}, .export_snapshot);
    }

    const CatalogCapture = struct {
        arena: std.heap.ArenaAllocator,
        value: @import("../system_catalog/projection.zig").TableListing,
    };
    fn captureCatalogTablesLocked(self: *LocalStandaloneMetadata, alloc: std.mem.Allocator, context: antfly.public_api.operation.RequestContext, input: system_catalog.TableList) !CatalogCapture {
        var request = input;
        const projection = @import("../system_catalog/projection.zig");
        try system_catalog.validateName(request.database);
        try system_catalog.validateName(request.namespace);
        var arena = std.heap.ArenaAllocator.init(alloc);
        errdefer arena.deinit();
        const a = arena.allocator();
        if (request.revision) |expected| if (expected != self.systemCatalogState().revision) return error.CatalogGenerationChanged;
        try self.ensureListingIndexesLocked();
        const store = try self.durableCatalogStore();
        var txn = try store.beginRead();
        defer txn.abort();
        const index = &self.system_catalog_state.?.index;
        var entries: std.ArrayListUnmanaged(projection.TableEntry) = .empty;
        var membership: [32]u8 = @splat(0);
        if (request.target) |target| {
            const table = (try self.resolveSystemCatalogLocked(target)) orelse return error.TableNotFound;
            try entries.append(a, .{ .name = target.table, .table = table });
        } else if (request.physical_name) |name| {
            const table = self.findTableByNameLocked(name) orelse return error.TableNotFound;
            try entries.append(a, .{ .name = table.name, .table = table.* });
        } else {
            const namespace = try index.namespaceFor(request.database, request.namespace);
            if (request.after_table_id) |id| {
                if (index.byId(.table, id)) |binding| {
                    if (binding.parent_id != namespace.id) return error.InvalidCatalogName;
                    request.after = binding.name;
                } else {
                    if (namespace.id != system_catalog.default_namespace_id) return error.CatalogGenerationChanged;
                    request.after = (self.manager.tables.get(id) orelse return error.CatalogGenerationChanged).name;
                }
                if (!std.mem.startsWith(u8, request.after.?, request.prefix orelse "")) return error.InvalidCatalogName;
            }
            if (namespace.id == system_catalog.default_namespace_id) {
                const bytes = try txn.get(listing_membership_key);
                if (bytes.len != 32) return error.InvalidCatalogRecord;
                membership = bytes[0..32].*;
            }
            try projection.checkMembership(request, membership);
            const base = try listingNamePrefix(a, namespace.id);
            const prefix = try std.mem.concat(a, u8, &.{ base, request.prefix orelse "" });
            const after = if (request.after) |name| try std.mem.concat(a, u8, &.{ base, name }) else prefix;
            var cursor = try txn.openCursor();
            defer cursor.close();
            var row = try cursor.seekAtOrAfter(if (std.mem.lessThan(u8, after, prefix)) prefix else after);
            while (row) |kv| : (row = try cursor.next()) {
                if (!std.mem.startsWith(u8, kv.key, prefix)) break;
                const name = kv.key[base.len..];
                if (request.after) |previous| if (!std.mem.lessThan(u8, previous, name)) continue;
                try context.ensureActive();
                if (kv.value.len != 8) return error.InvalidCatalogRecord;
                const id = std.mem.readInt(u64, kv.value[0..8], .little);
                const table = self.manager.tables.get(id) orelse return error.InvalidCatalogRecord;
                try entries.append(a, .{ .name = try a.dupe(u8, name), .table = table });
                if (request.limit) |limit| if (entries.items.len > limit) break;
            }
        }
        try projection.checkMembership(request, membership);
        const page = try projection.selectPage(entries.items, request, self.systemCatalogState().revision);
        for (page.entries) |*entry| {
            entry.name = try a.dupe(u8, entry.name);
            entry.table = try antfly.metadata.table_manager.cloneTable(a, entry.table);
        }
        var ranges: std.ArrayListUnmanaged(antfly.metadata.RangeRecord) = .empty;
        var intents: std.ArrayListUnmanaged(antfly.raft.PlacementIntent) = .empty;
        const RangeSelection = struct {
            prefix: []const u8,
            table_id: u64,
            fn less(_: void, left: @This(), right: @This()) bool {
                return std.mem.lessThan(u8, left.prefix, right.prefix);
            }
        };
        const selections = try a.alloc(RangeSelection, page.entries.len);
        for (selections, page.entries) |*selection, entry| selection.* = .{
            .prefix = try listingRangePrefix(a, entry.table.table_id),
            .table_id = entry.table.table_id,
        };
        std.mem.sort(RangeSelection, selections, {}, RangeSelection.less);
        var cursor = try txn.openCursor();
        defer cursor.close();
        var row = if (selections.len == 0) null else try cursor.seekAtOrAfter(selections[0].prefix);
        for (selections) |selection| {
            // A broad inventory walks adjacent ranges once. A narrow page
            // bounds unrelated skips before seeking its next selected table.
            var skipped: usize = 0;
            while (row) |kv| {
                if (!std.mem.lessThan(u8, kv.key, selection.prefix)) break;
                if (skipped == 16) {
                    row = try cursor.seekAtOrAfter(selection.prefix);
                    break;
                }
                row = try cursor.next();
                skipped += 1;
            }
            while (row) |kv| : (row = try cursor.next()) {
                if (!std.mem.startsWith(u8, kv.key, selection.prefix)) break;
                try context.ensureActive();
                if (kv.value.len != 8) return error.InvalidCatalogRecord;
                const id = std.mem.readInt(u64, kv.value[0..8], .little);
                const range = self.manager.ranges.get(id) orelse return error.InvalidCatalogRecord;
                if (range.table_id != selection.table_id) return error.InvalidCatalogRecord;
                try ranges.append(a, try antfly.metadata.table_manager.cloneRange(a, range));
                try intents.append(a, .{ .record = .{ .group_id = id, .replica_id = 1, .local_node_id = self.local_node_id, .bootstrap_mode = .persisted, .metadata_version = self.epoch }, .store_id = self.store_id, .peer_node_ids = &.{} });
            }
        }
        const stores = try a.alloc(antfly.metadata.StoreRecord, 1);
        stores[0] = .{ .store_id = self.store_id, .node_id = self.local_node_id, .api_url = try a.dupe(u8, self.api_url), .role = "data", .health_class = "healthy", .live = true };
        try context.ensureActive();
        return .{ .arena = arena, .value = .{ .revision = self.systemCatalogState().revision, .entries = page.entries, .legacy_membership = membership, .next_after = page.next, .next_table_id = if (page.next != null) page.entries[page.entries.len - 1].table.table_id else null, .ranges = ranges.items, .stores = stores, .placement_intents = intents.items } };
    }

    fn systemCatalog(ptr: *anyopaque, alloc: std.mem.Allocator, context: antfly.public_api.operation.RequestContext, call: system_catalog.Call) ![]u8 {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        try context.ensureActive();
        var lease = if (call == .mutate) (if (self.ha_catalog_server) |server| server.ha_mutation_barrier.acquireShared() else null) else null;
        defer if (lease) |*value| value.release();
        if (call == .mutate) if (self.ha_catalog_server) |server| {
            try server.ha_public_gate_state.checkWrite(server.ha_public_gate_state.currentGeneration());
            if (call.mutate.mutation.kind != .table or call.mutate.mutation.action != .create)
                return error.UnsupportedOperation;
        };
        if (!lockAtomicUntil(&self.mutex, context.deadline_ns)) return error.DeadlineExceeded;
        var locked = true;
        defer if (locked) self.mutex.unlock();
        if (self.catalog_durability_failed) return error.MetadataMutationOutcomeUnknown;
        try context.ensureActive();
        if (call == .table_status or call == .list_tables) {
            var capture = try self.captureCatalogTablesLocked(alloc, context, if (call == .table_status) call.table_status.listing() else call.list_tables);
            self.mutex.unlock();
            locked = false;
            defer capture.arena.deinit();
            try context.ensureActive();
            return std.json.Stringify.valueAlloc(alloc, capture.value, .{});
        }
        switch (call) {
            .table_status, .list_tables => unreachable,
            .export_snapshot => {
                const tables = try self.manager.listTables(alloc);
                defer self.manager.freeTables(alloc, tables);
                const ranges = try self.manager.listRanges(alloc);
                defer self.manager.freeRanges(alloc, ranges);
                return std.json.Stringify.valueAlloc(alloc, @import("../system_catalog/projection.zig").Export{
                    .epoch = self.epoch,
                    .tables = tables,
                    .ranges = ranges,
                    .system_catalog = self.systemCatalogState(),
                    .extension_packages = self.extension_catalog.packages.items,
                    .installed_extensions = self.extension_catalog.installed.items,
                    .extension_members = self.extension_catalog.members.items,
                    .extension_dependencies = self.extension_catalog.dependencies.items,
                }, .{});
            },
            .read => |request| {
                var empty = try system_catalog.StateIndex.init(alloc, .{});
                defer empty.deinit(alloc);
                const index = if (self.system_catalog_state) |*state| &state.index else &empty;
                const resources = try system_catalog.projectRead(alloc, index, request);
                defer alloc.free(resources);
                return std.json.Stringify.valueAlloc(alloc, system_catalog.State{ .revision = self.systemCatalogState().revision, .resources = resources }, .{});
            },
            .write_validation_revision => return std.json.Stringify.valueAlloc(alloc, antfly.metadata_api.MetadataHead{
                .metadata_group_id = 1,
                .metadata_epoch = self.epoch,
            }, .{}),
            .write_validation => |name| {
                const table = self.manager.findTableByName(name) orelse return error.TableNotFound;
                const shapes = try self.extension_catalog.tableDataShapes(name);
                try context.ensureActive();
                return std.json.Stringify.valueAlloc(alloc, @import("../system_catalog/projection.zig").WriteValidation{
                    .schema_json = table.schema_json,
                    .data_shapes = shapes,
                }, .{});
            },
            .query_definition => |name| {
                const table = self.manager.findTableByName(name);
                const definition: ?system_catalog.QueryDefinition = if (table) |value| system_catalog.QueryDefinition.fromTable(value) else null;
                return std.json.Stringify.valueAlloc(alloc, definition, .{});
            },
            .snapshot => return std.json.Stringify.valueAlloc(alloc, self.systemCatalogState(), .{}),
            .resolve => |target| {
                const table = try self.resolveSystemCatalogLocked(target);
                const identity: ?system_catalog.ResolvedTable = if (table) |value| system_catalog.ResolvedTable.fromTable(value) else null;
                return std.json.Stringify.valueAlloc(alloc, identity, .{});
            },
            .resolve_many => |request| {
                if (request.targets.len > 256) return error.CatalogCommandTooLarge;
                const revision = self.systemCatalogState().revision;
                if (request.expected_revision) |expected| if (expected != revision) return error.CatalogGenerationChanged;
                const tables = try alloc.alloc(?system_catalog.ResolvedTable, request.targets.len);
                defer alloc.free(tables);
                for (request.targets, tables) |target, *table| {
                    table.* = if (try self.resolveSystemCatalogLocked(target)) |value| blk: {
                        var identity = system_catalog.ResolvedTable.fromTable(value);
                        if (request.include_query_definitions) identity.query_definition = system_catalog.QueryDefinition.fromTable(value);
                        break :blk identity;
                    } else null;
                }
                return std.json.Stringify.valueAlloc(alloc, system_catalog.ResolvedMany{ .revision = revision, .tables = tables }, .{});
            },
            .mutate => |request| {
                if (request.mutation.table_id != 0 or request.mutation.storage_name.len != 0) return error.InvalidCatalogMutation;
                var arena = std.heap.ArenaAllocator.init(alloc);
                defer arena.deinit();
                const a = arena.allocator();
                const state = self.systemCatalogState();
                var command = request.mutation;
                var table: ?antfly.metadata.TableRecord = null;
                var ranges: []const antfly.metadata.RangeRecord = &.{};
                if (command.kind == .table and command.action == .create) {
                    const name = request.physical_name orelse return error.InvalidCatalogMutation;
                    if (!std.mem.startsWith(u8, name, "table:") or name.len > 1024) return error.InvalidCatalogMutation;
                    var req = try antfly.public_api.tables.parseStoredCreateTableRequest(a, request.create_table_json orelse return error.InvalidCatalogMutation);
                    const namespace = try state.namespaceFor(command.database, command.namespace);
                    const explicit = if (command.tablespace) |n| (state.find(.tablespace, 0, n) orelse return error.TablespaceNotFound).id else 0;
                    const policy = if (try state.effectiveTablespace(namespace, explicit)) |space| space.placement_policy else system_catalog.PlacementPolicy{};
                    if (req.num_shards == null) req.num_shards = policy.min_ranges;
                    table = try self.deriveCreatedTableRecord(name, req);
                    if (policy.placement_role) |role| table.?.placement_role = role;
                    // Standalone owns one local replica; policy metadata remains
                    // portable when a backup is restored into a cluster.
                    ranges = try antfly.public_api.tables.deriveInitialRanges(a, table.?);
                    command.table_id = table.?.table_id;
                    command.storage_name = name;
                } else if (request.create_table_json != null or request.physical_name != null) return error.InvalidCatalogMutation;
                const delta = self.planCatalogLocked(a, command) catch |err| {
                    if (err == error.CatalogAlreadyExists or err == error.TableAlreadyExists) {
                        if (self.ha_catalog_server) |server| server.acknowledgeHAExistingCatalog() catch return error.MetadataMutationOutcomeUnknown;
                    }
                    return err;
                };
                const empty: system_catalog.StateIndex = .{};
                const reader: CatalogReader = .{ .owner = self, .alloc = a, .index = if (self.system_catalog_state) |*catalog| &catalog.index else &empty };
                const result = try std.json.Stringify.valueAlloc(alloc, try system_catalog.mutationResult(reader, state.revision + 1, delta), .{});
                errdefer alloc.free(result);
                var mutation = try self.beginCatalogMutationLocked();
                defer mutation.deinit(self);
                if (table) |created| {
                    try mutation.upsertTable(self, created);
                    for (ranges) |range| try mutation.upsertRange(self, range);
                } else if (command.kind == .table and command.action == .set_tablespace) {
                    var current = (try self.resolveSystemCatalogLocked(.{ .database = command.database, .namespace = command.namespace, .table = command.name })) orelse return error.TableNotFound;
                    const namespace = try state.namespaceFor(command.database, command.namespace);
                    const policy = if (try state.effectiveTablespace(namespace, delta.upserts[0].tablespace_id)) |space| space.placement_policy else system_catalog.PlacementPolicy{};
                    current.placement_role = policy.placement_role orelse "data";
                    current.min_ranges = policy.min_ranges orelse 1;
                    if (self.storage_engine == .lite and current.min_ranges != 1) return error.InvalidCreateTableRequest;
                    try mutation.upsertTable(self, current);
                }
                try mutation.applyCatalog(self, delta);
                self.epoch +|= 1;
                try context.ensureActive();
                if (self.ha_catalog_server != null) {
                    try self.commitCatalogCreate(alloc, &mutation, .{
                        .table = table.?,
                        .ranges = ranges,
                        .binding = .{ .previous_revision = state.revision, .delta = delta },
                    });
                } else try mutation.commit(self);
                return result;
            },
        }
    }

    fn deriveCreatedTableRecord(self: *LocalStandaloneMetadata, table_name: []const u8, req: antfly.public_api.tables.CreateTableRequest) !antfly.metadata.TableRecord {
        if (self.ha_catalog_server != null) {
            if (req.replication_sources_json) |sources| {
                if (!std.mem.eql(u8, sources, "[]")) return error.HACatalogReplicationSourcesUnsupported;
            }
        }
        const replicated = !self.vector_source_storage_allowed or
            (if (req.replication_sources_json) |sources| !std.mem.eql(u8, sources, "[]") else false);
        var resolved_req = req;
        resolved_req.storage = try antfly.common.table_storage.Settings.resolveStandaloneCreate(req.storage, req.num_shards orelse 1, replicated, self.storage_engine != .local);
        return deriveStandaloneTableRecord(self.storage_engine, table_name, resolved_req);
    }

    fn createTable(ptr: *anyopaque, alloc: std.mem.Allocator, table_name: []const u8, req: antfly.public_api.tables.CreateTableRequest) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        const table = try self.deriveCreatedTableRecord(table_name, req);
        const ranges = try antfly.public_api.tables.deriveInitialRanges(alloc, table);
        defer {
            for (ranges) |record| antfly.metadata.table_manager.freeRange(alloc, record);
            alloc.free(ranges);
        }

        var lease = if (self.ha_catalog_server) |server| server.ha_mutation_barrier.acquireShared() else null;
        defer if (lease) |*value| value.release();
        if (self.ha_catalog_server) |server| {
            // Standby apply takes the HA transition lock before this catalog
            // lock. Reject non-writers through the lock-free role gate before
            // taking either lock; append/ack still recheck primary authority.
            try server.ha_public_gate_state.checkWrite(server.ha_public_gate_state.currentGeneration());
        }
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (self.findTableByNameLocked(table_name) != null) {
            if (self.ha_catalog_server) |server| {
                // A retry may observe a catalog whose original RemoteApply wait
                // timed out. Existence is safe to acknowledge only after the
                // required standby has applied the current catalog frontier.
                server.acknowledgeHAExistingCatalog() catch return error.MetadataMutationOutcomeUnknown;
            }
            return error.TableAlreadyExists;
        }
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, table);
        for (ranges) |range| try mutation.upsertRange(self, range);
        self.epoch +|= 1;
        try self.commitCatalogCreate(alloc, &mutation, .{ .table = table, .ranges = ranges });
    }

    // The caller owns the shared mutation barrier and catalog lock. The WAL
    // carries both physical rows and the logical binding as one commit.
    fn commitCatalogCreate(self: *LocalStandaloneMetadata, alloc: std.mem.Allocator, mutation: *CatalogMutation, value: CatalogCreate) !void {
        if (self.ha_catalog_server) |server| {
            const payload = try std.json.Stringify.valueAlloc(alloc, value, .{});
            defer alloc.free(payload);
            const commit = try server.appendHACatalogCreate(payload);
            {
                errdefer server.ha_public_gate_state.publishPrimaryFence(true);
                lockAtomic(&server.ha_state_mutex);
                defer server.ha_state_mutex.unlock();
                try server.ha_public_gate_state.checkWrite(commit.generation);
                try mutation.commit(self);
            }
            server.acknowledgeHACatalogCreate(commit) catch return error.MetadataMutationOutcomeUnknown;
        } else try mutation.commit(self);
    }

    fn applyHACatalogCreate(ptr: *anyopaque, record: antfly.hot_standby.replication_record.RecordView) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        if (record.kind != .metadata_mutation or record.payload_codec != .json or
            record.table_id != 0 or record.shard_id != 0) return error.InvalidHACatalogRecord;
        var parsed = try std.json.parseFromSlice(CatalogCreate, self.alloc, record.payload, .{ .allocate = .alloc_always });
        defer parsed.deinit();
        const value = parsed.value;
        if ((value.schema_version != 3 and value.schema_version != 4) or value.ranges.len == 0 or
            (value.schema_version == 3 and value.binding != null)) return error.InvalidHACatalogRecord;
        if (value.binding) |binding| {
            if (binding.delta.removes.len != 0 or binding.delta.upserts.len != 1) return error.InvalidHACatalogRecord;
            const resource = binding.delta.upserts[0];
            if (resource.kind != .table or resource.id != value.table.table_id or
                !std.mem.eql(u8, resource.storage_name, value.table.name)) return error.InvalidHACatalogRecord;
        }
        for (value.ranges) |range| {
            if (range.table_id != value.table.table_id) return error.InvalidHACatalogRecord;
        }
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (self.findTableByNameLocked(value.table.name)) |existing| {
            const encoded = try std.json.Stringify.valueAlloc(self.alloc, existing.*, .{});
            defer self.alloc.free(encoded);
            const expected = try std.json.Stringify.valueAlloc(self.alloc, value.table, .{});
            defer self.alloc.free(expected);
            if (!std.mem.eql(u8, encoded, expected)) return error.HACatalogReplayConflict;
            if (value.binding) |binding| {
                const state = self.systemCatalogState();
                if (state.revision <= binding.previous_revision or state.next_id < binding.delta.next_id)
                    return error.HACatalogReplayConflict;
                for (binding.delta.upserts) |resource| {
                    const actual = state.byId(resource.kind, resource.id) orelse return error.HACatalogReplayConflict;
                    const actual_json = try std.json.Stringify.valueAlloc(self.alloc, actual, .{});
                    defer self.alloc.free(actual_json);
                    const expected_json = try std.json.Stringify.valueAlloc(self.alloc, resource, .{});
                    defer self.alloc.free(expected_json);
                    if (!std.mem.eql(u8, actual_json, expected_json)) return error.HACatalogReplayConflict;
                }
            }
            return;
        }
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        if (value.binding) |binding| {
            if (self.systemCatalogState().revision != binding.previous_revision) return error.HACatalogReplayConflict;
            try mutation.applyCatalog(self, binding.delta);
        }
        try mutation.upsertTable(self, value.table);
        for (value.ranges) |range| try mutation.upsertRange(self, range);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn replayHACatalog(self: *LocalStandaloneMetadata, primary: *antfly.hot_standby.primary.Primary) !void {
        // Scan one record at a time; document WAL can be much larger than the
        // catalog and must not be materialized in memory during startup.
        try primary.log.wal.iterateFromStreamingWithContext(1, self, replayHACatalogEntry);
    }

    fn replayHACatalogEntry(self: *LocalStandaloneMetadata, entry: ha_wal.WalEntry) !ha_wal.WAL.ScanAction {
        const record = try antfly.hot_standby.replication_record.decode(entry.data);
        if (record.lsn != entry.lsn or record.lsn == 0 or record.previous_lsn != record.lsn - 1)
            return error.InvalidHACatalogRecord;
        if (record.kind == .metadata_mutation and record.table_id == 0 and record.shard_id == 0)
            try applyHACatalogCreate(self, record);
        return .@"continue";
    }

    fn adoptEmbeddedLiteRootIfNeeded(self: *LocalStandaloneMetadata, backend: *antfly.lite.backend.Handle) !void {
        const existing = try self.manager.listTables(self.alloc);
        defer self.manager.freeTables(self.alloc, existing);
        if (existing.len != 0) return;
        if (!(try backend.isEmbeddedArtifact()) and !(try backend.embeddedRootHasUserDocuments())) return;

        const table = try deriveStandaloneTableRecord(.lite, "default", .{});
        const ranges = try antfly.public_api.tables.deriveInitialRanges(self.alloc, table);
        defer {
            for (ranges) |record| antfly.metadata.table_manager.freeRange(self.alloc, record);
            self.alloc.free(ranges);
        }
        if (ranges.len != 1) return error.InvalidCreateTableRequest;
        const namespace = try std.fmt.allocPrint(self.alloc, "group-{d}/table-db", .{ranges[0].group_id});
        defer self.alloc.free(namespace);

        // The durable alias is published first. If catalog publication fails,
        // startup fails and the next attempt safely retries the idempotent
        // catalog adoption; it can never publish a table that points at an
        // empty namespace.
        try backend.adoptEmbeddedRootAsNamespace(namespace);

        // Embedded Lite is created with the deterministic identity of the
        // future standalone `default` table. Verify that invariant before
        // publishing metadata; never perform an O(live documents) identity
        // rewrite during startup or silently accept a mismatched artifact.
        const target_identity = antfly.db.DocIdentityNamespace{
            .table_id = table.table_id,
            .shard_id = ranges[0].group_id,
            .range_id = ranges[0].range_id,
        };
        try verifyAdoptedLiteIdentity(self.alloc, backend, namespace, target_identity);

        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (self.findTableByNameLocked("default") != null) return;
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, table);
        for (ranges) |range| try mutation.upsertRange(self, range);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn adoptEmbeddedLiteRootFromKernelIfNeeded(
        self: *LocalStandaloneMetadata,
        context: *kernel_owner_client.Context,
    ) !void {
        const existing = try self.manager.listTables(self.alloc);
        defer self.manager.freeTables(self.alloc, existing);
        if (existing.len != 0) return;
        const probe = try context.liteAdoptionProbe();
        if (probe.is_embedded_artifact == 0 and probe.embedded_root_has_user_documents == 0) return;

        const table = try deriveStandaloneTableRecord(.lite, "default", .{});
        const ranges = try antfly.public_api.tables.deriveInitialRanges(self.alloc, table);
        defer {
            for (ranges) |record| antfly.metadata.table_manager.freeRange(self.alloc, record);
            self.alloc.free(ranges);
        }
        if (ranges.len != 1) return error.InvalidCreateTableRequest;
        const namespace = try std.fmt.allocPrint(self.alloc, "group-{d}/table-db", .{ranges[0].group_id});
        defer self.alloc.free(namespace);

        try context.liteAdoptAndVerify(.{
            .namespace = .fromSlice(namespace),
            .identity_table_id = table.table_id,
            .identity_shard_id = ranges[0].group_id,
            .identity_range_id = ranges[0].range_id,
        });

        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (self.findTableByNameLocked("default") != null) return;
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try self.manager.upsertTable(table);
        for (ranges) |range| try self.manager.upsertRange(range);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn replaceTableDefinition(ptr: *anyopaque, expected: antfly.metadata.TableRecord, replacement: antfly.metadata.TableRecord) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();

        const current = self.findTableByNameLocked(replacement.name) orelse return error.TableNotFound;
        if (!antfly.metadata.table_manager.tableDefinitionsEqual(current.*, expected) or replacement.table_id != expected.table_id) return error.TableGenerationChanged;
        if (current.storage_migration != null and !antfly.metadata.table_manager.tableDefinitionsEqual(current.*, replacement))
            return error.TableTransitionActive;
        try antfly.public_api.indexes.validateArtifactEnrichmentsForTableIndexesJson(self.alloc, replacement.indexes_json);
        try antfly.inference.managed_embedder.validateEmbeddingProducerOwnershipJson(self.alloc, replacement.indexes_json);
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, replacement);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn publishVectorMigrationTable(ptr: *anyopaque, expected: antfly.metadata.TableRecord, replacement: antfly.metadata.TableRecord) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        if (!self.vector_source_storage_allowed or self.storage_engine != .local)
            return error.VectorStoreRequiresLocalSingleShardTable;
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.captureTable(self, replacement.table_id);
        try self.manager.publishVectorMigrationTable(expected, replacement);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn beginVectorMigrationCommand(ptr: *anyopaque, table_name: []const u8) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        try self.vector_migration_commands.begin(self.alloc, table_name);
    }

    fn endVectorMigrationCommand(ptr: *anyopaque, table_name: []const u8) void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        self.vector_migration_commands.end(self.alloc, table_name);
    }

    fn restoreTable(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        table_name: []const u8,
        location_uri: []const u8,
        connection: []const u8,
        artifact_backup_id: []const u8,
        manifest: *const antfly.public_api.backups.TableBackupManifest,
    ) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        try antfly.public_api.backups.validateTableManifest(alloc, manifest, manifest.backup_id);
        if (!std.mem.eql(u8, manifest.table_name, table_name) and
            !(system_catalog.isRestoreTarget(table_name) catch false)) return error.InvalidBackupRequest;
        var table = try antfly.public_api.backups.deriveRestoreTableRecord(alloc, table_name, location_uri, manifest);
        defer antfly.metadata.table_manager.freeTable(alloc, table);
        try antfly.public_api.indexes.validateArtifactEnrichmentsForTableIndexesJson(alloc, table.indexes_json);
        try antfly.inference.managed_embedder.validateEmbeddingProducerOwnershipJson(alloc, table.indexes_json);
        const ranges = try antfly.public_api.backups.deriveRestoreRanges(
            alloc,
            table.table_id,
            location_uri,
            connection,
            artifact_backup_id,
            manifest,
        );
        defer {
            for (ranges) |record| antfly.metadata.table_manager.freeRange(alloc, record);
            alloc.free(ranges);
        }
        if (self.storage_engine == .lite and ranges.len != 1) return error.InvalidBackupRequest;
        table.desired_replica_count = 1;

        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (self.findTableByNameLocked(table_name) != null) return error.TableAlreadyExists;
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        if (try system_catalog.restoreTarget(alloc, table_name)) |owned_target| {
            defer owned_target.deinit(alloc);
            const target = owned_target.value;
            var delta = try self.planCatalogLocked(alloc, .{ .action = .create, .kind = .table, .database = target.database, .namespace = target.namespace, .name = target.table, .table_id = table.table_id, .storage_name = table_name });
            defer delta.deinit(alloc);
            try mutation.applyCatalog(self, delta);
        }
        try mutation.upsertTable(self, table);
        for (ranges) |range| try mutation.upsertRange(self, range);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn dropTable(ptr: *anyopaque, _: std.mem.Allocator, table_name: []const u8) !void {
        var result = try dropTableExact(ptr, std.heap.page_allocator, table_name);
        result.deinit(std.heap.page_allocator);
    }

    fn dropTableExact(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        table_name: []const u8,
    ) !antfly.metadata.topology_protocol.DropResult {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const table = self.findTableByNameLocked(table_name) orelse return error.TableNotFound;
        if (table.storage_migration != null) return error.VectorMigrationActive;
        const table_id = table.table_id;
        const ranges = try self.manager.listRanges(alloc);
        defer self.manager.freeRanges(alloc, ranges);
        var dropped_group_ids = std.ArrayListUnmanaged(u64).empty;
        errdefer dropped_group_ids.deinit(alloc);
        for (ranges) |range| {
            if (range.table_id == table_id) try dropped_group_ids.append(alloc, range.group_id);
        }
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        if (self.systemCatalogState().byId(.table, table_id)) |binding| {
            const empty = [_]system_catalog.Resource{};
            const removed = [_]system_catalog.Resource{binding};
            try mutation.applyCatalog(self, .{ .upserts = @constCast(&empty), .removes = @constCast(&removed), .next_id = self.systemCatalogState().next_id });
        }
        try mutation.removeTable(self, table_id);
        self.epoch +|= 1;
        try mutation.commit(self);
        return .{
            .table_id = table_id,
            .expected_transition_generation = 0,
            .group_ids = try dropped_group_ids.toOwnedSlice(alloc),
        };
    }

    fn updateSchema(ptr: *anyopaque, alloc: std.mem.Allocator, table_name: []const u8, schema_json: []const u8) !void {
        _ = try updateSchemaVersioned(ptr, alloc, table_name, schema_json);
    }

    fn updateSchemaVersioned(ptr: *anyopaque, alloc: std.mem.Allocator, table_name: []const u8, schema_json: []const u8) !u32 {
        var result = try mutateSchema(ptr, alloc, table_name, .replace, schema_json, null);
        defer result.deinit(alloc);
        return result.version;
    }

    fn updateSchemaVersionedExpected(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        table_name: []const u8,
        schema_json: []const u8,
        expected_version: ?u32,
    ) !u32 {
        var result = try mutateSchema(ptr, alloc, table_name, .replace, schema_json, expected_version);
        defer result.deinit(alloc);
        return result.version;
    }

    fn mutateSchema(
        ptr: *anyopaque,
        alloc: std.mem.Allocator,
        table_name: []const u8,
        mode: antfly.public_api.tables.SchemaMutationMode,
        body: []const u8,
        expected_version: ?u32,
    ) !antfly.public_api.tables.SchemaMutationResult {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const table = self.findTableByNameLocked(table_name) orelse return error.TableNotFound;
        if (expected_version) |expected| {
            if (try antfly.public_api.tables.schemaVersion(table.schema_json) != expected)
                return error.SchemaVersionChanged;
        }
        const updated = try antfly.public_api.tables.applySchemaMutationRecord(alloc, table, mode, body);
        defer antfly.metadata.table_manager.freeTable(alloc, updated);
        const version = try antfly.public_api.tables.schemaVersion(updated.schema_json);
        var result = antfly.public_api.tables.SchemaMutationResult{
            .version = version,
            .schema_json = try alloc.dupe(u8, updated.schema_json),
        };
        errdefer result.deinit(alloc);
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, updated);
        self.epoch +|= 1;
        try mutation.commit(self);
        return result;
    }

    fn createIndex(ptr: *anyopaque, alloc: std.mem.Allocator, table_name: []const u8, index_name: []const u8, index_json: []const u8) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const table = self.findTableByNameLocked(table_name) orelse return error.TableNotFound;
        var updated = table.*;
        updated.indexes_json = try antfly.public_api.indexes.addIndexToTableIndexesJson(alloc, table.indexes_json, index_name, index_json);
        defer alloc.free(updated.indexes_json);
        try antfly.public_api.indexes.validateArtifactEnrichmentsForTableIndexesJson(alloc, updated.indexes_json);
        try antfly.inference.managed_embedder.validateEmbeddingProducerOwnershipJson(alloc, updated.indexes_json);
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, updated);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn dropIndex(ptr: *anyopaque, alloc: std.mem.Allocator, table_name: []const u8, index_name: []const u8) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const table = self.findTableByNameLocked(table_name) orelse return error.TableNotFound;
        const indexes_json = (try antfly.public_api.indexes.removeIndexFromTableIndexesJson(alloc, table.indexes_json, index_name)) orelse return error.IndexNotFound;
        defer alloc.free(indexes_json);
        try antfly.public_api.indexes.validateArtifactEnrichmentsForTableIndexesJson(alloc, indexes_json);
        try antfly.inference.managed_embedder.validateEmbeddingProducerOwnershipJson(alloc, indexes_json);
        var updated = table.*;
        updated.indexes_json = indexes_json;
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, updated);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn putArtifactEnrichment(ptr: *anyopaque, alloc: std.mem.Allocator, table_name: []const u8, artifact_name: []const u8, enrichment_json: []const u8) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const table = self.findTableByNameLocked(table_name) orelse return error.TableNotFound;
        var updated = table.*;
        updated.indexes_json = try antfly.public_api.indexes.addEnrichmentToTableIndexesJson(alloc, table.indexes_json, artifact_name, enrichment_json);
        defer alloc.free(updated.indexes_json);
        try antfly.public_api.indexes.validateArtifactEnrichmentsForTableIndexesJson(alloc, updated.indexes_json);
        try antfly.inference.managed_embedder.validateEmbeddingProducerOwnershipJson(alloc, updated.indexes_json);
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, updated);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn deleteArtifactEnrichment(ptr: *anyopaque, alloc: std.mem.Allocator, table_name: []const u8, artifact_name: []const u8) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const table = self.findTableByNameLocked(table_name) orelse return error.TableNotFound;
        const indexes_json = (try antfly.public_api.indexes.removeEnrichmentFromTableIndexesJson(alloc, table.indexes_json, artifact_name)) orelse return error.EnrichmentNotFound;
        defer alloc.free(indexes_json);
        try antfly.public_api.indexes.validateArtifactEnrichmentsForTableIndexesJson(alloc, indexes_json);
        try antfly.inference.managed_embedder.validateEmbeddingProducerOwnershipJson(alloc, indexes_json);
        var updated = table.*;
        updated.indexes_json = indexes_json;
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.upsertTable(self, updated);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn waitTableLifecycle(_: *anyopaque, _: []const u8, _: antfly.public_api.http_server.TableVisibility) !void {}

    fn waitTableProjection(ptr: *anyopaque, table_name: []const u8, schema_json: ?[]const u8, indexes_json: ?[]const u8) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const table = self.findTableByNameLocked(table_name) orelse return error.TableVisibilityTimeout;
        if (schema_json) |expected| {
            if (!std.mem.eql(u8, table.schema_json, expected)) return error.TableVisibilityTimeout;
        }
        if (indexes_json) |expected| {
            if (!std.mem.eql(u8, table.indexes_json, expected)) return error.TableVisibilityTimeout;
        }
    }

    fn runRound(ptr: *anyopaque) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        self.finalizeReadySchemaMigrations() catch |err| switch (err) {
            error.FileNotFound, error.WriterLocked, error.LsmRootWriterAlreadyOpen, error.LmdbUnexpected, error.Corrupted => {},
            else => return err,
        };
    }

    fn installExtension(ptr: *anyopaque, alloc: std.mem.Allocator, extension_name: []const u8, req: antfly.extensions.InstallExtensionRequest) !antfly.extensions.InstalledExtension {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const installed_at_ms: i64 = @intCast(@divTrunc(platform_time.realtimeNs(), std.time.ns_per_ms));
        var persisted_req = req;
        persisted_req.dry_run = false;
        if (req.dry_run) {
            var catalog = try self.cloneExtensionCatalogLocked();
            defer catalog.deinit();
            var planned = try catalog.installManifestOnly(extension_name, extension_name, persisted_req, installed_at_ms);
            defer planned.deinitOwned(self.alloc);
            return try antfly.extensions.cloneInstalledExtensionAlloc(alloc, planned);
        }
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.extensions(self);
        var installed = try self.extension_catalog.installManifestOnly(extension_name, extension_name, persisted_req, installed_at_ms);
        defer installed.deinitOwned(self.alloc);
        self.epoch +|= 1;
        try mutation.commit(self);
        return try self.extension_catalog.getInstalledAlloc(alloc, extension_name);
    }

    fn updateExtension(ptr: *anyopaque, alloc: std.mem.Allocator, extension_name: []const u8, req: antfly.extensions.UpdateExtensionRequest) !antfly.extensions.InstalledExtension {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var persisted_req = req;
        persisted_req.dry_run = false;
        if (req.dry_run) {
            var catalog = try self.cloneExtensionCatalogLocked();
            defer catalog.deinit();
            var planned = try catalog.updateManifestOnly(extension_name, persisted_req);
            defer planned.deinitOwned(self.alloc);
            return try antfly.extensions.cloneInstalledExtensionAlloc(alloc, planned);
        }
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.extensions(self);
        var installed = try self.extension_catalog.updateManifestOnly(extension_name, persisted_req);
        defer installed.deinitOwned(self.alloc);
        self.epoch +|= 1;
        try mutation.commit(self);
        return try self.extension_catalog.getInstalledAlloc(alloc, extension_name);
    }

    fn dropExtension(ptr: *anyopaque, _: std.mem.Allocator, extension_name: []const u8, req: antfly.extensions.DropExtensionRequest) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var persisted_req = req;
        persisted_req.dry_run = false;
        if (req.dry_run) {
            var catalog = try self.cloneExtensionCatalogLocked();
            defer catalog.deinit();
            return try catalog.dropInstalledWithMode(extension_name, persisted_req);
        }
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.extensions(self);
        try self.extension_catalog.dropInstalledWithMode(extension_name, persisted_req);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn enableExtension(ptr: *anyopaque, alloc: std.mem.Allocator, extension_name: []const u8) !antfly.extensions.InstalledExtension {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.extensions(self);
        try self.extension_catalog.enableInstalled(extension_name);
        self.epoch +|= 1;
        try mutation.commit(self);
        return try self.extension_catalog.getInstalledAlloc(alloc, extension_name);
    }

    fn disableExtension(ptr: *anyopaque, alloc: std.mem.Allocator, extension_name: []const u8) !antfly.extensions.InstalledExtension {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.extensions(self);
        try self.extension_catalog.disableInstalled(extension_name);
        self.epoch +|= 1;
        try mutation.commit(self);
        return try self.extension_catalog.getInstalledAlloc(alloc, extension_name);
    }

    fn configureExtension(ptr: *anyopaque, alloc: std.mem.Allocator, extension_name: []const u8, req: antfly.extensions.ConfigureExtensionRequest) !antfly.extensions.InstalledExtension {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.extensions(self);
        try self.extension_catalog.configureInstalled(extension_name, req);
        self.epoch +|= 1;
        try mutation.commit(self);
        return try self.extension_catalog.getInstalledAlloc(alloc, extension_name);
    }

    fn restoreExtensions(
        ptr: *anyopaque,
        _: std.mem.Allocator,
        installed: []const antfly.extensions.InstalledExtension,
        members: []const antfly.extensions.ExtensionMember,
        dependencies: []const antfly.extensions.ExtensionDependency,
    ) !void {
        const self: *LocalStandaloneMetadata = @ptrCast(@alignCast(ptr));
        if (installed.len == 0 and members.len == 0 and dependencies.len == 0) return;
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        try mutation.extensions(self);
        for (installed) |extension| try self.extension_catalog.upsertInstalled(extension);
        for (members) |member| try self.extension_catalog.upsertMember(member);
        for (dependencies) |dependency| try self.extension_catalog.upsertDependency(dependency);
        self.epoch +|= 1;
        try mutation.commit(self);
    }

    fn cloneExtensionCatalogLocked(self: *LocalStandaloneMetadata) !antfly.extensions.ExtensionCatalog {
        var catalog = antfly.extensions.ExtensionCatalog.init(self.alloc);
        errdefer catalog.deinit();
        try catalog.loadProjectedRows(
            self.extension_catalog.packages.items,
            self.extension_catalog.installed.items,
            self.extension_catalog.members.items,
            self.extension_catalog.dependencies.items,
        );
        return catalog;
    }

    fn syncExtensionPackageStore(self: *LocalStandaloneMetadata, io: std.Io, root_path: []const u8) !usize {
        const entries = try antfly.extensions.scanPackageStoreAlloc(self.alloc, io, root_path);
        defer antfly.extensions.freePackageStoreEntries(self.alloc, entries);

        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        var mutation = if (entries.len > 0) try self.beginCatalogMutationLocked() else null;
        defer if (mutation) |*active| active.deinit(self);
        if (mutation) |*active| try active.extensions(self);
        for (entries) |entry| try self.extension_catalog.registerPackage(entry.manifest);
        if (entries.len > 0) {
            self.epoch +|= 1;
            try mutation.?.commit(self);
        }
        return entries.len;
    }

    fn finalizeReadySchemaMigrations(self: *LocalStandaloneMetadata) !void {
        const now_ms = monotonicMs();
        const snapshot = blk: {
            lockAtomic(&self.mutex);
            defer self.mutex.unlock();
            var active_migration = false;
            var table_it = self.manager.tables.valueIterator();
            while (table_it.next()) |table| {
                if (table.read_schema_json.len > 0) {
                    active_migration = true;
                    break;
                }
            }
            if (!active_migration) return;
            if (now_ms -| self.last_schema_migration_finalize_at_ms < local_schema_migration_finalize_interval_ms) return;
            self.last_schema_migration_finalize_at_ms = now_ms;

            const tables = try self.manager.listTables(self.alloc);
            errdefer self.manager.freeTables(self.alloc, tables);
            const ranges = try self.manager.listRanges(self.alloc);
            break :blk .{ .tables = tables, .ranges = ranges };
        };
        defer self.manager.freeRanges(self.alloc, snapshot.ranges);
        defer self.manager.freeTables(self.alloc, snapshot.tables);

        const hosted_group_ids = try self.alloc.alloc(u64, snapshot.ranges.len);
        defer self.alloc.free(hosted_group_ids);
        for (snapshot.ranges, 0..) |range, i| hosted_group_ids[i] = range.group_id;

        var runtime_progress: ?antfly.data.runtime.DataServer.LocalSchemaProgressSnapshot = null;
        defer if (runtime_progress) |*progress| progress.deinit(self.alloc);
        var filesystem_progress: ?[]antfly.metadata.SchemaProgressRecord = null;
        defer if (filesystem_progress) |progress| self.alloc.free(progress);
        var shard_db_adapter: ?antfly.metadata.ShardDbAdapter = null;
        const progress: []const antfly.metadata.SchemaProgressRecord = progress: {
            if (self.local_schema_progress_provider) |provider| {
                shard_db_adapter = provider.shard_db_adapter;
                runtime_progress = try provider.collect(provider.ptr, self.alloc, snapshot.tables, snapshot.ranges);
                if (runtime_progress.?.records.len != 0) break :progress runtime_progress.?.records;
                // A complete runtime observation is authoritative even while
                // not ready. Do not contend with its live writer by reopening
                // the same root through the filesystem fallback.
                if (runtime_progress.?.runtime_coverage_complete) return;
            }
            // A control-only process has no legal filesystem fallback. If its
            // live data-server adapter is not installed yet, retain the
            // migration and retry on the next metadata round instead of
            // terminating the standalone runtime.
            if (comptime control_only_storage_sources) {
                if (shard_db_adapter == null) return;
            }
            filesystem_progress = try antfly.metadata.table_provisioner.collectLocalSchemaProgressWithOptions(
                self.alloc,
                self.replica_root_dir,
                group_ids.main_metadata_group_id,
                self.local_node_id,
                hosted_group_ids,
                snapshot.tables,
                snapshot.ranges,
                .{
                    .backend_runtime = self.backend_runtime,
                    .shard_db_adapter = shard_db_adapter,
                },
            );
            break :progress filesystem_progress.?;
        };
        if (progress.len == 0) return;

        lockAtomic(&self.mutex);
        defer self.mutex.unlock();

        var mutation = try self.beginCatalogMutationLocked();
        defer mutation.deinit(self);
        var changed = false;
        for (progress) |record| {
            const table = self.manager.tables.get(record.table_id) orelse continue;
            if (table.read_schema_json.len == 0) continue;

            const target_version = try localSchemaVersion(self.alloc, table.schema_json);
            if (record.schema_version != target_version) continue;

            var updated = try antfly.metadata.table_manager.cloneTable(self.alloc, table);
            defer antfly.metadata.table_manager.freeTable(self.alloc, updated);

            const read_version = try localSchemaVersion(self.alloc, updated.read_schema_json);
            if (read_version != target_version) {
                const next_indexes_json = try dropFullTextIndexForVersion(self.alloc, updated.indexes_json, read_version);
                self.alloc.free(updated.indexes_json);
                updated.indexes_json = next_indexes_json;
            }
            self.alloc.free(updated.read_schema_json);
            updated.read_schema_json = try self.alloc.dupe(u8, "");

            try mutation.upsertTable(self, updated);
            changed = true;
        }

        if (changed) {
            self.epoch +|= 1;
            try mutation.commit(self);
        } else {
            mutation.committed = true;
        }
    }

    fn findTableByNameLocked(self: *LocalStandaloneMetadata, table_name: []const u8) ?*const antfly.metadata.TableRecord {
        return self.manager.findTableByName(table_name);
    }

    const CatalogHead = @import("catalog_format.zig").Head;
    const CatalogRow = union(enum) {
        table: antfly.metadata.TableRecord,
        range: antfly.metadata.RangeRecord,
        resource: system_catalog.Resource,
        extensions: PersistedCatalog,
    };
    const catalog_head_key = @import("catalog_format.zig").head_key;
    const catalog_row_prefix = @import("catalog_format.zig").row_prefix;

    fn durableCatalogStore(self: *LocalStandaloneMetadata) !*antfly.storage_backend_erased.Store {
        return self.catalog_store orelse if (self.owned_catalog_store) |*store| store else error.CatalogStorageUnavailable;
    }

    fn loadCatalogRows(self: *LocalStandaloneMetadata) !bool {
        const store = try self.durableCatalogStore();
        var txn = try store.beginRead();
        defer txn.abort();
        const head_bytes = txn.get(catalog_head_key) catch |err| switch (err) {
            error.NotFound => {
                var probe = try txn.openCursor();
                defer probe.close();
                if (try probe.seekAtOrAfter(catalog_row_prefix)) |row| {
                    if (std.mem.startsWith(u8, row.key, catalog_row_prefix)) return error.InvalidCatalogRecord;
                }
                return false;
            },
            else => return err,
        };
        var head = try std.json.parseFromSlice(CatalogHead, self.alloc, head_bytes, .{});
        defer head.deinit();
        if (head.value.version != 1 or head.value.next_id < 3 or head.value.epoch == 0) return error.InvalidCatalogRecord;
        var arena = std.heap.ArenaAllocator.init(self.alloc);
        defer arena.deinit();
        const a = arena.allocator();
        var tables: std.ArrayListUnmanaged(antfly.metadata.TableRecord) = .empty;
        var ranges: std.ArrayListUnmanaged(antfly.metadata.RangeRecord) = .empty;
        var resources: std.ArrayListUnmanaged(system_catalog.Resource) = .empty;
        var extensions: PersistedCatalog = .{};
        var cursor = try txn.openCursor();
        defer cursor.close();
        var entry = try cursor.seekAtOrAfter(catalog_row_prefix);
        while (entry) |kv| : (entry = try cursor.next()) {
            if (!std.mem.startsWith(u8, kv.key, catalog_row_prefix)) break;
            const row = try std.json.parseFromSliceLeaky(CatalogRow, a, kv.value, .{ .allocate = .alloc_always });
            const expected = try catalogRowKey(a, row);
            if (!std.mem.eql(u8, expected, kv.key)) return error.InvalidCatalogRecord;
            switch (row) {
                .table => |value| try tables.append(a, value),
                .range => |value| try ranges.append(a, value),
                .resource => |value| try resources.append(a, value),
                .extensions => |value| extensions = value,
            }
        }
        const loaded = try self.manager.replaceProjectedTopology(tables.items, ranges.items);
        if (loaded.skipped_orphan_ranges != 0) return error.InvalidCatalogRecord;
        try self.extension_catalog.loadProjectedRows(extensions.extension_packages, extensions.installed_extensions, extensions.extension_members, extensions.extension_dependencies);
        self.system_catalog_state = try system_catalog.MutableState.clone(self.alloc, .{ .revision = head.value.revision, .next_id = head.value.next_id, .resources = resources.items });
        self.epoch = head.value.epoch;
        self.catalog_rows_initialized = true;
        return true;
    }

    fn loadPersistedCatalog(self: *LocalStandaloneMetadata) !void {
        if (try self.loadCatalogRows()) return;
        const raw = if (self.catalog_store) |store| blk: {
            var txn = try store.beginRead();
            defer txn.abort();
            const value = txn.get("catalog") catch |err| switch (err) {
                error.NotFound => return,
                else => return err,
            };
            break :blk try self.alloc.dupe(u8, value);
        } else readFileAlloc(
            self.alloc,
            self.backend_runtime.io() orelse std.Options.debug_io,
            self.catalog_path,
            64 * 1024 * 1024,
        ) catch |err| switch (err) {
            error.FileNotFound => return,
            else => return err,
        };
        defer self.alloc.free(raw);

        var parsed = try std.json.parseFromSlice(PersistedCatalog, self.alloc, raw, .{
            .allocate = .alloc_always,
            .ignore_unknown_fields = true,
        });
        defer parsed.deinit();

        _ = try self.manager.replaceProjectedTopology(parsed.value.tables, parsed.value.ranges);
        try self.extension_catalog.loadProjectedRows(
            parsed.value.extension_packages,
            parsed.value.installed_extensions,
            parsed.value.extension_members,
            parsed.value.extension_dependencies,
        );
        self.system_catalog_state = try system_catalog.MutableState.clone(self.alloc, parsed.value.system_catalog);
        self.epoch = @max(parsed.value.epoch, 1);
    }

    fn catalogRowKey(alloc: std.mem.Allocator, row: CatalogRow) ![]u8 {
        return switch (row) {
            .table => |r| std.fmt.allocPrint(alloc, catalog_row_prefix ++ "table/{d}", .{r.table_id}),
            .range => |r| std.fmt.allocPrint(alloc, catalog_row_prefix ++ "range/{d}", .{r.group_id}),
            .resource => |r| std.fmt.allocPrint(alloc, catalog_row_prefix ++ "resource/{s}/{d}", .{ @tagName(r.kind), r.id }),
            .extensions => alloc.dupe(u8, catalog_row_prefix ++ "extensions"),
        };
    }

    fn putCatalogRow(alloc: std.mem.Allocator, txn: *antfly.storage_backend_erased.WriteTxn, row: CatalogRow) !void {
        const key = try catalogRowKey(alloc, row);
        defer alloc.free(key);
        const value = try std.json.Stringify.valueAlloc(alloc, row, .{ .emit_null_optional_fields = false });
        defer alloc.free(value);
        try txn.put(key, value);
    }
    fn removeCatalogRow(alloc: std.mem.Allocator, txn: *antfly.storage_backend_erased.WriteTxn, row: CatalogRow) !void {
        const key = try catalogRowKey(alloc, row);
        defer alloc.free(key);
        try txn.delete(key);
    }

    const listing_index_prefix = "\x00\x00__standalone_listing_v1:";
    const listing_membership_key = listing_index_prefix ++ "legacy_membership";
    const ListingIdentity = struct { namespace: u64, name: []const u8, id: u64, legacy: bool };
    fn listingIdentity(table: ?antfly.metadata.TableRecord, binding: ?system_catalog.Resource) ?ListingIdentity {
        const row = table orelse return null;
        return .{ .namespace = if (binding) |r| r.parent_id else system_catalog.default_namespace_id, .name = if (binding) |r| r.name else row.name, .id = row.table_id, .legacy = binding == null };
    }
    fn listingNamePrefix(alloc: std.mem.Allocator, namespace: u64) ![]u8 {
        return std.fmt.allocPrint(alloc, listing_index_prefix ++ "name:{d}:", .{namespace});
    }
    fn listingNameKey(alloc: std.mem.Allocator, identity: ListingIdentity) ![]u8 {
        return std.fmt.allocPrint(alloc, listing_index_prefix ++ "name:{d}:{s}", .{ identity.namespace, identity.name });
    }
    fn listingRangePrefix(alloc: std.mem.Allocator, table_id: u64) ![]u8 {
        return std.fmt.allocPrint(alloc, listing_index_prefix ++ "range:{d}:", .{table_id});
    }
    fn listingRangeKey(alloc: std.mem.Allocator, row: antfly.metadata.RangeRecord) ![]u8 {
        return std.fmt.allocPrint(alloc, listing_index_prefix ++ "range:{d}:{d}", .{ row.table_id, row.group_id });
    }
    fn putListingIdentity(alloc: std.mem.Allocator, txn: *antfly.storage_backend_erased.WriteTxn, identity: ListingIdentity) !void {
        const key = try listingNameKey(alloc, identity);
        defer alloc.free(key);
        var id: [8]u8 = undefined;
        std.mem.writeInt(u64, &id, identity.id, .little);
        try txn.put(key, &id);
    }
    fn putListingRange(alloc: std.mem.Allocator, txn: *antfly.storage_backend_erased.WriteTxn, row: antfly.metadata.RangeRecord) !void {
        const key = try listingRangeKey(alloc, row);
        defer alloc.free(key);
        var id: [8]u8 = undefined;
        std.mem.writeInt(u64, &id, row.group_id, .little);
        try txn.put(key, &id);
    }
    fn toggleListingIdentity(fingerprint: *[32]u8, identity: ListingIdentity) void {
        if (!identity.legacy) return;
        for (fingerprint, @import("../system_catalog/projection.zig").legacyIdentity(identity.id, identity.name)) |*byte, value| byte.* ^= value;
    }
    fn rebuildListingIndexesLocked(self: *LocalStandaloneMetadata, txn: *antfly.storage_backend_erased.WriteTxn) !void {
        var arena = std.heap.ArenaAllocator.init(self.alloc);
        defer arena.deinit();
        const a = arena.allocator();
        var keys: std.ArrayListUnmanaged([]const u8) = .empty;
        {
            var cursor = try txn.openCursor();
            defer cursor.close();
            var row = try cursor.seekAtOrAfter(listing_index_prefix);
            while (row) |kv| : (row = try cursor.next()) {
                if (!std.mem.startsWith(u8, kv.key, listing_index_prefix)) break;
                try keys.append(a, try a.dupe(u8, kv.key));
            }
        }
        for (keys.items) |key| try txn.delete(key);
        var fingerprint: [32]u8 = @splat(0);
        var tables = self.manager.tables.valueIterator();
        while (tables.next()) |table| {
            const identity = listingIdentity(table.*, self.system_catalog_state.?.index.byId(.table, table.table_id)).?;
            try putListingIdentity(a, txn, identity);
            toggleListingIdentity(&fingerprint, identity);
        }
        var ranges = self.manager.ranges.valueIterator();
        while (ranges.next()) |range| try putListingRange(a, txn, range.*);
        try txn.put(listing_membership_key, &fingerprint);
    }
    fn ensureListingIndexesLocked(self: *LocalStandaloneMetadata) !void {
        if (self.catalog_listing_indexes_initialized) return;
        const store = try self.durableCatalogStore();
        var txn = try store.beginWrite();
        var open = true;
        defer if (open) txn.abort();
        try self.rebuildListingIndexesLocked(&txn);
        txn.commit() catch {
            self.catalog_durability_failed = true;
            return error.MetadataMutationOutcomeUnknown;
        };
        open = false;
        if (self.catalog_store != null) store.sync(true) catch {
            self.catalog_durability_failed = true;
            return error.MetadataMutationOutcomeUnknown;
        };
        self.catalog_listing_indexes_initialized = true;
    }
    fn updateListingIndexesLocked(self: *LocalStandaloneMetadata, txn: *antfly.storage_backend_erased.WriteTxn, mutation: *const CatalogMutation) !void {
        if (!self.catalog_listing_indexes_initialized or !self.catalog_rows_initialized) return self.rebuildListingIndexesLocked(txn);
        var arena = std.heap.ArenaAllocator.init(self.alloc);
        defer arena.deinit();
        const a = arena.allocator();
        var affected: std.AutoHashMapUnmanaged(u64, void) = .empty;
        var tables = mutation.previous_tables.keyIterator();
        while (tables.next()) |id| try affected.put(a, id.*, {});
        if (mutation.catalog_change) |change| {
            for (change.previous.items) |row| if (row.kind == .table) {
                try affected.put(a, row.id, {});
            };
            for (change.inserted.items) |row| if (row.kind == .table) {
                try affected.put(a, row.id, {});
            };
        }
        const bytes = try txn.get(listing_membership_key);
        if (bytes.len != 32) return error.InvalidCatalogRecord;
        var fingerprint = bytes[0..32].*;
        var ids = affected.keyIterator();
        while (ids.next()) |id| {
            const current_table = self.manager.tables.get(id.*);
            const current_binding = self.system_catalog_state.?.index.byId(.table, id.*);
            const previous_table = if (mutation.previous_tables.get(id.*)) |old| old else current_table;
            var previous_binding = current_binding;
            if (mutation.catalog_change) |change| {
                for (change.inserted.items) |row| if (row.kind == .table and row.id == id.*) {
                    previous_binding = null;
                    break;
                };
                for (change.previous.items) |row| if (row.kind == .table and row.id == id.*) {
                    previous_binding = row;
                    break;
                };
            }
            const old = listingIdentity(previous_table, previous_binding);
            const next = listingIdentity(current_table, current_binding);
            if (old != null and next != null and old.?.namespace == next.?.namespace and old.?.legacy == next.?.legacy and std.mem.eql(u8, old.?.name, next.?.name)) continue;
            if (old) |identity| {
                try txn.delete(try listingNameKey(a, identity));
                toggleListingIdentity(&fingerprint, identity);
            }
            if (next) |identity| {
                try putListingIdentity(a, txn, identity);
                toggleListingIdentity(&fingerprint, identity);
            }
        }
        if (!std.mem.eql(u8, bytes, &fingerprint)) try txn.put(listing_membership_key, &fingerprint);
        var ranges = mutation.previous_ranges.iterator();
        while (ranges.next()) |entry| {
            const next = self.manager.ranges.get(entry.key_ptr.*);
            if (entry.value_ptr.*) |old| {
                if (next != null and next.?.table_id == old.table_id) continue;
                try txn.delete(try listingRangeKey(a, old));
            }
            if (next) |row| try putListingRange(a, txn, row);
        }
    }

    fn persistMutationLocked(self: *LocalStandaloneMetadata, mutation: *CatalogMutation) !void {
        const store = try self.durableCatalogStore();
        var txn = try store.beginWrite();
        var txn_open = true;
        defer if (txn_open) txn.abort();
        if (!self.catalog_rows_initialized) {
            // One atomic import, also used for legacy JSON/Lite checkpoints.
            // A crash before the head commits leaves the old format readable.
            var tables = self.manager.tables.valueIterator();
            while (tables.next()) |row| try putCatalogRow(self.alloc, &txn, .{ .table = row.* });
            var ranges = self.manager.ranges.valueIterator();
            while (ranges.next()) |row| try putCatalogRow(self.alloc, &txn, .{ .range = row.* });
            for (self.systemCatalogState().resources) |row| try putCatalogRow(self.alloc, &txn, .{ .resource = row });
        } else {
            var tables = mutation.previous_tables.iterator();
            while (tables.next()) |entry| {
                if (self.manager.tables.get(entry.key_ptr.*)) |row| try putCatalogRow(self.alloc, &txn, .{ .table = row }) else if (entry.value_ptr.*) |old| try removeCatalogRow(self.alloc, &txn, .{ .table = old });
            }
            var ranges = mutation.previous_ranges.iterator();
            while (ranges.next()) |entry| {
                if (self.manager.ranges.get(entry.key_ptr.*)) |row| try putCatalogRow(self.alloc, &txn, .{ .range = row }) else if (entry.value_ptr.*) |old| try removeCatalogRow(self.alloc, &txn, .{ .range = old });
            }
            if (mutation.catalog_change) |change| {
                for (change.previous.items) |old| try removeCatalogRow(self.alloc, &txn, .{ .resource = old });
                for (change.inserted.items) |row| try putCatalogRow(self.alloc, &txn, .{ .resource = row });
            }
        }
        if (!self.catalog_rows_initialized or mutation.previous_extensions != null) try putCatalogRow(self.alloc, &txn, .{ .extensions = .{
            .extension_packages = self.extension_catalog.packages.items,
            .installed_extensions = self.extension_catalog.installed.items,
            .extension_members = self.extension_catalog.members.items,
            .extension_dependencies = self.extension_catalog.dependencies.items,
        } });
        try self.updateListingIndexesLocked(&txn, mutation);
        const state = self.systemCatalogState();
        const head = try std.json.Stringify.valueAlloc(self.alloc, CatalogHead{ .epoch = self.epoch, .revision = state.revision, .next_id = state.next_id }, .{});
        defer self.alloc.free(head);
        try txn.put(catalog_head_key, head);
        // Once commit is attempted, an I/O failure may be ambiguous. Preserve
        // the prepared in-memory state and fail closed until restart instead
        // of pretending a possibly durable command was rolled back.
        txn.commit() catch {
            self.catalog_durability_failed = true;
            mutation.committed = true;
            return error.MetadataMutationOutcomeUnknown;
        };
        txn_open = false;
        mutation.committed = true;
        self.catalog_rows_initialized = true;
        self.catalog_listing_indexes_initialized = true;
        // The owned LSM was opened with fully durable WAL commits. Syncing
        // its WAL/index again would add two redundant fsyncs to every DDL.
        // Borrowed stores (including Lite) retain the explicit sync boundary.
        if (self.catalog_store == null) {
            std.debug.assert(self.owned_catalog_backend.?.backend.options.wal_sync_on_commit);
            return;
        }
        store.sync(true) catch {
            self.catalog_durability_failed = true;
            return error.MetadataMutationOutcomeUnknown;
        };
    }
};

fn verifyAdoptedLiteIdentity(
    alloc: std.mem.Allocator,
    backend: *antfly.lite.backend.Handle,
    namespace: []const u8,
    target_identity: antfly.db.DocIdentityNamespace,
) !void {
    if (!target_identity.eql(antfly.lite.connection.embeddedRootIdentity())) {
        return error.InvalidEmbeddedLiteIdentity;
    }
    var db_opts = antfly.db.OpenOptions{
        .open_mode = .writer_no_replay,
        .start_index_workers = false,
        .start_optional_runtimes = false,
        .ttl_cleanup = .{ .enabled = false },
        .identity_namespace = target_identity,
    };
    try backend.configureDbOpenOptionsForNamespace(&db_opts, namespace);
    var adopted_db = try antfly.db.DB.open(alloc, namespace, db_opts);
    defer adopted_db.close();
    if (!adopted_db.core.identity_namespace.eql(target_identity)) return error.InvalidEmbeddedLiteIdentity;
}

fn deriveStandaloneTableRecord(
    storage_engine: antfly.common.config.StorageEngine,
    table_name: []const u8,
    req: antfly.public_api.tables.CreateTableRequest,
) !antfly.metadata.TableRecord {
    var resolved_req = req;
    const replicated = if (req.replication_sources_json) |sources| !std.mem.eql(u8, sources, "[]") else false;
    resolved_req.storage = try antfly.common.table_storage.Settings.resolveStandaloneCreate(req.storage, req.num_shards orelse 1, replicated, storage_engine != .local);
    if (storage_engine == .lite and (req.num_shards orelse 1) != 1) {
        return error.InvalidCreateTableRequest;
    }
    var table = antfly.public_api.tables.deriveTableRecord(table_name, resolved_req);
    // A standalone process owns the only replica regardless of whether its
    // local persistence is directory-backed or Lite single-file storage.
    table.desired_replica_count = 1;
    return table;
}

fn localSchemaVersion(alloc: std.mem.Allocator, schema_json: []const u8) !u32 {
    if (schema_json.len == 0) return 0;
    var parsed = try std.json.parseFromSlice(std.json.Value, alloc, schema_json, .{});
    defer parsed.deinit();
    const object = switch (parsed.value) {
        .object => |object| object,
        else => return error.InvalidTableSchema,
    };
    const version_value = object.get("version") orelse return 0;
    return switch (version_value) {
        .integer => |value| blk: {
            if (value < 0) return error.InvalidTableSchema;
            break :blk std.math.cast(u32, value) orelse return error.InvalidTableSchema;
        },
        else => return error.InvalidTableSchema,
    };
}

fn dropFullTextIndexForVersion(
    alloc: std.mem.Allocator,
    indexes_json: []const u8,
    version: u32,
) ![]u8 {
    var parsed = try std.json.parseFromSlice(std.json.Value, alloc, indexes_json, .{});
    defer parsed.deinit();
    const object = switch (parsed.value) {
        .object => |*object| object,
        else => return error.InvalidTableIndexMetadata,
    };

    var versioned_name_buf: [64]u8 = undefined;
    const stale_name = if (version == 0)
        antfly.public_api.tables.default_full_text_index_name
    else
        try std.fmt.bufPrint(&versioned_name_buf, "full_text_index_v{d}", .{version});
    _ = object.swapRemove(stale_name);
    return try std.fmt.allocPrint(alloc, "{f}", .{std.json.fmt(parsed.value, .{})});
}

fn monotonicMs() u64 {
    return @intCast(@divTrunc(platform_time.monotonicNs(), std.time.ns_per_ms));
}

pub fn run(init: std.process.Init) !void {
    const alloc = init.gpa;

    var args = try std.process.Args.Iterator.initAllocator(init.minimal.args, alloc);
    defer args.deinit();

    const argv0 = args.next() orelse "antfly_standalone";
    return try runFromIterator(init, argv0, &args);
}

pub fn runFromIterator(
    init: std.process.Init,
    _: []const u8,
    args: *std.process.Args.Iterator,
) !void {
    const alloc = init.gpa;
    var cli = try parseCli(alloc, args);
    defer cli.deinit(alloc);
    if (cli.help) {
        printUsage();
        return;
    }

    var termination_signals = antfly.common.runtime_lifecycle.ProcessSignalScope.install();
    defer termination_signals.deinit();
    var supervisor = antfly.common.runtime_lifecycle.RuntimeSupervisor.init(30_000);
    defer supervisor.markStopped();
    var setup_io = std.Io.Threaded.init(alloc, .{});
    defer setup_io.deinit();

    var secret_store: antfly.common.secrets.FileStore = undefined;
    var secret_store_initialized = false;
    defer if (secret_store_initialized) secret_store.deinit();

    if (cli.secret_store_paths.items.len > 0) {
        secret_store = try initLayeredSecretStore(alloc, setup_io.io(), cli.secret_store_paths.items);
        secret_store_initialized = true;
    } else {
        const default_secret_store_path = try resolveDefaultSecretStorePathBeforeConfig(alloc, cli);
        defer alloc.free(default_secret_store_path);
        secret_store = try antfly.common.secrets.FileStore.initWithIo(alloc, setup_io.io(), default_secret_store_path);
        secret_store_initialized = true;
    }

    var loaded_config: ?antfly.common.config.Config = if (cli.config_path) |config_path|
        try antfly.common.config.loadFromPathWithSecretsForDeploymentWithIo(
            alloc,
            setup_io.io(),
            config_path,
            &secret_store,
            .standalone,
        )
    else
        null;
    defer if (loaded_config) |*cfg| cfg.deinit();
    if (loaded_config) |*cfg| try applyHAConfigDefaults(alloc, &cli, cfg);

    antfly.common.config.Config.validateServerTlsConfig(if (loaded_config) |*cfg| cfg.tls else null) catch |err| {
        std.log.err("standalone startup rejected configured tls: built-in server TLS is unsupported; terminate TLS at a trusted reverse proxy", .{});
        return err;
    };
    validateCorsConfig(configuredCors(if (loaded_config) |*cfg| cfg else null)) catch |err| {
        std.log.err("standalone startup rejected invalid cors configuration err={}", .{err});
        return err;
    };

    var remote_content_runtime: antfly.common.remote_content_runtime.Runtime = undefined;
    var remote_content_runtime_initialized = false;
    defer if (remote_content_runtime_initialized) remote_content_runtime.deinit();
    var remote_content_facade = antfly.common.config.Config.RemoteContentConfig{};
    const remote_content = if (cli.config_path) |config_path| blk: {
        remote_content_runtime = try antfly.common.remote_content_runtime.Runtime.initWithIo(
            alloc,
            setup_io.io(),
            config_path,
            &secret_store,
            .standalone,
        );
        remote_content_runtime_initialized = true;
        remote_content_runtime.attach(&remote_content_facade);
        break :blk &remote_content_facade;
    } else if (loaded_config) |*cfg|
        if (cfg.remote_content) |*configured| configured else null
    else
        null;

    const storage_engine = cli.storage_engine orelse if (loaded_config) |*cfg| cfg.storage.engine else .local;
    if (storage_engine == .object) return error.UnsupportedStandaloneStorageEngine;
    const lite_path = if (storage_engine == .lite)
        (cli.storage_path orelse if (loaded_config) |*cfg| cfg.storage.lite_path else null) orelse return error.MissingLiteStoragePath
    else
        null;
    const lite_fsync = cli.storage_fsync orelse if (loaded_config) |*cfg| cfg.storage.lite_fsync else true;
    try validateEffectiveStandaloneStorage(cli, storage_engine, lite_path, if (loaded_config) |*cfg| cfg else null);
    if (loaded_config) |*cfg| cfg.deployment_mode = .standalone;

    const data_dir = try resolveLocalBaseDir(alloc, cli, if (loaded_config) |*cfg| cfg else null);
    defer alloc.free(data_dir);
    if (storage_engine == .local) try antfly.common.data_format.ensureCompatible(alloc, setup_io.io(), data_dir);
    // Validate and freeze the HA role before any startup helper can mutate a
    // primary-local sidecar that is not part of the continuous HA WAL.
    try validateHARole(cli);
    const ha_role_requested = haPrimaryRequested(cli) or haStandbyRequested(cli);
    const ha_mutation_guard_enabled = haContinuousMutationGuardEnabled(cli);

    const resolved = try resolvePaths(alloc, cli, if (loaded_config) |*cfg| cfg else null);
    defer resolved.deinit(alloc);

    try ensureDirPath(setup_io.io(), resolved.replica_root_dir);
    try ensureParent(setup_io.io(), resolved.replica_catalog_path);
    try ensureParent(setup_io.io(), resolved.local_metadata_catalog_path);
    try ensureDirPath(setup_io.io(), resolved.snapshot_root_dir);
    try ensureParent(setup_io.io(), resolved.secret_store_path);
    try ensureDirPath(setup_io.io(), resolved.auth_store_root_dir);

    const auth_enabled = resolveAuthEnabled(cli, if (loaded_config) |*cfg| cfg else null);
    var storage_kernel_context = kernel_owner_client.Context{};
    defer if (control_only_storage_sources) storage_kernel_context.deinit();
    if (comptime control_only_storage_sources) {
        try storage_kernel_context.ensureWith(.{
            .storage_kind = if (lite_path != null) .lite else .directory,
            .no_sync = @intFromBool(!lite_fsync),
            .storage_path = .fromSlice(lite_path orelse ""),
            .auth_storage_path = .fromSlice(if (auth_enabled) resolved.auth_store_root_dir else ""),
        });
        const security_json = try antfly.common.config.remoteContentSecurityJsonAlloc(alloc, remote_content);
        defer alloc.free(security_json);
        try storage_kernel_context.configureRemoteContentSecurity(security_json);
    }

    var node_backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer node_backend_runtime.deinit();
    // The linked inference archive retains std.Io for its full node lifetime.
    // Keep the corresponding host lane lease until after node destruction.
    var inference_lane_lease = try node_backend_runtime.ptr().acquireInferenceLane();
    defer inference_lane_lease.release();
    const inference_io = inference_lane_lease.io();
    var lite_backend: ?LegacyLiteHandle = null;
    if (comptime !control_only_storage_sources) {
        if (lite_path) |path| lite_backend = try antfly.lite.backend.Handle.openOrCreate(
            alloc,
            path,
            .{ .no_sync = !lite_fsync },
        );
    }
    defer if (comptime !control_only_storage_sources) if (lite_backend) |*backend| backend.deinit();
    if (comptime !control_only_storage_sources) {
        if (lite_backend) |*backend| node_backend_runtime.ptr().db_open_configurator = backend.dbOpenConfigurator();
    }

    // Restore jobs are storage-engine state. Local standalone keeps them in a
    // dedicated LSM root; Lite keeps them in its single-file reserved
    // namespace. Supplying the engine store during API construction avoids a
    // legacy LMDB sidecar and makes production LSM-only startup self-contained.
    const restore_job_root = if (lite_backend == null)
        try std.fmt.allocPrint(alloc, "{s}/api-restore-jobs", .{resolved.replica_root_dir})
    else
        null;
    defer if (restore_job_root) |path| alloc.free(path);
    var restore_job_backend: ?antfly.lsm_backend.BackendHandle = if (restore_job_root) |path|
        try antfly.lsm_backend.BackendHandle.open(alloc, path, .{})
    else
        null;
    defer if (restore_job_backend) |*backend| backend.close();
    var local_restore_job_store: ?antfly.storage_backend_erased.Store = if (restore_job_backend) |*backend|
        try backend.backend.runtimeStore(alloc, .{ .name = "system/api-restore-jobs" })
    else
        null;
    defer if (local_restore_job_store) |*store| store.deinit();
    var restore_job_store: ?*antfly.storage_backend_erased.Store = null;
    if (comptime !control_only_storage_sources) {
        restore_job_store = if (lite_backend) |*backend|
            try backend.runtimeStoreForNamespace("system/api-restore-jobs")
        else
            &local_restore_job_store.?;
    }
    // Incoming reverse-route observations are an exact, fenced directory, not
    // disposable cache state: retain one latest generation per logical graph
    // key so restarts and L1 eviction do not reintroduce all-shard probes.
    const incoming_graph_route_root = if (lite_backend == null)
        try std.fmt.allocPrint(alloc, "{s}/incoming-graph-routes", .{resolved.replica_root_dir})
    else
        null;
    defer if (incoming_graph_route_root) |path| alloc.free(path);
    var incoming_graph_route_backend: ?antfly.lsm_backend.BackendHandle = if (incoming_graph_route_root) |path|
        try antfly.lsm_backend.BackendHandle.open(alloc, path, .{})
    else
        null;
    defer if (incoming_graph_route_backend) |*backend| backend.close();
    var local_incoming_graph_route_store: ?antfly.storage_backend_erased.Store = if (incoming_graph_route_backend) |*backend|
        try backend.backend.runtimeStore(alloc, .{ .name = "system/incoming-graph-routes" })
    else
        null;
    defer if (local_incoming_graph_route_store) |*store| store.deinit();
    const incoming_graph_route_store = if (comptime control_only_storage_sources)
        &local_incoming_graph_route_store.?
    else if (lite_backend) |*backend|
        try backend.runtimeStoreForNamespace("system/incoming-graph-routes")
    else
        &local_incoming_graph_route_store.?;
    var storage_maintenance = try antfly.storage_maintenance.Coordinator.init(
        alloc,
        if (comptime control_only_storage_sources)
            storage_kernel_context.maintenanceSource()
        else if (lite_backend) |*backend|
            backend.maintenanceSource()
        else
            antfly.storage_maintenance.localSource,
        node_backend_runtime.ptr(),
    );
    defer storage_maintenance.deinit();

    // Standalone always owns a local Antfly node. In production its heavy
    // implementation is code-generated in the inference archive and reached
    // through an opaque internal ABI; the shipped artifact remains one binary.
    const loaded_cfg = if (loaded_config) |*cfg| cfg else null;
    const process_memory_resolution = resolveProcessMemoryBudget(
        cli,
        init.environ_map,
    ) catch |err| {
        std.log.err("invalid process memory budget; expected a MiB value representable on this platform", .{});
        return err;
    };
    const process_memory_limit_bytes = process_memory_resolution.limit_bytes;
    const configured_preload = if (loaded_cfg) |cfg| cfg.inference.preload else &.{};
    const loaded_preload = if (cli.inference_preload_models.items.len == 0 and configured_preload.len != 0) blk: {
        const out = try alloc.alloc(inference_bridge.WarmModel, configured_preload.len);
        for (configured_preload, 0..) |model, i| {
            out[i] = .{
                .kind = inference_bridge.String.init(model.kind),
                .name = inference_bridge.String.init(model.name),
                .backend = inference_bridge.OptionalString.init(model.backend),
                .format = inference_bridge.OptionalString.init(model.format),
                .quantization = inference_bridge.OptionalString.init(model.quantization),
                .residency_mode = switch (model.residency_mode orelse .auto) {
                    .auto => .auto,
                    .resident => .resident,
                    .streamed => .streamed,
                },
                .memory_budget_mb = model.memory_budget_mb orelse 0,
            };
        }
        break :blk out;
    } else &.{};
    defer if (loaded_preload.len != 0) alloc.free(loaded_preload);
    const active_preload = if (cli.inference_preload_models.items.len != 0)
        cli.inference_preload_models.items
    else
        loaded_preload;
    const content_security_json = if (loaded_cfg) |cfg|
        if (cfg.effectiveAntflyContentSecurity()) |security|
            try std.json.Stringify.valueAlloc(alloc, security.*, .{})
        else
            null
    else
        null;
    defer if (content_security_json) |json| alloc.free(json);
    const s3_credentials_json = if (loaded_cfg) |cfg|
        if (cfg.inference.s3_credentials) |credentials|
            try std.json.Stringify.valueAlloc(alloc, credentials, .{})
        else
            null
    else
        null;
    defer if (s3_credentials_json) |json| alloc.free(json);

    const configured_inference: antfly.common.config.Config.InferenceConfig =
        if (loaded_cfg) |cfg| cfg.inference else .{};
    // An explicit inference endpoint is a process-isolation contract, not a
    // fallback hint. In this mode standalone must not retain hidden in-process
    // routes or provider callbacks that can route a wedged native/GPU kernel
    // back into the database process.
    const embedded_inference_enabled = configured_inference.api_url == null;
    const effective_kernel_jit_mode = try resolveKernelJitMode(
        configured_inference.kernel_jit.mode,
        platform.env.getenv("ANTFLY_INFERENCE_KERNEL_JIT_MODE"),
        cli.inference_kernel_jit_mode,
    );
    var worker_environment: std.ArrayList(@typeInfo(@FieldType(InferenceRuntimeConfigWire, "worker_environment")).pointer.child) = .empty;
    defer worker_environment.deinit(alloc);
    var environment_iterator = init.environ_map.iterator();
    while (environment_iterator.next()) |entry| {
        try worker_environment.append(alloc, .{ .name = entry.key_ptr.*, .value = entry.value_ptr.* });
    }
    const inference_runtime_config_json = try std.json.Stringify.valueAlloc(alloc, InferenceRuntimeConfigWire{
        .embedded_enabled = embedded_inference_enabled,
        .worker_environment = worker_environment.items,
        .max_concurrent_requests = resolveInferenceMaxConcurrentRequests(loaded_cfg),
        .kernel_jit = .{
            .mode = effective_kernel_jit_mode,
            .cache_dir = configured_inference.kernel_jit.cache_dir,
            .max_cache_bytes_mb = configured_inference.kernel_jit.max_cache_bytes_mb,
            .preload_budget_ms = configured_inference.kernel_jit.preload_budget_ms,
        },
        .prompt_cache = .{
            .enabled = configured_inference.prompt_cache.enabled,
            .mode = configured_inference.prompt_cache.mode,
            .max_bytes_mb = configured_inference.prompt_cache.max_bytes_mb,
            .min_tokens = configured_inference.prompt_cache.min_tokens,
            .ttl_ms = configured_inference.prompt_cache.ttl_ms,
        },
    }, .{});
    defer alloc.free(inference_runtime_config_json);

    var handle: ?*anyopaque = null;
    const inference_create_context = inference_bridge.CreateContext{
        .abi_version = inference_bridge.abi_version,
        .data_dir_ptr = data_dir.ptr,
        .data_dir_len = data_dir.len,
        .models_dir = inference_bridge.OptionalString.init(cli.inference_models_dir orelse if (loaded_cfg) |cfg| cfg.inference.models_dir else null),
        .ml_dir = inference_bridge.OptionalString.init(cli.inference_ml_dir orelse if (loaded_cfg) |cfg| cfg.inference.ml_dir else null),
        .host_limit_bytes = try mibToBytes(cli.inference_host_budget_mb),
        .backend_limit_bytes = try mibToBytes(cli.inference_backend_budget_mb),
        .combined_limit_bytes = try mibToBytes(cli.inference_combined_budget_mb),
        .kv_limit_bytes = try mibToBytes(cli.inference_kv_budget_mb),
        .scratch_limit_bytes = try mibToBytes(cli.inference_scratch_budget_mb),
        .process_memory_limit_bytes = process_memory_limit_bytes,
        .process_memory_limit_provenance = inferenceMemoryLimitProvenance(
            process_memory_resolution.effective_source,
        ),
        .preload_ptr = if (!embedded_inference_enabled or active_preload.len == 0) null else active_preload.ptr,
        .preload_len = if (embedded_inference_enabled) active_preload.len else 0,
        .keep_alive = inference_bridge.OptionalString.init(if (loaded_cfg) |cfg| cfg.inference.keep_alive else null),
        .max_loaded_models = if (loaded_cfg) |cfg| cfg.inference.max_loaded_models orelse 0 else 0,
        .has_max_loaded_models = if (loaded_cfg) |cfg| @intFromBool(cfg.inference.max_loaded_models != null) else 0,
        .content_security_json = inference_bridge.OptionalString.init(content_security_json),
        .s3_credentials_json = inference_bridge.OptionalString.init(s3_credentials_json),
        .runtime_config_json = inference_bridge.String.init(inference_runtime_config_json),
        .executor = .init(&inference_io),
        .out_handle = &handle,
    };
    const antfly_node = if (comptime inline_inference_codegen) blk: {
        break :blk try inference_host.linkedInferenceCreate(&inference_create_context);
    } else blk: {
        const inference_api = try linkedInferenceApi(
            inference_bridge.Capability.provider |
                inference_bridge.Capability.route_manifest |
                inference_bridge.Capability.resource_budget |
                inference_bridge.Capability.request_admission,
        );
        const status = inference_api.create(&inference_create_context);
        if (!status.isOk()) return inference_bridge.errorFromStatus(status);
        break :blk handle orelse return error.InferenceRuntimeStartupFailed;
    };
    var local_inference_connection_context = LocalInferenceConnectionContext{
        .handle = antfly_node,
    };
    var embedded_provider_lifetime = EmbeddedInferenceProviderLifetime{
        .handle = antfly_node,
    };
    // Until DataServer exists, error cleanup is owned here. Once its
    // ResourceManager is attached below, the regular defer is registered
    // after DataServer's so tokenizer budget callbacks are torn down first.
    var antfly_node_needs_errdeinit = true;
    errdefer if (antfly_node_needs_errdeinit) {
        if (comptime inline_inference_codegen)
            inference_host.linkedInferenceDestroy(antfly_node)
        else
            linkedInferenceApiInfallible().destroy(antfly_node);
    };
    // Attach before opening any context-backed auth/system stores. Those
    // handles intentionally retain the storage context for their lifetime;
    // attaching afterward is rejected as a live-owner configuration mutation.
    if (comptime control_only_storage_sources)
        try storage_kernel_context.attachInferenceProvider(antfly_node);

    var active_audio_runtime = try antfly.common.audio_runtime.ActiveRuntime.init(
        alloc,
        setup_io.io(),
        if (loaded_config) |*cfg| cfg else null,
    );
    defer active_audio_runtime.deinit();

    const internal_service_secret = try secret_store.getOwned(alloc, internal_service_secret_key);
    defer if (internal_service_secret) |value| alloc.free(value);
    const internal_service_issuer = try secret_store.getOwned(alloc, internal_service_issuer_key);
    defer if (internal_service_issuer) |value| alloc.free(value);
    if (internal_service_secret != null or internal_service_issuer != null) {
        internal_service_auth.validateRuntimeConfig(
            internal_service_secret,
            null,
            internal_service_issuer,
        ) catch |err| {
            std.log.err(
                "standalone internal service credential is incomplete or invalid: configure {s} with at least {d} bytes and a printable {s}; err={s}",
                .{ internal_service_secret_key, internal_service_auth.minimum_secret_bytes, internal_service_issuer_key, @errorName(err) },
            );
            return err;
        };
    }

    var auth_backend: ?LegacyAuthBackend = null;
    var auth_runtime: ?antfly.storage_backend_erased.NamespaceStore = null;
    var kernel_auth_users_store: ?antfly.storage_backend_erased.Store = null;
    var kernel_auth_casbin_store: ?antfly.storage_backend_erased.Store = null;
    var kernel_auth_users_runtime: ?antfly.storage_backend_erased.NamespaceStore = null;
    var kernel_auth_casbin_runtime: ?antfly.storage_backend_erased.NamespaceStore = null;
    var auth_user_store: ?antfly.usermgr.StorageUserStore = null;
    var auth_casbin_store: ?antfly.usermgr.StorageCasbinAdapter = null;
    var user_manager: ?antfly.usermgr.UserManager = null;
    if (auth_enabled) {
        if (comptime control_only_storage_sources) {
            kernel_auth_users_store = try storage_kernel_context.systemStore(alloc, "system/auth-users");
            errdefer kernel_auth_users_store.?.deinit();
            kernel_auth_casbin_store = try storage_kernel_context.systemStore(alloc, "system/auth-casbin");
            errdefer kernel_auth_casbin_store.?.deinit();
            kernel_auth_users_runtime = try kernel_owner_client.singleNamespaceStore(alloc, &kernel_auth_users_store.?, "usermgr_users");
            errdefer kernel_auth_users_runtime.?.deinit();
            kernel_auth_casbin_runtime = try kernel_owner_client.singleNamespaceStore(alloc, &kernel_auth_casbin_store.?, "usermgr_casbin");
            errdefer kernel_auth_casbin_runtime.?.deinit();
            auth_user_store = antfly.usermgr.StorageUserStore.init(alloc, kernel_auth_users_runtime.?);
            auth_casbin_store = antfly.usermgr.StorageCasbinAdapter.init(alloc, kernel_auth_casbin_runtime.?);
        } else {
            auth_backend = try antfly.lsm_backend.BackendHandle.open(alloc, resolved.auth_store_root_dir, .{});
            errdefer if (auth_backend) |*backend| backend.close();
            auth_runtime = try auth_backend.?.backend.runtimeNamespaceStore(alloc);
            errdefer if (auth_runtime) |*runtime| runtime.deinit();
            auth_user_store = antfly.usermgr.StorageUserStore.init(alloc, auth_runtime.?);
            auth_casbin_store = antfly.usermgr.StorageCasbinAdapter.init(alloc, auth_runtime.?);
        }
        user_manager = try antfly.usermgr.UserManager.initWithIo(
            alloc,
            setup_io.io(),
            auth_user_store.?.iface(),
            try antfly.usermgr.initDefaultEnforcer(alloc, auth_casbin_store.?.iface()),
        );
        errdefer if (user_manager) |*manager| manager.deinit();
        if (ha_role_requested) {
            // Auth is carried by the portable seed, not the continuous HA WAL.
            // Creating a local default admin on either HA role after seeding
            // would acknowledge credentials that disappear on promotion.
            var seeded_admin = user_manager.?.getUser("admin") catch |err| switch (err) {
                error.UserNotFound => return error.HAAuthSeedMissing,
                else => return err,
            };
            seeded_admin.deinit(alloc);
        } else {
            // This seeds only the local auth store and must remain auth-gated.
            // Raft-backed metadata writes during metadata bootstrap can block
            // clustered startup before raft listeners are running.
            try antfly.usermgr.ensureDefaultAdminUser(&user_manager.?);
        }
    }
    defer if (user_manager) |*manager| manager.deinit();
    defer if (auth_runtime) |*runtime| runtime.deinit();
    defer if (comptime !control_only_storage_sources) if (auth_backend) |*backend| backend.close();
    defer if (kernel_auth_users_store) |*store| store.deinit();
    defer if (kernel_auth_casbin_store) |*store| store.deinit();
    defer if (kernel_auth_users_runtime) |*runtime| runtime.deinit();
    defer if (kernel_auth_casbin_runtime) |*runtime| runtime.deinit();

    const public_listener = resolvePublicListener(cli);
    const local_node_id = cli.local_node_id orelse 1;
    const public_api_url = try std.fmt.allocPrint(
        alloc,
        "http://{s}:{d}",
        .{ public_listener.bind_host, public_listener.bind_port },
    );
    defer alloc.free(public_api_url);

    var kernel_catalog_store: ?antfly.storage_backend_erased.Store = null;
    if (comptime control_only_storage_sources) {
        if (lite_path != null) kernel_catalog_store = try storage_kernel_context.systemStore(alloc, "system/metadata");
    }
    defer if (kernel_catalog_store) |*store| store.deinit();

    var local_metadata = LocalStandaloneMetadata.init(
        alloc,
        local_node_id,
        1,
        public_api_url,
        resolved.replica_root_dir,
        resolved.local_metadata_catalog_path,
        node_backend_runtime.ptr(),
        if (comptime control_only_storage_sources)
            if (kernel_catalog_store) |*store| store else null
        else if (lite_backend) |*backend|
            try backend.runtimeStoreForNamespace("system/metadata")
        else
            null,
        storage_engine,
    ) catch |err| {
        std.log.err("standalone startup failed step=local_metadata_init err={}", .{err});
        return err;
    };
    defer local_metadata.deinit();
    // An empty instance has a real catalog too. Publish it before readiness so
    // stopped-volume inspection can distinguish empty state from missing state.
    if (local_metadata.manager.tables.count() == 0) {
        var mutation = try local_metadata.beginCatalogMutationLocked();
        defer mutation.deinit(&local_metadata);
        try mutation.commit(&local_metadata);
    }
    local_metadata.vector_source_storage_allowed = !ha_role_requested;
    // Reject persisted experimental tables before HA can snapshot or mirror
    // primary roots whose references need a separate source-store lifecycle.
    if (ha_role_requested) {
        var tables = local_metadata.manager.tables.valueIterator();
        while (tables.next()) |table| {
            if (table.storage.dense_embeddings == .vector_store)
                return error.VectorStoreRequiresLocalSingleShardTable;
        }
    }
    if (lite_path != null) {
        if (comptime control_only_storage_sources) {
            try local_metadata.adoptEmbeddedLiteRootFromKernelIfNeeded(&storage_kernel_context);
            try storage_kernel_context.liteMarkStandalone();
        } else if (lite_backend) |*backend| {
            try local_metadata.adoptEmbeddedLiteRootIfNeeded(backend);
            try backend.markStandaloneArtifact();
        }
    }
    // API transaction sessions are engine state, not a sidecar. Keeping them
    // in a reserved Lite namespace makes a copied/reopened .aflite file a
    // complete database and preserves staged multi-request transactions.
    var kernel_session_backend: ?antfly.storage_backend_erased.Store = null;
    var kernel_restore_job_backend: ?antfly.storage_backend_erased.Store = null;
    if (comptime control_only_storage_sources) {
        if (lite_path != null) {
            kernel_session_backend = try storage_kernel_context.systemStore(alloc, "system/api-transaction-sessions");
            kernel_restore_job_backend = try storage_kernel_context.systemStore(alloc, "system/api-restore-jobs");
        }
    }
    defer if (kernel_session_backend) |*store| store.deinit();
    defer if (kernel_restore_job_backend) |*store| store.deinit();
    if (comptime control_only_storage_sources) {
        restore_job_store = if (kernel_restore_job_backend) |*store|
            store
        else if (local_restore_job_store) |*store|
            store
        else
            null;
    }
    const session_root = if (lite_path == null) try std.fmt.allocPrint(alloc, "{s}/api-transaction-sessions", .{resolved.replica_root_dir}) else null;
    defer if (session_root) |path| alloc.free(path);
    var session_backend: ?antfly.lsm_backend.BackendHandle = if (session_root) |path| try antfly.lsm_backend.BackendHandle.open(alloc, path, .{}) else null;
    defer if (session_backend) |*backend| backend.close();
    var native_session_store: ?antfly.storage_backend_erased.Store = if (session_backend) |*backend| try backend.backend.runtimeStore(alloc, .{ .name = "system/api-transaction-sessions" }) else null;
    defer if (native_session_store) |*store| store.deinit();
    var native_sessions = if (native_session_store) |*store| antfly.public_api.transactions.DurableSessionStore.initRuntime(alloc, store) else null;
    var lite_session_store = if (comptime control_only_storage_sources)
        if (kernel_session_backend) |*store|
            antfly.public_api.transactions.DurableSessionStore.initRuntime(alloc, store)
        else
            null
    else if (lite_backend) |*backend|
        antfly.public_api.transactions.DurableSessionStore.initRuntime(
            alloc,
            try backend.runtimeStoreForNamespace("system/api-transaction-sessions"),
        )
    else
        null;
    const synced_extension_packages = if (ha_role_requested)
        0
    else
        local_metadata.syncExtensionPackageStore(setup_io.io(), resolved.extension_package_store_dir) catch |err| {
            std.log.err("standalone startup failed step=sync_extension_packages err={}", .{err});
            return err;
        };
    if (synced_extension_packages > 0) {
        std.log.info("standalone synced extension package store path={s} packages={d}", .{ resolved.extension_package_store_dir, synced_extension_packages });
    }

    try validateHAPathsUnderRoot(cli, data_dir);
    const ha_startup_expectation = try haStartupExpectationFromCli(cli);
    const ha_startup_checkpoint_lsn = if (ha_startup_expectation) |expectation| blk: {
        if (comptime control_only_storage_sources) {
            const request_json = try std.json.Stringify.valueAlloc(alloc, expectation, .{});
            defer alloc.free(request_json);
            break :blk kernel_owner_client.haSeedValidateActivatedGeneration(request_json) catch |err| {
                std.log.err("standalone startup failed step=validate_ha_active_generation err={}", .{err});
                return err;
            };
        }
        break :blk antfly.hot_standby.seed_activation.validateActivatedGeneration(alloc, expectation) catch |err| {
            std.log.err("standalone startup failed step=validate_ha_active_generation err={}", .{err});
            return err;
        };
    } else null;
    // A reseed may rotate a persisted Lease fence only after the complete,
    // immutable activation chain on this exact target volume has validated.
    // A generic checkpoint or caller-selected startup generation never reaches
    // this receipt writer.
    if (ha_startup_checkpoint_lsn) |checkpoint_lsn| {
        if (ha_startup_expectation) |expectation| {
            if (init.environ_map.get("ANTFLY_HA_LEASE_SENTINEL_PATH")) |sentinel_path| {
                if (init.environ_map.get("ANTFLY_HA_LEASE_TOPOLOGY_ID")) |topology_id| {
                    if (!std.mem.eql(u8, topology_id, expectation.binding.topology_id)) return error.HALeaseSentinelScopeMismatch;
                    const existing = try antfly.hot_standby.kubernetes_lease_watchdog.loadValidatedRepairGenerationAlloc(
                        alloc,
                        setup_io.io(),
                        sentinel_path,
                        topology_id,
                        expectation.binding.node_id,
                    );
                    defer if (existing) |generation| alloc.free(generation);
                    if (existing == null and try antfly.hot_standby.kubernetes_lease_watchdog.sentinelExists(setup_io.io(), sentinel_path)) {
                        _ = try antfly.hot_standby.kubernetes_lease_watchdog.persistRepairReceipt(
                            alloc,
                            setup_io.io(),
                            sentinel_path,
                            topology_id,
                            expectation.binding.node_id,
                            expectation.expected.identity.timeline_id,
                            expectation.expected.identity.epoch,
                            checkpoint_lsn,
                            expectation.materialized_receipt_sha256.?,
                        );
                    }
                }
            }
        }
    }
    try migrateHALegacyLayoutFromCli(alloc, setup_io.io(), cli);
    var ha_sync_policy = try haSyncPolicyFromCli(alloc, cli);
    defer ha_sync_policy.deinit(alloc);
    const ha_retention_policy = try haRetentionPolicyFromCli(cli);
    var ha_primary = openHAPrimaryFromCli(alloc, setup_io.io(), cli) catch |err| {
        std.log.err("standalone startup failed step=open_ha_primary err={}", .{err});
        return err;
    };
    defer if (ha_primary) |*primary| primary.close();
    if (ha_primary) |*primary| try local_metadata.replayHACatalog(primary);
    var ha_standby = openHAStandbyFromCli(alloc, setup_io.io(), cli) catch |err| {
        std.log.err("standalone startup failed step=open_ha_standby err={}", .{err});
        return err;
    };
    defer if (ha_standby) |*standby| standby.close();
    if (ha_standby) |*standby| {
        if (ha_startup_checkpoint_lsn) |checkpoint_lsn| {
            const expectation = ha_startup_expectation orelse unreachable;
            bootstrapHAStandbyAtActivatedCheckpoint(
                alloc,
                standby,
                expectation.expected.generation,
                expectation.expected.slot_name,
                checkpoint_lsn,
            ) catch |err| {
                std.log.err("standalone startup failed step=bootstrap_ha_standby_checkpoint err={}", .{err});
                return err;
            };
        }
    }
    var ha_fence_store = openHAFenceStoreFromCli(alloc, setup_io.io(), cli) catch |err| {
        std.log.err("standalone startup failed step=open_ha_fence err={}", .{err});
        return err;
    };
    defer if (ha_fence_store) |*store| store.close();
    var ha_former_primary_log = openHAFormerPrimaryLogFromCli(alloc, setup_io.io(), cli) catch |err| {
        std.log.err("standalone startup failed step=open_ha_former_primary err={}", .{err});
        return err;
    };
    defer if (ha_former_primary_log) |*log| log.close();
    const admin_bearer_token = try resolveAdminBearerTokenFromCli(alloc, cli);
    defer if (admin_bearer_token) |token| alloc.free(token);
    const ha_pod_uid = try resolveHAPodUID(alloc);
    defer if (ha_pod_uid) |pod_uid| alloc.free(pod_uid);
    var ha_lease_watchdog = try RuntimeLeaseWatchdog.initFromEnv(
        alloc,
        setup_io.io(),
        init.environ_map,
        cli,
        ha_pod_uid,
    );
    if (ha_lease_watchdog) |*watchdog| watchdog.bindOwnedProcessBootID();
    defer if (ha_lease_watchdog) |*watchdog| watchdog.deinit(alloc);

    // Initialize DataServer without starting its listener — the unified
    // httpx.Server will serve the public API instead.
    var data_server = antfly.data.runtime.DataServer.initFromLocalMetadataSources(alloc, .{
        .bind_host = public_listener.bind_host,
        .bind_port = public_listener.bind_port,
        .enable_data_raft = false,
        .replica_root_dir = resolved.replica_root_dir,
        .replica_catalog_path = resolved.replica_catalog_path,
        .snapshot_root_dir = resolved.snapshot_root_dir,
        .storage_kernel_context_handle = if (control_only_storage_sources) storage_kernel_context.handle else null,
        .process_memory_limit_bytes = process_memory_limit_bytes,
        .process_memory_limit_source = storageMemoryLimitSource(process_memory_resolution.effective_source),
        .store_registration = .{
            .node_id = local_node_id,
            .store_id = 1,
            .api_url = public_api_url,
            .role = "data",
        },
        .api_server_cfg = .{
            .ha_failover_safe_mutations_only = ha_mutation_guard_enabled,
            .ha_remote_apply_mutations_enabled = haRemoteApplyMutationsEnabled(ha_sync_policy.policy),
            .ha_catalog_create_enabled = ha_role_requested and cli.ha_table_id == 0 and cli.ha_shard_id == 0,
            .auth_enabled = auth_enabled,
            .experimental = cli.experimental,
            .mcp_max_tool_result_bytes = if (loaded_config) |*cfg| cfg.mcp.max_tool_result_bytes else antfly.common.config.default_mcp_max_tool_result_bytes,
            .query_max_concurrent_requests = if (loaded_config) |*cfg| cfg.admission.query.max_concurrent_requests else antfly.common.config.default_query_max_concurrent_requests,
            .graph_execution_limits = if (loaded_config) |*cfg| cfg.graph_execution else .{},
            .write_max_concurrent_requests = if (loaded_config) |*cfg| cfg.admission.write.max_concurrent_requests else antfly.common.config.default_write_max_concurrent_requests,
            .inference_max_concurrent_requests = if (loaded_config) |*cfg| cfg.admission.inference.max_concurrent_requests else antfly.common.config.default_inference_max_concurrent_requests,
            .backup_operation_timeout_ms = if (loaded_config) |*cfg| cfg.backup.operation_timeout_ms else antfly.common.config.default_backup_operation_timeout_ms,
            .inference_request_admission_source = if (embedded_inference_enabled) .{
                .ptr = antfly_node,
                .try_acquire_fn = tryAcquireEmbeddedInferenceRequest,
                .release_fn = releaseEmbeddedInferenceRequest,
                .stats_fn = embeddedInferenceRequestStats,
            } else null,
            .local_inference_connection_target = if (embedded_inference_enabled) .{
                .capabilities = inference_connection_abi.Capability.streaming_response,
                .context = &local_inference_connection_context,
                .invoke = invokeLocalInferenceConnection,
            } else null,
            .ard_base_url = cli.ard_base_url,
            .ard_publisher_domain = cli.ard_publisher_domain orelse "antfly.local",
            .ard_display_name = cli.ard_display_name orelse "Antfly",
            .ard_public_catalog_enabled = cli.ard_public_catalog_enabled,
            .internal_service_secret = internal_service_secret,
            .internal_service_issuer = internal_service_issuer,
            .deployment_mode = .standalone,
            .storage_maintenance = &storage_maintenance,
            .admin_bearer_token = admin_bearer_token,
            .secret_store = &secret_store,
            .remote_content = remote_content,
            .inference_api_key = if (loaded_config) |*cfg| if (cfg.inference.api_key) |value| value else null else null,
            .extension_package_store_dir = resolved.extension_package_store_dir,
            .node_config = if (loaded_config) |*cfg| cfg else null,
            .user_manager = if (user_manager) |*manager| manager else null,
            .session_store = if (lite_session_store) |*store| store else if (native_sessions) |*store| store else null,
            .restore_job_store = restore_job_store,
            .incoming_graph_route_store = incoming_graph_route_store,
            .session_ttl_ns = if (loaded_config) |*cfg| cfg.transaction_sessions.ttl_seconds * std.time.ns_per_s else standalone_session_ttl_ns,
            .session_cleanup_interval_ns = if (loaded_config) |*cfg| cfg.transaction_sessions.cleanup_interval_seconds * std.time.ns_per_s else standalone_session_cleanup_interval_ns,
            .session_max_count = if (loaded_config) |*cfg| cfg.transaction_sessions.max_count else standalone_session_max_count,
            .session_max_record_bytes = if (loaded_config) |*cfg| cfg.transaction_sessions.max_record_bytes else standalone_session_max_record_bytes,
            .session_savepoint_limit = if (loaded_config) |*cfg| cfg.transaction_sessions.max_savepoints else standalone_session_savepoint_limit,
        },
        .ha = if (ha_primary != null or ha_standby != null or ha_fence_store != null or ha_former_primary_log != null) .{
            .admin_context = .{
                .primary = if (ha_primary) |*primary| primary else null,
                .primary_node_id = cli.ha_primary_node_id,
                .standby = if (ha_standby) |*standby| standby else null,
                .standby_node_id = cli.ha_standby_node_id,
                .fence_store = if (ha_fence_store) |*store| store else null,
                .former_primary_log = if (ha_former_primary_log) |*log| log else null,
            },
            .standby_owner = if (ha_standby != null) &ha_standby else null,
            .admin_bearer_token = admin_bearer_token,
            .seed_capture_root = cli.ha_seed_capture_root,
            .seed_activation_root = cli.ha_startup_target_root,
            .pod_uid = ha_pod_uid,
            .lease_watchdog_proof = if (ha_lease_watchdog) |*watchdog| watchdog.proofSource() else null,
            .repair_receipt = if (ha_lease_watchdog) |*watchdog| watchdog.repairReceiptSink() else null,
            .internal_primary = if (ha_primary) |*primary| primary else null,
            .primary_retention_policy = ha_retention_policy,
            .primary_sync_policy = ha_sync_policy.policy,
            .standby_replication = try haStandbyReplicationConfigFromCliWithBearerToken(cli, admin_bearer_token),
        } else .{},
        .backend_runtime = node_backend_runtime.ptr(),
    }, local_metadata.catalogSource(), local_metadata.statusSource());
    // A non-HA standalone process is the complete set of readers and writers
    // for its local generations; there is no older peer whose storage
    // capability must be negotiated. Provisioned storage defaults closed for
    // distributed startup, but LocalStandaloneMetadata has no remote store
    // reporter that could ever open that gate. Authorize the current native
    // format before the public listener becomes reachable so a freshly
    // created dense index is v2 from its first catalog publication. HA roles
    // remain closed until their replication protocol has an equivalent
    // all-peer capability fence.
    if (standaloneNativeAuthorityInitiallyPermitted(cli)) {
        data_server.provisioned_storage.setDenseNativeAuthorityPermitted(true);
    }
    defer data_server.deinitWithDeadline(supervisor.deadline());
    const managed_memory = data_server.provisioned_storage.resource_manager.snapshot().memory;
    std.log.info(
        "process memory policy operator_source={s} effective_source={s} configured_limit_bytes={d} effective_limit_bytes={d} managed_hard_limit_bytes={d}",
        .{
            @tagName(process_memory_resolution.source),
            @tagName(process_memory_resolution.effective_source),
            process_memory_resolution.configured_limit_bytes,
            data_server.provisioned_storage.effective_memory_limit_bytes,
            managed_memory.hard_limit_bytes,
        },
    );
    var inference_resource_owner = InferenceResourceBudgetOwner{
        .alloc = alloc,
        .manager = &data_server.provisioned_storage.resource_manager,
    };
    // This defer is registered before node teardown, so the node releases all
    // opaque admission leases before the owner validates its registry.
    defer inference_resource_owner.deinit();
    antfly_node_needs_errdeinit = false;
    defer {
        // DataServer sources, recovery workers, and durable API jobs retain the
        // embedded provider. Drain them while the node is valid, then release
        // tokenizer reservations while DataServer's ResourceManager is valid.
        // The earlier data_server.deinit defer performs final storage teardown.
        data_server.quiesceExternalProviderUsersWithDeadline(supervisor.deadline()) catch |err| {
            std.log.err("standalone provider shutdown barrier failed err={s}", .{@errorName(err)});
            @panic("standalone provider shutdown barrier failed");
        };
        embedded_provider_lifetime.quiesce();
        if (comptime inline_inference_codegen)
            inference_host.linkedInferenceDestroy(antfly_node)
        else
            linkedInferenceApiInfallible().destroy(antfly_node);
    }

    // Health, metrics, and watchdog supervision share the isolated control
    // lane but own and join their individual futures before releasing it.
    var control_lane_lease = try node_backend_runtime.ptr().acquireControlLane();
    defer control_lane_lease.release();
    const control_io = control_lane_lease.io();

    if (ha_lease_watchdog) |*watchdog| {
        data_server.ha_public_gate_state.requireExternalAuthority();
        if (watchdog.watchdog.latched) {
            data_server.ha_public_gate_state.publishPrimaryFence(true);
        } else {
            // The public listener is not created until this bounded first
            // authority attempt has completed. Failure leaves the primary
            // gate closed and is retried from the main runtime loop.
            try watchdog.poll(alloc, &data_server);
        }
    }
    var ha_watchdog_stop = std.atomic.Value(bool).init(false);
    var ha_watchdog_failed = std.atomic.Value(bool).init(false);
    var ha_watchdog_future = if (ha_lease_watchdog) |*watchdog|
        try control_io.concurrent(RuntimeLeaseWatchdog.runIndependent, .{
            watchdog,
            alloc,
            control_io,
            &data_server,
            &ha_watchdog_stop,
            &ha_watchdog_failed,
        })
    else
        null;
    defer if (ha_watchdog_future) |*future| {
        ha_watchdog_stop.store(true, .release);
        _ = future.await(control_io);
    };

    var inference_resource_budget = inference_bridge.ResourceBudget{
        .abi_version = inference_bridge.abi_version,
        .context = &inference_resource_owner,
        .retain_context = retainInferenceResourceOwner,
        .release_context = releaseInferenceResourceOwner,
        .reserve_admission = reserveInferenceResources,
        .retain_admission = retainInferenceResources,
        .release_admission = releaseInferenceResources,
        .observe_prompt_cache = observeInferencePromptCache,
        .observe_tokenizer_cache = observeInferenceTokenizerCache,
    };
    const configure_context = inference_bridge.ConfigureContext{
        .abi_version = inference_bridge.abi_version,
        .handle = antfly_node,
        .resource_budget = &inference_resource_budget,
    };
    if (comptime inline_inference_codegen) {
        try inference_host.linkedInferenceConfigure(&configure_context);
    } else {
        const configure_status = (try linkedInferenceApi(
            inference_bridge.Capability.resource_budget,
        )).configure(&configure_context);
        if (!configure_status.isOk()) return inference_bridge.errorFromStatus(configure_status);
    }
    data_server.setAntflyProvider(if (embedded_inference_enabled)
        inferenceBoundaryProvider(&embedded_provider_lifetime)
    else
        null);

    // Initialize API server (wires caches + sources) without binding a listener.
    if (ha_role_requested and cli.ha_table_id == 0 and cli.ha_shard_id == 0) {
        local_metadata.ha_catalog_server = &data_server;
        data_server.ha_catalog_apply_ctx = &local_metadata;
        data_server.ha_catalog_apply_fn = LocalStandaloneMetadata.applyHACatalogCreate;
    }
    try data_server.initApiServer();
    local_metadata.local_schema_progress_provider = localSchemaProgressProvider(&data_server);
    const api_server = &data_server.http_server.?;
    // Recovery is a startup concern: enqueue durable work before the listener is
    // marked ready instead of waiting for an unrelated request to arrive.
    try api_server.resumeRestoreJobsOnce();
    data_server.registerNodeIfConfigured() catch |err| {
        std.log.err("standalone startup failed step=register_node err={}", .{err});
        return err;
    };
    // Warm the query owner before starting recovery. The warmup completion is
    // the single handoff into provisioned startup catch-up; launching both
    // workers concurrently lets catch-up win the gate while warmup exits as
    // "already active", leaving neither a query owner nor a retry for the
    // warmup. If warmup itself cannot be scheduled, retain availability by
    // falling back to the durable catch-up owner directly.
    data_server.requestProvisionedCacheWarmup() catch |warmup_err| {
        std.log.warn("standalone startup provisioned cache warmup skipped err={}", .{warmup_err});
        data_server.requestProvisionedStartupCatchUpNow() catch |catch_up_err| {
            std.log.warn("standalone startup provisioned startup catch-up skipped err={}", .{catch_up_err});
        };
    };

    // ---------------------------------------------------------------
    // Unified httpx.Server — all routes on a single port
    // ---------------------------------------------------------------

    var handler = try antfly.public_api.kernel_bridge.createHandler(api_server);
    handler.initRuntime(alloc) catch |err| {
        antfly.public_api.kernel_bridge.deinitHandler(&handler);
        return err;
    };
    defer antfly.public_api.kernel_bridge.deinitHandler(&handler);

    const bind_host = public_listener.bind_host;
    const bind_port = public_listener.bind_port;
    const cors_config = configuredCors(api_server.cfg.node_config);

    var unified_api_ready = std.atomic.Value(bool).init(false);

    var unified_lifecycle = UnifiedServerLifecycle.init(control_io);
    const public_http_config = publicHttpServerConfig(bind_host, bind_port);
    var http_observer_lease = try node_backend_runtime.ptr().acquireWorkers(.{});
    defer http_observer_lease.release();
    var http_runtime = httpx.HttpRuntime.init(alloc, .{
        .observer_io = http_observer_lease.io(),
        .max_active_h1_requests = public_http_config.max_connections,
        .max_active_connections = @as(usize, public_http_config.max_connections) +| antfly.common.health_server.max_connections,
        .max_active_requests = @as(usize, public_http_config.max_request_tasks) +| antfly.common.health_server.max_connections,
    });
    defer http_runtime.deinit();
    var standalone_health = StandaloneHealthSource{
        .data_server = &data_server,
        .unified_api_ready = &unified_api_ready,
        .supervisor = &supervisor,
        .startup_checkpoint_lsn = ha_startup_checkpoint_lsn,
        .handler = &handler,
        .unified_lifecycle = &unified_lifecycle,
    };
    const health_enabled = cli.health_enabled orelse if (loaded_config) |*cfg| cfg.health_enabled else true;
    const health_port = if (health_enabled)
        cli.health_port orelse if (loaded_config) |*cfg| cfg.health_port else antfly.common.config.default_health_port
    else
        null;
    const health_server = antfly.common.health_server.HealthServer.startIfConfiguredOnHostWithRuntime(
        alloc,
        control_io,
        "standalone",
        public_listener.bind_host,
        health_port,
        standalone_health.readiness(),
        standalone_health.metricsWriter(),
        &http_runtime,
    ) catch |err| {
        std.log.err("standalone startup failed step=health_server err={}", .{err});
        return err;
    };
    defer if (health_server) |hs| hs.deinitWithDeadline(supervisor.deadline());

    var api_lane_lease = try node_backend_runtime.ptr().acquireApiLane();
    defer api_lane_lease.release();
    const public_io = api_lane_lease.io();
    var unified_future = (if (comptime inline_inference_codegen)
        control_io.concurrent(serveUnifiedWithInference, .{
            alloc,
            public_io,
            public_http_config,
            cors_config,
            &handler,
            antfly_node,
            embedded_inference_enabled,
            api_server,
            &local_metadata,
            &unified_api_ready,
            &unified_lifecycle,
            &http_runtime,
        })
    else
        control_io.concurrent(serveUnifiedWithLinkedInference, .{
            alloc,
            public_io,
            public_http_config,
            cors_config,
            &handler,
            antfly_node,
            embedded_inference_enabled,
            api_server,
            &local_metadata,
            &unified_api_ready,
            &unified_lifecycle,
            &http_runtime,
        })) catch |err| {
        std.log.err("standalone startup failed step=schedule_unified_http err={}", .{err});
        return err;
    };
    var future_awaited = false;
    defer if (!future_awaited) {
        unified_lifecycle.stop();
        _ = unified_future.await(control_io);
    };
    unified_lifecycle.waitForStartup(supervisor.startupDeadline(), termination_signals.token()) catch |err| {
        std.log.err("standalone startup failed step=bind_unified_http err={}", .{err});
        return err;
    };

    // Print only after the public listener has successfully bound.
    std.debug.print("standalone local metadata enabled (raft disabled)\n", .{});

    const runtime_cadence = antfly.raft.RuntimeCadence.fromMillis(
        antfly.raft.RuntimeCadence.default_raft_tick_ms,
        cli.control_tick_ms,
    ) catch return error.InvalidArguments;
    const tick_ms = @divExact(runtime_cadence.control_tick_ns, std.time.ns_per_ms);
    var req = std.posix.timespec{
        .sec = @intCast(tick_ms / std.time.ms_per_s),
        .nsec = @intCast((tick_ms % std.time.ms_per_s) * std.time.ns_per_ms),
    };
    try supervisor.publishReady();
    while (!supervisor.shouldStop(termination_signals.cancellationRequested())) {
        if (unified_lifecycle.runtimeFailure()) |err| return supervisor.fail("standalone", "unified-http", err);
        if (ha_watchdog_failed.load(.acquire)) return supervisor.fail("standalone", "ha-watchdog", error.HALeaseWatchdogWorkerFailed);
        data_server.runRound() catch |err| switch (err) {
            error.LsmRootWriterAlreadyOpen, error.WriterLocked => std.log.warn("standalone data round skipped err={}", .{err}),
            else => return supervisor.fail("standalone", "data-round", err),
        };
        if (!ha_role_requested) {
            LocalStandaloneMetadata.runRound(&local_metadata) catch |err| switch (err) {
                error.LsmRootWriterAlreadyOpen, error.WriterLocked => std.log.warn("standalone metadata round skipped err={}", .{err}),
                else => return supervisor.fail("standalone", "metadata-round", err),
            };
        }
        const err = std.posix.errno(std.posix.system.nanosleep(&req, &req));
        switch (err) {
            .SUCCESS => {},
            .INTR => continue,
            else => return supervisor.fail("standalone", "control-wait", std.posix.unexpectedErrno(err)),
        }
    }

    const process_shutdown_deadline = supervisor.deadline();
    unified_lifecycle.shutdown(process_shutdown_deadline);
    _ = unified_future.await(control_io);
    future_awaited = true;
    if (unified_lifecycle.runtimeFailure()) |err| return supervisor.fail("standalone", "unified-http", err);
}

fn validateEffectiveStandaloneStorage(
    cli: CliConfig,
    storage_engine: antfly.common.config.StorageEngine,
    lite_path: ?[]const u8,
    loaded_config: ?*const antfly.common.config.Config,
) !void {
    if (storage_engine == .object) return error.UnsupportedStandaloneStorageEngine;
    if (storage_engine != .lite) {
        if (cli.storage_path != null or cli.storage_fsync != null) return error.InvalidArguments;
        return;
    }
    const path = lite_path orelse return error.MissingLiteStoragePath;
    if (!std.mem.endsWith(u8, path, ".aflite")) return error.InvalidLiteStoragePath;
    if (loaded_config) |cfg| {
        if (cfg.metadata.orchestration_urls.len != 0 or cfg.metadata.raft_urls.len != 0) {
            return error.LiteExternalMetadataUnsupported;
        }
        if (cfg.shard_allocation.default_shards_per_table != 1 or
            cfg.shard_allocation.min_shards_per_table != 1 or
            !cfg.shard_allocation.disable_shard_alloc)
        {
            return error.LiteHorizontalShardingUnsupported;
        }
    }
}

pub fn runLite(
    init: std.process.Init,
    path: []const u8,
    host: []const u8,
    port: u16,
    fsync: bool,
    extra_args: []const []const u8,
) !void {
    const path_z = try init.gpa.dupeZ(u8, path);
    defer init.gpa.free(path_z);
    const host_z = try init.gpa.dupeZ(u8, host);
    defer init.gpa.free(host_z);
    var port_buf: [16]u8 = undefined;
    const port_z = try std.fmt.bufPrintZ(&port_buf, "{d}", .{port});
    var argv = std.ArrayListUnmanaged([*:0]const u8).empty;
    defer argv.deinit(init.gpa);
    try argv.appendSlice(init.gpa, &.{
        "--storage-engine",
        "lite",
        "--storage-path",
        path_z.ptr,
        "--host",
        host_z.ptr,
        "--port",
        port_z.ptr,
        if (fsync) "--fsync=true" else "--fsync=false",
    });
    const owned_extra = try init.gpa.alloc([:0]u8, extra_args.len);
    var owned_extra_count: usize = 0;
    defer {
        for (owned_extra[0..owned_extra_count]) |value| init.gpa.free(value);
        init.gpa.free(owned_extra);
    }
    for (extra_args, 0..) |arg, i| {
        owned_extra[i] = try init.gpa.dupeZ(u8, arg);
        owned_extra_count += 1;
        try argv.append(init.gpa, owned_extra[i].ptr);
    }
    var args = std.process.Args.Iterator.init(.{ .vector = argv.items });
    try runFromIterator(init, "antfly standalone", &args);
}

// Unified server task
// ---------------------------------------------------------------

fn serveUnifiedWithInference(
    alloc: std.mem.Allocator,
    io: std.Io,
    public_http_config: httpx.ServerConfig,
    cors_config: ?*const antfly.common.config.Config.CorsConfig,
    handler: *ApiKernelHandler,
    antfly_node: *anyopaque,
    register_inference_routes: bool,
    api_server: *ApiHttpServer,
    local_metadata: *LocalStandaloneMetadata,
    unified_api_ready: *std.atomic.Value(bool),
    lifecycle: *UnifiedServerLifecycle,
    http_runtime: *httpx.HttpRuntime,
) void {
    serveUnifiedInner(true, alloc, io, public_http_config, cors_config, handler, antfly_node, register_inference_routes, api_server, local_metadata, unified_api_ready, lifecycle, http_runtime) catch |err| {
        unified_api_ready.store(false, .release);
        lifecycle.publishFailure(err);
        std.debug.print("unified server error: {}\n", .{err});
        return;
    };
    unified_api_ready.store(false, .release);
    lifecycle.publishStopped();
}

fn serveUnifiedWithLinkedInference(
    alloc: std.mem.Allocator,
    io: std.Io,
    public_http_config: httpx.ServerConfig,
    cors_config: ?*const antfly.common.config.Config.CorsConfig,
    handler: *ApiKernelHandler,
    inference_handle: *anyopaque,
    register_inference_routes: bool,
    api_server: *ApiHttpServer,
    local_metadata: *LocalStandaloneMetadata,
    unified_api_ready: *std.atomic.Value(bool),
    lifecycle: *UnifiedServerLifecycle,
    http_runtime: *httpx.HttpRuntime,
) void {
    serveUnifiedInner(false, alloc, io, public_http_config, cors_config, handler, inference_handle, register_inference_routes, api_server, local_metadata, unified_api_ready, lifecycle, http_runtime) catch |err| {
        unified_api_ready.store(false, .release);
        lifecycle.publishFailure(err);
        std.debug.print("unified server error: {}\n", .{err});
        return;
    };
    unified_api_ready.store(false, .release);
    lifecycle.publishStopped();
}

fn serveUnifiedInner(
    comptime inline_inference: bool,
    alloc: std.mem.Allocator,
    io: std.Io,
    public_http_config: httpx.ServerConfig,
    cors_config: ?*const antfly.common.config.Config.CorsConfig,
    handler: *ApiKernelHandler,
    antfly_node: *anyopaque,
    register_inference_routes: bool,
    api_server: *ApiHttpServer,
    local_metadata: *LocalStandaloneMetadata,
    unified_api_ready: *std.atomic.Value(bool),
    lifecycle: *UnifiedServerLifecycle,
    http_runtime: *httpx.HttpRuntime,
) !void {
    var server_config = public_http_config;
    server_config.http_runtime = http_runtime;
    var server = httpx.Server.initWithConfig(alloc, io, server_config);
    defer server.deinit();
    var route_context = StandaloneHttpContext{
        .api_server = api_server,
        .cors_config = cors_config,
    };
    try lifecycle.attach(&server);
    defer lifecycle.detach(&server);

    if (corsEnabled(cors_config)) try server.use(corsMiddleware(&route_context));
    try server.use(inferenceAuthMiddleware(&route_context));
    try server.use(interactiveGenerateMiddleware());

    // Register inference AI routes under /ai/v1 and Traditional ML routes under /ml/v1.
    var linked_inference_routes: std.ArrayListUnmanaged(*LinkedInferenceRoute) = .empty;
    defer {
        for (linked_inference_routes.items) |route| alloc.destroy(route);
        linked_inference_routes.deinit(alloc);
    }
    if (register_inference_routes) {
        if (comptime inline_inference) {
            try inference_host.linkedInferenceRegisterRoutesOn(antfly_node, &server);
        } else {
            const functions = try linkedInferenceApi(inference_bridge.Capability.route_manifest);
            try registerLinkedInferenceManifest(
                alloc,
                &server,
                antfly_node,
                functions,
                &linked_inference_routes,
            );
        }
    }

    // Runtime roles consume the shared direct/linked kernel registrar instead
    // of carrying copies of the generated route manifest. Standalone supplies
    // its stronger Kubernetes-style root readiness contract locally.
    try server.get(antfly.public_api.http_routes.Routes.healthz, healthzHandler);
    try server.get(
        antfly.public_api.http_routes.Routes.readyz,
        httpx.Handler.bind(&route_context, readyzHandler),
    );
    try handler.registerRoutesWithoutProbes(&server);

    try server.use(httpx.Middleware.bind("storage-maintenance-admission", &route_context, storageMaintenanceAdmission));
    try registerAntfarmRoutes(&server);

    var listener_task = httpx.ListenerTask.init(&server);
    listener_task.start() catch |err| {
        const stats = http_runtime.stats();
        std.log.err(
            "standalone public listener admission failed err={s} requested_connections={d} requested_requests={d} runtime_connections={d} reserved_connections={d} runtime_requests={d} reserved_requests={d}",
            .{
                @errorName(err),
                public_http_config.max_connections,
                public_http_config.max_request_tasks,
                stats.connection_capacity,
                stats.reserved_connection_capacity,
                stats.request_capacity,
                stats.reserved_request_capacity,
            },
        );
        return err;
    };
    var listener_joined = false;
    defer if (!listener_joined) {
        listener_task.requestStop();
        listener_task.join() catch {};
    };
    if (public_http_config.port == 0) {
        const bound_address = server.boundAddress() orelse return error.PublicListenerAddressUnavailable;
        const bound_port = switch (bound_address) {
            .ip4 => |address| address.port,
            .ip6 => |address| address.port,
        };
        const bound_api_url = try std.fmt.allocPrint(
            alloc,
            "http://{s}:{d}",
            .{ public_http_config.host, bound_port },
        );
        defer alloc.free(bound_api_url);
        try local_metadata.setApiUrl(bound_api_url);
    }
    unified_api_ready.store(true, .release);
    try lifecycle.publishReady();

    if (server.boundAddress()) |addr| {
        std.debug.print("standalone public api listening on http://{f}\n", .{addr});
    }

    try listener_task.join();
    listener_joined = true;
}

const LinkedInferenceRoute = struct {
    functions: *const inference_bridge.FunctionTable,
    kernel_route_handle: *anyopaque,
    request_body: runtime_http_abi.RequestBodyMode,
    streaming_response: bool,
};

fn registerLinkedInferenceManifest(
    alloc: std.mem.Allocator,
    server: *httpx.Server,
    inference_handle: *anyopaque,
    functions: *const inference_bridge.FunctionTable,
    owned_routes: *std.ArrayListUnmanaged(*LinkedInferenceRoute),
) !void {
    var entries_ptr: ?[*]const inference_bridge.RouteManifestEntry = null;
    var entries_len: usize = 0;
    const status = functions.route_manifest(&.{
        .abi_version = inference_bridge.abi_version,
        .handle = inference_handle,
        .out_entries = &entries_ptr,
        .out_len = &entries_len,
    });
    if (!status.isOk()) return inference_bridge.errorFromStatus(status);
    const entries = if (entries_ptr) |ptr| ptr[0..entries_len] else &.{};
    for (entries) |entry| {
        const route = try alloc.create(LinkedInferenceRoute);
        errdefer alloc.destroy(route);
        route.* = .{
            .functions = functions,
            .kernel_route_handle = entry.route_handle,
            .request_body = entry.request_body,
            .streaming_response = entry.streaming_response != 0,
        };
        try owned_routes.append(alloc, route);
        errdefer _ = owned_routes.pop();
        try server.routeWithData(switch (entry.method) {
            .get => .GET,
            .post => .POST,
            .put => .PUT,
            .delete => .DELETE,
            .patch => .PATCH,
        }, entry.path.slice(), linkedInferenceHttpHandler, route);
    }
}

fn linkedInferenceHttpHandler(context: *httpx.Context) anyerror!httpx.Response {
    const route: *const LinkedInferenceRoute = @ptrCast(@alignCast(context.route_data orelse return error.InferenceRuntimeUnavailable));
    const source_headers = context.request.headers.iterator();
    const headers = try context.allocator.alloc(runtime_http_abi.HeaderView, source_headers.len);
    defer context.allocator.free(headers);
    for (source_headers, 0..) |header, i| {
        headers[i] = .{
            .name = runtime_http_abi.Bytes.init(header.name),
            .value = runtime_http_abi.Bytes.init(header.value),
        };
    }
    const params = try context.allocator.alloc(runtime_http_abi.RouteParamView, context.params.len);
    defer context.allocator.free(params);
    for (context.params, 0..) |param, i| {
        params[i] = .{
            .name = runtime_http_abi.Bytes.init(param.name),
            .value = runtime_http_abi.Bytes.init(param.value),
        };
    }

    const request_view: runtime_http_abi.HttpRequestView = .{
        .method = switch (context.request.method) {
            .GET => .get,
            .POST => .post,
            .PUT => .put,
            .DELETE => .delete,
            .PATCH => .patch,
            else => return error.MethodNotAllowed,
        },
        .path = runtime_http_abi.Bytes.init(context.request.uri.path),
        .query = runtime_http_abi.OptionalBytes.init(context.request.uri.query),
        .headers_ptr = if (headers.len == 0) null else headers.ptr,
        .headers_len = headers.len,
        .params_ptr = if (params.len == 0) null else params.ptr,
        .params_len = params.len,
        .body = runtime_http_abi.OptionalBytes.init(context.request.body),
        .authorization = runtime_http_abi.OptionalBytes.init(context.request.headers.get("Authorization")),
        .content_type = runtime_http_abi.OptionalBytes.init(context.request.headers.get("Content-Type")),
    };
    var transport = @import("../runtime_http_bridge.zig").Outbound{ .context = context };
    const body_source = if (route.request_body == .buffered) transport.bodySource() else runtime_http_abi.RequestBodySource{};
    var response_handle: ?*anyopaque = null;
    var response_view: runtime_http_abi.HttpResponseView = undefined;
    const status = route.functions.handle_http(&.{
        .abi_version = inference_bridge.abi_version,
        .route_handle = route.kernel_route_handle,
        .request = &request_view,
        .cancellation = transport.cancellation(),
        .body_source = body_source,
        .stream = if (route.streaming_response) transport.stream() else .{},
        .out_response_handle = &response_handle,
        .out_response = &response_view,
    });
    if (!status.isOk()) return inference_bridge.errorFromStatus(status);
    const owned_response_handle = response_handle orelse return error.RuntimeBoundaryFailure;
    defer route.functions.destroy_http_response(owned_response_handle);

    var response = httpx.Response.init(context.allocator, response_view.status);
    errdefer response.deinit();
    if (response_view.content_type.slice()) |content_type|
        try response.headers.set("Content-Type", content_type);
    const response_headers = if (response_view.headers_ptr) |ptr| ptr[0..response_view.headers_len] else &.{};
    for (response_headers) |header| {
        if (response_view.content_type.slice() != null and
            std.ascii.eqlIgnoreCase(header.name.slice(), "Content-Type")) continue;
        try response.headers.append(header.name.slice(), header.value.slice());
    }
    response.body = try context.allocator.dupe(u8, response_view.body.slice());
    response.body_owned = true;
    return response;
}

const public_http_connection_ceiling: u32 = 256;
const public_http_max_h1_inflight_bodies: u32 = 32;

fn publicHttpConnectionLimitForFdSoftLimit(fd_soft_limit: u64) u32 {
    // Public inbound sockets may use at most one quarter of the process FD
    // budget. Storage files, Raft, metadata, outbound providers, logs, and an
    // operator shell retain the rest even when the deployment lowers RLIMIT.
    const proportional_limit = @max(@as(u64, 1), fd_soft_limit / 4);
    return @intCast(@min(proportional_limit, public_http_connection_ceiling));
}

fn configuredPublicHttpConnectionLimit() u32 {
    if (comptime builtin.os.tag == .windows or builtin.os.tag == .freestanding) return public_http_connection_ceiling;
    const limit = std.posix.getrlimit(.NOFILE) catch return public_http_connection_ceiling;
    if (limit.cur == std.math.maxInt(@TypeOf(limit.cur))) return public_http_connection_ceiling;
    return publicHttpConnectionLimitForFdSoftLimit(@intCast(limit.cur));
}

fn publicHttpServerConfig(bind_host: []const u8, bind_port: u16) httpx.ServerConfig {
    return (httpx.ServerConfig{
        .host = bind_host,
        .port = bind_port,
        .max_body_size = antfly.public_api.http_server.public_api_max_request_body_bytes,
        // Bound aggregate request-body buffering across HTTP/1 and HTTP/2.
        // Four maximum-sized public requests may complete while excess uploads
        // are shed before allocator pressure becomes systemic.
        .request_body_buffer_budget_bytes = 256 * 1024 * 1024,
        // This is a transport safeguard for every H1 request body. Keep it
        // independent from admission.query.max_concurrent_requests.
        .max_h1_inflight_bodies = public_http_max_h1_inflight_bodies,
        .header_read_timeout_ms = 300_000,
        .body_read_timeout_ms = 300_000,
        .response_write_timeout_ms = 300_000,
        // Keep a large process-wide FD reserve for storage, Raft, outbound
        // clients, and diagnostics. This prevents the historical 1,000-socket
        // cliff under the common 1,024 descriptor soft limit.
        .max_connections = configuredPublicHttpConnectionLimit(),
        .accept_error_backoff_initial_ms = 5,
        .accept_error_backoff_max_ms = 1_000,
        .max_requests_per_connection = public_api_max_requests_per_connection,
        // httpx keeps SO_REUSEADDR separate from the opt-in SO_REUSEPORT flag,
        // preserving fast restarts without allowing two live runtimes to share
        // the public bind tuple.
        .reuse_address = true,
        .reuse_port = false,
    }).normalized();
}

fn healthzHandler(ctx: *httpx.Context) anyerror!httpx.Response {
    return ctx.json(.{ .status = "ok" });
}

fn readyzHandler(route_context: *StandaloneHttpContext, ctx: *httpx.Context) anyerror!httpx.Response {
    const server = route_context.api_server orelse {
        try ctx.setHeader("Retry-After", "1");
        return ctx.status(503).json(.{ .status = "not_ready" });
    };
    if (server.storageMaintenanceExclusiveActive()) {
        try ctx.setHeader("Retry-After", "1");
        return ctx.status(503).json(.{ .status = "maintenance" });
    }
    server.checkReady() catch {
        try ctx.setHeader("Retry-After", "1");
        return ctx.status(503).json(.{ .status = "not_ready" });
    };
    return ctx.json(.{ .status = "ready" });
}

fn storageMaintenanceAdmission(route_context: *StandaloneHttpContext, ctx: *httpx.Context, next: *httpx.Next) anyerror!httpx.Response {
    const api_server = route_context.api_server orelse return next.call(ctx);
    if (!api_server.storageMaintenanceExclusiveActive()) return next.call(ctx);
    const path = ctx.request.uri.path;
    if (std.mem.eql(u8, path, "/healthz") or
        std.mem.eql(u8, path, "/readyz") or
        std.mem.startsWith(u8, path, antfly.admin.routes.maintenance ++ "/"))
    {
        return next.call(ctx);
    }
    _ = ctx.status(503);
    return ctx.text("storage maintenance in progress");
}

fn inferenceAuthMiddleware(route_context: *StandaloneHttpContext) httpx.Middleware {
    return httpx.Middleware.bind("inference_auth", route_context, inferenceAuth);
}

fn inferenceAuth(route_context: *StandaloneHttpContext, ctx: *httpx.Context, next: *httpx.Next) anyerror!httpx.Response {
    if (!isInferenceApiPath(ctx.request.uri.path)) return next.call(ctx);

    const server = route_context.api_server orelse return inferenceNotReadyResponse(ctx);
    const permission: antfly.public_api.kernel_abi.InferencePermission = switch (ctx.request.method) {
        .GET, .HEAD, .OPTIONS => .read,
        else => .write,
    };
    const decision = server.authorizeInferenceRequest(.{
        .authorization = ctx.header("authorization"),
        .trusted_principal = ctx.header(antfly.public_api.http_server.trusted_principal_header),
    }, permission) catch |err| switch (err) {
        error.OutOfMemory => return err,
        else => return inferenceNotReadyResponse(ctx),
    };
    return switch (decision) {
        .allowed => next.call(ctx),
        .unauthorized => inferenceUnauthorizedResponse(ctx),
        .forbidden => inferenceForbiddenResponse(ctx, permission),
        .not_ready => inferenceNotReadyResponse(ctx),
    };
}

fn interactiveGenerateMiddleware() httpx.Middleware {
    return .{
        .name = "interactive_generate",
        .handler = struct {
            fn handler(ctx: *httpx.Context, next: *httpx.Next) anyerror!httpx.Response {
                if (!isInteractiveGeneratePath(ctx.request.uri.path)) return next.call(ctx);
                _ = antfly.db.enrichment_types.interactive_generate_inflight.fetchAdd(1, .monotonic);
                defer _ = antfly.db.enrichment_types.interactive_generate_inflight.fetchSub(1, .monotonic);
                return next.call(ctx);
            }
        }.handler,
    };
}

fn corsMiddleware(route_context: *StandaloneHttpContext) httpx.Middleware {
    return httpx.Middleware.bind("cors", route_context, corsRequest);
}

fn corsRequest(route_context: *StandaloneHttpContext, ctx: *httpx.Context, next: *httpx.Next) anyerror!httpx.Response {
    const config = route_context.cors_config orelse return next.call(ctx);
    if (!(config.enabled orelse true)) return next.call(ctx);

    const origin = ctx.header("origin") orelse return next.call(ctx);
    const requested_method = if (ctx.request.method == .OPTIONS)
        ctx.header("access-control-request-method")
    else
        null;
    const is_preflight = requested_method != null;
    const allowed_origin = corsAllowedOrigin(config, origin);

    const allowed = !(allowed_origin == null or
        (is_preflight and !corsMethodAllowed(config, requested_method.?)) or
        (is_preflight and !corsRequestHeadersAllowed(config, ctx.header("access-control-request-headers"))) or
        (!is_preflight and !corsMethodAllowed(config, ctx.request.method.toString())));
    if (is_preflight and !allowed) {
        try appendCorsPreflightVary(&ctx.response.headers, true);
        return ctx.status(403).text("CORS request denied");
    }

    if (!is_preflight) {
        const origin_policy = if (allowed) allowed_origin else null;
        // Before next: streaming commits read this transport-owned policy.
        // After next: linked handlers may return a separately owned Response.
        try applyCorsActualHeaders(ctx.allocator, &ctx.response.headers, config, origin_policy);
        var response = try next.call(ctx);
        errdefer response.deinit();
        try applyCorsActualHeaders(ctx.allocator, &response.headers, config, origin_policy);
        return response;
    }

    try applyCorsOriginHeaders(&ctx.response.headers, config, allowed_origin.?);
    try appendCorsPreflightVary(&ctx.response.headers, false);
    try applyCorsPreflightHeaders(ctx, config);
    return ctx.status(204).text("");
}

fn applyCorsOriginHeaders(
    headers: *httpx.Headers,
    config: *const antfly.common.config.Config.CorsConfig,
    allowed_origin: []const u8,
) !void {
    try headers.set("Access-Control-Allow-Origin", allowed_origin);
    if (!std.mem.eql(u8, allowed_origin, "*")) try appendCorsVaryOrigin(headers);
    if (config.allow_credentials orelse false) {
        try headers.set("Access-Control-Allow-Credentials", "true");
    } else {
        _ = headers.remove("Access-Control-Allow-Credentials");
    }
}

fn appendCorsVaryOrigin(headers: *httpx.Headers) !void {
    for (headers.iterator()) |header| {
        if (!std.ascii.eqlIgnoreCase(header.name, "Vary")) continue;
        var tokens = std.mem.splitScalar(u8, header.value, ',');
        while (tokens.next()) |token| {
            const value = std.mem.trim(u8, token, " \t");
            if (std.ascii.eqlIgnoreCase(value, "Origin") or std.mem.eql(u8, value, "*")) return;
        }
    }
    try headers.append("Vary", "Origin");
}

fn applyCorsActualHeaders(
    alloc: std.mem.Allocator,
    headers: *httpx.Headers,
    config: *const antfly.common.config.Config.CorsConfig,
    allowed_origin: ?[]const u8,
) !void {
    if (allowed_origin) |origin| {
        try applyCorsOriginHeaders(headers, config, origin);
        const exposed = config.exposed_headers orelse &cors_default_exposed_headers;
        if (exposed.len > 0) {
            const joined = try joinCorsValues(alloc, exposed);
            defer alloc.free(joined);
            try headers.set("Access-Control-Expose-Headers", joined);
        } else {
            _ = headers.remove("Access-Control-Expose-Headers");
        }
    } else {
        // A downstream response cannot relax the host's configured policy.
        _ = headers.remove("Access-Control-Allow-Origin");
        _ = headers.remove("Access-Control-Allow-Credentials");
        _ = headers.remove("Access-Control-Expose-Headers");
        try appendCorsVaryOrigin(headers);
    }
}

fn appendCorsPreflightVary(headers: *httpx.Headers, include_origin: bool) !void {
    if (include_origin) try headers.append("Vary", "Origin");
    try headers.append("Vary", "Access-Control-Request-Method");
    try headers.append("Vary", "Access-Control-Request-Headers");
}

fn applyCorsPreflightHeaders(ctx: *httpx.Context, config: *const antfly.common.config.Config.CorsConfig) !void {
    const methods = if (config.allowed_methods) |values|
        try joinCorsValues(ctx.allocator, values)
    else
        try joinCorsValues(ctx.allocator, &cors_default_methods);
    defer ctx.allocator.free(methods);
    try ctx.response.headers.set("Access-Control-Allow-Methods", methods);

    // With credentials, Fetch treats `*` as the literal header name rather
    // than a wildcard. The request list has already been token-validated, so
    // reflect it explicitly to preserve the configured "allow any" intent.
    const credentialed_wildcard_headers = (config.allow_credentials orelse false) and corsAllowsAnyHeader(config);
    const headers = if (credentialed_wildcard_headers and ctx.header("access-control-request-headers") != null)
        try ctx.allocator.dupe(u8, ctx.header("access-control-request-headers").?)
    else if (config.allowed_headers) |values|
        try joinCorsValues(ctx.allocator, values)
    else
        try joinCorsValues(ctx.allocator, &cors_default_headers);
    defer ctx.allocator.free(headers);
    try ctx.response.headers.set("Access-Control-Allow-Headers", headers);

    var max_age_buf: [10]u8 = undefined;
    const max_age = try std.fmt.bufPrint(&max_age_buf, "{d}", .{config.max_age orelse cors_default_max_age});
    try ctx.response.headers.set("Access-Control-Max-Age", max_age);
}

fn joinCorsValues(alloc: std.mem.Allocator, values: anytype) ![]u8 {
    var size: usize = 0;
    for (values, 0..) |value, i| size += value.len + @as(usize, if (i == 0) 0 else 2);
    const joined = try alloc.alloc(u8, size);
    var offset: usize = 0;
    for (values, 0..) |value, i| {
        if (i != 0) {
            @memcpy(joined[offset..][0..2], ", ");
            offset += 2;
        }
        @memcpy(joined[offset..][0..value.len], value);
        offset += value.len;
    }
    return joined;
}

fn corsAllowedOrigin(config: *const antfly.common.config.Config.CorsConfig, origin: []const u8) ?[]const u8 {
    if (!isSafeCorsOrigin(origin)) return null;
    if (config.allowed_origins) |origins| {
        if (origins.len != 0) {
            for (origins) |allowed| if (std.mem.eql(u8, allowed, "*")) return "*";
            for (origins) |allowed| {
                if (std.mem.eql(u8, allowed, origin)) return origin;
            }
            return null;
        }
    }
    return "*";
}

fn corsMethodAllowed(config: *const antfly.common.config.Config.CorsConfig, method: []const u8) bool {
    if (config.allowed_methods) |methods| {
        for (methods) |allowed| if (std.mem.eql(u8, allowed, method)) return true;
        return false;
    }
    for (cors_default_methods) |allowed| if (std.mem.eql(u8, allowed, method)) return true;
    return false;
}

fn corsRequestHeadersAllowed(config: *const antfly.common.config.Config.CorsConfig, requested: ?[]const u8) bool {
    const raw = requested orelse return true;
    var values = std.mem.splitScalar(u8, raw, ',');
    while (values.next()) |value| {
        const name = std.mem.trim(u8, value, " \t");
        if (!isHttpToken(name) or !corsHeaderAllowed(config, name)) return false;
    }
    return true;
}

fn corsHeaderAllowed(config: *const antfly.common.config.Config.CorsConfig, name: []const u8) bool {
    if (config.allowed_headers) |headers| {
        for (headers) |allowed| {
            if (std.mem.eql(u8, allowed, "*") or std.ascii.eqlIgnoreCase(allowed, name)) return true;
        }
        return false;
    }
    for (cors_default_headers) |allowed| if (std.ascii.eqlIgnoreCase(allowed, name)) return true;
    return false;
}

fn corsAllowsAnyHeader(config: *const antfly.common.config.Config.CorsConfig) bool {
    const headers = config.allowed_headers orelse return false;
    for (headers) |allowed| if (std.mem.eql(u8, allowed, "*")) return true;
    return false;
}

fn configuredCors(config: ?*const antfly.common.config.Config) ?*const antfly.common.config.Config.CorsConfig {
    const loaded = config orelse return null;
    return if (loaded.cors) |*cors| cors else null;
}

fn corsEnabled(config: ?*const antfly.common.config.Config.CorsConfig) bool {
    const cors = config orelse return false;
    return cors.enabled orelse true;
}

fn validateCorsConfig(config: ?*const antfly.common.config.Config.CorsConfig) !void {
    const cors = config orelse return;
    if (!(cors.enabled orelse true)) return;

    const allow_credentials = cors.allow_credentials orelse false;
    if (cors.allowed_origins) |origins| {
        if (origins.len == 0 and allow_credentials) return error.CorsCredentialsWithWildcardOrigin;
        for (origins) |origin| {
            if (!isSafeCorsOrigin(origin)) return error.InvalidCorsOrigin;
            if (allow_credentials and std.mem.eql(u8, origin, "*")) return error.CorsCredentialsWithWildcardOrigin;
            if (allow_credentials and std.mem.eql(u8, origin, "null")) return error.CorsCredentialsWithOpaqueOrigin;
        }
    } else if (allow_credentials) {
        return error.CorsCredentialsWithWildcardOrigin;
    }

    if (cors.allowed_methods) |methods| for (methods) |method| {
        if (httpx.Method.fromString(method) == null) return error.InvalidCorsMethod;
    };
    if (cors.allowed_headers) |headers| for (headers) |header| {
        if (!isHttpToken(header)) return error.InvalidCorsHeader;
    };
    if (cors.exposed_headers) |headers| for (headers) |header| {
        if (!isHttpToken(header)) return error.InvalidCorsHeader;
        if (allow_credentials and std.mem.eql(u8, header, "*")) return error.CorsCredentialsWithWildcardExposedHeaders;
    };
}

fn isSafeCorsOrigin(origin: []const u8) bool {
    if (std.mem.eql(u8, origin, "*") or std.mem.eql(u8, origin, "null")) return true;
    const scheme_end = std.mem.indexOf(u8, origin, "://") orelse return false;
    if (scheme_end == 0 or scheme_end + 3 == origin.len or !std.ascii.isAlphabetic(origin[0])) return false;
    for (origin[1..scheme_end]) |char| {
        if (!std.ascii.isAlphanumeric(char) and char != '+' and char != '-' and char != '.') return false;
    }
    for (origin[scheme_end + 3 ..]) |char| {
        if (char <= ' ' or char >= 0x7f or char == '/' or char == '?' or char == '#' or char == '@' or char == ',') return false;
    }
    return true;
}

fn isHttpToken(value: []const u8) bool {
    if (value.len == 0) return false;
    for (value) |char| switch (char) {
        'a'...'z', 'A'...'Z', '0'...'9', '!', '#', '$', '%', '&', '\'', '*', '+', '-', '.', '^', '_', '`', '|', '~' => {},
        else => return false,
    };
    return true;
}

fn isInferenceApiPath(path: []const u8) bool {
    return hasPathComponentPrefix(path, inference_bridge.ai_api_prefix) or
        hasPathComponentPrefix(path, inference_bridge.public_api_prefix);
}

const isInteractiveGeneratePath = inference_provider.isInteractiveGeneratePath;

fn hasPathComponentPrefix(path: []const u8, prefix: []const u8) bool {
    return std.mem.eql(u8, path, prefix) or
        (std.mem.startsWith(u8, path, prefix) and path.len > prefix.len and path[prefix.len] == '/');
}

fn inferenceUnauthorizedResponse(ctx: *httpx.Context) !httpx.Response {
    try ctx.setHeader("WWW-Authenticate", "Basic realm=\"antfly\", Bearer realm=\"antfly\", ApiKey realm=\"antfly\"");
    return ctx.status(401).json(.{
        .@"error" = "unauthorized",
        .message = "valid Basic, Bearer, or ApiKey credentials are required",
        .retryable = false,
    });
}

fn inferenceForbiddenResponse(
    ctx: *httpx.Context,
    permission: antfly.public_api.kernel_abi.InferencePermission,
) !httpx.Response {
    return ctx.status(403).json(.{
        .@"error" = "forbidden",
        .message = switch (permission) {
            .read => "inference read permission is required",
            .write => "inference write permission is required",
        },
        .retryable = false,
    });
}

fn inferenceNotReadyResponse(ctx: *httpx.Context) !httpx.Response {
    try ctx.setHeader("Retry-After", "1");
    return ctx.status(503).json(.{
        .@"error" = "not_ready",
        .message = "inference authentication is not ready",
        .retryable = true,
    });
}

fn registerAntfarmRoutes(server: anytype) !void {
    try server.get("/", antfarmIndexHandler);
    try server.get("/assets/*", antfarmAssetHandler);
    try server.get("/fonts/*", antfarmFontHandler);
    try server.get("/*", antfarmSpaHandler);
}

fn antfarmIndexHandler(ctx: *httpx.Context) anyerror!httpx.Response {
    return serveAntfarmFile(ctx, "index.html");
}

fn antfarmAssetHandler(ctx: *httpx.Context) anyerror!httpx.Response {
    return serveAntfarmPrefixedFile(ctx, "/assets/", "assets/");
}

fn antfarmFontHandler(ctx: *httpx.Context) anyerror!httpx.Response {
    return serveAntfarmPrefixedFile(ctx, "/fonts/", "fonts/");
}

fn antfarmSpaHandler(ctx: *httpx.Context) anyerror!httpx.Response {
    const path = ctx.request.uri.path;
    if (isAntfarmReservedPath(path)) {
        return ctx.status(404).text("not found");
    }
    if (!std.mem.startsWith(u8, path, "/")) {
        return ctx.status(400).text("invalid path");
    }

    const rel_path = path[1..];
    if (rel_path.len > 0 and std.mem.indexOfScalar(u8, rel_path, '.') != null) {
        return serveAntfarmFile(ctx, rel_path);
    }
    return serveAntfarmFile(ctx, "index.html");
}

fn serveAntfarmPrefixedFile(ctx: *httpx.Context, prefix: []const u8, rel_prefix: []const u8) anyerror!httpx.Response {
    const path = ctx.request.uri.path;
    if (!std.mem.startsWith(u8, path, prefix)) {
        return ctx.status(400).text("invalid path");
    }
    const suffix = path[prefix.len..];
    if (suffix.len == 0) {
        return ctx.status(404).text("not found");
    }

    var rel_buf: [1024]u8 = undefined;
    const rel_path = std.fmt.bufPrint(&rel_buf, "{s}{s}", .{ rel_prefix, suffix }) catch {
        return ctx.status(414).text("path too long");
    };
    return serveAntfarmFile(ctx, rel_path);
}

fn serveAntfarmFile(ctx: *httpx.Context, rel_path: []const u8) anyerror!httpx.Response {
    if (hasUnsafeStaticPath(rel_path)) {
        return ctx.status(400).text("invalid path");
    }

    if (try serveInstalledAntfarmFile(ctx, rel_path)) |resp| {
        return resp;
    }

    for (antfarm_asset_roots) |root| {
        var full_path_buf: [4096]u8 = undefined;
        const full_path = std.fmt.bufPrint(&full_path_buf, "{s}/{s}", .{ root, rel_path }) catch continue;
        if (try serveAntfarmPath(ctx, rel_path, full_path)) |resp| return resp;
    }

    return ctx.status(404).text("not found");
}

fn serveInstalledAntfarmFile(ctx: *httpx.Context, rel_path: []const u8) anyerror!?httpx.Response {
    const exe_dir = std.process.executableDirPathAlloc(ctx.io, ctx.allocator) catch return null;
    defer ctx.allocator.free(exe_dir);

    return serveAntfarmFileFromExecutableDir(ctx, exe_dir, rel_path);
}

fn serveAntfarmFileFromExecutableDir(ctx: *httpx.Context, exe_dir: []const u8, rel_path: []const u8) anyerror!?httpx.Response {
    for (antfarm_installed_asset_roots) |root| {
        var full_path_buf: [4096]u8 = undefined;
        const full_path = std.fmt.bufPrint(
            &full_path_buf,
            "{s}/{s}/{s}",
            .{ exe_dir, root, rel_path },
        ) catch continue;
        if (try serveAntfarmPath(ctx, rel_path, full_path)) |resp| return resp;
    }
    return null;
}

fn serveAntfarmPath(ctx: *httpx.Context, rel_path: []const u8, full_path: []const u8) anyerror!?httpx.Response {
    const body = std.Io.Dir.cwd().readFileAlloc(
        ctx.io,
        full_path,
        ctx.allocator,
        std.Io.Limit.limited(antfarm_max_file_bytes),
    ) catch |err| switch (err) {
        error.FileNotFound, error.NotDir, error.IsDir => return null,
        error.StreamTooLong => return try ctx.status(413).text("file too large"),
        else => return err,
    };

    var resp = httpx.Response.init(ctx.allocator, 200);
    errdefer resp.deinit();
    try resp.headers.set("Content-Type", antfarmContentType(rel_path));
    resp.body = body;
    resp.body_owned = true;
    return resp;
}

fn hasUnsafeStaticPath(path: []const u8) bool {
    if (path.len == 0) return true;
    if (path[0] == '/') return true;
    if (std.mem.indexOfScalar(u8, path, 0) != null) return true;
    if (std.mem.indexOfScalar(u8, path, '\\') != null) return true;
    if (std.mem.indexOf(u8, path, "..") != null) return true;
    var i: usize = 0;
    while (i + 2 < path.len) : (i += 1) {
        if (path[i] != '%' or path[i + 1] != '2') continue;
        if (path[i + 2] == 'f' or path[i + 2] == 'F' or path[i + 2] == 'e' or path[i + 2] == 'E') return true;
    }
    return false;
}

fn isAntfarmReservedPath(path: []const u8) bool {
    const reserved = [_][]const u8{
        "/api",
        "/db",
        "/ai",
        "/ml",
        "/antfly",
        "/metadata",
        "/admin",
        "/internal",
        "/mcp",
        "/a2a",
        "/.well-known",
        "/extensions",
        "/healthz",
        "/readyz",
        "/registry",
    };
    for (reserved) |prefix| {
        if (std.mem.eql(u8, path, prefix)) return true;
        if (path.len > prefix.len and std.mem.startsWith(u8, path, prefix) and path[prefix.len] == '/') return true;
    }
    return isVersionedApiPath(path);
}

fn isVersionedApiPath(path: []const u8) bool {
    if (path.len < 5 or path[0] != '/') return false;
    const first_separator = std.mem.indexOfScalarPos(u8, path, 1, '/') orelse return false;
    if (first_separator == 1 or first_separator + 2 >= path.len or path[first_separator + 1] != 'v') return false;

    var cursor = first_separator + 2;
    const digits_start = cursor;
    while (cursor < path.len and std.ascii.isDigit(path[cursor])) : (cursor += 1) {}
    if (cursor == digits_start) return false;
    return cursor == path.len or path[cursor] == '/';
}

fn antfarmContentType(path: []const u8) []const u8 {
    if (std.mem.endsWith(u8, path, ".html")) return "text/html; charset=utf-8";
    if (std.mem.endsWith(u8, path, ".css")) return "text/css; charset=utf-8";
    if (std.mem.endsWith(u8, path, ".js") or std.mem.endsWith(u8, path, ".mjs")) return "application/javascript; charset=utf-8";
    if (std.mem.endsWith(u8, path, ".json")) return "application/json";
    if (std.mem.endsWith(u8, path, ".png")) return "image/png";
    if (std.mem.endsWith(u8, path, ".jpg") or std.mem.endsWith(u8, path, ".jpeg")) return "image/jpeg";
    if (std.mem.endsWith(u8, path, ".svg")) return "image/svg+xml";
    if (std.mem.endsWith(u8, path, ".ico")) return "image/x-icon";
    if (std.mem.endsWith(u8, path, ".webp")) return "image/webp";
    if (std.mem.endsWith(u8, path, ".woff")) return "font/woff";
    if (std.mem.endsWith(u8, path, ".woff2")) return "font/woff2";
    if (std.mem.endsWith(u8, path, ".ttf")) return "font/ttf";
    if (std.mem.endsWith(u8, path, ".wasm")) return "application/wasm";
    if (std.mem.endsWith(u8, path, ".map")) return "application/json";
    if (std.mem.endsWith(u8, path, ".txt")) return "text/plain; charset=utf-8";
    return "application/octet-stream";
}

fn localReplicaRootReconcileHook(data_server: *antfly.data.runtime.DataServer) antfly.metadata_service.LocalReplicaRootReconcileHook {
    return .{
        .ptr = data_server,
        .vtable = &.{
            .run = runLocalReplicaRootReconcileHook,
        },
    };
}

fn localSchemaProgressProvider(data_server: *antfly.data.runtime.DataServer) LocalSchemaProgressProvider {
    return .{
        .ptr = data_server,
        .shard_db_adapter = data_server.localShardDbAdapter(),
        .collect = collectLocalSchemaProgress,
    };
}

fn collectLocalSchemaProgress(
    ptr: *anyopaque,
    alloc: std.mem.Allocator,
    tables: []const antfly.metadata.TableRecord,
    ranges: []const antfly.metadata.RangeRecord,
) !antfly.data.runtime.DataServer.LocalSchemaProgressSnapshot {
    const data_server: *antfly.data.runtime.DataServer = @ptrCast(@alignCast(ptr));
    return try data_server.collectLocalSchemaProgressSnapshot(alloc, tables, ranges);
}

fn localReplicaRootReconcilePermitHook(data_server: *antfly.data.runtime.DataServer) antfly.metadata_service.LocalReplicaRootReconcilePermitHook {
    return .{
        .ptr = data_server,
        .vtable = &.{
            .should_reconcile = runLocalReplicaRootReconcilePermitHook,
        },
    };
}

fn runLocalReplicaRootReconcileHook(
    ptr: *anyopaque,
    request: antfly.metadata_service.LocalReplicaRootReconcileHook.Request,
) !antfly.metadata.table_provisioner.ProvisionSummary {
    const data_server: *antfly.data.runtime.DataServer = @ptrCast(@alignCast(ptr));
    return try data_server.reconcileVisibleProvisionedReplicaStateFromSnapshot(
        request.metadata_group_id,
        request.group_ids,
        request.tables,
        request.ranges,
    );
}

fn runLocalReplicaRootReconcilePermitHook(ptr: *anyopaque) bool {
    const data_server: *antfly.data.runtime.DataServer = @ptrCast(@alignCast(ptr));
    return !data_server.shouldDeferProvisionedReplicaRootReconcile();
}

fn lockAtomic(mutex: *std.atomic.Mutex) void {
    platform_sync.lockYielding(mutex);
}

fn readFileAlloc(alloc: std.mem.Allocator, io: std.Io, path: []const u8, max_bytes: usize) ![]u8 {
    return try std.Io.Dir.cwd().readFileAlloc(io, path, alloc, .limited(max_bytes));
}

// Keep watch sleeps and deadline-aware locking on the same monotonic clock.
// Tests supply a manual clock to exercise confirmation and expiry without
// depending on the host scheduler.
const StandaloneWaitClock = struct {
    fn nowNs(_: @This()) u64 {
        return platform_time.monotonicNs();
    }

    fn sleepMs(_: @This(), ms: u64) void {
        platform_clock.Clock.real().sleepMs(ms);
    }

    fn yieldNow(_: @This()) void {
        platform_time.yieldNow();
    }
};

fn lockAtomicUntil(mutex: *std.atomic.Mutex, deadline_ns: ?u64) bool {
    return lockAtomicUntilWithClock(mutex, deadline_ns, StandaloneWaitClock{});
}

fn lockAtomicUntilWithClock(mutex: *std.atomic.Mutex, deadline_ns: ?u64, clock: anytype) bool {
    const deadline = deadline_ns orelse {
        lockAtomic(mutex);
        return true;
    };
    while (true) {
        if (clock.nowNs() >= deadline) return false;
        if (mutex.tryLock()) return true;
        clock.yieldNow();
    }
}

fn writeFileAtomically(alloc: std.mem.Allocator, io: std.Io, path: []const u8, contents: []const u8) !void {
    // Catalog mutations are serialized by LocalStandaloneMetadata.mutex.
    const tmp_path = try std.fmt.allocPrint(alloc, "{s}.tmp-standalone-metadata", .{path});
    defer alloc.free(tmp_path);

    {
        var file = try fs_paths.createFilePortable(io, tmp_path, .{ .truncate = true });
        defer file.close(io);
        var buf: [4096]u8 = undefined;
        var writer = file.writer(io, &buf);
        try writer.interface.writeAll(contents);
        try writer.end();
        try file.sync(io);
    }

    std.Io.Dir.rename(std.Io.Dir.cwd(), tmp_path, std.Io.Dir.cwd(), path, io) catch |err| {
        std.Io.Dir.cwd().deleteFile(io, tmp_path) catch {};
        return err;
    };
    const parent = std.fs.path.dirname(path) orelse if (std.fs.path.isAbsolute(path)) "/" else ".";
    try fs_paths.syncDirPortable(io, parent);
}

// ---------------------------------------------------------------
// CLI parsing
// ---------------------------------------------------------------

fn validPreloadModelKind(value: []const u8) bool {
    return std.mem.eql(u8, value, "embedder") or
        std.mem.eql(u8, value, "reranker") or
        std.mem.eql(u8, value, "generator") or
        std.mem.eql(u8, value, "chunker") or
        std.mem.eql(u8, value, "classifier") or
        std.mem.eql(u8, value, "rewriter") or
        std.mem.eql(u8, value, "reader") or
        std.mem.eql(u8, value, "transcriber") or
        std.mem.eql(u8, value, "extractor");
}

fn parsePreloadModelFlag(value: []const u8) !inference_bridge.WarmModel {
    const spec = try preload_model_spec.parse(value);
    return .{
        .kind = inference_bridge.String.init(if (validPreloadModelKind(spec.kind)) spec.kind else return error.InvalidArguments),
        .name = inference_bridge.String.init(spec.name),
        .backend = inference_bridge.OptionalString.init(spec.backend),
    };
}

/// Matches a parsed argument against a flag's canonical spelling or its
/// deprecated `--ha-*` alias. The `--ha-*` spellings predate the `ha` ->
/// `standby` rename (see HOT_STANDBY.md "Naming") and are kept working for
/// one minor release because the Kubernetes operator still generates them.
fn flagMatches(arg: []const u8, canonical: []const u8, legacy_alias: []const u8) bool {
    return std.mem.eql(u8, arg, canonical) or std.mem.eql(u8, arg, legacy_alias);
}

fn parseCli(alloc: std.mem.Allocator, args: *std.process.Args.Iterator) !CliConfig {
    var cfg = CliConfig{};
    errdefer cfg.deinit(alloc);
    while (args.next()) |arg| {
        if (std.mem.eql(u8, arg, "--help") or std.mem.eql(u8, arg, "-h")) {
            cfg.help = true;
            continue;
        }
        if (std.mem.eql(u8, arg, "--experimental")) {
            cfg.experimental = true;
            continue;
        }
        if (std.mem.eql(u8, arg, "--config")) {
            cfg.config_path = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--id")) {
            cfg.local_node_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--host")) {
            cfg.bind_host = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--port")) {
            cfg.bind_port = try std.fmt.parseInt(u16, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--health-port")) {
            cfg.health_port = try std.fmt.parseInt(u16, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--health")) {
            const value = args.next() orelse return error.InvalidArguments;
            cfg.health_enabled = parseBoolFlag(value) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.startsWith(u8, arg, "--health=")) {
            cfg.health_enabled = parseBoolFlag(arg["--health=".len..]) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--control-tick-ms")) {
            cfg.control_tick_ms = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--auth")) {
            const value = args.next() orelse return error.InvalidArguments;
            cfg.auth_enabled = parseBoolFlag(value) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.startsWith(u8, arg, "--auth=")) {
            cfg.auth_enabled = parseBoolFlag(arg["--auth=".len..]) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--ard-publisher-domain")) {
            cfg.ard_publisher_domain = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--ard-base-url")) {
            cfg.ard_base_url = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--ard-display-name")) {
            cfg.ard_display_name = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--ard-public-catalog")) {
            const value = args.next() orelse return error.InvalidArguments;
            cfg.ard_public_catalog_enabled = parseBoolFlag(value) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.startsWith(u8, arg, "--ard-public-catalog=")) {
            cfg.ard_public_catalog_enabled = parseBoolFlag(arg["--ard-public-catalog=".len..]) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--models-dir")) {
            cfg.inference_models_dir = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--ml-dir")) {
            cfg.inference_ml_dir = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--inference-host-budget-mb")) {
            cfg.inference_host_budget_mb = try std.fmt.parseInt(usize, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--inference-backend-budget-mb")) {
            cfg.inference_backend_budget_mb = try std.fmt.parseInt(usize, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--inference-combined-budget-mb")) {
            cfg.inference_combined_budget_mb = try std.fmt.parseInt(usize, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--inference-kv-budget-mb")) {
            cfg.inference_kv_budget_mb = try std.fmt.parseInt(usize, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--inference-scratch-budget-mb")) {
            cfg.inference_scratch_budget_mb = try std.fmt.parseInt(usize, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--process-memory-budget-mb") or
            std.mem.eql(u8, arg, "--inference-process-memory-budget-mb"))
        {
            cfg.inference_process_memory_budget_mb = try std.fmt.parseInt(usize, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (std.mem.eql(u8, arg, "--kernel-jit-mode")) {
            cfg.inference_kernel_jit_mode = std.meta.stringToEnum(
                antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode,
                args.next() orelse return error.InvalidArguments,
            ) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--preload-model")) {
            try cfg.inference_preload_models.append(alloc, try parsePreloadModelFlag(args.next() orelse return error.InvalidArguments));
            continue;
        }
        if (std.mem.eql(u8, arg, "--data-dir")) {
            cfg.data_dir = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--storage-engine")) {
            cfg.storage_engine = parseStorageEngine(args.next() orelse return error.InvalidArguments) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.startsWith(u8, arg, "--storage-engine=")) {
            cfg.storage_engine = parseStorageEngine(arg["--storage-engine=".len..]) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--storage-path")) {
            cfg.storage_path = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.startsWith(u8, arg, "--storage-path=")) {
            cfg.storage_path = arg["--storage-path=".len..];
            continue;
        }
        if (std.mem.eql(u8, arg, "--fsync")) {
            cfg.storage_fsync = parseBoolFlag(args.next() orelse return error.InvalidArguments) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.startsWith(u8, arg, "--fsync=")) {
            cfg.storage_fsync = parseBoolFlag(arg["--fsync=".len..]) orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--replica-root-dir")) {
            cfg.replica_root_dir = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--replica-catalog-path")) {
            cfg.replica_catalog_path = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--snapshot-root-dir")) {
            cfg.snapshot_root_dir = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--extension-package-store")) {
            cfg.extension_package_store_dir = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--secret-store-path")) {
            try cfg.secret_store_paths.append(alloc, args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-primary-log", "--ha-primary-log")) {
            cfg.ha_primary_log = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-primary-slots", "--ha-primary-slots")) {
            cfg.ha_primary_slots = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-primary-node-id", "--ha-primary-node-id")) {
            cfg.ha_primary_node_id = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-seed-capture-root", "--ha-seed-capture-root")) {
            cfg.ha_seed_capture_root = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-fence-wal", "--ha-fence-wal")) {
            cfg.ha_fence_wal = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-former-primary-log", "--ha-former-primary-log")) {
            cfg.ha_former_primary_log = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (std.mem.eql(u8, arg, "--admin-token-env")) {
            cfg.admin_token_env = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-retention-max-lag-lsn", "--ha-retention-max-lag-lsn")) {
            cfg.ha_retention_max_lag_lsn = try parsePositiveU64(args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-retention-max-retained-bytes", "--ha-retention-max-retained-bytes")) {
            cfg.ha_retention_max_retained_bytes = try parsePositiveU64(args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-retention-max-retained-age-ns", "--ha-retention-max-retained-age-ns")) {
            cfg.ha_retention_max_retained_age_ns = try parsePositiveU64(args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-sync-mode", "--ha-sync-mode")) {
            cfg.ha_sync_mode = try parseHASyncDurabilityMode(args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-sync-selection", "--ha-sync-selection")) {
            cfg.ha_sync_selection = try parseHASyncStandbySelection(args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-sync-required", "--ha-sync-required")) {
            cfg.ha_sync_required = try parsePositiveUsize(args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-sync-standby", "--ha-sync-standby")) {
            try cfg.ha_sync_standby_names.append(alloc, args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-sync-failure", "--ha-sync-failure")) {
            cfg.ha_sync_failure_policy = try parseHASyncFailurePolicy(args.next() orelse return error.InvalidArguments);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-log", "--ha-standby-log")) {
            cfg.ha_standby_log = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-progress", "--ha-standby-progress")) {
            cfg.ha_standby_progress = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-node-id", "--ha-standby-node-id")) {
            cfg.ha_standby_node_id = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-upstream-url", "--ha-standby-upstream-url")) {
            cfg.ha_standby_upstream_url = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-slot", "--ha-standby-slot")) {
            cfg.ha_standby_slot = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-target-root", "--ha-startup-target-root")) {
            cfg.ha_startup_target_root = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-topology-id", "--ha-startup-topology-id")) {
            cfg.ha_startup_topology_id = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-topology-generation", "--ha-startup-topology-generation")) {
            cfg.ha_startup_topology_generation = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-generation", "--ha-startup-generation")) {
            cfg.ha_startup_generation = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-slot-name", "--ha-startup-slot-name")) {
            cfg.ha_startup_slot_name = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-timeline-id", "--ha-startup-timeline-id")) {
            cfg.ha_startup_timeline_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-epoch", "--ha-startup-epoch")) {
            cfg.ha_startup_epoch = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-target-pvc-name", "--ha-startup-target-pvc-name")) {
            cfg.ha_startup_target_pvc_name = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-target-pvc-uid", "--ha-startup-target-pvc-uid")) {
            cfg.ha_startup_target_pvc_uid = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-manifest-sha256", "--ha-startup-manifest-sha256")) {
            cfg.ha_startup_manifest_sha256 = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-aggregate-sha256", "--ha-startup-aggregate-sha256")) {
            cfg.ha_startup_aggregate_sha256 = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-seed-receipt-sha256", "--ha-startup-seed-receipt-sha256")) {
            cfg.ha_startup_seed_receipt_sha256 = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-capture-receipt-sha256", "--ha-startup-capture-receipt-sha256")) {
            cfg.ha_startup_capture_receipt_sha256 = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-materialized-receipt-sha256", "--ha-startup-materialized-receipt-sha256")) {
            cfg.ha_startup_materialized_receipt_sha256 = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-materialized-aggregate-sha256", "--ha-startup-materialized-aggregate-sha256")) {
            cfg.ha_startup_materialized_aggregate_sha256 = args.next() orelse return error.InvalidArguments;
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-target-local-node-id", "--ha-startup-target-local-node-id")) {
            cfg.ha_startup_target_local_node_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-startup-target-replica-id", "--ha-startup-target-replica-id")) {
            cfg.ha_startup_target_replica_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-cluster-id", "--ha-cluster-id")) {
            cfg.ha_cluster_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-shard-id", "--ha-shard-id")) {
            cfg.ha_shard_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-table-id", "--ha-table-id")) {
            cfg.ha_table_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-timeline-id", "--ha-timeline-id")) {
            cfg.ha_timeline_id = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        if (flagMatches(arg, "--hot-standby-epoch", "--ha-epoch")) {
            cfg.ha_epoch = try std.fmt.parseInt(u64, args.next() orelse return error.InvalidArguments, 10);
            continue;
        }
        return error.InvalidArguments;
    }
    return cfg;
}

fn parseStorageEngine(value: []const u8) ?antfly.common.config.StorageEngine {
    if (std.mem.eql(u8, value, "local")) return .local;
    if (std.mem.eql(u8, value, "lite")) return .lite;
    if (std.mem.eql(u8, value, "object")) return .object;
    return null;
}

fn resolveLocalBaseDir(
    alloc: std.mem.Allocator,
    cli: CliConfig,
    cfg: ?*const antfly.common.config.Config,
) ![]u8 {
    if (cli.data_dir) |path| return try normalizeResolvedPathAlloc(alloc, path);
    return try antfly.common.config.resolveLocalBaseDir(alloc, cfg);
}

fn configLocalBaseDirHintFromRaw(alloc: std.mem.Allocator, raw: []const u8) !?[]u8 {
    var parsed = try std.json.parseFromSlice(std.json.Value, alloc, raw, .{});
    defer parsed.deinit();
    const root = switch (parsed.value) {
        .object => |object| object,
        else => return error.InvalidConfig,
    };
    const storage_value = root.get("storage") orelse return null;
    const storage = switch (storage_value) {
        .object => |object| object,
        else => return error.InvalidConfig,
    };
    const local_value = storage.get("local") orelse return null;
    const local = switch (local_value) {
        .object => |object| object,
        else => return error.InvalidConfig,
    };
    const base_value = local.get("base_dir") orelse return null;
    const base_dir = switch (base_value) {
        .string => |value| value,
        else => return error.InvalidConfig,
    };
    if (antfly.common.secrets.parseSecretReference(base_dir) != null)
        return error.InvalidConfig;
    return try alloc.dupe(u8, base_dir);
}

fn configLocalBaseDirHintFromPath(alloc: std.mem.Allocator, path: []const u8) !?[]u8 {
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const raw = try std.Io.Dir.cwd().readFileAlloc(io_impl.io(), path, alloc, .limited(16 * 1024 * 1024));
    defer alloc.free(raw);
    return try configLocalBaseDirHintFromRaw(alloc, raw);
}

fn resolveDefaultSecretStorePathBeforeConfig(alloc: std.mem.Allocator, cli: CliConfig) ![]u8 {
    const raw_base = if (cli.data_dir) |path|
        try alloc.dupe(u8, path)
    else if (cli.config_path) |config_path|
        (try configLocalBaseDirHintFromPath(alloc, config_path)) orelse
            try antfly.common.config.defaultLocalBaseDir(alloc)
    else
        try antfly.common.config.defaultLocalBaseDir(alloc);
    defer alloc.free(raw_base);
    const base = try normalizeResolvedPathAlloc(alloc, raw_base);
    defer alloc.free(base);
    const joined = try std.fs.path.join(alloc, &.{ base, "secrets.json" });
    defer alloc.free(joined);
    return try normalizeResolvedPathAlloc(alloc, joined);
}

fn resolvePaths(
    alloc: std.mem.Allocator,
    cli: CliConfig,
    cfg: ?*const antfly.common.config.Config,
) !ResolvedPaths {
    const local_base = try resolveLocalBaseDir(alloc, cli, cfg);
    defer alloc.free(local_base);
    const data_base = try std.fmt.allocPrint(alloc, "{s}/data", .{local_base});
    defer alloc.free(data_base);
    const metadata_base = try std.fmt.allocPrint(alloc, "{s}/metadata", .{local_base});
    defer alloc.free(metadata_base);

    const replica_root_dir = if (cli.replica_root_dir) |path|
        try normalizeResolvedPathAlloc(alloc, path)
    else blk: {
        const raw = try std.fmt.allocPrint(alloc, "{s}/replicas", .{data_base});
        defer alloc.free(raw);
        break :blk try normalizeResolvedPathAlloc(alloc, raw);
    };
    errdefer alloc.free(replica_root_dir);
    const replica_catalog_path = if (cli.replica_catalog_path) |path|
        try normalizeResolvedPathAlloc(alloc, path)
    else blk: {
        const raw = try std.fmt.allocPrint(alloc, "{s}/catalog.txt", .{data_base});
        defer alloc.free(raw);
        break :blk try normalizeResolvedPathAlloc(alloc, raw);
    };
    errdefer alloc.free(replica_catalog_path);
    const local_metadata_catalog_path = blk: {
        const raw = try std.fmt.allocPrint(alloc, "{s}/local-metadata.json", .{metadata_base});
        defer alloc.free(raw);
        break :blk try normalizeResolvedPathAlloc(alloc, raw);
    };
    errdefer alloc.free(local_metadata_catalog_path);
    const snapshot_root_dir = if (cli.snapshot_root_dir) |path|
        try normalizeResolvedPathAlloc(alloc, path)
    else blk: {
        const raw = try std.fmt.allocPrint(alloc, "{s}/snapshots", .{data_base});
        defer alloc.free(raw);
        break :blk try normalizeResolvedPathAlloc(alloc, raw);
    };
    errdefer alloc.free(snapshot_root_dir);
    const extension_package_store_dir = try resolveExtensionPackageStoreDir(alloc, cli.extension_package_store_dir, local_base);
    errdefer alloc.free(extension_package_store_dir);
    const secret_store_path = if (cli.primarySecretStorePath()) |path|
        try normalizeResolvedPathAlloc(alloc, path)
    else blk: {
        const raw = try std.fmt.allocPrint(alloc, "{s}/secrets.json", .{local_base});
        defer alloc.free(raw);
        break :blk try normalizeResolvedPathAlloc(alloc, raw);
    };
    errdefer alloc.free(secret_store_path);
    const auth_store_root_dir = blk: {
        const raw = try std.fmt.allocPrint(alloc, "{s}/auth", .{metadata_base});
        defer alloc.free(raw);
        break :blk try normalizeResolvedPathAlloc(alloc, raw);
    };
    errdefer alloc.free(auth_store_root_dir);

    return .{
        .replica_root_dir = replica_root_dir,
        .replica_catalog_path = replica_catalog_path,
        .local_metadata_catalog_path = local_metadata_catalog_path,
        .snapshot_root_dir = snapshot_root_dir,
        .extension_package_store_dir = extension_package_store_dir,
        .secret_store_path = secret_store_path,
        .auth_store_root_dir = auth_store_root_dir,
    };
}

fn initLayeredSecretStore(
    alloc: std.mem.Allocator,
    io: std.Io,
    raw_paths: []const []const u8,
) !antfly.common.secrets.FileStore {
    var normalized_paths: std.ArrayListUnmanaged([]const u8) = .empty;
    defer {
        for (normalized_paths.items) |path| alloc.free(path);
        normalized_paths.deinit(alloc);
    }
    for (raw_paths) |raw_path| {
        const normalized_path = try normalizeResolvedPathAlloc(alloc, raw_path);
        errdefer alloc.free(normalized_path);
        try normalized_paths.append(alloc, normalized_path);
    }
    return try antfly.common.secrets.FileStore.initLayeredWithIo(alloc, io, normalized_paths.items);
}

fn resolveExtensionPackageStoreDir(
    alloc: std.mem.Allocator,
    cli_path: ?[]const u8,
    local_base: []const u8,
) ![]u8 {
    const env_var_z = try alloc.dupeZ(u8, antfly.extensions.wasmtime_runtime.package_store_env);
    defer alloc.free(env_var_z);
    return try resolveExtensionPackageStoreDirWithEnv(
        alloc,
        cli_path,
        local_base,
        platform.env.getenvSlice(env_var_z),
    );
}

fn resolveExtensionPackageStoreDirWithEnv(
    alloc: std.mem.Allocator,
    cli_path: ?[]const u8,
    local_base: []const u8,
    env_path: ?[]const u8,
) ![]u8 {
    if (cli_path) |path| return try normalizeResolvedPathAlloc(alloc, path);
    if (env_path) |path| {
        if (std.mem.trim(u8, path, " \t\r\n").len > 0) {
            return try normalizeResolvedPathAlloc(alloc, path);
        }
    }

    const raw = try std.fmt.allocPrint(alloc, "{s}/extensions", .{local_base});
    defer alloc.free(raw);
    return try normalizeResolvedPathAlloc(alloc, raw);
}

fn normalizeResolvedPathAlloc(alloc: std.mem.Allocator, path: []const u8) ![]u8 {
    if (!std.fs.path.isAbsolute(path)) return try alloc.dupe(u8, path);

    var probe = path;
    while (true) {
        const resolved_z = std.Io.Dir.realPathFileAbsoluteAlloc(std.Options.debug_io, probe, alloc) catch |err| switch (err) {
            error.FileNotFound, error.NotDir => null,
            else => return err,
        };
        if (resolved_z) |resolved| {
            defer alloc.free(resolved);
            const resolved_prefix = resolved[0..resolved.len];
            if (probe.len == path.len) return try alloc.dupe(u8, resolved_prefix);

            const suffix_start: usize = if (probe.len == 1) 1 else probe.len + 1;
            const suffix = path[suffix_start..];
            return try std.fs.path.join(alloc, &.{ resolved_prefix, suffix });
        }

        const parent = std.fs.path.dirname(probe) orelse return try alloc.dupe(u8, path);
        if (parent.len == probe.len) return try alloc.dupe(u8, path);
        probe = parent;
    }
}

fn resolveMetadataRaftListener(
    cli: CliConfig,
    local_node_id: u64,
    cfg: ?*const antfly.common.config.Config,
) antfly.metadata.runtime.ListenerConfig {
    if (cfg) |loaded| {
        if (antfly.metadata.runtime.metadataClusterPeerUrl(loaded, local_node_id)) |url| {
            return antfly.metadata.runtime.parseHostPort(url) catch .{ .bind_host = cli.bind_host orelse "127.0.0.1", .bind_port = 0 };
        }
    }
    return antfly.metadata.runtime.resolveListener(cli.bind_host, null, cfg);
}

fn resolveMetadataApiListener(
    cfg: ?*const antfly.common.config.Config,
    local_node_id: u64,
    fallback_host: []const u8,
) antfly.metadata.runtime.ListenerConfig {
    if (cfg) |loaded| {
        if (antfly.metadata.runtime.metadataOrchestrationPeerUrl(loaded, local_node_id)) |url| {
            return antfly.metadata.runtime.parseHostPort(url) catch .{ .bind_host = fallback_host, .bind_port = 0 };
        }
    }
    return .{ .bind_host = fallback_host, .bind_port = 0 };
}

fn resolveMetadataClusterPeers(
    alloc: std.mem.Allocator,
    cfg: ?*const antfly.common.config.Config,
) ![]antfly.metadata.runtime.MetadataClusterPeer {
    if (cfg) |loaded| return try antfly.metadata.runtime.metadataClusterPeersFromConfig(alloc, loaded);
    return &.{};
}

fn resolvePublicListener(cli: CliConfig) antfly.metadata.runtime.ListenerConfig {
    return .{
        .bind_host = cli.bind_host orelse "127.0.0.1",
        .bind_port = cli.bind_port orelse default_public_port,
    };
}

/// Fills HA flags that were not given on the command line from the config
/// file's `ha` section. Command-line flags always win, so operator-generated
/// argument lists keep their exact meaning; the section only removes the need
/// to repeat the same paths and identity on every invocation. Strings borrow
/// from `cfg`, which outlives `cli` in `run`.
fn applyHAConfigDefaults(alloc: std.mem.Allocator, cli: *CliConfig, cfg: *const antfly.common.config.Config) !void {
    const ha = cfg.ha orelse return;
    if (cli.admin_token_env == null) cli.admin_token_env = ha.admin_token_env;
    if (cli.ha_cluster_id == null) cli.ha_cluster_id = ha.cluster_id;
    if (cli.ha_shard_id == null) cli.ha_shard_id = ha.shard_id;
    if (cli.ha_table_id == null) cli.ha_table_id = ha.table_id;
    if (cli.ha_timeline_id == null) cli.ha_timeline_id = ha.timeline_id;
    if (cli.ha_epoch == null) cli.ha_epoch = ha.epoch;
    if (cli.ha_primary_log == null) cli.ha_primary_log = ha.primary_log;
    if (cli.ha_primary_slots == null) cli.ha_primary_slots = ha.primary_slots;
    if (cli.ha_primary_node_id == null) cli.ha_primary_node_id = ha.primary_node_id;
    if (cli.ha_seed_capture_root == null) cli.ha_seed_capture_root = ha.seed_capture_root;
    if (cli.ha_standby_log == null) cli.ha_standby_log = ha.standby_log;
    if (cli.ha_standby_progress == null) cli.ha_standby_progress = ha.standby_progress;
    if (cli.ha_standby_node_id == null) cli.ha_standby_node_id = ha.standby_node_id;
    if (cli.ha_standby_upstream_url == null) cli.ha_standby_upstream_url = ha.standby_upstream_url;
    if (cli.ha_standby_slot == null) cli.ha_standby_slot = ha.standby_slot;
    if (cli.ha_fence_wal == null) cli.ha_fence_wal = ha.fence_wal;
    if (cli.ha_former_primary_log == null) cli.ha_former_primary_log = ha.former_primary_log;
    if (cli.ha_sync_mode == null) {
        if (ha.sync_mode) |raw| cli.ha_sync_mode = try parseHASyncDurabilityMode(raw);
    }
    if (cli.ha_sync_selection == null) {
        if (ha.sync_selection) |raw| cli.ha_sync_selection = try parseHASyncStandbySelection(raw);
    }
    if (cli.ha_sync_required == null) cli.ha_sync_required = ha.sync_required;
    if (cli.ha_sync_failure_policy == null) {
        if (ha.sync_failure) |raw| cli.ha_sync_failure_policy = try parseHASyncFailurePolicy(raw);
    }
    if (cli.ha_sync_standby_names.items.len == 0) {
        for (ha.sync_standbys) |name| try cli.ha_sync_standby_names.append(alloc, name);
    }
    if (cli.ha_retention_max_lag_lsn == null) cli.ha_retention_max_lag_lsn = ha.retention_max_lag_lsn;
    if (cli.ha_retention_max_retained_bytes == null) cli.ha_retention_max_retained_bytes = ha.retention_max_retained_bytes;
    if (cli.ha_retention_max_retained_age_ns == null) cli.ha_retention_max_retained_age_ns = ha.retention_max_retained_age_ns;
}

fn haPrimaryRequested(cli: CliConfig) bool {
    return cli.ha_primary_log != null or
        cli.ha_primary_slots != null or
        cli.ha_primary_node_id != null;
}

fn haStandbyRequested(cli: CliConfig) bool {
    return cli.ha_standby_log != null or
        cli.ha_standby_progress != null or
        cli.ha_standby_node_id != null or
        cli.ha_standby_upstream_url != null or
        cli.ha_standby_slot != null;
}

fn standaloneNativeAuthorityInitiallyPermitted(cli: CliConfig) bool {
    return !haPrimaryRequested(cli) and !haStandbyRequested(cli);
}

fn haContinuousMutationGuardEnabled(cli: CliConfig) bool {
    // A standby can never acknowledge public state changes: its only legal
    // mutation source is the authenticated replication stream. A primary,
    // however, has a supported catalog-bootstrap phase before a table identity
    // exists. Its continuous WAL is table-scoped, so enabling the fail-closed
    // ingress guard before both identity components are configured would make
    // it impossible to create the table whose identity must be supplied on the
    // HA restart.
    if (haStandbyRequested(cli)) return true;
    return haPrimaryRequested(cli) and
        cli.ha_shard_id != null and
        cli.ha_table_id != null;
}

fn haRemoteApplyMutationsEnabled(policy: antfly.hot_standby.primary.SyncPolicy) bool {
    return policy.mode == .remote_apply and
        policy.failure_policy == .block and
        policy.standby_names.len > 0;
}

fn haIdentityRequested(cli: CliConfig) bool {
    return cli.ha_cluster_id != null or
        cli.ha_shard_id != null or
        cli.ha_table_id != null or
        cli.ha_timeline_id != null or
        cli.ha_epoch != null;
}

fn haStartupGateRequested(cli: CliConfig) bool {
    return cli.ha_startup_target_root != null or
        cli.ha_startup_topology_id != null or
        cli.ha_startup_topology_generation != null or
        cli.ha_startup_generation != null or
        cli.ha_startup_slot_name != null or
        cli.ha_startup_timeline_id != null or
        cli.ha_startup_epoch != null or
        cli.ha_startup_target_pvc_name != null or
        cli.ha_startup_target_pvc_uid != null or
        cli.ha_startup_manifest_sha256 != null or
        cli.ha_startup_aggregate_sha256 != null or
        cli.ha_startup_seed_receipt_sha256 != null or
        cli.ha_startup_capture_receipt_sha256 != null or
        cli.ha_startup_materialized_receipt_sha256 != null or
        cli.ha_startup_materialized_aggregate_sha256 != null or
        cli.ha_startup_target_local_node_id != null or
        cli.ha_startup_target_replica_id != null;
}

fn haSyncPolicyRequested(cli: CliConfig) bool {
    return cli.ha_sync_mode != null or
        cli.ha_sync_selection != null or
        cli.ha_sync_required != null or
        cli.ha_sync_failure_policy != null or
        cli.ha_sync_standby_names.items.len > 0;
}

fn haRetentionPolicyRequested(cli: CliConfig) bool {
    return cli.ha_retention_max_lag_lsn != null or
        cli.ha_retention_max_retained_bytes != null or
        cli.ha_retention_max_retained_age_ns != null;
}

fn validateHARole(cli: CliConfig) !void {
    const primary_requested = haPrimaryRequested(cli);
    const standby_requested = haStandbyRequested(cli);
    if (primary_requested and standby_requested) return error.HAMultipleRolesConfigured;
    if (haIdentityRequested(cli) and !primary_requested and !standby_requested) return error.HARoleMissing;
    if (cli.ha_fence_wal != null and !primary_requested and !standby_requested) return error.HARoleMissing;
    if (cli.ha_former_primary_log != null and !primary_requested and !standby_requested) return error.HARoleMissing;
    if (cli.ha_seed_capture_root != null and !primary_requested and !standby_requested) return error.HARoleMissing;
    if (haStartupGateRequested(cli) and !primary_requested and !standby_requested) return error.HAStartupGateRequiresHARole;
    if (cli.ha_former_primary_log != null) {
        _ = try requireHAPath(cli.ha_former_primary_log, error.HAFormerPrimaryLogInvalid, error.HAFormerPrimaryLogInvalid);
    }
    if (cli.admin_token_env) |env_var| {
        switch (antfly.hot_standby.validation.classifyHAString(env_var)) {
            .ok => {},
            .missing => return error.AdminTokenEnvMissing,
            .padded => return error.AdminTokenEnvInvalid,
        }
        if (!antfly.hot_standby.validation.isEnvVarName(env_var)) return error.AdminTokenEnvInvalid;
    }
    if (primary_requested or standby_requested) {
        _ = try requireHAPath(cli.ha_fence_wal, error.HAFenceWalMissing, error.HAFenceWalInvalid);
    }
    if (primary_requested or standby_requested) try validateHAIdentity(cli);
    if (primary_requested) try validateHAPrimaryRoleComplete(cli);
    if (standby_requested) try validateHAStandbyRoleComplete(cli);
    if (haRetentionPolicyRequested(cli) and !primary_requested) return error.HARetentionPolicyRequiresPrimary;
    // A standby must preload the policy it will enforce if promotion opens a
    // primary in place. The mirror remains inactive while the standby owns the
    // runtime; it becomes authoritative only after the promoted-primary
    // handoff. Sync flags without any HA role are still invalid.
    if (haSyncPolicyRequested(cli) and !primary_requested and !standby_requested) return error.HASyncPolicyRequiresPrimary;
}

fn validateHAIdentity(cli: CliConfig) !void {
    if (cli.ha_cluster_id == null) return error.HAClusterIdMissing;
    if (cli.ha_timeline_id == null) return error.HATimelineIdMissing;
    if (cli.ha_epoch == null) return error.HAEpochMissing;
}

fn requireHAString(value: ?[]const u8, comptime missing_err: anyerror, comptime padded_err: anyerror) ![]const u8 {
    switch (antfly.hot_standby.validation.classifyHAString(value)) {
        .ok => return value.?,
        .missing => return missing_err,
        .padded => return padded_err,
    }
}

fn requireHAPath(value: ?[]const u8, comptime missing_err: anyerror, comptime invalid_err: anyerror) ![]const u8 {
    const raw = try requireHAString(value, missing_err, invalid_err);
    if (!antfly.hot_standby.validation.isAbsoluteNormalizedPath(raw)) return invalid_err;
    return raw;
}

fn requireHAPathWithinRoot(value: ?[]const u8, root: []const u8, comptime missing_err: anyerror, comptime invalid_err: anyerror) ![]const u8 {
    const raw = try requireHAPath(value, missing_err, invalid_err);
    if (!antfly.hot_standby.validation.isAbsoluteNormalizedPathWithinRoot(raw, root)) return invalid_err;
    return raw;
}

fn requireHAIdentifier(value: ?[]const u8, comptime missing_err: anyerror, comptime invalid_err: anyerror) ![]const u8 {
    const raw = try requireHAString(value, missing_err, invalid_err);
    if (!antfly.hot_standby.validation.isIdentifier(raw)) return invalid_err;
    return raw;
}

fn validateHAPrimaryRoleComplete(cli: CliConfig) !void {
    _ = try requireHAPath(cli.ha_primary_log, error.HAPrimaryLogMissing, error.HAPrimaryLogInvalid);
    _ = try requireHAPath(cli.ha_primary_slots, error.HAPrimarySlotsMissing, error.HAPrimarySlotsInvalid);
    _ = try requireHAIdentifier(cli.ha_primary_node_id, error.HAPrimaryNodeIdMissing, error.HAPrimaryNodeIdInvalid);
}

fn validateHAStandbyRoleComplete(cli: CliConfig) !void {
    _ = try requireHAPath(cli.ha_standby_log, error.HAStandbyLogMissing, error.HAStandbyLogInvalid);
    _ = try requireHAPath(cli.ha_standby_progress, error.HAStandbyProgressMissing, error.HAStandbyProgressInvalid);
    _ = try requireHAIdentifier(cli.ha_standby_node_id, error.HAStandbyNodeIdMissing, error.HAStandbyNodeIdInvalid);
}

fn validateHAPathsUnderRoot(cli: CliConfig, data_root: []const u8) !void {
    if (cli.ha_former_primary_log != null) {
        _ = try requireHAPathWithinRoot(cli.ha_former_primary_log, data_root, error.HAFormerPrimaryLogInvalid, error.HAFormerPrimaryLogInvalid);
    }
    if (haPrimaryRequested(cli) or haStandbyRequested(cli)) {
        _ = try requireHAPathWithinRoot(cli.ha_fence_wal, data_root, error.HAFenceWalMissing, error.HAFenceWalInvalid);
        if (cli.ha_seed_capture_root != null) {
            _ = try requireHAPathWithinRoot(cli.ha_seed_capture_root, data_root, error.HASeedCaptureRootMissing, error.HASeedCaptureRootInvalid);
        }
    }
    if (haPrimaryRequested(cli)) {
        _ = try requireHAPathWithinRoot(cli.ha_primary_log, data_root, error.HAPrimaryLogMissing, error.HAPrimaryLogInvalid);
        _ = try requireHAPathWithinRoot(cli.ha_primary_slots, data_root, error.HAPrimarySlotsMissing, error.HAPrimarySlotsInvalid);
    }
    if (haStandbyRequested(cli)) {
        _ = try requireHAPathWithinRoot(cli.ha_standby_log, data_root, error.HAStandbyLogMissing, error.HAStandbyLogInvalid);
        _ = try requireHAPathWithinRoot(cli.ha_standby_progress, data_root, error.HAStandbyProgressMissing, error.HAStandbyProgressInvalid);
    }
    if (haStartupGateRequested(cli)) {
        _ = try requireHAPathWithinRoot(cli.ha_startup_target_root, data_root, error.HAStartupTargetRootMissing, error.HAStartupTargetRootInvalid);
    }
}

fn haStartupExpectationFromCli(cli: CliConfig) !?antfly.hot_standby.seed_activation.StartupExpectation {
    if (!haStartupGateRequested(cli)) return null;
    const primary_requested = haPrimaryRequested(cli);
    const standby_requested = haStandbyRequested(cli);
    if (!primary_requested and !standby_requested) return error.HAStartupGateRequiresHARole;
    const runtime_node_id = if (primary_requested)
        try requireHAIdentifier(cli.ha_primary_node_id, error.HAPrimaryNodeIdMissing, error.HAPrimaryNodeIdInvalid)
    else
        try requireHAIdentifier(cli.ha_standby_node_id, error.HAStandbyNodeIdMissing, error.HAStandbyNodeIdInvalid);
    const startup_timeline_id = cli.ha_startup_timeline_id orelse if (standby_requested)
        cli.ha_timeline_id orelse return error.HATimelineIdMissing
    else
        return error.HAStartupTimelineIdMissing;
    const startup_epoch = cli.ha_startup_epoch orelse if (standby_requested)
        cli.ha_epoch orelse return error.HAEpochMissing
    else
        return error.HAStartupEpochMissing;
    const current_timeline_id = cli.ha_timeline_id orelse return error.HATimelineIdMissing;
    const current_epoch = cli.ha_epoch orelse return error.HAEpochMissing;
    if (standby_requested) {
        if (startup_timeline_id != current_timeline_id or startup_epoch != current_epoch)
            return error.HAStartupReplicationIdentityMismatch;
    } else if (startup_timeline_id > current_timeline_id or startup_epoch > current_epoch or
        (startup_timeline_id == current_timeline_id and startup_epoch == current_epoch))
    {
        // A promoted primary may reopen only the exact generation materialized
        // on a predecessor boundary. Equal, future, or incomparable authority
        // would turn a seed receipt into an alternate primary-creation path.
        return error.HAStartupReplicationIdentityMismatch;
    }
    const binding = antfly.hot_standby.seed_activation.ActivationBinding{
        .topology_id = try requireHAIdentifier(cli.ha_startup_topology_id, error.HAStartupTopologyIdMissing, error.HAStartupTopologyIdInvalid),
        .topology_generation = cli.ha_startup_topology_generation orelse return error.HAStartupTopologyGenerationMissing,
        .node_id = runtime_node_id,
        .target_pvc_name = try requireHAIdentifier(cli.ha_startup_target_pvc_name, error.HAStartupTargetPVCNameMissing, error.HAStartupTargetPVCNameInvalid),
        .target_pvc_uid = try requireHAIdentifier(cli.ha_startup_target_pvc_uid, error.HAStartupTargetPVCUIDMissing, error.HAStartupTargetPVCUIDInvalid),
    };
    const capture_receipt_sha256 = (try optionalHAStartupDigest(cli.ha_startup_capture_receipt_sha256)) orelse
        return error.HAStartupCaptureReceiptSHA256Missing;
    const materialized_receipt_sha256 = (try optionalHAStartupDigest(cli.ha_startup_materialized_receipt_sha256)) orelse
        return error.HAStartupMaterializedReceiptSHA256Missing;
    const materialized_aggregate_sha256 = (try optionalHAStartupDigest(cli.ha_startup_materialized_aggregate_sha256)) orelse
        return error.HAStartupMaterializedAggregateSHA256Missing;
    const target_local_node_id = cli.ha_startup_target_local_node_id orelse
        return error.HAStartupTargetLocalNodeIDMissing;
    if (target_local_node_id == 0) return error.HAStartupTargetLocalNodeIDInvalid;
    if (target_local_node_id != (cli.local_node_id orelse 1)) return error.HAStartupTargetLocalNodeIDMismatch;
    const target_replica_id = cli.ha_startup_target_replica_id orelse
        return error.HAStartupTargetReplicaIDMissing;
    if (target_replica_id == 0) return error.HAStartupTargetReplicaIDInvalid;
    // Standalone owns one local replica whose identity is fixed at 1. Opening
    // a generation materialized for any other replica would silently point the
    // catalog at a topology this runtime cannot own.
    if (target_replica_id != 1) return error.HAStartupTargetReplicaIDMismatch;
    const startup_slot_name = cli.ha_startup_slot_name orelse cli.ha_standby_slot orelse
        return error.HAStartupSlotNameMissing;
    return .{
        .target_root = try requireHAPath(cli.ha_startup_target_root, error.HAStartupTargetRootMissing, error.HAStartupTargetRootInvalid),
        .expected = .{
            .generation = try requireHAIdentifier(cli.ha_startup_generation, error.HAStartupGenerationMissing, error.HAStartupGenerationInvalid),
            .slot_name = try requireHAIdentifier(startup_slot_name, error.HAStartupSlotNameMissing, error.HAStartupSlotNameInvalid),
            .identity = .{
                .cluster_id = cli.ha_cluster_id orelse return error.HAClusterIdMissing,
                .shard_id = cli.ha_shard_id orelse 0,
                .table_id = cli.ha_table_id orelse 0,
                .timeline_id = startup_timeline_id,
                .epoch = startup_epoch,
            },
            .binding = binding,
            .capture_receipt_sha256 = capture_receipt_sha256,
        },
        .binding = binding,
        .manifest_sha256 = try optionalHAStartupDigest(cli.ha_startup_manifest_sha256),
        .aggregate_sha256 = try optionalHAStartupDigest(cli.ha_startup_aggregate_sha256),
        .seed_receipt_sha256 = try optionalHAStartupDigest(cli.ha_startup_seed_receipt_sha256),
        .capture_receipt_sha256 = capture_receipt_sha256,
        .materialized_receipt_sha256 = materialized_receipt_sha256,
        .materialized_aggregate_sha256 = materialized_aggregate_sha256,
        .target_local_node_id = target_local_node_id,
        .target_replica_id = target_replica_id,
    };
}

fn optionalHAStartupDigest(value: ?[]const u8) !?[]const u8 {
    const digest = value orelse return null;
    if (digest.len != 64) return error.HAStartupDigestInvalid;
    for (digest) |byte| {
        if (!((byte >= '0' and byte <= '9') or (byte >= 'a' and byte <= 'f'))) return error.HAStartupDigestInvalid;
    }
    return digest;
}

fn haStandbyReplicationConfigFromCli(cli: CliConfig) !?antfly.data.runtime.HAStandbyReplicationConfig {
    return try haStandbyReplicationConfigFromCliWithBearerToken(cli, null);
}

fn haStandbyReplicationConfigFromCliWithBearerToken(
    cli: CliConfig,
    bearer_token: ?[]const u8,
) !?antfly.data.runtime.HAStandbyReplicationConfig {
    if (cli.ha_standby_upstream_url == null and cli.ha_standby_slot == null) return null;
    const upstream = try requireHAString(cli.ha_standby_upstream_url, error.HAStandbyUpstreamUrlMissing, error.HAStandbyUpstreamUrlInvalid);
    const slot = try requireHAIdentifier(cli.ha_standby_slot, error.HAStandbySlotMissing, error.HAStandbySlotInvalid);
    const parsed = antfly.hot_standby.validation.parseURLNoHiddenWhitespace(upstream) catch return error.HAStandbyUpstreamUrlInvalid;
    if (!isHAReplicationUpstreamScheme(parsed)) return error.HAStandbyUpstreamUrlInvalid;
    if (parsed.host == null) return error.HAStandbyUpstreamUrlInvalid;
    return .{
        .upstream_base_uri = upstream,
        .slot_name = slot,
        .bearer_token = bearer_token,
        .standby_log_path = cli.ha_standby_log,
        .standby_progress_path = cli.ha_standby_progress,
    };
}

fn isHAReplicationUpstreamScheme(parsed: std.Uri) bool {
    return std.mem.eql(u8, parsed.scheme, "http") or std.mem.eql(u8, parsed.scheme, "https");
}

const OwnedHASyncPolicy = struct {
    policy: antfly.hot_standby.primary.SyncPolicy = .{},
    standby_names: []const []const u8 = &.{},

    fn deinit(self: *OwnedHASyncPolicy, alloc: std.mem.Allocator) void {
        if (self.standby_names.len > 0) alloc.free(self.standby_names);
        self.* = undefined;
    }
};

fn haSyncPolicyFromCli(alloc: std.mem.Allocator, cli: CliConfig) !OwnedHASyncPolicy {
    if (!haSyncPolicyRequested(cli)) return .{};
    if (!haPrimaryRequested(cli) and !haStandbyRequested(cli)) return error.HASyncPolicyRequiresPrimary;

    const names = try alloc.alloc([]const u8, cli.ha_sync_standby_names.items.len);
    errdefer alloc.free(names);
    @memcpy(names, cli.ha_sync_standby_names.items);
    const selection = cli.ha_sync_selection orelse .any;
    if (selection == .all and cli.ha_sync_required != null) return error.InvalidHASyncPolicy;

    const policy = antfly.hot_standby.primary.SyncPolicy{
        .mode = cli.ha_sync_mode orelse .remote_write,
        .selection = selection,
        .required = if (selection == .all) names.len else cli.ha_sync_required orelse 1,
        .standby_names = names,
        .failure_policy = cli.ha_sync_failure_policy orelse .block,
    };
    try validateHASyncPolicy(policy);

    return .{
        .policy = policy,
        .standby_names = names,
    };
}

fn haRetentionPolicyFromCli(cli: CliConfig) !antfly.hot_standby.slot_store.RetentionPolicy {
    if (!haRetentionPolicyRequested(cli)) return .{};
    if (!haPrimaryRequested(cli)) return error.HARetentionPolicyRequiresPrimary;
    return .{
        .max_lag_lsn = cli.ha_retention_max_lag_lsn orelse 0,
        .max_retained_bytes = cli.ha_retention_max_retained_bytes orelse 0,
        .max_retained_age_ns = cli.ha_retention_max_retained_age_ns orelse 0,
    };
}

fn validateHASyncPolicy(policy: antfly.hot_standby.primary.SyncPolicy) !void {
    if (policy.required == 0) return error.InvalidHASyncPolicy;
    if (policy.mode == .async) return;
    if (policy.standby_names.len == 0) return error.InvalidHASyncPolicy;
    if (policy.selection != .all and policy.required > policy.standby_names.len) {
        return error.InvalidHASyncPolicy;
    }
}

fn haPrimaryIdentity(cli: CliConfig) !antfly.hot_standby.primary.Identity {
    return .{
        .cluster_id = cli.ha_cluster_id orelse return error.HAClusterIdMissing,
        .shard_id = cli.ha_shard_id orelse 0,
        .table_id = cli.ha_table_id orelse 0,
        .timeline_id = cli.ha_timeline_id orelse return error.HATimelineIdMissing,
        .epoch = cli.ha_epoch orelse return error.HAEpochMissing,
    };
}

/// Moves an existing pre-0.3 `<root>/ha/` hot-standby tree to the canonical
/// `<root>/standby/` layout, once, before any hot-standby store below is
/// opened. See `storage/hot_standby/layout.zig` for the exact algorithm.
/// A no-op when no hot-standby path is configured at all, and idempotent on
/// every later startup once a root has been migrated.
fn migrateHALegacyLayoutFromCli(alloc: std.mem.Allocator, io: std.Io, cli: CliConfig) !void {
    const candidates = [_]?[]const u8{
        cli.ha_primary_log,
        cli.ha_primary_slots,
        cli.ha_standby_log,
        cli.ha_standby_progress,
        cli.ha_fence_wal,
        cli.ha_former_primary_log,
        cli.ha_seed_capture_root,
        cli.ha_startup_target_root,
    };
    var configured_paths: [candidates.len][]const u8 = undefined;
    var count: usize = 0;
    for (candidates) |maybe_path| {
        const path = maybe_path orelse continue;
        configured_paths[count] = path;
        count += 1;
    }
    if (count == 0) return;

    const report = try antfly.hot_standby.layout.migrateLegacyLayout(io, alloc, configured_paths[0..count]);
    if (report.changed()) {
        std.log.info(
            "standalone hot-standby layout migration moved legacy state: dirs_renamed={d} files_renamed={d}",
            .{ report.dirs_renamed, report.files_renamed },
        );
    }
    if (report.coexisting_roots != 0) {
        std.log.warn(
            "standalone hot-standby layout migration found {d} root(s) with both a legacy 'ha' tree and a canonical 'standby' tree present; the legacy tree was left in place",
            .{report.coexisting_roots},
        );
    }
}

fn openHAPrimaryFromCli(alloc: std.mem.Allocator, io: std.Io, cli: CliConfig) !?antfly.hot_standby.primary.Primary {
    if (!haPrimaryRequested(cli)) return null;
    const log_path = cli.ha_primary_log orelse return error.HAPrimaryLogMissing;
    const slots_path = cli.ha_primary_slots orelse return error.HAPrimarySlotsMissing;
    if (cli.ha_primary_node_id == null) return error.HAPrimaryNodeIdMissing;

    try ensureParent(io, log_path);
    try ensureParent(io, slots_path);

    const log_z = try alloc.dupeZ(u8, log_path);
    defer alloc.free(log_z);
    const slots_z = try alloc.dupeZ(u8, slots_path);
    defer alloc.free(slots_z);

    return try antfly.hot_standby.primary.Primary.open(alloc, log_z.ptr, slots_z.ptr, try haPrimaryIdentity(cli), .{});
}

fn haStandbyIdentity(cli: CliConfig) !antfly.hot_standby.standby.Identity {
    return .{
        .cluster_id = cli.ha_cluster_id orelse return error.HAClusterIdMissing,
        .shard_id = cli.ha_shard_id orelse 0,
        .table_id = cli.ha_table_id orelse 0,
        .timeline_id = cli.ha_timeline_id orelse return error.HATimelineIdMissing,
        .epoch = cli.ha_epoch orelse return error.HAEpochMissing,
    };
}

fn openHAStandbyFromCli(alloc: std.mem.Allocator, io: std.Io, cli: CliConfig) !?antfly.hot_standby.standby.Standby {
    if (!haStandbyRequested(cli)) return null;
    const log_path = cli.ha_standby_log orelse return error.HAStandbyLogMissing;
    const progress_path = cli.ha_standby_progress orelse return error.HAStandbyProgressMissing;
    if (cli.ha_standby_node_id == null) return error.HAStandbyNodeIdMissing;

    try ensureParent(io, log_path);
    try ensureParent(io, progress_path);

    const log_z = try alloc.dupeZ(u8, log_path);
    defer alloc.free(log_z);
    const progress_z = try alloc.dupeZ(u8, progress_path);
    defer alloc.free(progress_z);

    return try antfly.hot_standby.standby.Standby.open(alloc, log_z.ptr, progress_z.ptr, try haStandbyIdentity(cli), .{});
}

/// The activated storage snapshot already contains every mutation through the
/// receipt checkpoint. Bind an empty standby receive stream to that exact
/// boundary before its first upstream fetch so it starts at checkpoint + 1.
/// Existing progress is accepted only when it is at least as durable as the
/// same validated snapshot; silently combining older receive state with newer
/// materialized data would make both safe-read and promotion LSNs untrustworthy.
fn bootstrapHAStandbyAtActivatedCheckpoint(
    alloc: std.mem.Allocator,
    standby: *antfly.hot_standby.standby.Standby,
    generation: []const u8,
    slot_name: []const u8,
    checkpoint_lsn: u64,
) !void {
    const progress = standby.currentProgress();
    const payload = try std.json.Stringify.valueAlloc(alloc, .{
        .schema_version = @as(u16, 1),
        .kind = "activated-seed-checkpoint",
        .generation = generation,
        .slot_name = slot_name,
        .checkpoint_lsn = checkpoint_lsn,
    }, .{});
    defer alloc.free(payload);
    if (progress.received_lsn == 0 and progress.applied_lsn == 0 and progress.safe_read_lsn == 0) {
        try standby.bootstrapCheckpoint(checkpoint_lsn, payload);
        return;
    }
    try standby.verifyBootstrapCheckpoint(checkpoint_lsn, payload);
    if (progress.received_lsn < checkpoint_lsn or
        progress.applied_lsn < checkpoint_lsn or
        progress.safe_read_lsn < checkpoint_lsn)
    {
        return error.HAStartupStandbyProgressBehindCheckpoint;
    }
}

fn openHAFenceStoreFromCli(alloc: std.mem.Allocator, io: std.Io, cli: CliConfig) !?antfly.hot_standby.fencing.Store {
    const fence_wal_path = cli.ha_fence_wal orelse return null;
    if (!haPrimaryRequested(cli) and !haStandbyRequested(cli)) return error.HARoleMissing;

    try ensureParent(io, fence_wal_path);

    const fence_wal_z = try alloc.dupeZ(u8, fence_wal_path);
    defer alloc.free(fence_wal_z);

    return try antfly.hot_standby.fencing.Store.open(alloc, fence_wal_z.ptr, .{});
}

fn openHAFormerPrimaryLogFromCli(alloc: std.mem.Allocator, io: std.Io, cli: CliConfig) !?antfly.hot_standby.replication_log.ReplicationLog {
    const former_primary_log_path = cli.ha_former_primary_log orelse return null;
    if (!haPrimaryRequested(cli) and !haStandbyRequested(cli)) return error.HARoleMissing;
    if (cli.ha_primary_log) |primary_log_path| {
        if (std.mem.eql(u8, former_primary_log_path, primary_log_path)) return null;
    }
    if (cli.ha_standby_log) |standby_log_path| {
        if (std.mem.eql(u8, former_primary_log_path, standby_log_path)) return null;
    }

    try ensureParent(io, former_primary_log_path);

    const former_primary_log_z = try alloc.dupeZ(u8, former_primary_log_path);
    defer alloc.free(former_primary_log_z);

    return try antfly.hot_standby.replication_log.ReplicationLog.open(former_primary_log_z.ptr, .{});
}

fn resolveAdminBearerTokenFromCli(alloc: std.mem.Allocator, cli: CliConfig) !?[]u8 {
    const raw_env_var = cli.admin_token_env orelse return null;
    const env_var = std.mem.trim(u8, raw_env_var, " \t\r\n");
    if (env_var.len == 0) return error.AdminTokenEnvMissing;
    if (!antfly.hot_standby.validation.isEnvVarName(env_var)) return error.AdminTokenEnvInvalid;

    const env_var_z = try alloc.dupeZ(u8, env_var);
    defer alloc.free(env_var_z);

    const raw_token_z = std.c.getenv(env_var_z.ptr) orelse return error.AdminTokenMissing;
    const token = std.mem.trim(u8, std.mem.span(raw_token_z), " \t\r\n");
    if (token.len == 0) return error.AdminTokenMissing;
    return try alloc.dupe(u8, token);
}

fn resolveHAPodUID(alloc: std.mem.Allocator) !?[]u8 {
    const raw_z = std.c.getenv("ANTFLY_POD_UID") orelse return null;
    const pod_uid = std.mem.trim(u8, std.mem.span(raw_z), " \t\r\n");
    if (!antfly.hot_standby.validation.isIdentifier(pod_uid)) return error.HAPodUIDInvalid;
    return try alloc.dupe(u8, pod_uid);
}

fn ensureDirPath(io: std.Io, dir_path: []const u8) !void {
    try std.Io.Dir.cwd().createDirPath(io, dir_path);
}

fn ensureParent(io: std.Io, file_path: []const u8) !void {
    if (std.fs.path.dirname(file_path)) |parent| {
        var dir = std.Io.Dir.cwd().openDir(io, parent, .{}) catch |err| switch (err) {
            error.FileNotFound => {
                try std.Io.Dir.cwd().createDirPath(io, parent);
                return;
            },
            else => return err,
        };
        dir.close(io);
    }
}

fn resolveAuthEnabled(cli: CliConfig, cfg: ?*const antfly.common.config.Config) bool {
    if (cli.auth_enabled) |value| return value;
    if (cfg) |loaded| return loaded.auth_enabled;
    return false;
}

fn resolveInferenceModelsDir(cli: CliConfig, cfg: ?*const antfly.common.config.Config) ?[]const u8 {
    if (cli.inference_models_dir) |value| return value;
    if (cfg) |loaded| return loaded.inference.models_dir;
    return null;
}

fn resolveInferenceMaxConcurrentRequests(cfg: ?*const antfly.common.config.Config) u32 {
    return if (cfg) |config|
        config.admission.inference.max_concurrent_requests
    else
        antfly.common.config.default_inference_max_concurrent_requests;
}

fn resolveInferenceMlDir(cli: CliConfig, cfg: ?*const antfly.common.config.Config) ?[]const u8 {
    if (cli.inference_ml_dir) |value| return value;
    if (cfg) |loaded| return loaded.inference.ml_dir;
    return null;
}

const InferenceBudgetOverrides = struct {
    host_limit_bytes: usize,
    backend_limit_bytes: usize,
    combined_limit_bytes: usize,
    kv_limit_bytes: usize,
    scratch_limit_bytes: usize,
};

fn resolveInferenceBudgetOverrides(cli: CliConfig) !InferenceBudgetOverrides {
    return .{
        .host_limit_bytes = try mibToBytes(cli.inference_host_budget_mb),
        .backend_limit_bytes = try mibToBytes(cli.inference_backend_budget_mb),
        .combined_limit_bytes = try mibToBytes(cli.inference_combined_budget_mb),
        .kv_limit_bytes = try mibToBytes(cli.inference_kv_budget_mb),
        .scratch_limit_bytes = try mibToBytes(cli.inference_scratch_budget_mb),
    };
}

/// Process-owned lifetime fence for the embedded provider callback ABI. DB and
/// API runtimes borrow this stable object rather than the model-manager handle
/// directly, so shutdown can reject new calls and wait for every admitted call
/// before destroying the underlying inference node.
const EmbeddedInferenceProviderLifetime = inference_provider.EmbeddedInferenceProviderLifetime;

test "embedded provider lifetime rejects new calls and joins admitted calls" {
    var handle_storage: u8 = 0;
    var lifetime = EmbeddedInferenceProviderLifetime{ .handle = &handle_storage };
    var guard = try lifetime.acquire();

    const Quiesce = struct {
        lifetime: *EmbeddedInferenceProviderLifetime,
        returned: std.atomic.Value(bool) = .init(false),

        fn run(self: *@This()) void {
            self.lifetime.quiesce();
            self.returned.store(true, .release);
        }
    };
    var quiesce = Quiesce{ .lifetime = &lifetime };
    var thread = try std.testing.io.concurrent(Quiesce.run, .{&quiesce});
    defer {
        guard.deinit();
        thread.await(std.testing.io);
    }
    while (lifetime.isAccepting()) std.atomic.spinLoopHint();

    try std.testing.expect(!quiesce.returned.load(.acquire));
    try std.testing.expectError(error.InferenceProviderShuttingDown, lifetime.acquire());
    guard.deinit();
    thread.await(std.testing.io);

    try std.testing.expect(quiesce.returned.load(.acquire));
    try std.testing.expectEqual(@as(usize, 0), lifetime.activeCallCount());
    try std.testing.expectError(error.InferenceProviderShuttingDown, lifetime.acquire());
}

const inferenceBoundaryProvider = inference_provider.inferenceBoundaryProvider;

const invokeInferenceProvider = inference_provider.invokeInferenceProvider;

const invokeInferenceProviderControlled = inference_provider.invokeInferenceProviderControlled;

fn invokeInferenceProviderWithBinary(
    comptime Result: type,
    alloc: std.mem.Allocator,
    provider_context: *anyopaque,
    operation: inference_bridge.ProviderOperation,
    request: anytype,
    deadline_ns: ?u64,
    binary_payloads: []const inference_bridge.ProviderBinaryPayload,
    attachment_refs: []const inference_bridge.ProviderAttachmentRef,
) !Result {
    return try invokeInferenceProviderWithBinaryContext(Result, alloc, provider_context, operation, request, requestContextFromControls(deadline_ns, .none), binary_payloads, attachment_refs);
}

const invokeInferenceProviderWithBinaryControlled = inference_provider.invokeInferenceProviderWithBinaryControlled;

const requestContextFromControls = inference_provider.requestContextFromControls;

const invokeInferenceProviderWithBinaryContext = inference_provider.invokeInferenceProviderWithBinaryContext;

const ProviderInvocationCancellation = struct {
    token: CancellationToken,

    fn requested(raw: ?*const anyopaque) callconv(.c) u8 {
        const self: *const ProviderInvocationCancellation = @ptrCast(@alignCast(raw orelse return 1));
        return @intFromBool(self.token.isCancelled());
    }

    fn view(self: *const ProviderInvocationCancellation) runtime_http_abi.CancellationView {
        if (self.token.ptr == null or self.token.is_cancelled_fn == null) return .{};
        return .{ .context = self, .is_cancelled = requested };
    }
};

const linkedInferenceApi = inference_provider.linkedInferenceApi;

const linkedInferenceApiInfallible = inference_provider.linkedInferenceApiInfallible;

/// Dispatch the runtime-reserved local-inference connection through the same
/// embedded route handler used by the public inference API. This preserves the
/// destination's validation and admission semantics without opening a second
/// connection to our own listener.
fn invokeLocalInferenceConnection(context: *const inference_connection_abi.InvokeContext) callconv(.c) inference_connection_abi.Status {
    invokeLocalInferenceConnectionFallible(context) catch |err| {
        std.log.err("local inference connection failed err={}", .{err});
        return inference_connection_abi.statusFromError(err);
    };
    return .ok;
}

const LocalInferenceInvocationLifetime = inference_provider.LocalInferenceInvocationLifetime;

test "standalone local inference lifetime distinguishes deadline from upstream cancellation" {
    const expired = LocalInferenceInvocationLifetime{
        .upstream = .{},
        .deadline_ns = platform_time.monotonicNs(),
    };
    try std.testing.expectError(error.Timeout, expired.check());
    try std.testing.expect(expired.cancellation().requested());

    const Cancelled = struct {
        fn requested(_: ?*const anyopaque) callconv(.c) u8 {
            return 1;
        }
    };
    const canceled = LocalInferenceInvocationLifetime{
        .upstream = .{ .context = &expired, .is_cancelled = Cancelled.requested },
        .deadline_ns = std.math.maxInt(u64),
    };
    try std.testing.expectError(error.Canceled, canceled.check());
}

const ownedInferenceConnectionBytes = inference_provider.ownedInferenceConnectionBytes;

const optionalOwnedInferenceConnectionBytes = inference_provider.optionalOwnedInferenceConnectionBytes;

const invokeLocalInferenceConnectionFallible = inference_provider.invokeLocalInferenceConnectionFallible;

fn tryAcquireEmbeddedInferenceRequest(handle: *anyopaque) bool {
    if (comptime inline_inference_codegen) {
        return inference_host.linkedInferenceTryAcquireRequest(handle);
    }
    return linkedInferenceApiInfallible().try_acquire_request(handle) != 0;
}

fn releaseEmbeddedInferenceRequest(handle: *anyopaque) void {
    if (comptime inline_inference_codegen) {
        inference_host.linkedInferenceReleaseRequest(handle);
        return;
    }
    linkedInferenceApiInfallible().release_request(handle);
}

fn embeddedInferenceRequestStats(handle: *anyopaque) antfly.common.request_admission.RequestAdmission.Stats {
    const stats = if (comptime inline_inference_codegen)
        inference_host.linkedInferenceRequestAdmissionStats(handle)
    else blk: {
        var result: inference_bridge.RequestAdmissionStats = undefined;
        linkedInferenceApiInfallible().request_admission_stats(handle, &result);
        break :blk result;
    };
    return .{
        .capacity = stats.capacity,
        .in_flight = stats.in_flight,
        .peak_in_flight = stats.peak_in_flight,
        .rejected_total = stats.rejected_total,
    };
}

const inferenceProviderEmbedDenseTexts = inference_provider.inferenceProviderEmbedDenseTexts;

const inferenceProviderEmbedDenseTextsWithContext = inference_provider.inferenceProviderEmbedDenseTextsWithContext;

const inferenceProviderEmbedSparseTexts = inference_provider.inferenceProviderEmbedSparseTexts;

const inferenceProviderEmbedSparseTextsWithContext = inference_provider.inferenceProviderEmbedSparseTextsWithContext;

const inferenceProviderEmbedDenseParts = inference_provider.inferenceProviderEmbedDenseParts;

const inferenceProviderEmbedDensePartsWithContext = inference_provider.inferenceProviderEmbedDensePartsWithContext;

const inferenceProviderEmbedDensePartsBorrowed = inference_provider.inferenceProviderEmbedDensePartsBorrowed;

const inferenceProviderRerankTexts = inference_provider.inferenceProviderRerankTexts;

const inferenceProviderRerankTextsWithContext = inference_provider.inferenceProviderRerankTextsWithContext;

const inferenceProviderGenerateText = inference_provider.inferenceProviderGenerateText;

const inferenceProviderGenerateTextWithContext = inference_provider.inferenceProviderGenerateTextWithContext;

const inferenceProviderGenerateMessages = inference_provider.inferenceProviderGenerateMessages;

const inferenceProviderGenerateJson = inference_provider.inferenceProviderGenerateJson;

const inferenceProviderGenerateMessagesWithContext = inference_provider.inferenceProviderGenerateMessagesWithContext;

const inferenceProviderGenerateMessagesWithAttachments = inference_provider.inferenceProviderGenerateMessagesWithAttachments;

const inferenceProviderGenerateMessagesWithAttachmentsWithContext = inference_provider.inferenceProviderGenerateMessagesWithAttachmentsWithContext;

const inferenceProviderGenerateMessagesWithAttachmentsControlled = inference_provider.inferenceProviderGenerateMessagesWithAttachmentsControlled;

const inferenceProviderModelCapabilities = inference_provider.inferenceProviderModelCapabilities;

const inferenceProviderModelCapabilitiesWithContext = inference_provider.inferenceProviderModelCapabilitiesWithContext;

const inferenceProviderChunkInput = inference_provider.inferenceProviderChunkInput;

const inferenceProviderChunkInputWithContext = inference_provider.inferenceProviderChunkInputWithContext;

const inferenceProviderChunkInputControlled = inference_provider.inferenceProviderChunkInputControlled;

const inferenceProviderRewriteTexts = inference_provider.inferenceProviderRewriteTexts;

const inferenceProviderClassifyTexts = inference_provider.inferenceProviderClassifyTexts;

const inferenceProviderReadImages = inference_provider.inferenceProviderReadImages;

const inferenceProviderReadImagesWithContext = inference_provider.inferenceProviderReadImagesWithContext;

const inferenceProviderReadEncodedImages = inference_provider.inferenceProviderReadEncodedImages;

const inferenceProviderReadEncodedImagesWithContext = inference_provider.inferenceProviderReadEncodedImagesWithContext;

const inferenceProviderReadEncodedImagesControlled = inference_provider.inferenceProviderReadEncodedImagesControlled;

const inferenceProviderReadEncodedImagesReported = inference_provider.inferenceProviderReadEncodedImagesReported;

const inferenceProviderReadEncodedImagesReportedWithContext = inference_provider.inferenceProviderReadEncodedImagesReportedWithContext;

const inferenceProviderReadEncodedImagesReportedControlled = inference_provider.inferenceProviderReadEncodedImagesReportedControlled;

const encodedImageProviderMetadata = inference_provider.encodedImageProviderMetadata;

const EncodedImageProviderPayloads = inference_provider.EncodedImageProviderPayloads;

const encodedImageProviderPayloadsAlloc = inference_provider.encodedImageProviderPayloadsAlloc;

const inferenceProviderReadRasterImagesReported = inference_provider.inferenceProviderReadRasterImagesReported;

const inferenceProviderReadRasterImagesReportedWithContext = inference_provider.inferenceProviderReadRasterImagesReportedWithContext;

const inferenceProviderReadRasterImagesReportedControlled = inference_provider.inferenceProviderReadRasterImagesReportedControlled;

const inferenceProviderEmbedDenseRasters = inference_provider.inferenceProviderEmbedDenseRasters;

const RasterProviderPayloads = inference_provider.RasterProviderPayloads;

const rasterProviderPayloadsAlloc = inference_provider.rasterProviderPayloadsAlloc;

const inferenceProviderTranscribeAudio = inference_provider.inferenceProviderTranscribeAudio;

const inferenceProviderTranscribeAudioWithContext = inference_provider.inferenceProviderTranscribeAudioWithContext;

const inferenceProviderExtract = inference_provider.inferenceProviderExtract;

const inferenceProviderExtractWithContext = inference_provider.inferenceProviderExtractWithContext;

const inferenceProviderExtractControlled = inference_provider.inferenceProviderExtractControlled;

const inferenceProviderListModelsJson = inference_provider.inferenceProviderListModelsJson;

fn inferenceResourceSlices(amounts: *const inference_bridge.AdmissionAmounts) ![3]antfly.resource_manager.SliceAmount {
    return .{
        .{
            .slice = .inference_model_residency,
            .bytes = @intCast(try std.math.add(usize, amounts.host_weight_bytes, amounts.backend_weight_bytes)),
        },
        .{
            .slice = .inference_kv_working_set,
            .bytes = @intCast(try std.math.add(usize, amounts.host_kv_bytes, amounts.backend_kv_bytes)),
        },
        .{
            .slice = .inference_scratch_working_set,
            .bytes = @intCast(try std.math.add(usize, amounts.host_scratch_bytes, amounts.backend_scratch_bytes)),
        },
    };
}

fn inferenceHostCharge(amounts: *const inference_bridge.AdmissionAmounts) !u64 {
    var total = try std.math.add(usize, amounts.host_weight_bytes, amounts.host_kv_bytes);
    total = try std.math.add(usize, total, amounts.host_scratch_bytes);
    if (builtin.os.tag == .macos) {
        total = try std.math.add(usize, total, amounts.backend_weight_bytes);
        total = try std.math.add(usize, total, amounts.backend_kv_bytes);
        total = try std.math.add(usize, total, amounts.backend_scratch_bytes);
    }
    return @intCast(total);
}

const InferenceResourceBudgetOwner = struct {
    alloc: std.mem.Allocator,
    manager: *antfly.resource_manager.ResourceManager,
    lease_pool_mutex: std.atomic.Mutex = .unlocked,
    free_leases: ?*InferenceAdmissionLease = null,
    active_leases: std.AutoHashMapUnmanaged(usize, *InferenceAdmissionLease) = .empty,
    next_lease_token: usize = 1,
    observer_mutex: std.atomic.Mutex = .unlocked,
    prompt_cache_observers: std.AutoHashMapUnmanaged(usize, u64) = .empty,
    tokenizer_mutex: std.atomic.Mutex = .unlocked,
    tokenizer_cache_observers: std.AutoHashMapUnmanaged(usize, u64) = .empty,
    outstanding_admission_leases: std.atomic.Value(usize) = .init(0),
    references: std.atomic.Value(usize) = .init(1),
    closing: std.atomic.Value(bool) = .init(false),
    lifetime_mutex: std.atomic.Mutex = .unlocked,

    fn deinit(self: *@This()) void {
        lockAtomic(&self.lifetime_mutex);
        defer self.lifetime_mutex.unlock();
        if (self.closing.swap(true, .acq_rel))
            @panic("inference resource owner closed twice");
        if (self.references.load(.acquire) != 1)
            @panic("inference resource owner closed with retained capability contexts");
        if (self.outstanding_admission_leases.load(.acquire) != 0 or
            self.active_leases.count() != 0)
            @panic("inference resource owner closed with active admission leases");
        self.active_leases.deinit(self.alloc);
        self.active_leases = .empty;
        var current = self.free_leases;
        while (current) |lease| {
            current = lease.next_free;
            self.alloc.destroy(lease);
        }
        self.free_leases = null;
        if (self.prompt_cache_observers.count() != 0)
            @panic("inference resource owner closed with prompt cache observers");
        self.prompt_cache_observers.deinit(self.alloc);
        self.prompt_cache_observers = .empty;
        if (self.tokenizer_cache_observers.count() != 0)
            @panic("inference resource owner closed with tokenizer cache observers");
        self.tokenizer_cache_observers.deinit(self.alloc);
        self.tokenizer_cache_observers = .empty;
    }

    fn acquireLease(self: *@This()) !*InferenceAdmissionLease {
        lockAtomic(&self.lease_pool_mutex);
        if (self.free_leases) |lease| {
            self.free_leases = lease.next_free;
            self.lease_pool_mutex.unlock();
            lease.next_free = null;
            return lease;
        }
        self.lease_pool_mutex.unlock();
        return try self.alloc.create(InferenceAdmissionLease);
    }

    fn recycleLease(self: *@This(), lease: *InferenceAdmissionLease) void {
        lockAtomic(&self.lease_pool_mutex);
        defer self.lease_pool_mutex.unlock();
        lease.next_free = self.free_leases;
        self.free_leases = lease;
    }

    fn registerLease(self: *@This(), lease: *InferenceAdmissionLease) !usize {
        lockAtomic(&self.lease_pool_mutex);
        defer self.lease_pool_mutex.unlock();

        // Tokens are monotonically advanced and never expose the recycled
        // pointer. On integer wrap, skip zero and any token still active.
        // This prevents a delayed duplicate release from targeting a newer
        // reservation that happens to reuse the same pool slot.
        var token = self.next_lease_token;
        while (token == 0 or self.active_leases.contains(token)) {
            token +%= 1;
        }
        self.next_lease_token = token +% 1;
        if (self.next_lease_token == 0) self.next_lease_token = 1;
        try self.active_leases.put(self.alloc, token, lease);
        return token;
    }

    fn retainLease(
        self: *@This(),
        token: usize,
        retained: []const antfly.resource_manager.SliceAmount,
        retained_host_charge_bytes: u64,
    ) !void {
        lockAtomic(&self.lease_pool_mutex);
        defer self.lease_pool_mutex.unlock();
        const lease = self.active_leases.get(token) orelse {
            self.manager.recordAccountingError();
            return error.InvalidArguments;
        };
        try lease.reservation.retain(retained, retained_host_charge_bytes);
    }

    fn takeLease(self: *@This(), token: usize) ?*InferenceAdmissionLease {
        lockAtomic(&self.lease_pool_mutex);
        defer self.lease_pool_mutex.unlock();
        const removed = self.active_leases.fetchRemove(token) orelse return null;
        return removed.value;
    }

    fn observePromptCache(
        self: *@This(),
        observer_id: usize,
        previous: u64,
        next: u64,
    ) bool {
        return self.observeCacheUsage(
            &self.observer_mutex,
            &self.prompt_cache_observers,
            .inference_prompt_cache,
            observer_id,
            previous,
            next,
            false,
        );
    }

    fn observeTokenizerCache(
        self: *@This(),
        observer_id: usize,
        previous: u64,
        next: u64,
    ) bool {
        return self.observeCacheUsage(
            &self.tokenizer_mutex,
            &self.tokenizer_cache_observers,
            .inference_tokenizer_cache,
            observer_id,
            previous,
            next,
            true,
        );
    }

    fn observeCacheUsage(
        self: *@This(),
        mutex: *std.atomic.Mutex,
        observers: *std.AutoHashMapUnmanaged(usize, u64),
        slice: antfly.resource_manager.Slice,
        observer_id: usize,
        previous: u64,
        next: u64,
        enforce_growth_limits: bool,
    ) bool {
        lockAtomic(mutex);
        defer mutex.unlock();

        if (observer_id == 0) {
            self.manager.recordAccountingError();
            return false;
        }
        var inserted = false;
        const current = observers.getPtr(observer_id) orelse blk: {
            if (previous != 0) {
                self.manager.recordAccountingError();
                return false;
            }
            if (next == 0) return true;
            const entry = observers.getOrPut(
                self.alloc,
                observer_id,
            ) catch {
                self.manager.recordAccountingError();
                return false;
            };
            entry.value_ptr.* = 0;
            inserted = true;
            break :blk entry.value_ptr;
        };
        if (current.* != previous) {
            self.manager.recordAccountingError();
            return false;
        }
        const accepted = if (enforce_growth_limits)
            self.manager.tryAdjustUsageIdentity(slice, observer_id, current.*, next)
        else
            self.manager.tryObserveUsageIdentity(slice, observer_id, current.*, next);
        if (!accepted) {
            if (inserted) _ = observers.remove(observer_id);
            return false;
        }
        current.* = next;
        if (next == 0) _ = observers.remove(observer_id);
        return true;
    }
};

const InferenceAdmissionLease = struct {
    reservation: antfly.resource_manager.BatchReservation,
    next_free: ?*InferenceAdmissionLease = null,
};

fn retainInferenceResourceOwner(context: *anyopaque) callconv(.c) u8 {
    const owner: *InferenceResourceBudgetOwner = @ptrCast(@alignCast(context));
    lockAtomic(&owner.lifetime_mutex);
    defer owner.lifetime_mutex.unlock();
    if (owner.closing.load(.acquire)) return 0;
    const current = owner.references.load(.acquire);
    if (current == 0 or current == std.math.maxInt(usize)) return 0;
    owner.references.store(current + 1, .release);
    return 1;
}

fn releaseInferenceResourceOwner(context: *anyopaque) callconv(.c) void {
    const owner: *InferenceResourceBudgetOwner = @ptrCast(@alignCast(context));
    lockAtomic(&owner.lifetime_mutex);
    defer owner.lifetime_mutex.unlock();
    // The standalone runtime owns the base reference until node destruction
    // completes; capability consumers may never release that final edge.
    const current = owner.references.load(.acquire);
    if (current <= 1)
        @panic("inference resource capability released its owner's base reference");
    owner.references.store(current - 1, .release);
}

fn reserveInferenceResources(
    context: *anyopaque,
    amounts: *const inference_bridge.AdmissionAmounts,
    out_lease: *usize,
) callconv(.c) inference_bridge.Status {
    const owner: *InferenceResourceBudgetOwner = @ptrCast(@alignCast(context));
    out_lease.* = 0;
    const slices = inferenceResourceSlices(amounts) catch |err| return inference_bridge.statusFromError(err);
    const host_charge = inferenceHostCharge(amounts) catch |err| return inference_bridge.statusFromError(err);
    const lease = owner.acquireLease() catch |err|
        return inference_bridge.statusFromError(err);
    lease.* = .{
        .reservation = owner.manager.reserveBatchClassifiedWithHostCharge(
            &slices,
            host_charge,
        ) catch |err| {
            owner.recycleLease(lease);
            return inference_bridge.statusFromError(err);
        },
    };
    const lease_token = owner.registerLease(lease) catch |err| {
        lease.reservation.release();
        owner.recycleLease(lease);
        return inference_bridge.statusFromError(err);
    };
    _ = owner.outstanding_admission_leases.fetchAdd(1, .acq_rel);
    out_lease.* = lease_token;
    return .ok;
}

fn retainInferenceResources(
    context: *anyopaque,
    lease_token: usize,
    retained: *const inference_bridge.AdmissionAmounts,
) callconv(.c) inference_bridge.Status {
    const owner: *InferenceResourceBudgetOwner = @ptrCast(@alignCast(context));
    if (lease_token == 0) return inference_bridge.statusFromError(error.InvalidArguments);
    const slices = inferenceResourceSlices(retained) catch |err| return inference_bridge.statusFromError(err);
    const host_charge = inferenceHostCharge(retained) catch |err| return inference_bridge.statusFromError(err);
    owner.retainLease(lease_token, &slices, host_charge) catch |err|
        return inference_bridge.statusFromError(err);
    return .ok;
}

fn releaseInferenceResources(context: *anyopaque, lease_token: usize) callconv(.c) void {
    const owner: *InferenceResourceBudgetOwner = @ptrCast(@alignCast(context));
    if (lease_token == 0) return;
    const lease = owner.takeLease(lease_token) orelse {
        owner.manager.recordAccountingError();
        return;
    };
    lease.reservation.release();
    owner.recycleLease(lease);
    _ = owner.outstanding_admission_leases.fetchSub(1, .acq_rel);
}

fn observeInferencePromptCache(
    context: *anyopaque,
    observer_id: usize,
    previous: u64,
    next: u64,
) callconv(.c) u8 {
    const owner: *InferenceResourceBudgetOwner = @ptrCast(@alignCast(context));
    return @intFromBool(owner.observePromptCache(observer_id, previous, next));
}

fn observeInferenceTokenizerCache(
    context: *anyopaque,
    observer_id: usize,
    previous: u64,
    next: u64,
) callconv(.c) u8 {
    const owner: *InferenceResourceBudgetOwner = @ptrCast(@alignCast(context));
    return @intFromBool(owner.observeTokenizerCache(observer_id, previous, next));
}

fn mibToBytes(value: usize) !usize {
    return process_memory_budget.mibToBytes(value);
}

fn resolveProcessMemoryBudget(
    cli: CliConfig,
    env: *const std.process.Environ.Map,
) !process_memory_budget.EffectiveResolution {
    return process_memory_budget.resolveSystemDetailed(
        cli.inference_process_memory_budget_mb,
        env.get(process_memory_budget.canonical_env),
        env.get(process_memory_budget.inference_compat_env),
    );
}

fn storageMemoryLimitSource(
    source: process_memory_budget.EffectiveSource,
) antfly.public_api.MemoryLimitSource {
    return switch (source) {
        .explicit => .explicit,
        .cgroup_v2 => .cgroup_v2,
        .cgroup_v1 => .cgroup_v1,
        .host => .host,
        .unavailable => .unavailable,
    };
}

fn inferenceMemoryLimitProvenance(
    source: process_memory_budget.EffectiveSource,
) inference_bridge.ProcessMemoryLimitProvenance {
    return switch (source) {
        .explicit => .explicit,
        .cgroup_v2 => .cgroup_v2,
        .cgroup_v1 => .cgroup_v1,
        .host => .host,
        .unavailable => .unavailable,
    };
}

fn printUsage() void {
    std.debug.print(
        \\Usage: antfly standalone [options]
        \\
        \\Options:
        \\  --config <path>                       JSON common config file
        \\  --host <host>                         Public API host (default: 127.0.0.1)
        \\  --port <port>                         Public API port (default: 8080)
        \\  --auth <true|false>                   Enable authentication for public APIs (default: false)
        \\  --id <node-id>                        Local node id (default: 1)
        \\  --health <true|false>                 Enable health/metrics server (default: true)
        \\  --health-port <port>                  Dedicated health/metrics port on --host (default: 4200)
        \\  --experimental                        Enable experimental A2A protocol surfaces
        \\  --ard-base-url <url>                  Absolute public base URL for ARD catalog artifact links
        \\  --ard-publisher-domain <name>         ARD did:web publisher domain (default: antfly.local)
        \\  --ard-display-name <name>             ARD catalog host display name (default: Antfly)
        \\  --ard-public-catalog <bool>           Publish anonymous /.well-known ARD bootstrap when auth is enabled
        \\  --control-tick-ms <ms>                Control scheduling interval, 1-60000 (default: 100)
        \\  --models-dir <path>                   Embedded AI models directory (default: ~/.antfly/inference/models)
        \\  --ml-dir <path>                       Embedded Traditional ML directory (default: ~/.antfly/inference/ml)
        \\  --inference-host-budget-mb <n>        Embedded inference native generation host budget override
        \\  --inference-backend-budget-mb <n>     Embedded inference native generation backend budget override
        \\  --inference-combined-budget-mb <n>    Embedded inference native generation combined budget override
        \\  --process-memory-budget-mb <n>        Whole-process host-memory envelope (0: auto-detect)
        \\  --inference-kv-budget-mb <n>          Embedded inference native generation KV cache budget override
        \\  --inference-scratch-budget-mb <n>     Embedded inference native generation scratch budget override
        \\  --inference-process-memory-budget-mb <n> Compatibility alias for --process-memory-budget-mb
        \\  --kernel-jit-mode <off|shadow|on|required> Embedded inference runtime JIT mode override
        \\  --preload-model <kind:name|kind:backend:name> Preload and warm an embedded model before serving
        \\  --data-dir <path>                     Local Antfly data directory root
        \\  --storage-engine lite                 Use the single-file Lite engine
        \\  --storage-path <path.aflite>          Lite database path (required with Lite)
        \\  --fsync <true|false>                  Lite commit durability (default: true)
        \\  --replica-root-dir <path>             Replica root directory
        \\  --replica-catalog-path <path>         Replica catalog file path
        \\  --snapshot-root-dir <path>            Snapshot root directory
        \\  --extension-package-store <path>      Extension package store directory
        \\  --secret-store-path <path>            Antfly secrets.json file path; repeat for fallback layers
        \\  --hot-standby-primary-log <path>      Enable hot-standby primary WAL/admin API with this replication log path
        \\  --hot-standby-primary-slots <path>    Hot-standby primary replication slot store path
        \\  --hot-standby-primary-node-id <id>    Hot-standby primary node id for typed admin receipts
        \\  --hot-standby-seed-capture-root <path> Durable runtime-owned immutable seed generation root
        \\  --hot-standby-fence-wal <path>        Durable hot-standby promotion fence WAL path
        \\  --hot-standby-former-primary-log <path> Durable hot-standby log used by former-primary rewind admin workflows
        \\  --admin-token-env <name>              Require Authorization: Bearer token from this environment variable for admin and hot-standby APIs
        \\  --hot-standby-retention-max-lag-lsn <n> Hot-standby primary marks slots reseed-required after this LSN retention lag
        \\  --hot-standby-retention-max-retained-bytes <n> Hot-standby primary marks oldest slots reseed-required above this retained WAL byte cap
        \\  --hot-standby-retention-max-retained-age-ns <n> Hot-standby primary marks oldest slots reseed-required above this retained WAL age cap
        \\  --hot-standby-sync-mode <mode>        Hot-standby primary sync mode: async, remote-write, remote-apply
        \\  --hot-standby-sync-selection <selection> Hot-standby sync standby selection: any, first, all
        \\  --hot-standby-sync-required <n>       Hot-standby sync required standby acknowledgements
        \\  --hot-standby-sync-standby <name>     Hot-standby sync standby name; repeat for multiple standbys
        \\  --hot-standby-sync-failure <policy>   Hot-standby sync failure policy: block, fail-closed, degrade-to-async
        \\  --hot-standby-log <path>              Enable hot-standby admin API with this received replication log path
        \\  --hot-standby-progress <path>         Hot-standby durable receive/apply progress WAL path
        \\  --hot-standby-node-id <id>            Hot-standby node id for typed admin receipts
        \\  --hot-standby-upstream-url <url>      Upstream primary URL for continuous standby pull/apply
        \\  --hot-standby-slot <name>             Upstream replication slot name for continuous standby pull/apply
        \\  --hot-standby-startup-target-root <path> Activated generation root; requires the complete startup evidence set
        \\  --hot-standby-startup-topology-id <id> Exact topology id bound into the activation receipt
        \\  --hot-standby-startup-topology-generation <n> Exact topology generation bound into the activation receipt
        \\  --hot-standby-startup-generation <id> Exact activated seed generation
        \\  --hot-standby-startup-slot-name <id>  Exact slot bound into the activation receipt
        \\  --hot-standby-startup-timeline-id <id> Exact predecessor timeline bound into the activation receipt
        \\  --hot-standby-startup-epoch <id>      Exact predecessor epoch bound into the activation receipt
        \\  --hot-standby-startup-target-pvc-name <name> Exact target PVC name bound into the activation receipt
        \\  --hot-standby-startup-target-pvc-uid <uid> Exact target PVC UID bound into the activation receipt
        \\  --hot-standby-startup-capture-receipt-sha256 <sha256> Exact runtime capture authority digest
        \\  --hot-standby-startup-materialized-receipt-sha256 <sha256> Exact materialized topology receipt digest
        \\  --hot-standby-startup-materialized-aggregate-sha256 <sha256> Exact materialized file aggregate digest
        \\  --hot-standby-startup-target-local-node-id <id> Exact local node id used to materialize the live generation
        \\  --hot-standby-startup-target-replica-id <id> Exact replica id used to materialize the live generation
        \\  --hot-standby-cluster-id <id>         Hot-standby replicated cluster id
        \\  --hot-standby-shard-id <id>           Hot-standby replicated shard id (default: 0)
        \\  --hot-standby-table-id <id>           Hot-standby replicated table id (default: 0)
        \\  --hot-standby-timeline-id <id>        Hot-standby primary timeline id
        \\  --hot-standby-epoch <id>              Hot-standby primary epoch
        \\  --ha-* spellings of the flags above    Deprecated aliases for --hot-standby-*; kept for one minor release
        \\  -h, --help                            Show this help
        \\
    , .{});
}

fn parseBoolFlag(raw: []const u8) ?bool {
    if (std.mem.eql(u8, raw, "true")) return true;
    if (std.mem.eql(u8, raw, "false")) return false;
    return null;
}

fn parseHASyncDurabilityMode(raw: []const u8) !antfly.hot_standby.primary.DurabilityMode {
    if (std.mem.eql(u8, raw, "async")) return .async;
    if (std.mem.eql(u8, raw, "remote_write") or std.mem.eql(u8, raw, "remote-write")) return .remote_write;
    if (std.mem.eql(u8, raw, "remote_apply") or std.mem.eql(u8, raw, "remote-apply")) return .remote_apply;
    return error.InvalidHASyncMode;
}

fn parseHASyncStandbySelection(raw: []const u8) !antfly.hot_standby.primary.StandbySelection {
    if (std.mem.eql(u8, raw, "any")) return .any;
    if (std.mem.eql(u8, raw, "first")) return .first;
    if (std.mem.eql(u8, raw, "all")) return .all;
    return error.InvalidHASyncSelection;
}

fn parseHASyncFailurePolicy(raw: []const u8) !antfly.hot_standby.primary.FailurePolicy {
    if (std.mem.eql(u8, raw, "block")) return .block;
    if (std.mem.eql(u8, raw, "fail_closed") or std.mem.eql(u8, raw, "fail-closed")) return .fail_closed;
    if (std.mem.eql(u8, raw, "degrade_to_async") or std.mem.eql(u8, raw, "degrade-to-async")) return .degrade_to_async;
    return error.InvalidHASyncFailurePolicy;
}

fn parsePositiveUsize(raw: []const u8) !usize {
    const value = std.fmt.parseInt(usize, raw, 10) catch return error.InvalidArguments;
    if (value == 0) return error.InvalidHASyncPolicy;
    return value;
}

fn parsePositiveU64(raw: []const u8) !u64 {
    const value = std.fmt.parseInt(u64, raw, 10) catch return error.InvalidArguments;
    if (value == 0) return error.InvalidHARetentionPolicy;
    return value;
}

const RecordingRouteMethod = enum {
    get,
    post,
    put,
    delete,
};

const RecordingRoute = struct {
    method: RecordingRouteMethod,
    path: []u8,
};

const RecordingServer = struct {
    allocator: std.mem.Allocator,
    routes: std.ArrayListUnmanaged(RecordingRoute) = .empty,

    fn deinit(self: *@This()) void {
        for (self.routes.items) |route| self.allocator.free(route.path);
        self.routes.deinit(self.allocator);
    }

    fn append(self: *@This(), method: RecordingRouteMethod, comptime path: []const u8) !void {
        try self.routes.append(self.allocator, .{
            .method = method,
            .path = try self.allocator.dupe(u8, path),
        });
    }

    pub fn get(self: *@This(), comptime path: []const u8, _: anytype) !void {
        try self.append(.get, path);
    }

    pub fn post(self: *@This(), comptime path: []const u8, _: anytype) !void {
        try self.append(.post, path);
    }

    pub fn put(self: *@This(), comptime path: []const u8, _: anytype) !void {
        try self.append(.put, path);
    }

    pub fn delete(self: *@This(), comptime path: []const u8, _: anytype) !void {
        try self.append(.delete, path);
    }

    fn hasRoute(self: *const @This(), method: RecordingRouteMethod, path: []const u8) bool {
        for (self.routes.items) |route| {
            if (route.method == method and std.mem.eql(u8, route.path, path)) return true;
        }
        return false;
    }
};

test "standalone runtime module compiles" {
    _ = run;
    _ = runFromIterator;
    try std.testing.expect(isInteractiveGeneratePath("/ai/v1/generate"));
    try std.testing.expect(isInteractiveGeneratePath("/ai/v1/chat/completions"));
    try std.testing.expect(isInteractiveGeneratePath("/ml/v1/generate/batch"));
    try std.testing.expect(!isInteractiveGeneratePath("/ai/v1/embed"));
    try std.testing.expect(!isInteractiveGeneratePath("/ai/v10/generate"));
}

test "HA Lease minimum grace contains poll request and scheduling margin" {
    const minimum_grace_ns = ha_lease_min_grace_ms * std.time.ns_per_ms;
    const request_timeout_ns = @as(u64, ha_lease_request_timeout_ms) * std.time.ns_per_ms;
    try std.testing.expect(ha_lease_poll_interval_ns + request_timeout_ns + ha_lease_timing_jitter_ns < minimum_grace_ns);
}

test "standalone Lite enforces one shard and one replica" {
    const lite = try deriveStandaloneTableRecord(.lite, "docs", .{});
    try std.testing.expectEqual(@as(u32, 1), lite.min_ranges);
    try std.testing.expectEqual(@as(u32, 1), lite.desired_replica_count);
    try std.testing.expectError(
        error.InvalidCreateTableRequest,
        deriveStandaloneTableRecord(.lite, "split", .{ .num_shards = 2 }),
    );

    const local = try deriveStandaloneTableRecord(.local, "local", .{ .num_shards = 2 });
    try std.testing.expectEqual(@as(u32, 2), local.min_ranges);
    try std.testing.expectEqual(@as(u32, 1), local.desired_replica_count);
}

test "standalone table storage defaults persist without migrating legacy tables" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(path);
    var backend = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend.deinit();
    {
        var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://127.0.0.1:8080", ".", path, backend.ptr(), null, .local);
        defer metadata.deinit();
        // Old catalog records have no storage field. Their interpretation is
        // independent of the new-table creation policy.
        var legacy = try std.json.parseFromSlice(antfly.metadata.TableRecord, alloc, "{\"table_id\":7,\"name\":\"legacy\"}", .{});
        defer legacy.deinit();
        try std.testing.expectEqual(.primary_lsm, legacy.value.storage.dense_embeddings);
        try metadata.manager.upsertTable(legacy.value);
        try LocalStandaloneMetadata.createTable(&metadata, alloc, "new", .{});
        try LocalStandaloneMetadata.createTable(&metadata, alloc, "opt_out", .{ .storage = .{ .dense_embeddings = .primary_lsm } });
        try LocalStandaloneMetadata.createTable(&metadata, alloc, "multiple_shards", .{ .num_shards = 2 });
        const scoped = try metadata.statusSource().systemCatalog(alloc, .{}, .{ .mutate = .{
            .mutation = .{ .action = .create, .kind = .table, .name = "scoped" },
            .physical_name = "table:scoped-default",
            .create_table_json = "{}",
        } });
        alloc.free(scoped);
        metadata.vector_source_storage_allowed = false;
        const scoped_standby = try metadata.statusSource().systemCatalog(alloc, .{}, .{ .mutate = .{
            .mutation = .{ .action = .create, .kind = .table, .name = "scoped_standby" },
            .physical_name = "table:scoped-standby",
            .create_table_json = "{}",
        } });
        alloc.free(scoped_standby);
        try std.testing.expectError(error.VectorStoreRequiresLocalSingleShardTable, metadata.statusSource().systemCatalog(alloc, .{}, .{ .mutate = .{
            .mutation = .{ .action = .create, .kind = .table, .name = "scoped_unsupported" },
            .physical_name = "table:scoped-unsupported",
            .create_table_json = "{\"storage\":{\"dense_embeddings\":\"vector_store\"}}",
        } }));
        try LocalStandaloneMetadata.createTable(&metadata, alloc, "ha", .{});
        try std.testing.expectError(error.VectorStoreRequiresLocalSingleShardTable, LocalStandaloneMetadata.createTable(&metadata, alloc, "unsupported", .{ .storage = .{ .dense_embeddings = .vector_store } }));
    }
    var reopened = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://127.0.0.1:8080", ".", path, backend.ptr(), null, .local);
    defer reopened.deinit();
    try std.testing.expectEqual(.vector_store, reopened.findTableByNameLocked("new").?.storage.dense_embeddings);
    try std.testing.expectEqual(.vector_store, reopened.findTableByNameLocked("table:scoped-default").?.storage.dense_embeddings);
    try std.testing.expectEqual(.primary_lsm, reopened.findTableByNameLocked("table:scoped-standby").?.storage.dense_embeddings);
    for ([_][]const u8{ "legacy", "opt_out", "multiple_shards", "ha" }) |name|
        try std.testing.expectEqual(.primary_lsm, reopened.findTableByNameLocked(name).?.storage.dense_embeddings);
    try std.testing.expect(reopened.findTableByNameLocked("unsupported") == null);
}

test "standalone Lite adoption preserves deterministic embedded document identity" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/identity-adoption.aflite", .{tmp.sub_path});
    defer alloc.free(path);

    {
        var embedded = try antfly.lite.connection.Connection.create(alloc, path, true);
        defer embedded.close();
        try embedded.db.batch(.{ .writes = &.{.{ .key = "doc:portable", .value = "{\"body\":\"portable\"}" }} });
    }

    var backend = try antfly.lite.backend.Handle.open(alloc, path, .{});
    defer backend.deinit();
    const target = antfly.lite.connection.embeddedRootIdentity();
    const default_table = try deriveStandaloneTableRecord(.lite, "default", .{});
    const default_range = antfly.public_api.tables.deriveInitialRange(default_table);
    try std.testing.expectEqual(default_table.table_id, target.table_id);
    try std.testing.expectEqual(default_range.group_id, target.shard_id);
    try std.testing.expectEqual(default_range.range_id, target.range_id);
    const namespace = try std.fmt.allocPrint(alloc, "group-{d}/table-db", .{target.shard_id});
    defer alloc.free(namespace);
    try backend.adoptEmbeddedRootAsNamespace(namespace);
    try verifyAdoptedLiteIdentity(alloc, &backend, namespace, target);
    // Retry after a crash boundary is a no-op.
    try verifyAdoptedLiteIdentity(alloc, &backend, namespace, target);

    var opts = antfly.db.OpenOptions{
        .open_mode = .query_readonly,
        .start_index_workers = false,
        .start_optional_runtimes = false,
        .identity_namespace = target,
    };
    const restored_namespace = try std.fmt.allocPrint(alloc, "/restored/root/{s}", .{namespace});
    defer alloc.free(restored_namespace);
    try backend.configureDbOpenOptionsForNamespace(&opts, restored_namespace);
    var adopted = try antfly.db.DB.open(alloc, namespace, opts);
    defer adopted.close();
    try std.testing.expect(adopted.core.identity_namespace.eql(target));
    const value = (try adopted.get(alloc, "doc:portable")) orelse return error.MissingAdoptedDocument;
    defer alloc.free(value);
    try std.testing.expectEqualStrings("{\"body\":\"portable\"}", value);
}

test "standalone validates effective Lite CLI and config settings" {
    const lite_cli = CliConfig{ .storage_engine = .lite, .storage_path = "data.aflite" };
    try validateEffectiveStandaloneStorage(lite_cli, .lite, "data.aflite", null);
    try std.testing.expectError(
        error.InvalidLiteStoragePath,
        validateEffectiveStandaloneStorage(lite_cli, .lite, "data.db", null),
    );
    try std.testing.expectError(
        error.InvalidArguments,
        validateEffectiveStandaloneStorage(.{ .storage_path = "unused.aflite" }, .local, null, null),
    );

    var distributed = try antfly.common.config.Config.parseFromSlice(std.testing.allocator,
        \\{"deployment_mode":"distributed","storage":{"engine":"local","local":{"base_dir":"data"}}}
    );
    defer distributed.deinit();
    try std.testing.expectError(
        error.LiteHorizontalShardingUnsupported,
        validateEffectiveStandaloneStorage(lite_cli, .lite, "data.aflite", &distributed),
    );
}

test "standalone runtime local generator accepts media url data uris" {
    const alloc = std.testing.allocator;
    const messages = [_]antfly.inference.ChatMessage{.{
        .role = .user,
        .content = .{ .parts = &.{
            .{ .text = "describe" },
            .{ .media = .{
                .url = "DATA:IMAGE/PNG;BASE64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD",
                .mime_type = "image/png",
            } },
        } },
    }};

    const preflight = try inference_host.preflightLocalGenerateMessages(&messages);
    var converted = try inference_host.convertLocalGenerateMessages(alloc, &messages, preflight.decoded_media_bytes);
    defer converted.deinit(alloc);

    try std.testing.expectEqual(@as(usize, 1), converted.messages.len);
    const message = converted.messages[0];
    try std.testing.expectEqualStrings("describe", message.content);
    try std.testing.expectEqual(@as(usize, 1), message.image_bytes.?.len);
    var expected = [_]u8{0} ** 24;
    @memcpy(expected[0..8], "\x89PNG\r\n\x1a\n");
    std.mem.writeInt(u32, expected[16..20], 2, .big);
    std.mem.writeInt(u32, expected[20..24], 3, .big);
    try std.testing.expectEqualSlices(u8, &expected, message.image_bytes.?[0]);
    try std.testing.expectEqual(@as(usize, 2), message.content_parts.?.len);
    try std.testing.expectEqual(@as(usize, 0), message.content_parts.?[1].image);
}

test "standalone runtime local dense embed preserves borrowed binary media" {
    const raw = [_]u8{ 1, 2, 3 };
    const parts = [_]antfly.template.ContentPart{
        .{ .text = "caption" },
        .{ .media_url = "data:image/png;base64,AA==" },
        .{ .binary = .{ .mime_type = "image/png", .data = &raw } },
    };
    const direct = try inference_host.localAntflyDirectDenseParts(std.testing.allocator, &parts);
    defer std.testing.allocator.free(direct);

    try std.testing.expectEqual(@as(usize, 3), direct.len);
    try std.testing.expectEqualStrings("caption", direct[0].text);
    try std.testing.expectEqualStrings("data:image/png;base64,AA==", direct[1].image_url);
    try std.testing.expectEqualStrings("image/png", direct[2].media.mime_type);
    try std.testing.expectEqual(@intFromPtr(raw[0..].ptr), @intFromPtr(direct[2].media.data.ptr));
    try std.testing.expectEqualSlices(u8, &raw, direct[2].media.data);
}

test "standalone encoded reader ABI round trips borrowed payloads" {
    const alloc = std.testing.allocator;
    const png = [_]u8{ 0x89, 'P', 'N', 'G', 1 };
    const jpeg = [_]u8{ 0xff, 0xd8, 0xff, 2 };
    const images = [_]antfly.readers.EncodedImage{
        .{ .bytes = &png, .mime_type = "image/png" },
        .{ .bytes = &jpeg, .mime_type = "image/jpeg" },
    };
    const request = antfly.readers.EncodedRequest{
        .images = &images,
        .prompt = "<OCR>",
        .max_tokens = 128,
        .source_fingerprint = "mixed-deadbeef",
    };

    const FakeReader = struct {
        first_ptr: [*]const u8,
        second_ptr: [*]const u8,
        calls: usize = 0,

        fn read(
            ptr: *anyopaque,
            result_alloc: std.mem.Allocator,
            model: []const u8,
            encoded: antfly.readers.EncodedRequest,
        ) ![]antfly.readers.Result {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.calls += 1;
            try std.testing.expectEqualStrings("florence2", model);
            try std.testing.expectEqualStrings("<OCR>", encoded.prompt.?);
            try std.testing.expectEqual(@as(i64, 128), encoded.max_tokens.?);
            try std.testing.expectEqualStrings("mixed-deadbeef", encoded.source_fingerprint.?);
            try std.testing.expectEqual(@as(usize, 2), encoded.images.len);
            try std.testing.expectEqualStrings("image/png", encoded.images[0].mime_type);
            try std.testing.expectEqualStrings("image/jpeg", encoded.images[1].mime_type);
            try std.testing.expectEqual(@intFromPtr(self.first_ptr), @intFromPtr(encoded.images[0].bytes.ptr));
            try std.testing.expectEqual(@intFromPtr(self.second_ptr), @intFromPtr(encoded.images[1].bytes.ptr));

            const out = try result_alloc.alloc(antfly.readers.Result, 2);
            out[0] = .{ .text = try result_alloc.dupe(u8, "first") };
            out[1] = .{ .text = try result_alloc.dupe(u8, "second") };
            return out;
        }
    };
    var fake = FakeReader{ .first_ptr = png[0..].ptr, .second_ptr = jpeg[0..].ptr };
    var state = inference_host.LinkedInferenceState{
        .alloc = alloc,
        .executor = try @import("../runtime_io_abi.zig").Borrow.init(&std.testing.io).receive(),
        .io = std.testing.io,
        .node = undefined, // The model-free override must not enter Node.
        .warm_models = undefined,
        .content_security = null,
        .s3_credentials = null,
        .runtime_config = undefined,
        .owned_models_dir = null,
        .owned_ml_dir = null,
        .route_validator = undefined,
        .read_encoded_images_override = .{ .ptr = &fake, .read_fn = FakeReader.read },
    };
    var lifetime = EmbeddedInferenceProviderLifetime{ .handle = &state };

    // Traverse the production caller, ProviderInvokeContext construction,
    // host operation dispatch, borrowed-payload decode, JSON response, and
    // response destruction without loading a model.
    const results = try inferenceProviderReadEncodedImages(&lifetime, alloc, "florence2", request);
    defer {
        for (results) |*result| antfly.readers.deinitResult(alloc, result);
        alloc.free(results);
    }
    try std.testing.expectEqual(@as(usize, 1), fake.calls);
    try std.testing.expectEqualStrings("first", results[0].text);
    try std.testing.expectEqualStrings("second", results[1].text);

    // Keep malformed-context coverage at the codec boundary, where a corrupt
    // pointer/count pair can be represented without dereferencing it.
    var payloads = try encodedImageProviderPayloadsAlloc(alloc, request.images);
    defer payloads.deinit(alloc);
    const metadata = encodedImageProviderMetadata("florence2", request);
    const request_json = try std.json.Stringify.valueAlloc(alloc, metadata, .{});
    defer alloc.free(request_json);

    try std.testing.expectError(
        error.InvalidArguments,
        inference_host.decodeReadEncodedImagesProviderRequest(alloc, request_json, null, payloads.payloads.len, payloads.refs.ptr, payloads.refs.len),
    );
    var bad_metadata = metadata;
    bad_metadata.image_count += 1;
    const bad_json = try std.json.Stringify.valueAlloc(alloc, bad_metadata, .{});
    defer alloc.free(bad_json);
    try std.testing.expectError(
        error.InvalidArguments,
        inference_host.decodeReadEncodedImagesProviderRequest(alloc, bad_json, payloads.payloads.ptr, payloads.payloads.len, payloads.refs.ptr, payloads.refs.len),
    );

    const invalid_mime_images = [_]antfly.readers.EncodedImage{.{ .bytes = &png, .mime_type = "" }};
    try std.testing.expectError(
        error.InvalidArguments,
        inferenceProviderReadEncodedImages(&lifetime, alloc, "florence2", .{ .images = &invalid_mime_images }),
    );
    try std.testing.expectEqual(@as(usize, 1), fake.calls);

    const empty_request = antfly.readers.EncodedRequest{ .images = &.{} };
    const empty_metadata = encodedImageProviderMetadata("florence2", empty_request);
    const empty_json = try std.json.Stringify.valueAlloc(alloc, empty_metadata, .{});
    defer alloc.free(empty_json);
    try std.testing.expectError(
        error.ReadBatchTooLarge,
        inference_host.decodeReadEncodedImagesProviderRequest(alloc, empty_json, null, 0, null, 0),
    );

    const unused_handle: *anyopaque = @ptrFromInt(1);
    try std.testing.expectError(
        error.ReadBatchTooLarge,
        inferenceProviderReadEncodedImages(unused_handle, alloc, "florence2", empty_request),
    );
    try std.testing.expectError(
        error.ReadBatchTooLarge,
        inferenceProviderReadEncodedImagesReported(unused_handle, alloc, "florence2", empty_request),
    );
}

test "standalone raster reader ABI preserves borrowed strided pages and identity" {
    const alloc = std.testing.allocator;
    var first = [_]u8{ 1, 2, 3, 255, 4, 5, 6, 255 };
    var second = [_]u8{ 7, 8, 9, 255, 10, 11, 12, 255 };
    const images = [_]antfly.readers.RasterImage{
        .{ .bytes = &first, .width = 2, .height = 1, .stride_bytes = 8, .item_id = "page:1", .source_fingerprint = "doc", .page_number = 1 },
        .{ .bytes = &second, .width = 2, .height = 1, .stride_bytes = 8, .item_id = "page:2", .source_fingerprint = "doc", .page_number = 2 },
    };
    const request = antfly.readers.RasterRequest{
        .images = &images,
        .prompt = "<OCR>",
        .max_tokens = 128,
        .source_fingerprint = "doc",
    };

    const FakeReader = struct {
        expected: [2][*]const u8,
        observed_addresses: [2]usize = .{ 0, 0 },
        calls: usize = 0,

        fn read(
            ptr: *anyopaque,
            result_alloc: std.mem.Allocator,
            model: []const u8,
            raster_request: antfly.readers.RasterRequest,
        ) !antfly.readers.BatchResult {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.calls += 1;
            try std.testing.expectEqualStrings("florence2", model);
            try std.testing.expectEqualStrings("<OCR>", raster_request.prompt.?);
            try std.testing.expectEqual(@as(usize, 2), raster_request.images.len);
            const out = try result_alloc.alloc(antfly.readers.Result, 2);
            var filled: usize = 0;
            errdefer {
                for (out[0..filled]) |*result| antfly.readers.deinitResult(result_alloc, result);
                result_alloc.free(out);
            }
            for (raster_request.images, 0..) |raster, i| {
                try std.testing.expectEqual(@intFromPtr(self.expected[i]), @intFromPtr(raster.bytes.ptr));
                try std.testing.expectEqual(@as(usize, 8), raster.stride_bytes);
                self.observed_addresses[i] = @intFromPtr(raster.bytes.ptr);
                out[i] = .{
                    .text = try result_alloc.dupe(u8, if (i == 0) "first" else "second"),
                    .item_id = try result_alloc.dupe(u8, raster.item_id),
                    .source_fingerprint = if (raster.source_fingerprint) |value| try result_alloc.dupe(u8, value) else null,
                    .page_number = raster.page_number,
                };
                filled += 1;
            }
            return .{
                .items = out,
                .execution = .{ .requested_items = 2, .native_batches = 1, .native_items = 2 },
            };
        }
    };
    var fake = FakeReader{ .expected = .{ first[0..].ptr, second[0..].ptr } };
    var state = inference_host.LinkedInferenceState{
        .alloc = alloc,
        .executor = try @import("../runtime_io_abi.zig").Borrow.init(&std.testing.io).receive(),
        .io = std.testing.io,
        .node = undefined, // The model-free override must not enter Node.
        .warm_models = undefined,
        .content_security = null,
        .s3_credentials = null,
        .runtime_config = undefined,
        .owned_models_dir = null,
        .owned_ml_dir = null,
        .route_validator = undefined,
        .read_raster_images_override = .{ .ptr = &fake, .read_fn = FakeReader.read },
    };
    var lifetime = EmbeddedInferenceProviderLifetime{ .handle = &state };

    var batch = try inferenceProviderReadRasterImagesReported(&lifetime, alloc, "florence2", request);
    defer batch.deinit(alloc);
    try std.testing.expectEqual(@as(usize, 1), fake.calls);
    try std.testing.expectEqual(@intFromPtr(first[0..].ptr), fake.observed_addresses[0]);
    try std.testing.expectEqualStrings("first", batch.items[0].text);
    first[0] = 99;
    try std.testing.expectEqualStrings("first", batch.items[0].text);

    var payloads = try rasterProviderPayloadsAlloc(alloc, request.images);
    defer payloads.deinit(alloc);
    const metadata = inference_bridge.ReadRasterImagesRequest{
        .model = "florence2",
        .raster_count = request.images.len,
        .rasters = payloads.metadata,
        .prompt = request.prompt,
        .max_tokens = request.max_tokens,
        .source_fingerprint = request.source_fingerprint,
    };
    const request_json = try std.json.Stringify.valueAlloc(alloc, metadata, .{});
    defer alloc.free(request_json);
    try std.testing.expectError(
        error.InvalidArguments,
        inference_host.decodeReadRasterImagesProviderRequest(
            alloc,
            request_json,
            null,
            payloads.payloads.len,
            payloads.refs.ptr,
            payloads.refs.len,
        ),
    );
    payloads.refs[1].item_index = payloads.refs[0].item_index;
    try std.testing.expectError(
        error.InvalidArguments,
        inference_host.decodeReadRasterImagesProviderRequest(
            alloc,
            request_json,
            payloads.payloads.ptr,
            payloads.payloads.len,
            payloads.refs.ptr,
            payloads.refs.len,
        ),
    );
}

test "standalone runtime local generator preflights mixed resident media exactly" {
    const messages = [_]antfly.inference.ChatMessage{.{
        .role = .user,
        .content = .{ .parts = &.{
            .{ .text = "listen" },
            .{ .media = .{
                .data = "AQID",
                .mime_type = "audio/wav",
            } },
            .{ .image_url = .{ .url = "data:image/png;base64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD" } },
        } },
    }};

    const preflight = try inference_host.preflightLocalGenerateMessages(&messages);
    try std.testing.expectEqual(@as(usize, "listen".len), preflight.text_bytes);
    try std.testing.expectEqual(
        @as(usize, "AQID".len + "data:image/png;base64,iVBORw0KGgoAAAAAAAAAAAAAAAIAAAAD".len),
        preflight.encoded_media_bytes,
    );
    try std.testing.expectEqual(@as(usize, 27), preflight.decoded_media_bytes);
    try std.testing.expectEqual(@as(usize, 2), preflight.media_count);
    try std.testing.expectEqual(@as(usize, 1), preflight.image_count);
    try std.testing.expect(preflight.has_audio);
}

test "standalone runtime local generator refuses decode allocation beyond preflight" {
    var no_storage: [0]u8 = .{};
    var fixed = std.heap.FixedBufferAllocator.init(&no_storage);
    var budget = inference_host.LocalGenerateDecodeBudget{ .remaining_bytes = 1 };

    try std.testing.expectError(
        error.RemoteContentTooLarge,
        inference_host.decodeLocalGenerateDataUri(
            fixed.allocator(),
            "data:image/png;base64,AQI=",
            null,
            &budget,
        ),
    );
    try std.testing.expectEqual(@as(usize, 1), budget.remaining_bytes);
}

test "standalone runtime leaves auth disabled unless config or cli enables it" {
    try std.testing.expect(!resolveAuthEnabled(.{}, null));
    try std.testing.expect(resolveAuthEnabled(.{ .auth_enabled = true }, null));
    try std.testing.expect(!resolveAuthEnabled(.{ .auth_enabled = false }, null));
}

test "standalone continuous HA mutation guard follows role lifecycle" {
    try std.testing.expect(standaloneNativeAuthorityInitiallyPermitted(.{}));
    try std.testing.expect(!standaloneNativeAuthorityInitiallyPermitted(.{ .ha_primary_log = "/ha/primary.wal" }));
    try std.testing.expect(!standaloneNativeAuthorityInitiallyPermitted(.{ .ha_standby_log = "/ha/standby.wal" }));
    try std.testing.expect(!haContinuousMutationGuardEnabled(.{}));
    try std.testing.expect(!haContinuousMutationGuardEnabled(.{ .ha_primary_log = "/ha/primary.wal" }));
    try std.testing.expect(!haContinuousMutationGuardEnabled(.{
        .ha_primary_log = "/ha/primary.wal",
        .ha_shard_id = 10,
    }));
    try std.testing.expect(haContinuousMutationGuardEnabled(.{
        .ha_primary_log = "/ha/primary.wal",
        .ha_shard_id = 10,
        .ha_table_id = 20,
    }));
    try std.testing.expect(haContinuousMutationGuardEnabled(.{ .ha_standby_log = "/ha/standby.wal" }));
    try std.testing.expect(!haRemoteApplyMutationsEnabled(.{}));
    try std.testing.expect(!haRemoteApplyMutationsEnabled(.{
        .mode = .remote_write,
        .failure_policy = .block,
        .standby_names = &.{"standby-a"},
    }));
    try std.testing.expect(haRemoteApplyMutationsEnabled(.{
        .mode = .remote_apply,
        .failure_policy = .block,
        .standby_names = &.{"standby-a"},
    }));
}

test "standalone runtime parses experimental flag" {
    const argv = [_][*:0]const u8{"--experimental"};
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var parsed = try parseCli(std.testing.allocator, &iter);
    defer parsed.deinit(std.testing.allocator);
    try std.testing.expect(parsed.experimental);
}

test "standalone inference middleware reuses public API authentication" {
    const alloc = std.testing.allocator;
    const Harness = struct {
        fn next(_: *httpx.Next, ctx: *httpx.Context) anyerror!httpx.Response {
            return ctx.status(204).text("next");
        }

        fn expect(
            middleware: httpx.Middleware,
            path: []const u8,
            authorization: ?[]const u8,
            expected_status: u16,
        ) !void {
            return expectMethod(middleware, .GET, path, authorization, expected_status);
        }

        fn expectMethod(
            middleware: httpx.Middleware,
            method: httpx.Method,
            path: []const u8,
            authorization: ?[]const u8,
            expected_status: u16,
        ) !void {
            var request = try httpx.Request.init(std.testing.allocator, method, path);
            defer request.deinit();
            if (authorization) |value| try request.setHeader("authorization", value);

            var ctx = httpx.Context.init(std.testing.allocator, std.testing.io, &request);
            defer ctx.deinit();
            var next_handler = httpx.Next{ ._call = next };
            var response = try middleware.invoke(&ctx, &next_handler);
            defer response.deinit();

            try std.testing.expectEqual(expected_status, response.status.code);
            if (expected_status == 401) {
                try std.testing.expectEqualStrings(
                    "{\"error\":\"unauthorized\",\"message\":\"valid Basic, Bearer, or ApiKey credentials are required\",\"retryable\":false}",
                    response.body.?,
                );
                try std.testing.expectEqualStrings(
                    "Basic realm=\"antfly\", Bearer realm=\"antfly\", ApiKey realm=\"antfly\"",
                    response.headers.get("WWW-Authenticate").?,
                );
            } else if (expected_status == 403) {
                const required = if (method == .GET or method == .HEAD or method == .OPTIONS) "read" else "write";
                const expected = try std.fmt.allocPrint(
                    std.testing.allocator,
                    "{{\"error\":\"forbidden\",\"message\":\"inference {s} permission is required\",\"retryable\":false}}",
                    .{required},
                );
                defer std.testing.allocator.free(expected);
                try std.testing.expectEqualStrings(
                    expected,
                    response.body.?,
                );
            } else if (expected_status == 503) {
                try std.testing.expectEqualStrings(
                    "{\"error\":\"not_ready\",\"message\":\"inference authentication is not ready\",\"retryable\":true}",
                    response.body.?,
                );
                try std.testing.expectEqualStrings("1", response.headers.get("Retry-After").?);
            }
        }
    };

    var route_context = StandaloneHttpContext{ .api_server = null };
    try Harness.expect(inferenceAuthMiddleware(&route_context), "/ai/v1/models", null, 503);

    var store = antfly.usermgr.MemoryStore.init(alloc);
    defer store.deinit();
    var policy_store = antfly.casbin.MemoryAdapter.init(alloc);
    defer policy_store.deinit();
    var manager = try antfly.usermgr.UserManager.init(
        alloc,
        store.iface(),
        try antfly.usermgr.initDefaultEnforcer(alloc, policy_store.iface()),
    );
    defer manager.deinit();
    var user = try manager.createUser("admin", "admin", &.{});
    defer user.deinit(alloc);

    var api_server = antfly.public_api.http_server.ApiHttpServer.init(alloc, .{
        .auth_enabled = true,
        .user_manager = &manager,
    }, .{ .ptr = undefined, .vtable = undefined }, null, null);
    defer api_server.deinit();

    route_context.api_server = &api_server;
    const middleware = inferenceAuthMiddleware(&route_context);
    var table_read = try antfly.usermgr.Permission.initOwned(alloc, .table, "documents", .read);
    defer table_read.deinit(alloc);
    try manager.addPermissionToUser("admin", table_read);
    for ([_][]const u8{ "/ai/v1/models", "/ml/v1/metrics" }) |path| {
        try Harness.expect(middleware, path, null, 401);
        try Harness.expect(middleware, path, "Basic YWRtaW46d3Jvbmc=", 401);
        try Harness.expect(middleware, path, "Basic YWRtaW46YWRtaW4=", 403);
    }

    var inference_read = try antfly.usermgr.Permission.initOwned(alloc, .inference, "*", .read);
    defer inference_read.deinit(alloc);
    try manager.addPermissionToUser("admin", inference_read);
    for ([_][]const u8{ "/ai/v1/models", "/ml/v1/metrics" }) |path| {
        try Harness.expect(middleware, path, "Basic YWRtaW46YWRtaW4=", 204);
    }
    try Harness.expectMethod(middleware, .POST, "/ai/v1/generate", "Basic YWRtaW46YWRtaW4=", 403);

    var inference_write = try antfly.usermgr.Permission.initOwned(alloc, .inference, "*", .write);
    defer inference_write.deinit(alloc);
    try manager.addPermissionToUser("admin", inference_write);
    try Harness.expectMethod(middleware, .POST, "/ai/v1/generate", "Basic YWRtaW46YWRtaW4=", 204);

    var global_read = try antfly.usermgr.Permission.initOwned(alloc, .@"*", "*", .read);
    defer global_read.deinit(alloc);
    try std.testing.expect(antfly.public_api.http_server.permissionsAllow(&.{global_read}, .inference, "*", .read));

    for ([_][]const u8{ "/ai/v10/models", "/ml/v1evil/metrics", "/healthz", "/auth/v1/login" }) |path| {
        try Harness.expect(middleware, path, null, 204);
    }

    api_server.cfg.user_manager = null;
    try Harness.expect(middleware, "/ai/v1/models", null, 503);

    api_server.cfg.auth_enabled = false;
    api_server.cfg.user_manager = &manager;
    try Harness.expect(middleware, "/ai/v1/models", null, 204);

    api_server.cfg.user_manager = null;
    api_server.cfg.trusted_principal_secret = "test-secret";
    try Harness.expect(middleware, "/ai/v1/models", null, 401);
}

test "standalone CORS middleware finalizes independently owned responses" {
    const Harness = struct {
        fn next(_: *httpx.Next, ctx: *httpx.Context) anyerror!httpx.Response {
            // Models the linked API/inference response, which does not own
            // the outer context's response builder or middleware headers.
            var response = httpx.Response.init(ctx.allocator, 200);
            errdefer response.deinit();
            try response.headers.set("Access-Control-Allow-Origin", "*");
            try response.headers.set("Access-Control-Allow-Credentials", "true");
            try response.headers.set("Vary", "Accept-Encoding");
            try response.headers.set("X-Request-ID", "preserved");
            return response;
        }
    };
    const alloc = std.testing.allocator;
    var config = antfly.common.config.Config.CorsConfig{
        .allowed_origins = &.{@constCast("https://allowed.example")},
        .allow_credentials = false,
    };
    var route_context = StandaloneHttpContext{ .api_server = null, .cors_config = &config };
    for ([_][]const u8{ "https://allowed.example", "https://denied.example" }) |origin| {
        var request = try httpx.Request.init(alloc, .POST, "/db/v1/agents/retrieval");
        defer request.deinit();
        try request.setHeader("Origin", origin);
        var ctx = httpx.Context.init(alloc, std.testing.io, &request);
        defer ctx.deinit();
        var next = httpx.Next{ ._call = Harness.next };
        var response = try corsRequest(&route_context, &ctx, &next);
        defer response.deinit();
        if (std.mem.eql(u8, origin, "https://allowed.example")) {
            try std.testing.expectEqualStrings(origin, response.headers.get("Access-Control-Allow-Origin").?);
            try std.testing.expectEqualStrings(origin, ctx.response.headers.get("Access-Control-Allow-Origin").?);
        } else {
            try std.testing.expect(response.headers.get("Access-Control-Allow-Origin") == null);
        }
        try std.testing.expect(response.headers.get("Access-Control-Allow-Credentials") == null);
        try std.testing.expectEqualStrings("preserved", response.headers.get("X-Request-ID").?);
        var vary_count: usize = 0;
        for (response.headers.iterator()) |header| {
            if (std.ascii.eqlIgnoreCase(header.name, "Vary")) vary_count += 1;
        }
        try std.testing.expectEqual(@as(usize, 2), vary_count);
        try applyCorsActualHeaders(alloc, &response.headers, &config, null);
        try std.testing.expectEqualStrings("Accept-Encoding", response.headers.get("Vary").?);
    }
}

test "standalone CORS middleware enforces dynamic configuration for system catalog clients" {
    const Harness = struct {
        fn next(_: *httpx.Next, ctx: *httpx.Context) anyerror!httpx.Response {
            return ctx.status(209).text("next");
        }

        fn execute(
            config: *const antfly.common.config.Config.CorsConfig,
            method: httpx.Method,
            origin: ?[]const u8,
            requested_method: ?[]const u8,
            requested_headers: ?[]const u8,
        ) !httpx.Response {
            var request = try httpx.Request.init(std.testing.allocator, method, "/ai/v1/models");
            defer request.deinit();
            if (origin) |value| try request.setHeader("origin", value);
            if (requested_method) |value| try request.setHeader("access-control-request-method", value);
            if (requested_headers) |value| try request.setHeader("access-control-request-headers", value);

            var ctx = httpx.Context.init(std.testing.allocator, std.testing.io, &request);
            defer ctx.deinit();
            var next_handler = httpx.Next{ ._call = next };
            var route_context = StandaloneHttpContext{ .api_server = null, .cors_config = config };
            return corsMiddleware(&route_context).invoke(&ctx, &next_handler);
        }
    };

    var defaults: antfly.common.config.Config.CorsConfig = .{};
    try validateCorsConfig(&defaults);
    {
        var response = try Harness.execute(&defaults, .GET, "https://any.example", null, null);
        defer response.deinit();
        try std.testing.expectEqual(@as(u16, 209), response.status.code);
        try std.testing.expectEqualStrings("*", response.headers.get("Access-Control-Allow-Origin").?);
        try std.testing.expectEqualStrings(
            "X-Request-ID, Retry-After, Deprecation, X-RateLimit-Limit, X-RateLimit-Remaining, X-RateLimit-Reset, X-Antfly-Next-Cursor",
            response.headers.get("Access-Control-Expose-Headers").?,
        );
    }
    {
        var response = try Harness.execute(
            &defaults,
            .OPTIONS,
            "https://any.example",
            "POST",
            "content-type, AUTHORIZATION",
        );
        defer response.deinit();
        try std.testing.expectEqual(@as(u16, 204), response.status.code);
        try std.testing.expectEqualStrings("GET, POST, PUT, DELETE, OPTIONS, PATCH", response.headers.get("Access-Control-Allow-Methods").?);
        try std.testing.expectEqualStrings("Content-Type, Authorization, X-Requested-With, Accept, Origin", response.headers.get("Access-Control-Allow-Headers").?);
        try std.testing.expectEqualStrings("3600", response.headers.get("Access-Control-Max-Age").?);
    }
    {
        var response = try Harness.execute(&defaults, .OPTIONS, "https://any.example", "BREW", null);
        defer response.deinit();
        try std.testing.expectEqual(@as(u16, 403), response.status.code);
        try std.testing.expect(response.headers.get("Access-Control-Allow-Origin") == null);
    }
    {
        var response = try Harness.execute(&defaults, .OPTIONS, null, "POST", null);
        defer response.deinit();
        try std.testing.expectEqual(@as(u16, 209), response.status.code);
    }

    var exact_origin = "https://allowed.example".*;
    var post_method = "POST".*;
    var allowed_header = "X-Token".*;
    var exposed_header = "X-Request-Id".*;
    var exact_origins = [_][]u8{exact_origin[0..]};
    var post_methods = [_][]u8{post_method[0..]};
    var allowed_headers = [_][]u8{allowed_header[0..]};
    var exposed_headers = [_][]u8{exposed_header[0..]};
    var exact = antfly.common.config.Config.CorsConfig{
        .allowed_origins = &exact_origins,
        .allowed_methods = &post_methods,
        .allowed_headers = &allowed_headers,
        .exposed_headers = &exposed_headers,
        .allow_credentials = true,
        .max_age = 7,
    };
    try validateCorsConfig(&exact);
    {
        var response = try Harness.execute(&exact, .POST, exact_origin[0..], null, null);
        defer response.deinit();
        try std.testing.expectEqualStrings(exact_origin[0..], response.headers.get("Access-Control-Allow-Origin").?);
        try std.testing.expectEqualStrings("true", response.headers.get("Access-Control-Allow-Credentials").?);
        try std.testing.expectEqualStrings("X-Request-Id", response.headers.get("Access-Control-Expose-Headers").?);
    }
    {
        var response = try Harness.execute(&exact, .POST, "https://denied.example", null, null);
        defer response.deinit();
        try std.testing.expectEqual(@as(u16, 209), response.status.code);
        try std.testing.expect(response.headers.get("Access-Control-Allow-Origin") == null);
        try std.testing.expectEqualStrings("Origin", response.headers.get("Vary").?);
    }
    {
        var response = try Harness.execute(&exact, .GET, exact_origin[0..], null, null);
        defer response.deinit();
        try std.testing.expect(response.headers.get("Access-Control-Allow-Origin") == null);
        try std.testing.expectEqualStrings("Origin", response.headers.get("Vary").?);
    }
    {
        var response = try Harness.execute(&exact, .OPTIONS, exact_origin[0..], "POST", "x-token");
        defer response.deinit();
        try std.testing.expectEqual(@as(u16, 204), response.status.code);
        try std.testing.expectEqualStrings("POST", response.headers.get("Access-Control-Allow-Methods").?);
        try std.testing.expectEqualStrings("X-Token", response.headers.get("Access-Control-Allow-Headers").?);
        try std.testing.expectEqualStrings("7", response.headers.get("Access-Control-Max-Age").?);
    }

    var wildcard = "*".*;
    var wildcard_origins = [_][]u8{wildcard[0..]};
    var wildcard_credentials = antfly.common.config.Config.CorsConfig{
        .allowed_origins = &wildcard_origins,
        .allow_credentials = true,
    };
    try std.testing.expectError(error.CorsCredentialsWithWildcardOrigin, validateCorsConfig(&wildcard_credentials));
    var default_wildcard_credentials = antfly.common.config.Config.CorsConfig{ .allow_credentials = true };
    try std.testing.expectError(error.CorsCredentialsWithWildcardOrigin, validateCorsConfig(&default_wildcard_credentials));
    var opaque_origin = "null".*;
    var opaque_origins = [_][]u8{opaque_origin[0..]};
    var opaque_credentials = antfly.common.config.Config.CorsConfig{
        .allowed_origins = &opaque_origins,
        .allow_credentials = true,
    };
    try std.testing.expectError(error.CorsCredentialsWithOpaqueOrigin, validateCorsConfig(&opaque_credentials));

    var wildcard_header = "*".*;
    var wildcard_headers = [_][]u8{wildcard_header[0..]};
    var credentialed_any_header = antfly.common.config.Config.CorsConfig{
        .allowed_origins = &exact_origins,
        .allowed_methods = &post_methods,
        .allowed_headers = &wildcard_headers,
        .allow_credentials = true,
    };
    try validateCorsConfig(&credentialed_any_header);
    {
        var response = try Harness.execute(
            &credentialed_any_header,
            .OPTIONS,
            exact_origin[0..],
            "POST",
            "X-Trace-Id, X-Client-Version",
        );
        defer response.deinit();
        try std.testing.expectEqual(@as(u16, 204), response.status.code);
        try std.testing.expectEqualStrings(
            "X-Trace-Id, X-Client-Version",
            response.headers.get("Access-Control-Allow-Headers").?,
        );
    }

    var credentialed_wildcard_exposed = credentialed_any_header;
    credentialed_wildcard_exposed.exposed_headers = &wildcard_headers;
    try std.testing.expectError(
        error.CorsCredentialsWithWildcardExposedHeaders,
        validateCorsConfig(&credentialed_wildcard_exposed),
    );

    var injected_origin = "https://allowed.example\r\nX-Injected: true".*;
    var injected_origins = [_][]u8{injected_origin[0..]};
    var unsafe = antfly.common.config.Config.CorsConfig{ .allowed_origins = &injected_origins };
    try std.testing.expectError(error.InvalidCorsOrigin, validateCorsConfig(&unsafe));
    unsafe.enabled = false;
    try validateCorsConfig(&unsafe);

    var injected_header = "X-Safe\r\nX-Injected".*;
    var injected_headers = [_][]u8{injected_header[0..]};
    var unsafe_header = antfly.common.config.Config.CorsConfig{ .allowed_headers = &injected_headers };
    try std.testing.expectError(error.InvalidCorsHeader, validateCorsConfig(&unsafe_header));
}

test "standalone runtime local replica reconcile permit blocks only active startup catch-up" {
    var data_server = antfly.data.runtime.DataServer{
        .alloc = std.testing.allocator,
        .provisioned_storage = undefined,
        .read_source = undefined,
        .write_source = undefined,
        .status_source = undefined,
        .api_server_cfg = undefined,
        .query_async_limit = .limited(1),
        .listener_cfg = undefined,
    };

    data_server.provisioned_startup_catch_up_active.store(false, .monotonic);
    data_server.provisioned_startup_catch_up_dirty.store(true, .monotonic);
    data_server.last_provision_metadata_epoch = null;
    data_server.last_provision_fingerprint = null;
    try std.testing.expect(runLocalReplicaRootReconcilePermitHook(&data_server));

    data_server.last_provision_metadata_epoch = 17;
    try std.testing.expect(runLocalReplicaRootReconcilePermitHook(&data_server));

    data_server.last_provision_fingerprint = 99;
    try std.testing.expect(runLocalReplicaRootReconcilePermitHook(&data_server));

    data_server.provisioned_startup_catch_up_active.store(true, .monotonic);
    data_server.provisioned_startup_catch_up_dirty.store(false, .monotonic);
    try std.testing.expect(!runLocalReplicaRootReconcilePermitHook(&data_server));

    data_server.provisioned_startup_catch_up_active.store(false, .monotonic);
    data_server.provisioned_startup_catch_up_dirty.store(false, .monotonic);
    try std.testing.expect(runLocalReplicaRootReconcilePermitHook(&data_server));
}

test "standalone runtime registers antfarm static routes" {
    var server = RecordingServer{ .allocator = std.testing.allocator };
    defer server.deinit();

    try registerAntfarmRoutes(&server);

    try std.testing.expect(server.hasRoute(.get, "/"));
    try std.testing.expect(server.hasRoute(.get, "/assets/*"));
    try std.testing.expect(server.hasRoute(.get, "/fonts/*"));
    try std.testing.expect(server.hasRoute(.get, "/*"));
}

test "standalone runtime antfarm assets support archive and prefix layouts outside cwd" {
    const alloc = std.testing.allocator;
    const io = std.testing.io;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const root = try tmp.dir.realPathFileAlloc(io, ".", alloc);
    defer alloc.free(root);

    for ([_][]const u8{ "archive", "prefix/bin" }) |layout| {
        const exe_dir = try std.fs.path.join(alloc, &.{ root, layout });
        defer alloc.free(exe_dir);
        try std.Io.Dir.cwd().createDirPath(io, exe_dir);
        const asset_root = if (std.mem.eql(u8, layout, "archive")) "archive/share/antfly/antfarm" else "prefix/share/antfly/antfarm";
        for ([_][]const u8{ "index.html", "assets/app.js", "fonts/test.woff2" }) |rel_path| {
            const path = try std.fs.path.join(alloc, &.{ root, asset_root, rel_path });
            defer alloc.free(path);
            try std.Io.Dir.cwd().createDirPath(io, std.fs.path.dirname(path).?);
            var file = try std.Io.Dir.cwd().createFile(io, path, .{});
            defer file.close(io);
            try file.writeStreamingAll(io, rel_path);

            var request = try httpx.Request.init(alloc, .GET, "/");
            defer request.deinit();
            var ctx = httpx.Context.init(alloc, io, &request);
            defer ctx.deinit();
            var response = (try serveAntfarmFileFromExecutableDir(&ctx, exe_dir, rel_path)).?;
            defer response.deinit();
            try std.testing.expectEqual(@as(u16, 200), response.status.code);
            try std.testing.expectEqualStrings(rel_path, response.body.?);
            try std.testing.expectEqualStrings(antfarmContentType(rel_path), response.headers.get("Content-Type").?);
            try std.testing.expectEqual(null, try serveAntfarmFileFromExecutableDir(&ctx, exe_dir, "missing.js"));
        }
    }
}

test "standalone runtime antfarm path guards keep api routes reserved" {
    try std.testing.expect(isAntfarmReservedPath("/db/v1/tables"));
    try std.testing.expect(isAntfarmReservedPath("/ai/v1/models"));
    try std.testing.expect(isAntfarmReservedPath("/antfly/readyz"));
    try std.testing.expect(isAntfarmReservedPath(antfly.admin.routes.ha_primary_status));
    try std.testing.expect(isAntfarmReservedPath("/a2a"));
    try std.testing.expect(isAntfarmReservedPath("/.well-known/agent-card.json"));
    try std.testing.expect(isAntfarmReservedPath("/extensions/v1/packages"));
    try std.testing.expect(isAntfarmReservedPath("/unknown/v1/status"));
    try std.testing.expect(isAntfarmReservedPath("/unknown/v12/status"));
    try std.testing.expect(!isAntfarmReservedPath("/models/version/status"));
    try std.testing.expect(!isAntfarmReservedPath("/models"));
    try std.testing.expect(hasUnsafeStaticPath("../index.html"));
    try std.testing.expect(hasUnsafeStaticPath("%2e%2e/index.html"));
    try std.testing.expect(!hasUnsafeStaticPath("assets/index.js"));
}

test "parse cli accepts config path" {
    var argv = [_][*:0]const u8{ "--config", "antfly.json" };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("antfly.json", cfg.config_path.?);
}

test "parse cli accepts secret store path" {
    var argv = [_][*:0]const u8{ "--secret-store-path", "/run/antfly/secrets/secrets.json" };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("/run/antfly/secrets/secrets.json", cfg.secret_store_paths.items[0]);
}

test "parse cli accepts extension package store path" {
    var argv = [_][*:0]const u8{ "--extension-package-store", "/opt/antfly/extensions" };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("/opt/antfly/extensions", cfg.extension_package_store_dir.?);
}

test "parse cli accepts ARD identity flags" {
    var argv = [_][*:0]const u8{
        "--ard-publisher-domain",
        "tenant.example.com",
        "--ard-base-url",
        "https://tenant.example.com",
        "--ard-display-name",
        "Tenant Antfly",
        "--ard-public-catalog",
        "true",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("https://tenant.example.com", cfg.ard_base_url.?);
    try std.testing.expectEqualStrings("tenant.example.com", cfg.ard_publisher_domain.?);
    try std.testing.expectEqualStrings("Tenant Antfly", cfg.ard_display_name.?);
    try std.testing.expect(cfg.ard_public_catalog_enabled);
}

test "parse cli accepts canonical host port and models dir flags" {
    var argv = [_][*:0]const u8{
        "--host",
        "127.0.0.1",
        "--port",
        "8080",
        "--models-dir",
        "/tmp/models",
        "--ml-dir",
        "/tmp/ml",
        "--preload-model",
        "generator:metal:gemma-e2b",
        "--data-dir",
        "/tmp/antfly-data",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("127.0.0.1", cfg.bind_host.?);
    try std.testing.expectEqual(@as(u16, 8080), cfg.bind_port.?);
    try std.testing.expectEqualStrings("/tmp/models", cfg.inference_models_dir.?);
    try std.testing.expectEqualStrings("/tmp/ml", cfg.inference_ml_dir.?);
    try std.testing.expectEqual(@as(usize, 1), cfg.inference_preload_models.items.len);
    try std.testing.expectEqualStrings("generator", cfg.inference_preload_models.items[0].kind.slice());
    try std.testing.expectEqualStrings("gemma-e2b", cfg.inference_preload_models.items[0].name.slice());
    try std.testing.expectEqualStrings("metal", cfg.inference_preload_models.items[0].backend.slice().?);
    try std.testing.expectEqualStrings("/tmp/antfly-data", cfg.data_dir.?);
}

test "parse cli preserves registry variants and recognizes explicit preload backends" {
    var argv = [_][*:0]const u8{
        "--preload-model",
        "embedder:owner/model:i8",
        "--preload-model",
        "generator:metal:owner/model:Q4_K_M",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);

    try std.testing.expectEqual(@as(usize, 2), cfg.inference_preload_models.items.len);
    try std.testing.expectEqualStrings("owner/model:i8", cfg.inference_preload_models.items[0].name.slice());
    try std.testing.expect(cfg.inference_preload_models.items[0].backend.slice() == null);
    try std.testing.expectEqualStrings("owner/model:Q4_K_M", cfg.inference_preload_models.items[1].name.slice());
    try std.testing.expectEqualStrings("metal", cfg.inference_preload_models.items[1].backend.slice().?);
}

test "parse cli accepts HA primary runtime flags" {
    var argv = [_][*:0]const u8{
        "--hot-standby-primary-log",
        "/tmp/ha-primary.log",
        "--hot-standby-primary-slots",
        "/tmp/ha-slots.wal",
        "--hot-standby-primary-node-id",
        "primary-a",
        "--hot-standby-seed-capture-root",
        "/tmp/ha-seed-captures",
        "--hot-standby-fence-wal",
        "/tmp/ha-fence.wal",
        "--hot-standby-former-primary-log",
        "/tmp/ha-primary.log",
        "--admin-token-env",
        "ANTFLY_HA_ADMIN_TOKEN",
        "--hot-standby-retention-max-lag-lsn",
        "500",
        "--hot-standby-retention-max-retained-bytes",
        "8192",
        "--hot-standby-retention-max-retained-age-ns",
        "1000000",
        "--hot-standby-cluster-id",
        "100",
        "--hot-standby-shard-id",
        "10",
        "--hot-standby-table-id",
        "20",
        "--hot-standby-timeline-id",
        "3",
        "--hot-standby-epoch",
        "4",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try std.testing.expect(haPrimaryRequested(cfg));
    try std.testing.expectEqualStrings("/tmp/ha-primary.log", cfg.ha_primary_log.?);
    try std.testing.expectEqualStrings("/tmp/ha-slots.wal", cfg.ha_primary_slots.?);
    try std.testing.expectEqualStrings("primary-a", cfg.ha_primary_node_id.?);
    try std.testing.expectEqualStrings("/tmp/ha-seed-captures", cfg.ha_seed_capture_root.?);
    try std.testing.expectEqualStrings("/tmp/ha-fence.wal", cfg.ha_fence_wal.?);
    try std.testing.expectEqualStrings("/tmp/ha-primary.log", cfg.ha_former_primary_log.?);
    try std.testing.expectEqualStrings("ANTFLY_HA_ADMIN_TOKEN", cfg.admin_token_env.?);
    try std.testing.expectEqual(@as(u64, 500), cfg.ha_retention_max_lag_lsn.?);
    try std.testing.expectEqual(@as(u64, 8192), cfg.ha_retention_max_retained_bytes.?);
    try std.testing.expectEqual(@as(u64, 1000000), cfg.ha_retention_max_retained_age_ns.?);
    try std.testing.expectEqual(@as(u64, 100), cfg.ha_cluster_id.?);
    try std.testing.expectEqual(@as(u64, 10), cfg.ha_shard_id.?);
    try std.testing.expectEqual(@as(u64, 20), cfg.ha_table_id.?);
    try std.testing.expectEqual(@as(u64, 3), cfg.ha_timeline_id.?);
    try std.testing.expectEqual(@as(u64, 4), cfg.ha_epoch.?);
}

test "parse cli accepts HA primary sync policy flags" {
    var argv = [_][*:0]const u8{
        "--hot-standby-primary-log",
        "/tmp/ha-primary.log",
        "--hot-standby-primary-slots",
        "/tmp/ha-slots.wal",
        "--hot-standby-primary-node-id",
        "primary-a",
        "--hot-standby-fence-wal",
        "/tmp/ha-fence.wal",
        "--hot-standby-cluster-id",
        "100",
        "--hot-standby-timeline-id",
        "3",
        "--hot-standby-epoch",
        "4",
        "--hot-standby-sync-mode",
        "remote-apply",
        "--hot-standby-sync-selection",
        "first",
        "--hot-standby-sync-required",
        "2",
        "--hot-standby-sync-standby",
        "standby-a",
        "--hot-standby-sync-standby",
        "standby-b",
        "--hot-standby-sync-failure",
        "fail-closed",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);

    try validateHARole(cfg);
    var sync_policy = try haSyncPolicyFromCli(std.testing.allocator, cfg);
    defer sync_policy.deinit(std.testing.allocator);

    try std.testing.expectEqual(antfly.hot_standby.primary.DurabilityMode.remote_apply, sync_policy.policy.mode);
    try std.testing.expectEqual(antfly.hot_standby.primary.StandbySelection.first, sync_policy.policy.selection);
    try std.testing.expectEqual(@as(usize, 2), sync_policy.policy.required);
    try std.testing.expectEqual(antfly.hot_standby.primary.FailurePolicy.fail_closed, sync_policy.policy.failure_policy);
    try std.testing.expectEqual(@as(usize, 2), sync_policy.policy.standby_names.len);
    try std.testing.expectEqualStrings("standby-a", sync_policy.policy.standby_names[0]);
    try std.testing.expectEqualStrings("standby-b", sync_policy.policy.standby_names[1]);
}

test "parse cli treats ALL HA sync policy as all named standbys" {
    var argv = [_][*:0]const u8{
        "--hot-standby-primary-log",
        "/tmp/ha-primary.log",
        "--hot-standby-primary-slots",
        "/tmp/ha-primary.slots",
        "--hot-standby-primary-node-id",
        "primary-a",
        "--hot-standby-fence-wal",
        "/tmp/ha-fence.wal",
        "--hot-standby-cluster-id",
        "100",
        "--hot-standby-timeline-id",
        "3",
        "--hot-standby-epoch",
        "4",
        "--hot-standby-sync-mode",
        "remote-apply",
        "--hot-standby-sync-selection",
        "all",
        "--hot-standby-sync-standby",
        "standby-a",
        "--hot-standby-sync-standby",
        "standby-b",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);

    try validateHARole(cfg);
    var sync_policy = try haSyncPolicyFromCli(std.testing.allocator, cfg);
    defer sync_policy.deinit(std.testing.allocator);

    try std.testing.expectEqual(antfly.hot_standby.primary.DurabilityMode.remote_apply, sync_policy.policy.mode);
    try std.testing.expectEqual(antfly.hot_standby.primary.StandbySelection.all, sync_policy.policy.selection);
    try std.testing.expectEqual(@as(usize, 2), sync_policy.policy.required);
    try std.testing.expectEqual(@as(usize, 2), sync_policy.policy.standby_names.len);

    cfg.ha_sync_required = 1;
    try std.testing.expectError(error.InvalidHASyncPolicy, haSyncPolicyFromCli(std.testing.allocator, cfg));
}

test "parse cli accepts HA primary retention policy flags" {
    var argv = [_][*:0]const u8{
        "--hot-standby-primary-log",
        "/tmp/ha-primary.log",
        "--hot-standby-primary-slots",
        "/tmp/ha-slots.wal",
        "--hot-standby-primary-node-id",
        "primary-a",
        "--hot-standby-fence-wal",
        "/tmp/ha-fence.wal",
        "--hot-standby-cluster-id",
        "100",
        "--hot-standby-timeline-id",
        "3",
        "--hot-standby-epoch",
        "4",
        "--hot-standby-retention-max-lag-lsn",
        "50",
        "--hot-standby-retention-max-retained-bytes",
        "4096",
        "--hot-standby-retention-max-retained-age-ns",
        "1000000",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);

    try validateHARole(cfg);
    const retention_policy = try haRetentionPolicyFromCli(cfg);
    try std.testing.expectEqual(@as(u64, 50), retention_policy.max_lag_lsn);
    try std.testing.expectEqual(@as(u64, 4096), retention_policy.max_retained_bytes);
    try std.testing.expectEqual(@as(u64, 1000000), retention_policy.max_retained_age_ns);
}

test "promoted HA primary retains exact predecessor startup provenance" {
    const digest_a = "aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa";
    const digest_b = "bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb";
    const digest_c = "cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc";
    var cfg = CliConfig{
        .local_node_id = 1,
        .ha_primary_log = "/tmp/active/live-generations/generation-a/primary.wal",
        .ha_primary_slots = "/tmp/active/live-generations/generation-a/slots",
        .ha_primary_node_id = "standby-a",
        .ha_fence_wal = "/tmp/active/live-generations/generation-a/fence.wal",
        .ha_cluster_id = 100,
        .ha_shard_id = 10,
        .ha_table_id = 20,
        .ha_timeline_id = 2,
        .ha_epoch = 2,
        .ha_startup_target_root = "/tmp/active",
        .ha_startup_topology_id = "topology-a",
        .ha_startup_topology_generation = 3,
        .ha_startup_generation = "generation-a",
        .ha_startup_slot_name = "standby-a",
        .ha_startup_timeline_id = 1,
        .ha_startup_epoch = 1,
        .ha_startup_target_pvc_name = "standby-a-data",
        .ha_startup_target_pvc_uid = "pvc-uid-1",
        .ha_startup_capture_receipt_sha256 = digest_a,
        .ha_startup_materialized_receipt_sha256 = digest_b,
        .ha_startup_materialized_aggregate_sha256 = digest_c,
        .ha_startup_target_local_node_id = 1,
        .ha_startup_target_replica_id = 1,
    };

    try validateHARole(cfg);
    const expectation = (try haStartupExpectationFromCli(cfg)) orelse return error.TestExpectedEqual;
    try std.testing.expectEqualStrings("standby-a", expectation.expected.slot_name);
    try std.testing.expectEqualStrings("standby-a", expectation.binding.node_id);
    try std.testing.expectEqual(@as(u64, 100), expectation.expected.identity.cluster_id);
    try std.testing.expectEqual(@as(u64, 1), expectation.expected.identity.timeline_id);
    try std.testing.expectEqual(@as(u64, 1), expectation.expected.identity.epoch);

    cfg.ha_startup_timeline_id = 3;
    try std.testing.expectError(error.HAStartupReplicationIdentityMismatch, haStartupExpectationFromCli(cfg));
    cfg.ha_startup_timeline_id = 2;
    cfg.ha_startup_epoch = 2;
    try std.testing.expectError(error.HAStartupReplicationIdentityMismatch, haStartupExpectationFromCli(cfg));
}

test "parse cli accepts HA standby runtime flags" {
    var argv = [_][*:0]const u8{
        "--id",
        "7",
        "--hot-standby-log",
        "/tmp/ha-standby.log",
        "--hot-standby-progress",
        "/tmp/ha-standby-progress.wal",
        "--hot-standby-node-id",
        "standby-a",
        "--hot-standby-seed-capture-root",
        "/tmp/ha-seed-captures",
        "--hot-standby-fence-wal",
        "/tmp/ha-fence.wal",
        "--hot-standby-upstream-url",
        "http://primary.antfly.svc:8080",
        "--hot-standby-slot",
        "standby-a",
        "--hot-standby-startup-target-root",
        "/tmp/active",
        "--hot-standby-startup-topology-id",
        "topology-a",
        "--hot-standby-startup-topology-generation",
        "3",
        "--hot-standby-startup-generation",
        "generation-a",
        "--hot-standby-startup-target-pvc-name",
        "standby-a-data",
        "--hot-standby-startup-target-pvc-uid",
        "pvc-uid-1",
        "--hot-standby-startup-capture-receipt-sha256",
        "aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa",
        "--hot-standby-startup-materialized-receipt-sha256",
        "bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb",
        "--hot-standby-startup-materialized-aggregate-sha256",
        "cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc",
        "--hot-standby-startup-target-local-node-id",
        "7",
        "--hot-standby-startup-target-replica-id",
        "1",
        "--hot-standby-cluster-id",
        "100",
        "--hot-standby-shard-id",
        "10",
        "--hot-standby-table-id",
        "20",
        "--hot-standby-timeline-id",
        "3",
        "--hot-standby-epoch",
        "4",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try validateHARole(cfg);
    try std.testing.expect(!haPrimaryRequested(cfg));
    try std.testing.expect(haStandbyRequested(cfg));
    try std.testing.expectEqualStrings("/tmp/ha-standby.log", cfg.ha_standby_log.?);
    try std.testing.expectEqualStrings("/tmp/ha-standby-progress.wal", cfg.ha_standby_progress.?);
    try std.testing.expectEqualStrings("standby-a", cfg.ha_standby_node_id.?);
    try std.testing.expectEqualStrings("/tmp/ha-seed-captures", cfg.ha_seed_capture_root.?);
    try std.testing.expectEqualStrings("/tmp/ha-fence.wal", cfg.ha_fence_wal.?);
    try std.testing.expectEqualStrings("http://primary.antfly.svc:8080", cfg.ha_standby_upstream_url.?);
    try std.testing.expectEqualStrings("standby-a", cfg.ha_standby_slot.?);
    try std.testing.expectEqualStrings("/tmp/active", cfg.ha_startup_target_root.?);
    try std.testing.expectEqualStrings("topology-a", cfg.ha_startup_topology_id.?);
    try std.testing.expectEqual(@as(u64, 3), cfg.ha_startup_topology_generation.?);
    try std.testing.expectEqualStrings("generation-a", cfg.ha_startup_generation.?);
    try std.testing.expectEqualStrings("standby-a-data", cfg.ha_startup_target_pvc_name.?);
    try std.testing.expectEqualStrings("pvc-uid-1", cfg.ha_startup_target_pvc_uid.?);
    try std.testing.expectEqualStrings("aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa", cfg.ha_startup_capture_receipt_sha256.?);
    try std.testing.expectEqualStrings("bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb", cfg.ha_startup_materialized_receipt_sha256.?);
    try std.testing.expectEqualStrings("cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc", cfg.ha_startup_materialized_aggregate_sha256.?);
    try std.testing.expectEqual(@as(u64, 7), cfg.ha_startup_target_local_node_id.?);
    try std.testing.expectEqual(@as(u64, 1), cfg.ha_startup_target_replica_id.?);
    try std.testing.expectEqual(@as(u64, 100), cfg.ha_cluster_id.?);
    try std.testing.expectEqual(@as(u64, 10), cfg.ha_shard_id.?);
    try std.testing.expectEqual(@as(u64, 20), cfg.ha_table_id.?);
    try std.testing.expectEqual(@as(u64, 3), cfg.ha_timeline_id.?);
    try std.testing.expectEqual(@as(u64, 4), cfg.ha_epoch.?);

    const replication_cfg = (try haStandbyReplicationConfigFromCliWithBearerToken(cfg, "runtime-secret-token")) orelse return error.TestExpectedEqual;
    try std.testing.expectEqualStrings("http://primary.antfly.svc:8080", replication_cfg.upstream_base_uri);
    try std.testing.expectEqualStrings("standby-a", replication_cfg.slot_name);
    try std.testing.expectEqualStrings("runtime-secret-token", replication_cfg.bearer_token orelse return error.TestExpectedEqual);
    const startup = (try haStartupExpectationFromCli(cfg)) orelse return error.TestExpectedEqual;
    try std.testing.expectEqualStrings("/tmp/active", startup.target_root);
    try std.testing.expectEqualStrings("topology-a", startup.binding.topology_id);
    try std.testing.expectEqual(@as(u64, 3), startup.binding.topology_generation);
    try std.testing.expectEqualStrings("generation-a", startup.expected.generation);
    try std.testing.expectEqualStrings(startup.capture_receipt_sha256.?, startup.expected.capture_receipt_sha256.?);
    try std.testing.expectEqualStrings("bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb", startup.materialized_receipt_sha256.?);
    try std.testing.expectEqualStrings("cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc", startup.materialized_aggregate_sha256.?);
    try std.testing.expectEqual(@as(u64, 7), startup.target_local_node_id.?);
    try std.testing.expectEqual(@as(u64, 1), startup.target_replica_id.?);

    var missing_capture_authority = cfg;
    missing_capture_authority.ha_startup_capture_receipt_sha256 = null;
    try std.testing.expectError(error.HAStartupCaptureReceiptSHA256Missing, haStartupExpectationFromCli(missing_capture_authority));

    var missing_materialized_receipt = cfg;
    missing_materialized_receipt.ha_startup_materialized_receipt_sha256 = null;
    try std.testing.expectError(error.HAStartupMaterializedReceiptSHA256Missing, haStartupExpectationFromCli(missing_materialized_receipt));

    var missing_materialized_aggregate = cfg;
    missing_materialized_aggregate.ha_startup_materialized_aggregate_sha256 = null;
    try std.testing.expectError(error.HAStartupMaterializedAggregateSHA256Missing, haStartupExpectationFromCli(missing_materialized_aggregate));

    var missing_target_local_node = cfg;
    missing_target_local_node.ha_startup_target_local_node_id = null;
    try std.testing.expectError(error.HAStartupTargetLocalNodeIDMissing, haStartupExpectationFromCli(missing_target_local_node));

    var missing_target_replica = cfg;
    missing_target_replica.ha_startup_target_replica_id = null;
    try std.testing.expectError(error.HAStartupTargetReplicaIDMissing, haStartupExpectationFromCli(missing_target_replica));

    var wrong_target_local_node = cfg;
    wrong_target_local_node.ha_startup_target_local_node_id = 8;
    try std.testing.expectError(error.HAStartupTargetLocalNodeIDMismatch, haStartupExpectationFromCli(wrong_target_local_node));

    var wrong_target_replica = cfg;
    wrong_target_replica.ha_startup_target_replica_id = 2;
    try std.testing.expectError(error.HAStartupTargetReplicaIDMismatch, haStartupExpectationFromCli(wrong_target_replica));
}

test "deprecated --ha-* flags remain aliases for --hot-standby-* flags" {
    // HOT_STANDBY.md "Naming": the role segment collapses for --ha-standby-*
    // (`--hot-standby-log` etc.), --ha-primary-* keeps its role segment
    // (`--hot-standby-primary-*`), and every other --ha-* just swaps its
    // prefix. Every pair below must parse to the identical field value
    // regardless of which spelling is used, proving flagMatches keeps the
    // legacy alias working.
    const Pair = struct {
        canonical: [:0]const u8,
        legacy: [:0]const u8,
        value: [:0]const u8,
        field: []const u8,
    };
    const pairs = [_]Pair{
        .{ .canonical = "--hot-standby-primary-log", .legacy = "--ha-primary-log", .value = "/tmp/primary.log", .field = "ha_primary_log" },
        .{ .canonical = "--hot-standby-primary-slots", .legacy = "--ha-primary-slots", .value = "/tmp/slots.wal", .field = "ha_primary_slots" },
        .{ .canonical = "--hot-standby-primary-node-id", .legacy = "--ha-primary-node-id", .value = "primary-a", .field = "ha_primary_node_id" },
        .{ .canonical = "--hot-standby-seed-capture-root", .legacy = "--ha-seed-capture-root", .value = "/tmp/seed-captures", .field = "ha_seed_capture_root" },
        .{ .canonical = "--hot-standby-fence-wal", .legacy = "--ha-fence-wal", .value = "/tmp/fence.wal", .field = "ha_fence_wal" },
        .{ .canonical = "--hot-standby-former-primary-log", .legacy = "--ha-former-primary-log", .value = "/tmp/former-primary.log", .field = "ha_former_primary_log" },
        .{ .canonical = "--hot-standby-retention-max-lag-lsn", .legacy = "--ha-retention-max-lag-lsn", .value = "500", .field = "ha_retention_max_lag_lsn" },
        .{ .canonical = "--hot-standby-retention-max-retained-bytes", .legacy = "--ha-retention-max-retained-bytes", .value = "8192", .field = "ha_retention_max_retained_bytes" },
        .{ .canonical = "--hot-standby-retention-max-retained-age-ns", .legacy = "--ha-retention-max-retained-age-ns", .value = "1000000", .field = "ha_retention_max_retained_age_ns" },
        .{ .canonical = "--hot-standby-sync-mode", .legacy = "--ha-sync-mode", .value = "remote-apply", .field = "ha_sync_mode" },
        .{ .canonical = "--hot-standby-sync-selection", .legacy = "--ha-sync-selection", .value = "first", .field = "ha_sync_selection" },
        .{ .canonical = "--hot-standby-sync-required", .legacy = "--ha-sync-required", .value = "2", .field = "ha_sync_required" },
        .{ .canonical = "--hot-standby-sync-failure", .legacy = "--ha-sync-failure", .value = "fail-closed", .field = "ha_sync_failure_policy" },
        .{ .canonical = "--hot-standby-log", .legacy = "--ha-standby-log", .value = "/tmp/standby.log", .field = "ha_standby_log" },
        .{ .canonical = "--hot-standby-progress", .legacy = "--ha-standby-progress", .value = "/tmp/standby-progress.wal", .field = "ha_standby_progress" },
        .{ .canonical = "--hot-standby-node-id", .legacy = "--ha-standby-node-id", .value = "standby-a", .field = "ha_standby_node_id" },
        .{ .canonical = "--hot-standby-upstream-url", .legacy = "--ha-standby-upstream-url", .value = "http://primary.antfly.svc:8080", .field = "ha_standby_upstream_url" },
        .{ .canonical = "--hot-standby-slot", .legacy = "--ha-standby-slot", .value = "standby-a", .field = "ha_standby_slot" },
        .{ .canonical = "--hot-standby-startup-target-root", .legacy = "--ha-startup-target-root", .value = "/tmp/active", .field = "ha_startup_target_root" },
        .{ .canonical = "--hot-standby-startup-topology-id", .legacy = "--ha-startup-topology-id", .value = "topology-a", .field = "ha_startup_topology_id" },
        .{ .canonical = "--hot-standby-startup-topology-generation", .legacy = "--ha-startup-topology-generation", .value = "3", .field = "ha_startup_topology_generation" },
        .{ .canonical = "--hot-standby-startup-generation", .legacy = "--ha-startup-generation", .value = "generation-a", .field = "ha_startup_generation" },
        .{ .canonical = "--hot-standby-startup-slot-name", .legacy = "--ha-startup-slot-name", .value = "standby-a", .field = "ha_startup_slot_name" },
        .{ .canonical = "--hot-standby-startup-timeline-id", .legacy = "--ha-startup-timeline-id", .value = "1", .field = "ha_startup_timeline_id" },
        .{ .canonical = "--hot-standby-startup-epoch", .legacy = "--ha-startup-epoch", .value = "1", .field = "ha_startup_epoch" },
        .{ .canonical = "--hot-standby-startup-target-pvc-name", .legacy = "--ha-startup-target-pvc-name", .value = "standby-a-data", .field = "ha_startup_target_pvc_name" },
        .{ .canonical = "--hot-standby-startup-target-pvc-uid", .legacy = "--ha-startup-target-pvc-uid", .value = "pvc-uid-1", .field = "ha_startup_target_pvc_uid" },
        .{ .canonical = "--hot-standby-startup-manifest-sha256", .legacy = "--ha-startup-manifest-sha256", .value = "sha-manifest", .field = "ha_startup_manifest_sha256" },
        .{ .canonical = "--hot-standby-startup-aggregate-sha256", .legacy = "--ha-startup-aggregate-sha256", .value = "sha-aggregate", .field = "ha_startup_aggregate_sha256" },
        .{ .canonical = "--hot-standby-startup-seed-receipt-sha256", .legacy = "--ha-startup-seed-receipt-sha256", .value = "sha-seed-receipt", .field = "ha_startup_seed_receipt_sha256" },
        .{ .canonical = "--hot-standby-startup-capture-receipt-sha256", .legacy = "--ha-startup-capture-receipt-sha256", .value = "sha-capture-receipt", .field = "ha_startup_capture_receipt_sha256" },
        .{ .canonical = "--hot-standby-startup-materialized-receipt-sha256", .legacy = "--ha-startup-materialized-receipt-sha256", .value = "sha-materialized-receipt", .field = "ha_startup_materialized_receipt_sha256" },
        .{ .canonical = "--hot-standby-startup-materialized-aggregate-sha256", .legacy = "--ha-startup-materialized-aggregate-sha256", .value = "sha-materialized-aggregate", .field = "ha_startup_materialized_aggregate_sha256" },
        .{ .canonical = "--hot-standby-startup-target-local-node-id", .legacy = "--ha-startup-target-local-node-id", .value = "7", .field = "ha_startup_target_local_node_id" },
        .{ .canonical = "--hot-standby-startup-target-replica-id", .legacy = "--ha-startup-target-replica-id", .value = "1", .field = "ha_startup_target_replica_id" },
        .{ .canonical = "--hot-standby-cluster-id", .legacy = "--ha-cluster-id", .value = "100", .field = "ha_cluster_id" },
        .{ .canonical = "--hot-standby-shard-id", .legacy = "--ha-shard-id", .value = "10", .field = "ha_shard_id" },
        .{ .canonical = "--hot-standby-table-id", .legacy = "--ha-table-id", .value = "20", .field = "ha_table_id" },
        .{ .canonical = "--hot-standby-timeline-id", .legacy = "--ha-timeline-id", .value = "3", .field = "ha_timeline_id" },
        .{ .canonical = "--hot-standby-epoch", .legacy = "--ha-epoch", .value = "4", .field = "ha_epoch" },
    };

    inline for (pairs) |pair| {
        var canonical_argv = [_][*:0]const u8{ pair.canonical, pair.value };
        var canonical_iter = std.process.Args.Iterator.init(.{ .vector = canonical_argv[0..] });
        var canonical_cfg = try parseCli(std.testing.allocator, &canonical_iter);
        defer canonical_cfg.deinit(std.testing.allocator);

        var legacy_argv = [_][*:0]const u8{ pair.legacy, pair.value };
        var legacy_iter = std.process.Args.Iterator.init(.{ .vector = legacy_argv[0..] });
        var legacy_cfg = try parseCli(std.testing.allocator, &legacy_iter);
        defer legacy_cfg.deinit(std.testing.allocator);

        try std.testing.expectEqualDeep(@field(canonical_cfg, pair.field), @field(legacy_cfg, pair.field));
    }

    // ha_sync_standby_names is list-appended rather than assigned, so it is
    // checked separately from the scalar/string table above.
    var canonical_sync_standby_argv = [_][*:0]const u8{ "--hot-standby-sync-standby", "standby-a" };
    var canonical_sync_standby_iter = std.process.Args.Iterator.init(.{ .vector = canonical_sync_standby_argv[0..] });
    var canonical_sync_standby_cfg = try parseCli(std.testing.allocator, &canonical_sync_standby_iter);
    defer canonical_sync_standby_cfg.deinit(std.testing.allocator);

    var legacy_sync_standby_argv = [_][*:0]const u8{ "--ha-sync-standby", "standby-a" };
    var legacy_sync_standby_iter = std.process.Args.Iterator.init(.{ .vector = legacy_sync_standby_argv[0..] });
    var legacy_sync_standby_cfg = try parseCli(std.testing.allocator, &legacy_sync_standby_iter);
    defer legacy_sync_standby_cfg.deinit(std.testing.allocator);

    try std.testing.expectEqualDeep(
        canonical_sync_standby_cfg.ha_sync_standby_names.items,
        legacy_sync_standby_cfg.ha_sync_standby_names.items,
    );
}

test "standalone HA standby replication flags require upstream and slot" {
    try std.testing.expectError(error.HAStandbySlotMissing, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http://primary.antfly.svc:8080",
    }));
    try std.testing.expectError(error.HAStandbyUpstreamUrlMissing, haStandbyReplicationConfigFromCli(.{
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbyUpstreamUrlMissing, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = " \t ",
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbySlotMissing, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http://primary.antfly.svc:8080",
        .ha_standby_slot = " \t ",
    }));

    try std.testing.expectError(error.HAStandbyUpstreamUrlInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "  http://primary.antfly.svc:8080 \n",
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbyUpstreamUrlInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http://primary.antfly.svc:8080/\treplication",
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbyUpstreamUrlInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http://primary antfly.svc:8080",
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbySlotInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http://primary.antfly.svc:8080",
        .ha_standby_slot = " standby-a\t",
    }));
    try std.testing.expectError(error.HAStandbyUpstreamUrlInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "primary.antfly.svc:8080",
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbyUpstreamUrlInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http:///replication",
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbyUpstreamUrlInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "file:///tmp/primary",
        .ha_standby_slot = "standby-a",
    }));
    try std.testing.expectError(error.HAStandbySlotInvalid, haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http://primary.antfly.svc:8080",
        .ha_standby_slot = "standby a",
    }));

    const replication_cfg = (try haStandbyReplicationConfigFromCli(.{
        .ha_standby_upstream_url = "http://primary.antfly.svc:8080",
        .ha_standby_slot = "standby-a",
    })) orelse return error.TestExpectedEqual;
    try std.testing.expectEqualStrings("http://primary.antfly.svc:8080", replication_cfg.upstream_base_uri);
    try std.testing.expectEqualStrings("standby-a", replication_cfg.slot_name);
}

test "standalone HA string classifier distinguishes missing padded and valid values" {
    try std.testing.expectEqual(antfly.hot_standby.validation.HAStringValidation.missing, antfly.hot_standby.validation.classifyHAString(null));
    try std.testing.expectEqual(antfly.hot_standby.validation.HAStringValidation.missing, antfly.hot_standby.validation.classifyHAString(""));
    try std.testing.expectEqual(antfly.hot_standby.validation.HAStringValidation.missing, antfly.hot_standby.validation.classifyHAString(" \t\r\n"));
    try std.testing.expectEqual(antfly.hot_standby.validation.HAStringValidation.padded, antfly.hot_standby.validation.classifyHAString(" standby-a"));
    try std.testing.expectEqual(antfly.hot_standby.validation.HAStringValidation.padded, antfly.hot_standby.validation.classifyHAString("standby-a\n"));
    try std.testing.expectEqual(antfly.hot_standby.validation.HAStringValidation.ok, antfly.hot_standby.validation.classifyHAString("standby-a"));

    try std.testing.expectError(error.HAStandbySlotMissing, requireHAString(null, error.HAStandbySlotMissing, error.HAStandbySlotInvalid));
    try std.testing.expectError(error.HAStandbySlotMissing, requireHAString(" \t", error.HAStandbySlotMissing, error.HAStandbySlotInvalid));
    try std.testing.expectError(error.HAStandbySlotInvalid, requireHAString(" standby-a ", error.HAStandbySlotMissing, error.HAStandbySlotInvalid));
    try std.testing.expectEqualStrings("standby-a", try requireHAString("standby-a", error.HAStandbySlotMissing, error.HAStandbySlotInvalid));
}

test "standalone HA primary identity defaults shard and table to whole instance" {
    const identity = try haPrimaryIdentity(.{
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    });
    try std.testing.expectEqual(@as(u64, 100), identity.cluster_id);
    try std.testing.expectEqual(@as(u64, 0), identity.shard_id);
    try std.testing.expectEqual(@as(u64, 0), identity.table_id);
    try std.testing.expectEqual(@as(u64, 3), identity.timeline_id);
    try std.testing.expectEqual(@as(u64, 4), identity.epoch);
}

test "standalone HA standby identity defaults shard and table to whole instance" {
    const identity = try haStandbyIdentity(.{
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    });
    try std.testing.expectEqual(@as(u64, 100), identity.cluster_id);
    try std.testing.expectEqual(@as(u64, 0), identity.shard_id);
    try std.testing.expectEqual(@as(u64, 0), identity.table_id);
    try std.testing.expectEqual(@as(u64, 3), identity.timeline_id);
    try std.testing.expectEqual(@as(u64, 4), identity.epoch);
}

test "standalone HA runtime rejects ambiguous role flags" {
    try std.testing.expectError(error.HAMultipleRolesConfigured, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_standby_log = "/tmp/standby.log",
    }));
    try std.testing.expectError(error.HARoleMissing, validateHARole(.{
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HARoleMissing, validateHARole(.{
        .ha_fence_wal = "/tmp/fence.wal",
    }));
    try std.testing.expectError(error.HARoleMissing, validateHARole(.{
        .ha_former_primary_log = "/tmp/former-primary.wal",
    }));
    try validateHARole(.{
        .admin_token_env = "ANTFLY_HA_ADMIN_TOKEN",
    });
    try std.testing.expectError(error.AdminTokenEnvMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .admin_token_env = " \t ",
    }));
    try std.testing.expectError(error.AdminTokenEnvInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .admin_token_env = " ANTFLY_HA_ADMIN_TOKEN ",
    }));
    try std.testing.expectError(error.AdminTokenEnvInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .admin_token_env = "bad-token-env",
    }));
    try std.testing.expectError(error.AdminTokenEnvInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .admin_token_env = "9TOKEN",
    }));
    try std.testing.expectError(error.HAFenceWalMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
    }));
    try std.testing.expectError(error.HAFenceWalMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = " \t ",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAFenceWalInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = " /tmp/fence.wal ",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAFenceWalInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAFormerPrimaryLogInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_former_primary_log = " /tmp/former-primary.wal ",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAFormerPrimaryLogInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_former_primary_log = "/tmp/../former-primary.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAClusterIdMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HATimelineIdMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAEpochMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
    }));
    try std.testing.expectError(error.HAPrimaryLogMissing, validateHARole(.{
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimarySlotsMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimaryNodeIdMissing, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimaryLogInvalid, validateHARole(.{
        .ha_primary_log = " /tmp/primary.log ",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimaryLogInvalid, validateHARole(.{
        .ha_primary_log = "primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimarySlotsInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = " /tmp/slots.wal ",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimarySlotsInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp//slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimaryNodeIdInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = " primary-a ",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAPrimaryNodeIdInvalid, validateHARole(.{
        .ha_primary_log = "/tmp/primary.log",
        .ha_primary_slots = "/tmp/slots.wal",
        .ha_primary_node_id = "primary a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAClusterIdMissing, validateHARole(.{
        .ha_standby_log = "/tmp/standby.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyLogMissing, validateHARole(.{
        .ha_standby_progress = "/tmp/progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyProgressMissing, validateHARole(.{
        .ha_standby_log = "/tmp/standby.log",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyNodeIdMissing, validateHARole(.{
        .ha_standby_log = "/tmp/standby.log",
        .ha_standby_progress = "/tmp/progress.wal",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyLogInvalid, validateHARole(.{
        .ha_standby_log = " /tmp/standby.log ",
        .ha_standby_progress = "/tmp/progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyLogInvalid, validateHARole(.{
        .ha_standby_log = "standby.log",
        .ha_standby_progress = "/tmp/progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyProgressInvalid, validateHARole(.{
        .ha_standby_log = "/tmp/standby.log",
        .ha_standby_progress = " /tmp/progress.wal ",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyProgressInvalid, validateHARole(.{
        .ha_standby_log = "/tmp/standby.log",
        .ha_standby_progress = "/tmp/../progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyNodeIdInvalid, validateHARole(.{
        .ha_standby_log = "/tmp/standby.log",
        .ha_standby_progress = "/tmp/progress.wal",
        .ha_standby_node_id = " standby-a ",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HAStandbyNodeIdInvalid, validateHARole(.{
        .ha_standby_log = "/tmp/standby.log",
        .ha_standby_progress = "/tmp/progress.wal",
        .ha_standby_node_id = "standby a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }));
    try std.testing.expectError(error.HARetentionPolicyRequiresPrimary, validateHARole(.{
        .ha_retention_max_lag_lsn = 50,
    }));
    try std.testing.expectError(error.HARetentionPolicyRequiresPrimary, validateHARole(.{
        .ha_retention_max_retained_bytes = 4096,
    }));
    try std.testing.expectError(error.HARetentionPolicyRequiresPrimary, validateHARole(.{
        .ha_retention_max_retained_age_ns = 1000000,
    }));
    try std.testing.expectError(error.InvalidHARetentionPolicy, parsePositiveU64("0"));
    try std.testing.expectError(error.HASyncPolicyRequiresPrimary, validateHARole(.{
        .ha_sync_mode = .remote_write,
    }));

    var promoted_policy_cli = CliConfig{
        .ha_standby_log = "/tmp/standby.log",
        .ha_standby_progress = "/tmp/progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
        .ha_sync_mode = .remote_apply,
        .ha_sync_required = 1,
        .ha_sync_failure_policy = .block,
    };
    defer promoted_policy_cli.deinit(std.testing.allocator);
    try promoted_policy_cli.ha_sync_standby_names.append(std.testing.allocator, "primary-a");
    try validateHARole(promoted_policy_cli);
    var promoted_policy = try haSyncPolicyFromCli(std.testing.allocator, promoted_policy_cli);
    defer promoted_policy.deinit(std.testing.allocator);
    try std.testing.expectEqual(antfly.hot_standby.primary.DurabilityMode.remote_apply, promoted_policy.policy.mode);
    try std.testing.expectEqual(@as(usize, 1), promoted_policy.policy.required);
    try std.testing.expectEqualStrings("primary-a", promoted_policy.policy.standby_names[0]);
    try std.testing.expectEqual(antfly.hot_standby.primary.FailurePolicy.block, promoted_policy.policy.failure_policy);
    try std.testing.expectError(error.InvalidHASyncPolicy, haSyncPolicyFromCli(std.testing.allocator, .{
        .ha_primary_log = "/tmp/primary.log",
        .ha_fence_wal = "/tmp/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
        .ha_sync_mode = .remote_write,
        .ha_sync_required = 1,
    }));
}

test "standalone hot-standby startup migrates a legacy layout before opening local handles" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const root: [:0]u8 = try tmp.dir.realPathFileAlloc(std.testing.io, ".", alloc);
    defer alloc.free(root);

    const writeFile = struct {
        fn call(dir_path: []const u8, name: []const u8, body: []const u8) !void {
            const a = std.testing.allocator;
            const path = try std.fs.path.join(a, &.{ dir_path, name });
            defer a.free(path);
            if (std.fs.path.dirname(path)) |parent| try std.Io.Dir.cwd().createDirPath(std.testing.io, parent);
            var file = try std.Io.Dir.cwd().createFile(std.testing.io, path, .{ .truncate = true });
            defer file.close(std.testing.io);
            try file.writeStreamingAll(std.testing.io, body);
        }
    }.call;
    const testPathExists = struct {
        fn call(path: []const u8) bool {
            std.Io.Dir.cwd().access(std.testing.io, path, .{}) catch return false;
            return true;
        }
    }.call;

    const legacy = try std.fs.path.join(alloc, &.{ root, "ha" });
    defer alloc.free(legacy);
    try writeFile(legacy, "primary.wal", "primary");
    try writeFile(legacy, "slots", "slots");
    try writeFile(legacy, "standby.wal", "standby-log");
    try writeFile(legacy, "standby-progress.wal", "standby-progress");
    try writeFile(legacy, "fence.wal", "fence");
    try writeFile(legacy, "seed-captures/generations/gen-1/complete.json", "capture");

    const canonical = try std.fs.path.join(alloc, &.{ root, "standby" });
    defer alloc.free(canonical);
    const primary_log = try std.fs.path.join(alloc, &.{ canonical, "primary.wal" });
    defer alloc.free(primary_log);
    const primary_slots = try std.fs.path.join(alloc, &.{ canonical, "slots" });
    defer alloc.free(primary_slots);
    const standby_log = try std.fs.path.join(alloc, &.{ canonical, "log.wal" });
    defer alloc.free(standby_log);
    const standby_progress = try std.fs.path.join(alloc, &.{ canonical, "progress.wal" });
    defer alloc.free(standby_progress);
    const fence_wal = try std.fs.path.join(alloc, &.{ canonical, "fence.wal" });
    defer alloc.free(fence_wal);
    const seed_capture_root = try std.fs.path.join(alloc, &.{ canonical, "seed-captures" });
    defer alloc.free(seed_capture_root);

    const cli = CliConfig{
        .ha_primary_log = primary_log,
        .ha_primary_slots = primary_slots,
        .ha_standby_log = standby_log,
        .ha_standby_progress = standby_progress,
        .ha_fence_wal = fence_wal,
        .ha_seed_capture_root = seed_capture_root,
    };

    try migrateHALegacyLayoutFromCli(alloc, std.testing.io, cli);

    try std.testing.expect(!testPathExists(legacy));
    try std.testing.expect(testPathExists(primary_log));
    try std.testing.expect(testPathExists(primary_slots));
    try std.testing.expect(testPathExists(standby_log));
    try std.testing.expect(testPathExists(standby_progress));
    try std.testing.expect(testPathExists(fence_wal));
    const migrated_capture = try std.fs.path.join(alloc, &.{ seed_capture_root, "generations/gen-1/complete.json" });
    defer alloc.free(migrated_capture);
    try std.testing.expect(testPathExists(migrated_capture));

    // Calling again with an already-canonical tree is a no-op that must not
    // error, matching every later startup on this node.
    try migrateHALegacyLayoutFromCli(alloc, std.testing.io, cli);
    try std.testing.expect(testPathExists(primary_log));
}

test "standalone hot-standby startup migration is a no-op with no hot-standby paths configured" {
    try migrateHALegacyLayoutFromCli(std.testing.allocator, std.testing.io, .{});
}

test "standalone HA runtime requires HA paths under resolved data root" {
    const root = "/tmp/antfly-data-root";
    const primary_cfg = CliConfig{
        .ha_primary_log = root ++ "/ha/primary.log",
        .ha_primary_slots = root ++ "/ha/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_former_primary_log = root ++ "/ha/primary.log",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    };
    try validateHARole(primary_cfg);
    try validateHAPathsUnderRoot(primary_cfg, root);

    try std.testing.expectError(error.HAPrimaryLogInvalid, validateHAPathsUnderRoot(.{
        .ha_primary_log = "/tmp/outside/primary.log",
        .ha_primary_slots = root ++ "/ha/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }, root));
    try std.testing.expectError(error.HAPrimarySlotsInvalid, validateHAPathsUnderRoot(.{
        .ha_primary_log = root ++ "/ha/primary.log",
        .ha_primary_slots = "/tmp/outside/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }, root));
    try std.testing.expectError(error.HAFenceWalInvalid, validateHAPathsUnderRoot(.{
        .ha_primary_log = root ++ "/ha/primary.log",
        .ha_primary_slots = root ++ "/ha/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = "/tmp/outside/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }, root));
    try std.testing.expectError(error.HAFormerPrimaryLogInvalid, validateHAPathsUnderRoot(.{
        .ha_primary_log = root ++ "/ha/primary.log",
        .ha_primary_slots = root ++ "/ha/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_former_primary_log = "/tmp/outside/former-primary.log",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }, root));

    const standby_cfg = CliConfig{
        .ha_standby_log = root ++ "/ha/standby.log",
        .ha_standby_progress = root ++ "/ha/standby-progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    };
    try validateHARole(standby_cfg);
    try validateHAPathsUnderRoot(standby_cfg, root);

    try std.testing.expectError(error.HAStandbyLogInvalid, validateHAPathsUnderRoot(.{
        .ha_standby_log = "/tmp/outside/standby.log",
        .ha_standby_progress = root ++ "/ha/standby-progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }, root));
    try std.testing.expectError(error.HAStandbyProgressInvalid, validateHAPathsUnderRoot(.{
        .ha_standby_log = root ++ "/ha/standby.log",
        .ha_standby_progress = "/tmp/outside/standby-progress.wal",
        .ha_standby_node_id = "standby-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }, root));
    try std.testing.expectError(error.HAPrimaryLogInvalid, validateHAPathsUnderRoot(.{
        .ha_primary_log = "/tmp/antfly-data-root2/ha/primary.log",
        .ha_primary_slots = root ++ "/ha/slots.wal",
        .ha_primary_node_id = "primary-a",
        .ha_fence_wal = root ++ "/ha/fence.wal",
        .ha_cluster_id = 100,
        .ha_timeline_id = 3,
        .ha_epoch = 4,
    }, root));
}

test "standalone activated seed bootstraps exact standby checkpoint and rejects older progress" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();

    const receive_path = try std.fmt.allocPrintSentinel(alloc, ".zig-cache/tmp/{s}/standby.wal", .{tmp.sub_path}, 0);
    defer alloc.free(receive_path);
    const progress_path = try std.fmt.allocPrintSentinel(alloc, ".zig-cache/tmp/{s}/standby-progress.wal", .{tmp.sub_path}, 0);
    defer alloc.free(progress_path);
    const identity = antfly.hot_standby.standby.Identity{
        .cluster_id = 101,
        .shard_id = 202,
        .table_id = 303,
        .timeline_id = 4,
        .epoch = 5,
    };

    {
        var standby = try antfly.hot_standby.standby.Standby.open(alloc, receive_path.ptr, progress_path.ptr, identity, .{});
        defer standby.close();
        try bootstrapHAStandbyAtActivatedCheckpoint(alloc, &standby, "seed-generation-7", "standby-a", 41);
        const progress = standby.currentProgress();
        try std.testing.expectEqual(@as(u64, 41), progress.received_lsn);
        try std.testing.expectEqual(@as(u64, 41), progress.applied_lsn);
        try std.testing.expectEqual(@as(u64, 41), progress.safe_read_lsn);
        try std.testing.expectEqual(@as(u64, 42), standby.nextReceiveLsn());
        try bootstrapHAStandbyAtActivatedCheckpoint(alloc, &standby, "seed-generation-7", "standby-a", 41);
        try std.testing.expectError(
            error.StandbyBootstrapCheckpointMismatch,
            bootstrapHAStandbyAtActivatedCheckpoint(alloc, &standby, "seed-generation-other", "standby-a", 41),
        );
        try std.testing.expectError(
            error.StandbyBootstrapCheckpointMissing,
            bootstrapHAStandbyAtActivatedCheckpoint(alloc, &standby, "seed-generation-8", "standby-a", 42),
        );
    }

    var reopened = try antfly.hot_standby.standby.Standby.open(alloc, receive_path.ptr, progress_path.ptr, identity, .{});
    defer reopened.close();
    try std.testing.expectEqual(@as(u64, 42), reopened.nextReceiveLsn());
}

test "standalone HA runtime validates bearer token env name before lookup" {
    const alloc = std.testing.allocator;
    const c = struct {
        extern fn setenv(name: [*:0]const u8, value: [*:0]const u8, overwrite: c_int) c_int;
        extern fn unsetenv(name: [*:0]const u8) c_int;
    };
    const env_name = "ANTFLY_HA_ADMIN_TOKEN_TEST_VALUE";

    try std.testing.expect((try resolveAdminBearerTokenFromCli(alloc, .{})) == null);
    try std.testing.expectError(error.AdminTokenEnvMissing, resolveAdminBearerTokenFromCli(alloc, .{
        .admin_token_env = " \t ",
    }));
    try std.testing.expectError(error.AdminTokenEnvInvalid, resolveAdminBearerTokenFromCli(alloc, .{
        .admin_token_env = "bad-token-env",
    }));
    try std.testing.expectError(error.AdminTokenEnvInvalid, resolveAdminBearerTokenFromCli(alloc, .{
        .admin_token_env = "9TOKEN",
    }));
    try std.testing.expectError(error.AdminTokenMissing, resolveAdminBearerTokenFromCli(alloc, .{
        .admin_token_env = "ANTFLY_HA_ADMIN_TOKEN_SHOULD_NOT_EXIST",
    }));

    try std.testing.expectEqual(@as(c_int, 0), c.setenv(env_name, " secret-token\n", 1));
    defer _ = c.unsetenv(env_name);
    const token = try resolveAdminBearerTokenFromCli(alloc, .{
        .admin_token_env = env_name,
    });
    defer alloc.free(token.?);
    try std.testing.expectEqualStrings("secret-token", token.?);

    try std.testing.expectEqual(@as(c_int, 0), c.setenv(env_name, " \t\n", 1));
    try std.testing.expectError(error.AdminTokenMissing, resolveAdminBearerTokenFromCli(alloc, .{
        .admin_token_env = env_name,
    }));
}

test "standalone runtime defaults public listener to antfarm port" {
    const listener = resolvePublicListener(.{});
    try std.testing.expectEqualStrings("127.0.0.1", listener.bind_host);
    try std.testing.expectEqual(@as(u16, default_public_port), listener.bind_port);
}

test "standalone public HTTP server is restart-safe and uses public API request body limit" {
    const cfg = publicHttpServerConfig("127.0.0.1", 8080);
    try std.testing.expect(cfg.reuse_address);
    try std.testing.expect(!cfg.reuse_port);
    try std.testing.expectEqual(antfly.public_api.http_server.public_api_max_request_body_bytes, cfg.max_body_size);
    try std.testing.expectEqual(@as(usize, 256 * 1024 * 1024), cfg.request_body_buffer_budget_bytes);
    try std.testing.expect(cfg.max_connections >= 1);
    try std.testing.expect(cfg.max_connections <= public_http_connection_ceiling);
    try std.testing.expectEqual(cfg.max_connections, cfg.max_request_tasks);
    try std.testing.expectEqual(public_http_max_h1_inflight_bodies, cfg.max_h1_inflight_bodies);
    try std.testing.expectEqual(@as(u32, 5), cfg.accept_error_backoff_initial_ms);
    try std.testing.expectEqual(@as(u32, 1_000), cfg.accept_error_backoff_max_ms);
    try std.testing.expectEqual(@as(u32, 256), publicHttpConnectionLimitForFdSoftLimit(1024));
    try std.testing.expectEqual(@as(u32, 128), publicHttpConnectionLimitForFdSoftLimit(512));
    try std.testing.expectEqual(@as(u32, 32), publicHttpConnectionLimitForFdSoftLimit(128));
    try std.testing.expectEqual(@as(u32, 1), publicHttpConnectionLimitForFdSoftLimit(3));
}

test "standalone rejects configured server TLS instead of serving plaintext" {
    try antfly.common.config.Config.validateServerTlsConfig(null);
    try std.testing.expectError(error.ServerTlsUnsupported, antfly.common.config.Config.validateServerTlsConfig(.{}));
}

test "standalone Lite transaction sessions survive file reopen" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/sessions.aflite", .{tmp.sub_path});
    defer alloc.free(path);

    var txn_id: antfly.db.types.TxnId = undefined;
    {
        var backend = try antfly.lite.backend.Handle.openOrCreate(alloc, path, .{ .no_sync = false });
        defer backend.deinit();
        var durable = antfly.public_api.transactions.DurableSessionStore.initRuntime(
            alloc,
            try backend.runtimeStoreForNamespace("system/api-transaction-sessions"),
        );
        var registry = antfly.public_api.transactions.SessionRegistry.init(&durable);
        defer registry.deinit(alloc);
        txn_id = (try registry.begin(alloc, .{ .sync_level = .write }, 1)).txn_id;
    }

    {
        var backend = try antfly.lite.backend.Handle.open(alloc, path, .{});
        defer backend.deinit();
        var durable = antfly.public_api.transactions.DurableSessionStore.initRuntime(
            alloc,
            try backend.runtimeStoreForNamespace("system/api-transaction-sessions"),
        );
        var registry = antfly.public_api.transactions.SessionRegistry.init(&durable);
        defer registry.deinit(alloc);
        const restored = registry.getInfo(txn_id) orelse return error.TestExpectedEqual;
        try std.testing.expectEqual(txn_id, restored.txn_id);
        try std.testing.expectEqual(antfly.db.types.SyncLevel.write, restored.sync_level);
    }
}

test "antfly config uses cli override before common config" {
    const alloc = std.testing.allocator;
    var cfg = antfly.common.config.Config{
        .registry = antfly.common.provider_registry.Registry.init(alloc),
        .transcribers = antfly.transcribing.Registry.init(alloc),
        .readers = antfly.readers.Registry.init(alloc),
        .text_to_speech = antfly.synthesizing.Registry.init(alloc),
        .inference = .{
            .api_url = try alloc.dupe(u8, "http://127.0.0.1:9000"),
            .models_dir = try alloc.dupe(u8, "/tmp/from-config"),
            .ml_dir = try alloc.dupe(u8, "/tmp/ml-from-config"),
        },
    };
    defer cfg.deinit();

    const cli = CliConfig{
        .inference_models_dir = "/tmp/from-cli",
        .inference_ml_dir = "/tmp/ml-from-cli",
        .inference_backend_budget_mb = 8192,
    };
    try std.testing.expectEqualStrings("/tmp/from-cli", resolveInferenceModelsDir(cli, &cfg).?);
    try std.testing.expectEqualStrings("/tmp/ml-from-cli", resolveInferenceMlDir(cli, &cfg).?);
    try std.testing.expectEqual(@as(usize, 8192 * 1024 * 1024), (try resolveInferenceBudgetOverrides(cli)).backend_limit_bytes);
}

test "standalone memory budget conversion rejects overflow" {
    try std.testing.expectError(error.InvalidArguments, mibToBytes(std.math.maxInt(usize)));
    try std.testing.expectEqual(
        @as(usize, 0),
        try process_memory_budget.resolve(0, "invalid", "14000"),
    );
}

test "standalone public api caps keep alive request reuse" {
    try std.testing.expect(public_api_max_requests_per_connection > 0);
    try std.testing.expect(public_api_max_requests_per_connection < 1000);
}

test "standalone public api body limit matches common http listener" {
    try std.testing.expectEqual(antfly.common.http.default_max_request_bytes, public_api_max_body_size);
}

test "standalone readiness follows api initialization and unified listener" {
    try std.testing.expect(!standaloneReadyFromState(false, false));
    try std.testing.expect(!standaloneReadyFromState(false, true));
    try std.testing.expect(!standaloneReadyFromState(true, false));
    try std.testing.expect(standaloneReadyFromState(true, true));
}

test "standalone startup checkpoint readiness requires applied and safe-read progress" {
    try std.testing.expect(!startupCheckpointSatisfied(.{ .received_lsn = 11, .applied_lsn = 10, .safe_read_lsn = 10 }, 11));
    try std.testing.expect(!startupCheckpointSatisfied(.{ .received_lsn = 11, .applied_lsn = 11, .safe_read_lsn = 10 }, 11));
    try std.testing.expect(startupCheckpointSatisfied(.{ .received_lsn = 11, .applied_lsn = 11, .safe_read_lsn = 11 }, 11));
}

test "standalone public ready endpoint fails closed before API initialization" {
    var route_context = StandaloneHttpContext{ .api_server = null };

    var request = try httpx.Request.init(std.testing.allocator, .GET, "/readyz");
    defer request.deinit();
    var ctx = httpx.Context.init(std.testing.allocator, std.testing.io, &request);
    defer ctx.deinit();
    var response = try readyzHandler(&route_context, &ctx);
    defer response.deinit();

    try std.testing.expectEqual(@as(u16, 503), response.status.code);
    try std.testing.expectEqualStrings("{\"status\":\"not_ready\"}", response.body.?);
    try std.testing.expectEqualStrings("1", response.headers.get("Retry-After").?);
}

test "parse cli accepts inference budget overrides" {
    var argv = [_][*:0]const u8{
        "--inference-host-budget-mb",
        "4096",
        "--inference-backend-budget-mb",
        "12288",
        "--inference-combined-budget-mb",
        "16384",
        "--inference-kv-budget-mb",
        "2048",
        "--inference-scratch-budget-mb",
        "1024",
        "--process-memory-budget-mb",
        "14000",
        "--kernel-jit-mode",
        "required",
    };
    var iter = std.process.Args.Iterator.init(.{ .vector = argv[0..] });
    var cfg = try parseCli(std.testing.allocator, &iter);
    defer cfg.deinit(std.testing.allocator);
    try std.testing.expectEqual(@as(usize, 4096), cfg.inference_host_budget_mb);
    try std.testing.expectEqual(@as(usize, 12288), cfg.inference_backend_budget_mb);
    try std.testing.expectEqual(@as(usize, 16384), cfg.inference_combined_budget_mb);
    try std.testing.expectEqual(@as(usize, 2048), cfg.inference_kv_budget_mb);
    try std.testing.expectEqual(@as(usize, 1024), cfg.inference_scratch_budget_mb);
    try std.testing.expectEqual(@as(?usize, 14000), cfg.inference_process_memory_budget_mb);
    try std.testing.expectEqual(antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode.required, cfg.inference_kernel_jit_mode.?);
}

test "standalone preserves effective process envelope provenance for inference" {
    const Case = struct {
        source: process_memory_budget.EffectiveSource,
        expected: inference_bridge.ProcessMemoryLimitProvenance,
    };
    inline for ([_]Case{
        .{ .source = .explicit, .expected = .explicit },
        .{ .source = .cgroup_v2, .expected = .cgroup_v2 },
        .{ .source = .cgroup_v1, .expected = .cgroup_v1 },
        .{ .source = .host, .expected = .host },
        .{ .source = .unavailable, .expected = .unavailable },
    }) |case| {
        try std.testing.expectEqual(
            case.expected,
            inferenceMemoryLimitProvenance(case.source),
        );
    }
}

test "standalone kernel JIT mode precedence is CLI then environment then config" {
    const Mode = antfly.common.config.Config.InferenceConfig.KernelJitConfig.Mode;
    try std.testing.expectEqual(Mode.on, try resolveKernelJitMode(.shadow, "on", null));
    try std.testing.expectEqual(Mode.required, try resolveKernelJitMode(.shadow, "invalid", .required));
    try std.testing.expectError(error.InvalidArguments, resolveKernelJitMode(.shadow, "invalid", null));
}

test "inference config falls back to common config" {
    const alloc = std.testing.allocator;
    var cfg = antfly.common.config.Config{
        .registry = antfly.common.provider_registry.Registry.init(alloc),
        .transcribers = antfly.transcribing.Registry.init(alloc),
        .readers = antfly.readers.Registry.init(alloc),
        .text_to_speech = antfly.synthesizing.Registry.init(alloc),
        .admission = .{
            .inference = .{ .max_concurrent_requests = 0 },
        },
        .inference = .{
            .api_url = try alloc.dupe(u8, "http://127.0.0.1:8089"),
            .models_dir = try alloc.dupe(u8, "/tmp/antfly-models"),
            .ml_dir = try alloc.dupe(u8, "/tmp/antfly-ml"),
            .kernel_jit = .{
                .mode = .shadow,
                .cache_dir = try alloc.dupe(u8, "/tmp/antfly-jit"),
                .max_cache_bytes_mb = 256,
                .preload_budget_ms = 120_000,
            },
            .prompt_cache = .{
                .enabled = true,
                .mode = .simple,
                .max_bytes_mb = 256,
                .min_tokens = 48,
                .ttl_ms = 120_000,
            },
            .preload = try alloc.dupe(antfly.common.config.Config.InferenceConfig.WarmModelConfig, &.{
                .{
                    .kind = try alloc.dupe(u8, "generator"),
                    .name = try alloc.dupe(u8, "antflydb/gemma-e2b"),
                    .backend = try alloc.dupe(u8, "metal"),
                    .format = try alloc.dupe(u8, "gguf"),
                    .quantization = try alloc.dupe(u8, "q4_k"),
                },
            }),
        },
    };
    defer cfg.deinit();

    try std.testing.expectEqualStrings("/tmp/antfly-models", resolveInferenceModelsDir(.{}, &cfg).?);
    try std.testing.expectEqualStrings("/tmp/antfly-ml", resolveInferenceMlDir(.{}, &cfg).?);
    try std.testing.expectEqual(@as(u32, 0), resolveInferenceMaxConcurrentRequests(&cfg));
    try std.testing.expectEqual(
        antfly.common.config.default_inference_max_concurrent_requests,
        resolveInferenceMaxConcurrentRequests(null),
    );
    try std.testing.expectEqual(@as(usize, 1), cfg.inference.preload.len);
    try std.testing.expectEqualStrings("generator", cfg.inference.preload[0].kind);
    try std.testing.expectEqualStrings("antflydb/gemma-e2b", cfg.inference.preload[0].name);
    try std.testing.expectEqualStrings("metal", cfg.inference.preload[0].backend.?);
    try std.testing.expectEqualStrings("gguf", cfg.inference.preload[0].format.?);
    try std.testing.expectEqualStrings("q4_k", cfg.inference.preload[0].quantization.?);
}

test "inference admission bridge charges combined native residency to resource manager" {
    var budgets = antfly.resource_manager.Options.defaultBudgets();
    budgets[@intFromEnum(antfly.resource_manager.Slice.inference_model_residency)] =
        .{ .hard_limit_bytes = 100 };
    var manager = antfly.resource_manager.ResourceManager.init(.{ .budgets = budgets });
    var owner = InferenceResourceBudgetOwner{
        .alloc = std.testing.allocator,
        .manager = &manager,
    };
    defer owner.deinit();
    try std.testing.expectEqual(@as(u8, 1), retainInferenceResourceOwner(&owner));
    try std.testing.expectEqual(@as(usize, 2), owner.references.load(.acquire));
    releaseInferenceResourceOwner(&owner);
    try std.testing.expectEqual(@as(usize, 1), owner.references.load(.acquire));

    const oversized = inference_bridge.AdmissionAmounts{
        .host_weight_bytes = 80,
        .backend_weight_bytes = 30,
        .host_kv_bytes = 0,
        .backend_kv_bytes = 0,
        .host_scratch_bytes = 0,
        .backend_scratch_bytes = 0,
    };
    var lease_token: usize = 0;
    try std.testing.expectEqual(
        error.ResourceRequestTooLarge,
        inference_bridge.errorFromStatus(reserveInferenceResources(&owner, &oversized, &lease_token)),
    );
    try std.testing.expectEqual(@as(usize, 0), lease_token);
    try std.testing.expectEqual(
        @as(u64, 0),
        manager.sliceStats(.inference_model_residency).used_bytes,
    );

    const admitted = inference_bridge.AdmissionAmounts{
        .host_weight_bytes = 60,
        .backend_weight_bytes = 30,
        .host_kv_bytes = 0,
        .backend_kv_bytes = 0,
        .host_scratch_bytes = 0,
        .backend_scratch_bytes = 0,
    };
    try std.testing.expect(reserveInferenceResources(&owner, &admitted, &lease_token).isOk());
    try std.testing.expect(lease_token != 0);
    try std.testing.expectEqual(
        @as(u64, 90),
        manager.sliceStats(.inference_model_residency).used_bytes,
    );
    try std.testing.expectEqual(
        if (builtin.os.tag == .macos) @as(u64, 90) else @as(u64, 60),
        manager.snapshot().memory.used_bytes,
    );

    const retained = inference_bridge.AdmissionAmounts{
        .host_weight_bytes = 40,
        .backend_weight_bytes = 30,
        .host_kv_bytes = 0,
        .backend_kv_bytes = 0,
        .host_scratch_bytes = 0,
        .backend_scratch_bytes = 0,
    };
    try std.testing.expect(retainInferenceResources(&owner, lease_token, &retained).isOk());
    try std.testing.expectEqual(
        @as(u64, 70),
        manager.sliceStats(.inference_model_residency).used_bytes,
    );

    const unavailable = inference_bridge.AdmissionAmounts{
        .host_weight_bytes = 31,
        .backend_weight_bytes = 0,
        .host_kv_bytes = 0,
        .backend_kv_bytes = 0,
        .host_scratch_bytes = 0,
        .backend_scratch_bytes = 0,
    };
    var unavailable_lease: usize = 0;
    try std.testing.expectEqual(
        error.ResourceTemporarilyUnavailable,
        inference_bridge.errorFromStatus(reserveInferenceResources(&owner, &unavailable, &unavailable_lease)),
    );
    try std.testing.expectEqual(@as(usize, 0), unavailable_lease);

    releaseInferenceResources(&owner, lease_token);
    try std.testing.expectEqual(
        @as(u64, 0),
        manager.sliceStats(.inference_model_residency).used_bytes,
    );
    try std.testing.expectEqual(@as(u64, 0), manager.snapshot().memory.used_bytes);

    var replacement_token: usize = 0;
    const replacement = inference_bridge.AdmissionAmounts{
        .host_weight_bytes = 10,
        .backend_weight_bytes = 0,
        .host_kv_bytes = 0,
        .backend_kv_bytes = 0,
        .host_scratch_bytes = 0,
        .backend_scratch_bytes = 0,
    };
    try std.testing.expect(reserveInferenceResources(&owner, &replacement, &replacement_token).isOk());
    try std.testing.expect(replacement_token != lease_token);
    releaseInferenceResources(&owner, lease_token);
    try std.testing.expectEqual(@as(u64, 10), manager.snapshot().memory.used_bytes);
    try std.testing.expect(!retainInferenceResources(&owner, lease_token, &replacement).isOk());
    try std.testing.expectEqual(@as(u64, 10), manager.snapshot().memory.used_bytes);
    releaseInferenceResources(&owner, replacement_token);
    try std.testing.expectEqual(@as(u64, 0), manager.snapshot().memory.used_bytes);

    try std.testing.expectEqual(@as(u8, 1), observeInferencePromptCache(&owner, 1, 0, 10));
    try std.testing.expectEqual(@as(u8, 1), observeInferencePromptCache(&owner, 2, 0, 20));
    try std.testing.expectEqual(@as(u8, 0), observeInferencePromptCache(&owner, 1, 0, 0));
    try std.testing.expectEqual(@as(u64, 30), manager.snapshot().memory.used_bytes);
    try std.testing.expectEqual(@as(u8, 1), observeInferencePromptCache(&owner, 1, 10, 0));
    try std.testing.expectEqual(@as(u8, 1), observeInferencePromptCache(&owner, 2, 20, 0));
    try std.testing.expectEqual(@as(u64, 0), manager.snapshot().memory.used_bytes);

    try std.testing.expectEqual(@as(u8, 1), observeInferenceTokenizerCache(&owner, 11, 0, 10));
    try std.testing.expectEqual(@as(u8, 1), observeInferenceTokenizerCache(&owner, 22, 0, 10));
    try std.testing.expectEqual(@as(u8, 0), observeInferenceTokenizerCache(&owner, 11, 0, 0));
    try std.testing.expectEqual(@as(u64, 20), manager.snapshot().memory.used_bytes);
    try std.testing.expectEqual(@as(u8, 1), observeInferenceTokenizerCache(&owner, 11, 10, 0));
    try std.testing.expectEqual(@as(u64, 10), manager.snapshot().memory.used_bytes);
    try std.testing.expectEqual(@as(u8, 1), observeInferenceTokenizerCache(&owner, 22, 10, 0));
    try std.testing.expectEqual(@as(u64, 0), manager.snapshot().memory.used_bytes);
}

test "standalone tokenizer bridge enforces growth and permits exact teardown" {
    var budgets = antfly.resource_manager.Options.defaultBudgets();
    budgets[@intFromEnum(antfly.resource_manager.Slice.inference_tokenizer_cache)] =
        .{ .hard_limit_bytes = 16 };
    var manager = antfly.resource_manager.ResourceManager.init(.{
        .memory_budget = .{ .hard_limit_bytes = 20 },
        .budgets = budgets,
        .identity_allocator = std.testing.allocator,
    });
    defer manager.deinit(std.testing.allocator);
    var owner = InferenceResourceBudgetOwner{
        .alloc = std.testing.allocator,
        .manager = &manager,
    };
    defer owner.deinit();

    try std.testing.expectEqual(
        @as(u8, 1),
        observeInferenceTokenizerCache(&owner, 101, 0, 12),
    );
    try std.testing.expectEqual(
        @as(u8, 0),
        observeInferenceTokenizerCache(&owner, 101, 12, 18),
    );
    try std.testing.expectEqual(
        @as(u64, 12),
        manager.sliceStats(.inference_tokenizer_cache).used_bytes,
    );
    try std.testing.expectEqual(
        @as(u8, 1),
        observeInferenceTokenizerCache(&owner, 101, 12, 0),
    );
    try std.testing.expectEqual(@as(u64, 0), manager.snapshot().memory.used_bytes);
}

test "standalone runtime resolves paths from common storage base dir" {
    const alloc = std.testing.allocator;
    var cfg = antfly.common.config.Config{
        .registry = antfly.common.provider_registry.Registry.init(alloc),
        .transcribers = antfly.transcribing.Registry.init(alloc),
        .readers = antfly.readers.Registry.init(alloc),
        .text_to_speech = antfly.synthesizing.Registry.init(alloc),
        .metadata = .{},
        .storage = .{
            .local_base_dir = try alloc.dupe(u8, "/tmp/antflydb"),
        },
        .inference = .{},
    };
    defer cfg.deinit();

    const resolved = try resolvePaths(alloc, .{}, &cfg);
    defer resolved.deinit(alloc);
    const expected_data_base = try normalizeResolvedPathAlloc(alloc, "/tmp/antflydb/data");
    defer alloc.free(expected_data_base);
    const expected_metadata_base = try normalizeResolvedPathAlloc(alloc, "/tmp/antflydb/metadata");
    defer alloc.free(expected_metadata_base);
    const expected_replica_root = try std.fs.path.join(alloc, &.{ expected_data_base, "replicas" });
    defer alloc.free(expected_replica_root);
    const expected_replica_catalog = try std.fs.path.join(alloc, &.{ expected_data_base, "catalog.txt" });
    defer alloc.free(expected_replica_catalog);
    const expected_local_metadata = try std.fs.path.join(alloc, &.{ expected_metadata_base, "local-metadata.json" });
    defer alloc.free(expected_local_metadata);
    const expected_snapshot_root = try std.fs.path.join(alloc, &.{ expected_data_base, "snapshots" });
    defer alloc.free(expected_snapshot_root);
    const expected_extension_store = try normalizeResolvedPathAlloc(alloc, "/tmp/antflydb/extensions");
    defer alloc.free(expected_extension_store);
    try std.testing.expectEqualStrings(expected_replica_root, resolved.replica_root_dir);
    try std.testing.expectEqualStrings(expected_replica_catalog, resolved.replica_catalog_path);
    try std.testing.expectEqualStrings(expected_local_metadata, resolved.local_metadata_catalog_path);
    try std.testing.expectEqualStrings(expected_snapshot_root, resolved.snapshot_root_dir);
    try std.testing.expectEqualStrings(expected_extension_store, resolved.extension_package_store_dir);
    const expected_secret_store = try normalizeResolvedPathAlloc(alloc, "/tmp/antflydb/secrets.json");
    defer alloc.free(expected_secret_store);
    try std.testing.expectEqualStrings(expected_secret_store, resolved.secret_store_path);
}

test "standalone resolves the default secret store before full config parsing" {
    const alloc = std.testing.allocator;
    const base_dir = (try configLocalBaseDirHintFromRaw(alloc,
        \\{"storage":{"local":{"base_dir":"/var/lib/antfly"}},"generators":{"default":{"api_key":"${secret:generator.key}"}}}
    )).?;
    defer alloc.free(base_dir);
    try std.testing.expectEqualStrings("/var/lib/antfly", base_dir);

    try std.testing.expect((try configLocalBaseDirHintFromRaw(alloc, "{}")) == null);
    try std.testing.expectError(
        error.InvalidConfig,
        configLocalBaseDirHintFromRaw(alloc,
            \\{"storage":{"local":{"base_dir":"${secret:data.dir}"}}}
        ),
    );
}

test "standalone runtime resolves explicit extension package store path" {
    const alloc = std.testing.allocator;
    const resolved = try resolvePaths(alloc, .{ .extension_package_store_dir = "/opt/antfly/extensions" }, null);
    defer resolved.deinit(alloc);
    try std.testing.expectEqualStrings("/opt/antfly/extensions", resolved.extension_package_store_dir);
}

test "standalone runtime resolves extension package store env before local default" {
    const alloc = std.testing.allocator;

    const env_resolved = try resolveExtensionPackageStoreDirWithEnv(alloc, null, "/tmp/antflydb", "/antfly-extension-env");
    defer alloc.free(env_resolved);
    try std.testing.expectEqualStrings("/antfly-extension-env", env_resolved);

    const cli_resolved = try resolveExtensionPackageStoreDirWithEnv(alloc, "/antfly-cli-extensions", "/tmp/antflydb", "/antfly-extension-env");
    defer alloc.free(cli_resolved);
    try std.testing.expectEqualStrings("/antfly-cli-extensions", cli_resolved);
}

test "standalone runtime resolves explicit secret store path" {
    const alloc = std.testing.allocator;
    var cli = CliConfig{};
    defer cli.deinit(alloc);
    try cli.secret_store_paths.append(alloc, "/run/antfly/secrets/secrets.json");
    const resolved = try resolvePaths(alloc, cli, null);
    defer resolved.deinit(alloc);
    try std.testing.expectEqualStrings("/run/antfly/secrets/secrets.json", resolved.secret_store_path);
}

test "standalone runtime data dir overrides common storage base dir" {
    const alloc = std.testing.allocator;
    var cfg = antfly.common.config.Config{
        .registry = antfly.common.provider_registry.Registry.init(alloc),
        .transcribers = antfly.transcribing.Registry.init(alloc),
        .readers = antfly.readers.Registry.init(alloc),
        .text_to_speech = antfly.synthesizing.Registry.init(alloc),
        .metadata = .{},
        .storage = .{
            .local_base_dir = try alloc.dupe(u8, "/tmp/from-config"),
        },
        .inference = .{},
    };
    defer cfg.deinit();

    const local_base = try resolveLocalBaseDir(alloc, .{ .data_dir = "/tmp/from-cli" }, &cfg);
    defer alloc.free(local_base);
    try std.testing.expectEqualStrings("/tmp/from-cli", local_base);

    const resolved = try resolvePaths(alloc, .{ .data_dir = "/tmp/from-cli" }, &cfg);
    defer resolved.deinit(alloc);
    try std.testing.expectEqualStrings("/tmp/from-cli/data/replicas", resolved.replica_root_dir);
    try std.testing.expectEqualStrings("/tmp/from-cli/data/catalog.txt", resolved.replica_catalog_path);
    try std.testing.expectEqualStrings("/tmp/from-cli/metadata/local-metadata.json", resolved.local_metadata_catalog_path);
    try std.testing.expectEqualStrings("/tmp/from-cli/data/snapshots", resolved.snapshot_root_dir);
}

test "standalone standby catalog create rejects before contended locks" {
    const alloc = std.testing.allocator;
    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var server: antfly.data.runtime.DataServer = undefined;
    server.ha_public_gate_state = .{};
    server.ha_public_gate_state.configureStandby(.{ .received_lsn = 1, .applied_lsn = 1, .safe_read_lsn = 1 });
    server.ha_mutation_barrier = .{};
    server.ha_state_mutex = .unlocked;
    var metadata = LocalStandaloneMetadata{
        .alloc = alloc,
        .manager = antfly.metadata.TableManager.init(alloc),
        .extension_catalog = antfly.extensions.ExtensionCatalog.init(alloc),
        .local_node_id = 1,
        .store_id = 1,
        .api_url = try alloc.dupe(u8, "http://127.0.0.1:8080"),
        .replica_root_dir = try alloc.dupe(u8, "."),
        .catalog_path = try alloc.dupe(u8, "unused-catalog"),
        .catalog_store = null,
        .backend_runtime = backend_runtime.ptr(),
        .ha_catalog_server = &server,
    };
    defer metadata.deinit();
    try metadata.manager.upsertTable(antfly.public_api.tables.deriveTableRecord("existing", .{}));
    // Model apply owning the HA lock while another catalog operation owns the
    // catalog lock. Neither new nor repeated creates may wait for either lock.
    lockAtomic(&server.ha_state_mutex);
    defer server.ha_state_mutex.unlock();
    lockAtomic(&metadata.mutex);
    defer metadata.mutex.unlock();
    for ([_][]const u8{ "new_table", "existing" }) |name| {
        try std.testing.expectError(error.HAReadOnlyStandby, LocalStandaloneMetadata.createTable(&metadata, alloc, name, .{}));
        try std.testing.expectError(error.HAReadOnlyStandby, LocalStandaloneMetadata.systemCatalog(&metadata, alloc, .{}, .{
            .mutate = .{ .mutation = .{ .kind = .table, .action = .create, .name = name } },
        }));
    }
}

test "standalone metadata rolls back an undurable catalog mutation" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const catalog_dir = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog-directory", .{tmp.sub_path});
    defer alloc.free(catalog_dir);
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    try ensureDirPath(io_impl.io(), catalog_dir);

    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var metadata = LocalStandaloneMetadata{
        .alloc = alloc,
        .manager = antfly.metadata.TableManager.init(alloc),
        .extension_catalog = antfly.extensions.ExtensionCatalog.init(alloc),
        .local_node_id = 1,
        .store_id = 1,
        .api_url = try alloc.dupe(u8, "http://127.0.0.1:8080"),
        .replica_root_dir = try alloc.dupe(u8, "."),
        .catalog_path = try alloc.dupe(u8, catalog_dir),
        .catalog_store = null,
        .backend_runtime = backend_runtime.ptr(),
    };
    defer metadata.deinit();

    {
        var mutation = try metadata.beginCatalogMutationLocked();
        defer mutation.deinit(&metadata);
        const table = antfly.public_api.tables.deriveTableRecord("docs", .{});
        try mutation.upsertTable(&metadata, table);
        metadata.epoch = 9;
        var persist_failed = false;
        mutation.commit(&metadata) catch {
            persist_failed = true;
        };
        try std.testing.expect(persist_failed);
    }
    try std.testing.expectEqual(@as(u64, 1), metadata.epoch);
    try std.testing.expect(metadata.findTableByNameLocked("docs") == null);
}

test "system catalog standby create replays physical rows and logical binding durably" {
    const alloc = std.testing.allocator;
    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(path);
    const table = antfly.public_api.tables.deriveTableRecord("table:replicated-customer", .{});
    const ranges = try antfly.public_api.tables.deriveInitialRanges(alloc, table);
    defer {
        for (ranges) |range| antfly.metadata.table_manager.freeRange(alloc, range);
        alloc.free(ranges);
    }
    var arena = std.heap.ArenaAllocator.init(alloc);
    defer arena.deinit();
    var payload: []u8 = undefined;
    {
        var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://127.0.0.1:8080", ".", path, backend_runtime.ptr(), null, .local);
        defer metadata.deinit();
        const delta = try metadata.planCatalogLocked(arena.allocator(), .{
            .kind = .table,
            .action = .create,
            .name = "customers",
            .table_id = table.table_id,
            .storage_name = table.name,
        });
        payload = try std.json.Stringify.valueAlloc(arena.allocator(), LocalStandaloneMetadata.CatalogCreate{
            .table = table,
            .ranges = ranges,
            .binding = .{ .previous_revision = metadata.systemCatalogState().revision, .delta = delta },
        }, .{});
        const record = antfly.hot_standby.replication_record.Record{
            .kind = .metadata_mutation,
            .payload_codec = .json,
            .cluster_id = 1,
            .timeline_id = 1,
            .epoch = 1,
            .lsn = 1,
            .previous_lsn = 0,
            .payload = payload,
        };
        try LocalStandaloneMetadata.applyHACatalogCreate(&metadata, record);
        const revision = metadata.systemCatalogState().revision;
        try LocalStandaloneMetadata.applyHACatalogCreate(&metadata, record);
        try std.testing.expectEqual(revision, metadata.systemCatalogState().revision);
        try std.testing.expectEqual(table.table_id, (try metadata.resolveSystemCatalogLocked(.{ .table = "customers" })).?.table_id);
    }
    var recovered = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://127.0.0.1:8080", ".", path, backend_runtime.ptr(), null, .local);
    defer recovered.deinit();
    try std.testing.expectEqual(table.table_id, (try recovered.resolveSystemCatalogLocked(.{ .table = "customers" })).?.table_id);
    try std.testing.expectEqual(ranges.len, recovered.manager.ranges.count());
    try LocalStandaloneMetadata.applyHACatalogCreate(&recovered, .{
        .kind = .metadata_mutation,
        .payload_codec = .json,
        .cluster_id = 1,
        .timeline_id = 1,
        .epoch = 1,
        .lsn = 1,
        .previous_lsn = 0,
        .payload = payload,
    });
}

test "standalone metadata advertises a linearizable owned snapshot" {
    const alloc = std.testing.allocator;
    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const catalog_path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(catalog_path);
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://127.0.0.1:8080", ".", catalog_path, backend_runtime.ptr(), null, .local);
    defer metadata.deinit();
    try metadata.manager.upsertTable(.{ .table_id = 7, .name = "docs" });
    try metadata.manager.upsertRange(.{
        .group_id = 7001,
        .table_id = 7,
        .start_key = "",
    });
    metadata.epoch = 9;

    const source = metadata.statusSource();
    var snapshot = (try source.linearizableSnapshot(.{})) orelse return error.TestUnexpectedResult;
    defer source.freeAdminSnapshot(&snapshot);
    try std.testing.expectEqual(@as(u64, 9), snapshot.status.metadata_epoch);
    try std.testing.expectEqual(@as(usize, 1), snapshot.tables.len);
    try std.testing.expectEqualStrings("docs", snapshot.tables[0].name);

    // Scheduled repair must resolve a complete physical descriptor without
    // falling back to the unbounded administrative snapshot.
    const catalog = metadata.catalogSource();
    var descriptor = (try antfly.public_api.table_catalog.tableGroupDescriptorProjection(
        alloc,
        catalog,
        "docs",
        7001,
        platform_time.monotonicNs() + std.time.ns_per_s,
    )).?;
    defer descriptor.deinit(alloc);
    try std.testing.expectEqual(@as(u64, 7), descriptor.table_id);
    try std.testing.expectEqualStrings(metadata.manager.findTableByName("docs").?.schema_json, descriptor.schema_json);
    try std.testing.expectError(error.CatalogRoutingSnapshotTimeout, antfly.public_api.table_catalog.tableGroupDescriptorProjection(alloc, catalog, "docs", 7001, 0));

    try metadata.setApiUrl("http://127.0.0.1:49152");
    var rebound_snapshot = (try source.linearizableSnapshot(.{})) orelse return error.TestUnexpectedResult;
    defer source.freeAdminSnapshot(&rebound_snapshot);
    try std.testing.expectEqual(@as(usize, 1), rebound_snapshot.stores.len);
    try std.testing.expectEqualStrings("http://127.0.0.1:49152", rebound_snapshot.stores[0].api_url);

    var dropped = try source.dropTableExact(alloc, "docs");
    defer dropped.deinit(alloc);
    try std.testing.expectEqual(@as(u64, 7), dropped.table_id);
    try std.testing.expectEqualSlices(u64, &.{7001}, dropped.group_ids);
    try std.testing.expect(metadata.findTableByNameLocked("docs") == null);
}

test "standalone schema mutation supports atomic merge patch and version CAS" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const catalog_path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(catalog_path);

    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://127.0.0.1:8080", ".", catalog_path, backend_runtime.ptr(), null, .local);
    defer metadata.deinit();
    try metadata.manager.upsertTable(.{
        .table_id = 7,
        .name = "docs",
        .schema_json = "{\"version\":0,\"description\":\"before\"}",
    });

    const source = metadata.statusSource();
    var result = try source.mutateSchema(alloc, "docs", .merge_patch, "{\"description\":\"after\"}", 0);
    defer result.deinit(alloc);
    try std.testing.expectEqual(@as(u32, 1), result.version);
    try std.testing.expect(std.mem.indexOf(u8, result.schema_json, "\"description\":\"after\"") != null);
    try std.testing.expectError(
        error.SchemaVersionChanged,
        source.mutateSchema(alloc, "docs", .replace, "{}", 0),
    );
}

test "standalone routing watch does not report absence after one probe" {
    const alloc = std.testing.allocator;
    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var metadata = LocalStandaloneMetadata{
        .alloc = alloc,
        .manager = antfly.metadata.TableManager.init(alloc),
        .extension_catalog = antfly.extensions.ExtensionCatalog.init(alloc),
        .local_node_id = 1,
        .store_id = 1,
        .api_url = try alloc.dupe(u8, "http://127.0.0.1:8080"),
        .replica_root_dir = try alloc.dupe(u8, "."),
        .catalog_path = try alloc.dupe(u8, ".zig-cache/unused-routing-watch-catalog"),
        .catalog_store = null,
        .backend_runtime = backend_runtime.ptr(),
    };
    defer metadata.deinit();
    metadata.epoch = 9;

    const start_ns = platform_time.monotonicNs();
    const deadline_ns = start_ns + 60 * std.time.ns_per_ms;
    const result = try (try metadata.catalogSource().routingSource()).waitForChange(
        .{ .metadata_group_id = group_ids.main_metadata_group_id, .revision = 9 },
        deadline_ns,
        2 * std.time.ns_per_ms,
    );
    const end_ns = platform_time.monotonicNs();
    switch (result) {
        .authoritative_absence => {},
        // Scheduler delays can exhaust the confirmation budget. In that case
        // the deadline-aware mutex correctly refuses the final read and the
        // watch must retry instead of claiming authoritative absence.
        .retry => try std.testing.expect(end_ns >= deadline_ns),
        .changed => return error.TestUnexpectedResult,
    }
    // The old one-probe implementation returned in roughly 2 ms. Keep a
    // generous lower bound that still rejects premature absence or retry.
    try std.testing.expect(end_ns -| start_ns >= 30 * std.time.ns_per_ms);
}

test "standalone routing watch confirms absence before deadline and retries after expiry" {
    const ManualClock = struct {
        now_ns: u64 = 0,
        sleep_delay_ms: u64 = 0,
        sleep_count: usize = 0,

        fn nowNs(self: *@This()) u64 {
            return self.now_ns;
        }

        fn sleepMs(self: *@This(), ms: u64) void {
            self.sleep_count += 1;
            self.now_ns += (ms + self.sleep_delay_ms) * std.time.ns_per_ms;
        }

        fn yieldNow(self: *@This()) void {
            self.now_ns += std.time.ns_per_ms;
        }
    };

    const alloc = std.testing.allocator;
    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var metadata = LocalStandaloneMetadata{
        .alloc = alloc,
        .manager = antfly.metadata.TableManager.init(alloc),
        .extension_catalog = antfly.extensions.ExtensionCatalog.init(alloc),
        .local_node_id = 1,
        .store_id = 1,
        .api_url = try alloc.dupe(u8, "http://127.0.0.1:8080"),
        .replica_root_dir = try alloc.dupe(u8, "."),
        .catalog_path = try alloc.dupe(u8, ".zig-cache/unused-routing-watch-catalog"),
        .catalog_store = null,
        .backend_runtime = backend_runtime.ptr(),
    };
    defer metadata.deinit();
    metadata.epoch = 9;

    const observed_token = antfly.metadata_api.CatalogRoutingChangeToken{
        .metadata_group_id = group_ids.main_metadata_group_id,
        .revision = 9,
    };
    const deadline_ns = 60 * std.time.ns_per_ms;
    const probe_interval_ns = 2 * std.time.ns_per_ms;

    // A stable, uncontended watch must reserve time for confirmation. Merely
    // waiting until expiry and always returning retry is a regression.
    var clock = ManualClock{};
    try std.testing.expectEqual(
        .authoritative_absence,
        try metadata.catalogWaitForRoutingChangeWithClock(observed_token, deadline_ns, probe_interval_ns, &clock),
    );
    try std.testing.expect(clock.now_ns >= 30 * std.time.ns_per_ms);
    try std.testing.expect(clock.now_ns < deadline_ns);
    try std.testing.expect(clock.sleep_count > 1);

    // Simulate a scheduler pause that overshoots the outer deadline.
    clock = .{ .sleep_delay_ms = 150 };
    try std.testing.expectEqual(
        .retry,
        try metadata.catalogWaitForRoutingChangeWithClock(observed_token, deadline_ns, probe_interval_ns, &clock),
    );
    try std.testing.expect(clock.now_ns >= deadline_ns);
    try std.testing.expectEqual(@as(usize, 1), clock.sleep_count);

    // An expired caller budget must not start a watch or confirm absence.
    clock = .{ .now_ns = deadline_ns };
    try std.testing.expectEqual(
        .retry,
        try metadata.catalogWaitForRoutingChangeWithClock(observed_token, deadline_ns, probe_interval_ns, &clock),
    );
    try std.testing.expectEqual(@as(usize, 0), clock.sleep_count);

    // Mutex contention consumes the same deadline budget as watch sleeps.
    clock = .{};
    try std.testing.expect(metadata.mutex.tryLock());
    defer metadata.mutex.unlock();
    try std.testing.expectEqual(
        .retry,
        try metadata.catalogWaitForRoutingChangeWithClock(observed_token, deadline_ns, probe_interval_ns, &clock),
    );
    try std.testing.expectEqual(deadline_ns, clock.now_ns);
    try std.testing.expectEqual(@as(usize, 0), clock.sleep_count);
}

test "standalone metadata rejects corrupt catalog without double-freeing owned paths" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const catalog_path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/corrupt-catalog.json", .{tmp.sub_path});
    defer alloc.free(catalog_path);
    try writeFileAtomically(alloc, std.Options.debug_io, catalog_path, "{not-json");

    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    const result = LocalStandaloneMetadata.init(
        alloc,
        1,
        1,
        "http://127.0.0.1:8080",
        ".",
        catalog_path,
        backend_runtime.ptr(),
        null,
        .local,
    );
    if (result) |value| {
        var metadata = value;
        defer metadata.deinit();
        return error.TestUnexpectedResult;
    } else |_| {}
}

test "standalone metadata finalizes schema migration from resident runtime evidence" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const catalog_path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(catalog_path);

    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://127.0.0.1:8080", ".", catalog_path, backend_runtime.ptr(), null, .local);
    defer metadata.deinit();
    try metadata.manager.upsertTable(.{
        .table_id = 7,
        .name = "docs",
        .schema_json = "{\"version\":1}",
        .read_schema_json = "{\"version\":0}",
        .indexes_json = "{\"full_text_index_v0\":{\"type\":\"full_text\"},\"full_text_index_v1\":{\"type\":\"full_text\"}}",
    });
    try metadata.manager.upsertRange(.{
        .group_id = 70,
        .table_id = 7,
        .start_key = "",
    });

    const Provider = struct {
        fn collect(
            _: *anyopaque,
            provider_alloc: std.mem.Allocator,
            _: []const antfly.metadata.TableRecord,
            _: []const antfly.metadata.RangeRecord,
        ) !antfly.data.runtime.DataServer.LocalSchemaProgressSnapshot {
            const records = try provider_alloc.alloc(antfly.metadata.SchemaProgressRecord, 1);
            records[0] = .{ .table_id = 7, .node_id = 1, .schema_version = 1 };
            return .{ .records = records, .runtime_coverage_complete = true };
        }
    };
    metadata.local_schema_progress_provider = .{
        .ptr = undefined,
        .collect = Provider.collect,
    };

    try metadata.finalizeReadySchemaMigrations();
    const table = metadata.findTableByNameLocked("docs") orelse return error.TestUnexpectedResult;
    try std.testing.expectEqualStrings("", table.read_schema_json);
    try std.testing.expect(std.mem.indexOf(u8, table.indexes_json, "full_text_index_v0") == null);
    try std.testing.expect(std.mem.indexOf(u8, table.indexes_json, "full_text_index_v1") != null);
}

test "standalone metadata finalizes schema migration through split shard adapter fallback" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const catalog_path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(catalog_path);

    var backend_runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend_runtime.deinit();
    var metadata = LocalStandaloneMetadata{
        .alloc = alloc,
        .manager = antfly.metadata.TableManager.init(alloc),
        .extension_catalog = antfly.extensions.ExtensionCatalog.init(alloc),
        .local_node_id = 1,
        .store_id = 1,
        .api_url = try alloc.dupe(u8, "http://127.0.0.1:8080"),
        .replica_root_dir = try alloc.dupe(u8, "."),
        .catalog_path = try alloc.dupe(u8, catalog_path),
        .catalog_store = null,
        .backend_runtime = backend_runtime.ptr(),
    };
    defer metadata.deinit();
    try metadata.manager.upsertTable(.{
        .table_id = 7,
        .name = "docs",
        .schema_json = "{\"version\":1}",
        .read_schema_json = "{\"version\":0}",
        .indexes_json = "{\"full_text_index_v0\":{\"type\":\"full_text\"},\"full_text_index_v1\":{\"type\":\"full_text\"}}",
    });
    try metadata.manager.upsertRange(.{
        .group_id = 70,
        .table_id = 7,
        .start_key = "",
    });

    const Provider = struct {
        fn collect(
            _: *anyopaque,
            provider_alloc: std.mem.Allocator,
            _: []const antfly.metadata.TableRecord,
            _: []const antfly.metadata.RangeRecord,
        ) !antfly.data.runtime.DataServer.LocalSchemaProgressSnapshot {
            return .{
                .records = try provider_alloc.alloc(antfly.metadata.SchemaProgressRecord, 0),
                .runtime_coverage_complete = false,
            };
        }
    };
    const Adapter = struct {
        calls: usize = 0,

        fn fetchMedianKey(_: *anyopaque, _: std.mem.Allocator, _: u64) !?[]u8 {
            return null;
        }

        fn schemaIndexReady(
            ptr: *anyopaque,
            _: std.mem.Allocator,
            table_name: []const u8,
            group_id: u64,
            schema_version: u32,
            read_schema_version: u32,
        ) !bool {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.calls += 1;
            try std.testing.expectEqualStrings("docs", table_name);
            try std.testing.expectEqual(@as(u64, 70), group_id);
            try std.testing.expectEqual(@as(u32, 1), schema_version);
            try std.testing.expectEqual(@as(u32, 0), read_schema_version);
            return true;
        }
    };
    var adapter = Adapter{};
    metadata.local_schema_progress_provider = .{
        .ptr = undefined,
        .collect = Provider.collect,
        .shard_db_adapter = .{
            .ptr = &adapter,
            .vtable = &.{
                .fetch_median_key = Adapter.fetchMedianKey,
                .schema_index_ready = Adapter.schemaIndexReady,
            },
        },
    };

    try metadata.finalizeReadySchemaMigrations();

    try std.testing.expectEqual(@as(usize, 1), adapter.calls);
    const table = metadata.findTableByNameLocked("docs") orelse return error.TestUnexpectedResult;
    try std.testing.expectEqualStrings("", table.read_schema_json);
    try std.testing.expect(std.mem.indexOf(u8, table.indexes_json, "full_text_index_v0") == null);
    try std.testing.expect(std.mem.indexOf(u8, table.indexes_json, "full_text_index_v1") != null);
}

test "standalone unified server lifecycle propagates startup failure" {
    var lifecycle = UnifiedServerLifecycle.init(std.testing.io);
    lifecycle.publishFailure(error.AddressInUse);
    var cancellation = antfly.common.runtime_lifecycle.CancellationSource{};
    try std.testing.expectError(
        error.AddressInUse,
        lifecycle.waitForStartup(
            antfly.common.runtime_lifecycle.ShutdownDeadline.afterMilliseconds(100),
            cancellation.token(),
        ),
    );
    try std.testing.expectEqual(error.AddressInUse, lifecycle.runtimeFailure().?);
}

test "runtime lease watchdog publishes active self-fenced proof from exact expired lease" {
    const expired =
        \\{"metadata":{"annotations":{"antfly.io/ha-fence-topology-id":"topology-7"}},"spec":{"holderIdentity":"primary-a","leaseDurationSeconds":30,"renewTime":"2026-07-15T12:00:00Z","leaseTransitions":3}}
    ;
    const after_expiry: u64 = 1_784_116_831 * std.time.ns_per_s;
    var runtime_watchdog = RuntimeLeaseWatchdog{
        .watchdog = try antfly.hot_standby.kubernetes_lease_watchdog.Watchdog.init(.{
            .scope = .{
                .topology_id = "topology-7",
                .node_id = "standby-a",
                .data_generation = "initial",
            },
            .grace_ns = 10 * std.time.ns_per_s,
            .sentinel_path = "/tmp/lease-fenced",
        }, null, null),
        .io = std.testing.io,
        .executor = undefined,
        .uri = undefined,
        .token_path = "",
        .lease_name = "topology-ha-fence",
        .lease_namespace = "default",
        .stable_topology_id = "topology-7",
        .node_id = "standby-a",
        .pod_uid = "standby-pod-uid",
        .process_boot_id = [_]u8{'a'} ** 64,
    };
    const observed_monotonic_ns = platform_time.authorityNs();
    const decision = try runtime_watchdog.watchdog.observe(
        std.testing.allocator,
        expired,
        after_expiry,
        observed_monotonic_ns,
    );
    platform_sync.lockYielding(&runtime_watchdog.proof_mutex);
    runtime_watchdog.publishValidatedObservationLocked(decision, observed_monotonic_ns);
    runtime_watchdog.proof_mutex.unlock();

    try std.testing.expectEqual(antfly.hot_standby.kubernetes_lease_watchdog.Decision.waiting, decision);
    const proof = (try RuntimeLeaseWatchdog.proofSnapshot(&runtime_watchdog, std.testing.allocator)).?;
    defer std.testing.allocator.free(proof.observed_holder_node_id);
    try std.testing.expect(proof.active);
    try std.testing.expect(!proof.authority_granted);
    try std.testing.expectEqual(@as(u64, 0), proof.authority_remaining_ms);
    try std.testing.expectEqual(@as(u64, 3), proof.observed_lease_transitions);
    try std.testing.expectEqualStrings("primary-a", proof.observed_holder_node_id);
}

test "standalone metadata catalog source provides compact routing" {
    var metadata: LocalStandaloneMetadata = undefined;
    _ = try metadata.catalogSource().routingSource();
}

test "system catalog standalone join planning retains and replaces compact generations" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(path);
    var backend = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend.deinit();
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    defer metadata.deinit();
    try metadata.manager.upsertTable(.{ .table_id = 77, .name = "docs" });
    try metadata.manager.upsertRange(.{ .table_id = 77, .group_id = 101, .range_id = 1, .start_key = "" });
    const source = metadata.statusSource();
    const first = (try source.acquireJoinPlanning(.{})).?;
    defer first.release();
    const reused = (try source.acquireJoinPlanning(.{})).?;
    defer reused.release();
    try std.testing.expect(first == reused);
    try std.testing.expectEqualSlices(u64, &.{101}, first.findTable("docs").?.group_ids);

    try std.testing.expect(metadata.manager.removeRange(101));
    try metadata.manager.upsertRange(.{ .table_id = 77, .group_id = 102, .range_id = 1, .start_key = "" });
    metadata.epoch += 1;
    const replaced = (try source.acquireJoinPlanning(.{})).?;
    defer replaced.release();
    try std.testing.expect(first != replaced);
    try std.testing.expectEqualSlices(u64, &.{102}, replaced.findTable("docs").?.group_ids);
    try std.testing.expectEqualSlices(u64, &.{101}, first.findTable("docs").?.group_ids);
    metadata.catalog_durability_failed = true;
    try std.testing.expectError(error.MetadataMutationOutcomeUnknown, source.acquireJoinPlanning(.{}));
}

test "runtime lease watchdog fetch and validation failures publish no bootstrap capability" {
    inline for ([_]RuntimeLeaseWatchdog.ObservationFailureStage{ .fetch, .validation }) |stage| {
        var runtime_watchdog = RuntimeLeaseWatchdog{
            .watchdog = try antfly.hot_standby.kubernetes_lease_watchdog.Watchdog.init(.{
                .scope = .{
                    .topology_id = "topology-7",
                    .node_id = "primary-a",
                    .data_generation = "initial",
                },
                .grace_ns = 10 * std.time.ns_per_s,
                .sentinel_path = "/tmp/lease-fenced",
            }, null, null),
            .io = std.testing.io,
            .executor = undefined,
            .uri = undefined,
            .token_path = "",
            .lease_name = "topology-ha-fence",
            .lease_namespace = "default",
            .stable_topology_id = "topology-7",
            .node_id = "primary-a",
            .pod_uid = "primary-pod-uid",
            .process_boot_id = [_]u8{'a'} ** 64,
        };
        platform_sync.lockYielding(&runtime_watchdog.proof_mutex);
        const transition = runtime_watchdog.transitionObservationFailureLocked(stage, 1);
        const repeated_transition = runtime_watchdog.transitionObservationFailureLocked(stage, 2);
        runtime_watchdog.proof_mutex.unlock();
        try std.testing.expectEqual(antfly.hot_standby.kubernetes_lease_watchdog.Decision.waiting, transition.decision);
        try std.testing.expect(transition.should_log);
        try std.testing.expectEqual(antfly.hot_standby.kubernetes_lease_watchdog.Decision.waiting, repeated_transition.decision);
        try std.testing.expect(!repeated_transition.should_log);
        try std.testing.expectEqual(stage == .fetch, runtime_watchdog.fetch_failure_logged);
        try std.testing.expectEqual(stage == .validation, runtime_watchdog.validation_failure_logged);

        const proof = (try RuntimeLeaseWatchdog.proofSnapshot(&runtime_watchdog, std.testing.allocator)).?;
        defer std.testing.allocator.free(proof.observed_holder_node_id);
        try std.testing.expect(!proof.active);
        try std.testing.expect(!proof.authority_granted);
        try std.testing.expectEqual(@as(u64, 0), proof.authority_remaining_ms);
        try std.testing.expectEqual(@as(u64, 0), proof.observed_lease_transitions);
        try std.testing.expectEqual(@as(usize, 0), proof.observed_holder_node_id.len);
    }

    var source = RuntimeLeaseWatchdog{
        .watchdog = try antfly.hot_standby.kubernetes_lease_watchdog.Watchdog.init(.{
            .scope = .{
                .topology_id = "topology-7",
                .node_id = "primary-a",
                .data_generation = "initial",
            },
            .grace_ns = 10 * std.time.ns_per_s,
            .sentinel_path = "/tmp/lease-fenced",
        }, null, null),
        .io = std.testing.io,
        .executor = undefined,
        .uri = undefined,
        .token_path = "",
        .lease_name = "topology-ha-fence",
        .lease_namespace = "default",
        .stable_topology_id = "topology-7",
        .node_id = "primary-a",
        .pod_uid = "primary-pod-uid",
        .process_boot_id = [_]u8{'a'} ** 64,
    };
    source.watchdog.cfg.scope.process_boot_id = &source.process_boot_id;

    var placed = source;
    placed.process_boot_id = [_]u8{'b'} ** 64;
    placed.bindOwnedProcessBootID();

    try std.testing.expectEqualStrings(&placed.process_boot_id, placed.watchdog.cfg.scope.process_boot_id);
    try std.testing.expect(placed.watchdog.cfg.scope.process_boot_id.ptr == placed.process_boot_id[0..].ptr);
}

test "runtime lease watchdog retains a bounded Kubernetes response budget" {
    try std.testing.expectEqual(@as(usize, 256 * 1024), ha_lease_max_response_bytes);
}

test "runtime lease watchdog prefers a DNS-verified Kubernetes API host and retains the injected port" {
    var env = std.process.Environ.Map.init(std.testing.allocator);
    defer env.deinit();
    try env.put("KUBERNETES_SERVICE_HOST", "10.96.0.1");
    try env.put("KUBERNETES_SERVICE_PORT_HTTPS", "443");

    const default_endpoint = try haLeaseAPIEndpoint(&env);
    try std.testing.expectEqualStrings(ha_lease_default_api_host, default_endpoint.host);
    try std.testing.expectEqualStrings("443", default_endpoint.port);

    try env.put(ha_lease_api_host_env, "kubernetes.default.svc.cluster.local");
    const overridden_endpoint = try haLeaseAPIEndpoint(&env);
    try std.testing.expectEqualStrings("kubernetes.default.svc.cluster.local", overridden_endpoint.host);
    try std.testing.expectEqualStrings("443", overridden_endpoint.port);
}

test "system catalog standalone checkpoint preserves bindings and rolls back undurable changes" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(path);
    var backend = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend.deinit();
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    defer metadata.deinit();
    const source = metadata.statusSource();
    // An unbound legacy table gets a catalog binding when its placement is
    // updated. Replacing the manager record must not invalidate delta strings.
    try metadata.manager.upsertTable(.{ .table_id = 77, .name = "legacy" });
    const adopt = try source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .set_tablespace, .kind = .table, .name = "legacy" } } });
    alloc.free(adopt);
    const legacy = (try metadata.resolveSystemCatalogLocked(.{ .table = "legacy" })).?;
    try std.testing.expectEqualStrings("legacy", legacy.name);
    try std.testing.expectEqual(@as(u64, 77), legacy.table_id);
    const create_db = try source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .create, .kind = .database, .name = "analytics" } } });
    alloc.free(create_db);
    const create_table = try source.systemCatalog(alloc, .{}, .{ .mutate = .{
        .mutation = .{ .action = .create, .kind = .table, .database = "analytics", .name = "events" },
        .physical_name = "table:stable-test-identity",
        .create_table_json = "{}",
    } });
    alloc.free(create_table);
    const rename = try source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .rename, .kind = .database, .name = "analytics", .new_name = "warehouse" } } });
    alloc.free(rename);
    metadata.deinit();
    metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    const table = (try metadata.resolveSystemCatalogLocked(.{ .database = "warehouse", .table = "events" })).?;
    try std.testing.expectEqualStrings("table:stable-test-identity", table.name);
    try std.testing.expect((try metadata.resolveSystemCatalogLocked(.{ .database = "analytics", .table = "events" })) == null);
    try std.testing.expectError(error.DatabaseNotEmpty, source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .drop, .kind = .database, .name = "warehouse" } } }));
    const projection = @import("../system_catalog/projection.zig");
    const listing_bytes = try source.systemCatalog(alloc, .{}, .{ .list_tables = .{ .database = "warehouse" } });
    defer alloc.free(listing_bytes);
    var listing = try std.json.parseFromSlice(projection.TableListing, alloc, listing_bytes, .{});
    defer listing.deinit();
    try std.testing.expectEqual(@as(usize, 1), listing.value.entries.len);
    try std.testing.expectEqualStrings("events", listing.value.entries[0].name);
    try std.testing.expectEqual(table.table_id, listing.value.entries[0].table.table_id);
    const exported = try metadata.catalogSource().exportCatalog(alloc);
    defer alloc.free(exported);
    var seed = try std.json.parseFromSlice(antfly.hot_standby.seed_materialization.LogicalCatalog, alloc, exported, .{});
    defer seed.deinit();
    try std.testing.expect(seed.value.system_catalog != null);
    const destination = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/seeded.json", .{tmp.sub_path});
    defer alloc.free(destination);
    {
        var file = try std.Io.Dir.cwd().createFile(std.testing.io, destination, .{});
        defer file.close(std.testing.io);
        const encoded = try std.json.Stringify.valueAlloc(alloc, seed.value, .{ .emit_null_optional_fields = false });
        defer alloc.free(encoded);
        try file.writeStreamingAll(std.testing.io, encoded);
    }
    var seeded = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", destination, backend.ptr(), null, .local);
    defer seeded.deinit();
    const restored = (try seeded.resolveSystemCatalogLocked(.{ .database = "warehouse", .table = "events" })).?;
    try std.testing.expectEqual(table.table_id, restored.table_id);
    try std.testing.expectEqualStrings(table.name, restored.name);
    try std.testing.expectEqual(metadata.systemCatalogState().next_id, seeded.systemCatalogState().next_id);
    try std.testing.expectEqual(metadata.systemCatalogState().revision, seeded.systemCatalogState().revision);
    const previous_store = metadata.owned_catalog_store;
    metadata.owned_catalog_store = null;
    defer metadata.owned_catalog_store = previous_store;
    try std.testing.expectError(error.CatalogStorageUnavailable, source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .rename, .kind = .database, .name = "warehouse", .new_name = "undurable" } } }));
    try std.testing.expect((try metadata.resolveSystemCatalogLocked(.{ .database = "warehouse", .table = "events" })) != null);
    try std.testing.expect((try metadata.resolveSystemCatalogLocked(.{ .database = "undurable", .table = "events" })) == null);
}

test "system catalog standalone writes bounded deltas and recovers an ambiguous sync" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(path);
    var backend = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend.deinit();
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    defer metadata.deinit();
    var arena = std.heap.ArenaAllocator.init(alloc);
    defer arena.deinit();
    const a = arena.allocator();
    const resources = try a.alloc(system_catalog.Resource, 1000);
    for (resources, 0..) |*resource, i| resource.* = .{ .kind = .database, .id = i + 100, .name = try std.fmt.allocPrint(a, "tenant_{d}", .{i}) };
    metadata.system_catalog_state.?.deinit();
    metadata.system_catalog_state = try system_catalog.MutableState.clone(alloc, .{ .resources = resources, .revision = 1, .next_id = 1100 });
    const source = metadata.statusSource();
    const imported = try source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .rename, .kind = .database, .name = "tenant_999", .new_name = "target" } } });
    alloc.free(imported);
    const before = metadata.owned_catalog_backend.?.backend.snapshotWriteStats().wal_append_bytes;
    const renamed = try source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .rename, .kind = .database, .name = "target", .new_name = "renamed" } } });
    alloc.free(renamed);
    const written = metadata.owned_catalog_backend.?.backend.snapshotWriteStats().wal_append_bytes - before;
    // One resource and the head, regardless of the thousand unrelated tenants.
    try std.testing.expect(written > 0 and written < 4096);

    // Reopen without a graceful backend flush: acknowledged row transactions
    // must recover directly from their WAL, including the latest rename.
    metadata.owned_catalog_store.?.deinit();
    metadata.owned_catalog_store = null;
    metadata.owned_catalog_backend.?.abandonAfterCrash();
    metadata.owned_catalog_backend = null;
    metadata.deinit();
    metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    try std.testing.expect(metadata.system_catalog_state.?.index.find(.database, 0, "renamed") != null);

    const Failure = struct {
        fn sync(_: *anyopaque, _: bool) !void {
            return error.InjectedSyncFailure;
        }
    };
    var failing = metadata.owned_catalog_store.?.vtable.*;
    failing.sync = Failure.sync;
    metadata.owned_catalog_store.?.vtable = &failing;
    // Exercise the borrowed-store contract, which requires an explicit sync.
    metadata.catalog_store = &metadata.owned_catalog_store.?;
    try std.testing.expectError(error.MetadataMutationOutcomeUnknown, source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .rename, .kind = .database, .name = "renamed", .new_name = "committed" } } }));
    try std.testing.expectError(error.MetadataMutationOutcomeUnknown, source.systemCatalog(alloc, .{}, .{ .read = .{ .kind = .database, .name = "committed" } }));
    metadata.deinit();
    metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    try std.testing.expect(metadata.system_catalog_state.?.index.find(.database, 0, "committed") != null);
    try std.testing.expect(metadata.system_catalog_state.?.index.find(.database, 0, "renamed") == null);
    try std.testing.expectEqual(@as(usize, 1001), metadata.system_catalog_state.?.index.list(.database, 0).len);
}

test "system catalog standalone routing generation retains old identity through publication" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(path);
    var backend = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend.deinit();
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    defer metadata.deinit();
    try LocalStandaloneMetadata.createTable(&metadata, alloc, "docs", .{});
    var old = try antfly.public_api.table_catalog.RoutingSession.init(alloc, metadata.catalogSource(), null);
    defer old.deinit();
    var same = try antfly.public_api.table_catalog.RoutingSession.init(alloc, metadata.catalogSource(), null);
    defer same.deinit();
    try std.testing.expect(old.generation.? == same.generation.?);
    const revision = old.snapshot.value.catalog_revision;
    try LocalStandaloneMetadata.createTable(&metadata, alloc, "new", .{});
    var current = try antfly.public_api.table_catalog.RoutingSession.init(alloc, metadata.catalogSource(), null);
    defer current.deinit();
    try std.testing.expect(current.generation.? != old.generation.?);
    try std.testing.expectEqual(revision, old.snapshot.value.catalog_revision);
    try std.testing.expect(!old.table_indexes.contains("new"));
    try std.testing.expect(current.table_indexes.contains("new"));
}

test "system catalog standalone imports main checkpoints and current logical seeds atomically" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    var runtime = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer runtime.deinit();
    const resources = [_]system_catalog.Resource{
        .{ .kind = .database, .id = 10, .name = "analytics" },
        .{ .kind = .namespace, .id = 11, .parent_id = 10, .name = "public" },
    };
    const logical_seed = try std.json.Stringify.valueAlloc(alloc, LocalStandaloneMetadata.PersistedCatalog{
        .epoch = 7,
        .system_catalog = .{ .revision = 3, .next_id = 12, .resources = &resources },
        .tables = &.{.{ .table_id = 77, .name = "legacy" }},
        .ranges = &.{.{ .table_id = 77, .group_id = 7001, .start_key = "" }},
    }, .{});
    defer alloc.free(logical_seed);
    // Main's JSON checkpoint has no system_catalog field. The current HA
    // seed does; both enter the same atomic row import path.
    const main_checkpoint = "{\"epoch\":7,\"tables\":[{\"table_id\":77,\"name\":\"legacy\"}],\"ranges\":[{\"table_id\":77,\"group_id\":7001,\"start_key\":\"\"}]}";
    for ([_]bool{ true, false }) |from_main| {
        const input = if (from_main) main_checkpoint else logical_seed;
        for ([_]antfly.common.config.StorageEngine{ .local, .lite }) |engine| {
            const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/{s}-{s}{s}", .{ tmp.sub_path, if (from_main) "main" else "seed", @tagName(engine), if (engine == .lite) ".aflite" else ".json" });
            defer alloc.free(path);
            {
                var lite: ?antfly.lite.backend.Handle = if (engine == .lite) try antfly.lite.backend.Handle.openOrCreate(alloc, path, .{}) else null;
                defer if (lite) |*handle| handle.deinit();
                const store = if (lite) |*handle| try handle.runtimeStoreForNamespace("system/metadata") else null;
                if (store) |target| {
                    var txn = try target.beginWrite();
                    var txn_open = true;
                    defer if (txn_open) txn.abort();
                    try txn.put("catalog", input);
                    try txn.commit();
                    txn_open = false;
                    try target.sync(true);
                } else try writeFileAtomically(alloc, runtime.ptr().io().?, path, input);
                var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, runtime.ptr(), store, engine);
                defer metadata.deinit();
                try std.testing.expect(!metadata.catalog_rows_initialized);
                const renamed = try metadata.statusSource().systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = if (from_main) .{ .action = .create, .kind = .database, .name = "warehouse" } else .{ .action = .rename, .kind = .database, .name = "analytics", .new_name = "warehouse" } } });
                alloc.free(renamed);
                try std.testing.expect(metadata.catalog_rows_initialized);
            }
            {
                var lite: ?antfly.lite.backend.Handle = if (engine == .lite) try antfly.lite.backend.Handle.open(alloc, path, .{}) else null;
                defer if (lite) |*handle| handle.deinit();
                const store = if (lite) |*handle| try handle.runtimeStoreForNamespace("system/metadata") else null;
                var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, runtime.ptr(), store, engine);
                defer metadata.deinit();
                try std.testing.expect(metadata.catalog_rows_initialized);
                const warehouse = metadata.system_catalog_state.?.index.find(.database, 0, "warehouse").?;
                if (!from_main) try std.testing.expectEqual(@as(u64, 10), warehouse.id);
                try std.testing.expect(metadata.system_catalog_state.?.index.find(.database, 0, "analytics") == null);
                try std.testing.expectEqualStrings("legacy", metadata.manager.tables.get(77).?.name);
                try std.testing.expectEqual(@as(u64, 77), metadata.manager.ranges.get(7001).?.table_id);
                // The old source still exists; a malformed new-format head must
                // fail closed instead of silently returning that stale catalog.
                const durable = try metadata.durableCatalogStore();
                var txn = try durable.beginWrite();
                var txn_open = true;
                defer if (txn_open) txn.abort();
                try txn.put(LocalStandaloneMetadata.catalog_head_key, "{\"version\":999,\"epoch\":8,\"revision\":4,\"next_id\":12}");
                try txn.commit();
                txn_open = false;
                try durable.sync(true);
            }
            {
                var lite: ?antfly.lite.backend.Handle = if (engine == .lite) try antfly.lite.backend.Handle.open(alloc, path, .{}) else null;
                defer if (lite) |*handle| handle.deinit();
                const store = if (lite) |*handle| try handle.runtimeStoreForNamespace("system/metadata") else null;
                try std.testing.expectError(error.InvalidCatalogRecord, LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, runtime.ptr(), store, engine));
            }
        }
    }
}

test "system catalog standalone ordered pages own captured data across rename drop and restart" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/ordered.json", .{tmp.sub_path});
    defer alloc.free(path);
    var backend = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend.deinit();
    var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    defer metadata.deinit();
    const source = metadata.statusSource();
    for ([_][]const u8{ "c", "a", "b" }) |name| {
        const physical = try std.fmt.allocPrint(alloc, "table:{s}", .{name});
        defer alloc.free(physical);
        const result = try source.systemCatalog(alloc, .{}, .{ .mutate = .{
            .mutation = .{ .action = .create, .kind = .table, .name = name },
            .physical_name = physical,
            .create_table_json = "{}",
        } });
        alloc.free(result);
    }
    var captured = try metadata.captureCatalogTablesLocked(alloc, .{}, .{ .limit = 1 });
    defer captured.arena.deinit();
    try std.testing.expectEqual(@as(usize, 1), captured.value.entries.len);
    try std.testing.expectEqualStrings("a", captured.value.entries[0].name);
    try std.testing.expectEqual(@as(usize, 1), captured.value.ranges.len);
    const rename = try source.systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .rename, .kind = .table, .name = "a", .new_name = "z" } } });
    alloc.free(rename);
    try std.testing.expectError(error.CatalogGenerationChanged, source.systemCatalog(alloc, .{}, .{ .list_tables = .{ .limit = 1, .revision = captured.value.revision, .after_table_id = captured.value.next_table_id } }));
    var dropped = try source.dropTableExact(alloc, "table:a");
    defer dropped.deinit(alloc);
    // The page is serialized after releasing the metadata mutex. Its names,
    // schemas, ranges and store URL must survive a simultaneous mutation.
    try std.testing.expectEqualStrings("a", captured.value.entries[0].name);
    try std.testing.expectEqualStrings("table:a", captured.value.entries[0].table.name);
    try std.testing.expectEqual(captured.value.entries[0].table.table_id, captured.value.ranges[0].table_id);
    try std.testing.expectEqualStrings("http://localhost", captured.value.stores[0].api_url);
    metadata.deinit();
    metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    var page = try metadata.captureCatalogTablesLocked(alloc, .{}, .{ .prefix = "b", .limit = 1 });
    defer page.arena.deinit();
    try std.testing.expectEqual(@as(usize, 1), page.value.entries.len);
    try std.testing.expectEqualStrings("b", page.value.entries[0].name);
    try std.testing.expect(page.value.next_after == null);
    var all = try metadata.captureCatalogTablesLocked(alloc, .{}, .{});
    defer all.arena.deinit();
    try std.testing.expectEqual(@as(usize, 2), all.value.entries.len);
    try std.testing.expectEqualStrings("b", all.value.entries[0].name);
    try std.testing.expectEqualStrings("c", all.value.entries[1].name);
}

test "standalone fills ha flags from the config ha section without overriding flags" {
    const alloc = std.testing.allocator;
    var cfg = try antfly.common.config.Config.parseFromSlice(alloc,
        \\{
        \\  "ha": {
        \\    "admin": { "token_env": "ANTFLY_HA_ADMIN_TOKEN" },
        \\    "identity": { "cluster_id": 7, "shard_id": 1, "timeline_id": 3, "epoch": 2 },
        \\    "primary": { "log": "/data/ha/primary.wal", "slots": "/data/ha/slots", "node_id": "primary-a" },
        \\    "sync": { "mode": "remote-apply", "selection": "all", "standbys": ["standby-a"], "failure": "degrade-to-async" },
        \\    "retention": { "max_lag_lsn": 4096 },
        \\    "fence_wal": "/data/ha/fence.wal"
        \\  }
        \\}
    );
    defer cfg.deinit();

    var cli = CliConfig{ .ha_epoch = 9, .ha_primary_node_id = "flag-primary" };
    defer cli.deinit(alloc);
    try applyHAConfigDefaults(alloc, &cli, &cfg);

    try std.testing.expectEqualStrings("ANTFLY_HA_ADMIN_TOKEN", cli.admin_token_env.?);
    try std.testing.expectEqual(@as(u64, 7), cli.ha_cluster_id.?);
    try std.testing.expectEqual(@as(u64, 1), cli.ha_shard_id.?);
    try std.testing.expectEqual(@as(u64, 3), cli.ha_timeline_id.?);
    try std.testing.expectEqual(@as(u64, 9), cli.ha_epoch.?);
    try std.testing.expectEqualStrings("/data/ha/primary.wal", cli.ha_primary_log.?);
    try std.testing.expectEqualStrings("flag-primary", cli.ha_primary_node_id.?);
    try std.testing.expect(haPrimaryRequested(cli));
    try std.testing.expect(!haStandbyRequested(cli));
    try std.testing.expectEqual(antfly.hot_standby.primary.DurabilityMode.remote_apply, cli.ha_sync_mode.?);
    try std.testing.expectEqual(antfly.hot_standby.primary.StandbySelection.all, cli.ha_sync_selection.?);
    try std.testing.expectEqual(antfly.hot_standby.primary.FailurePolicy.degrade_to_async, cli.ha_sync_failure_policy.?);
    try std.testing.expectEqual(@as(usize, 1), cli.ha_sync_standby_names.items.len);
    try std.testing.expectEqual(@as(u64, 4096), cli.ha_retention_max_lag_lsn.?);
    try std.testing.expectEqualStrings("/data/ha/fence.wal", cli.ha_fence_wal.?);
    try std.testing.expect(cli.ha_standby_log == null);
}

test "system catalog offline migration publishes rows and fences server startup" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/catalog.json", .{tmp.sub_path});
    defer alloc.free(path);
    var backend = try antfly.db.background_runtime.BackendRuntimeHandle.init(alloc, .{});
    defer backend.deinit();
    {
        var metadata = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
        defer metadata.deinit();
        try LocalStandaloneMetadata.createTable(&metadata, alloc, "docs", .{ .storage = .{ .dense_embeddings = .primary_lsm } });
        const tenant = try metadata.statusSource().systemCatalog(alloc, .{}, .{ .mutate = .{ .mutation = .{ .action = .create, .kind = .database, .name = "preserved" } } });
        alloc.free(tenant);
    }
    const Offline = @import("offline_catalog.zig").Catalog;
    {
        var catalog = try Offline.open(alloc, std.testing.io, path);
        defer catalog.deinit();
        const table = &catalog.document.value.object.getPtr("tables").?.array.items[0];
        const a = catalog.document.arena.allocator();
        const admission = try std.json.parseFromSliceLeaky(std.json.Value, a, "{\"request\":{\"job_id\":\"offline\",\"mode\":\"offline\"}}", .{});
        try table.object.put(a, "storage_migration", admission);
        try catalog.publish(table.*);
    }
    try std.testing.expectError(error.VectorMigrationOfflineAdmission, LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local));
    {
        var catalog = try Offline.open(alloc, std.testing.io, path);
        defer catalog.deinit();
        const table = &catalog.document.value.object.getPtr("tables").?.array.items[0];
        _ = table.object.swapRemove("storage_migration");
        const a = catalog.document.arena.allocator();
        try table.object.put(a, "storage", try std.json.parseFromSliceLeaky(std.json.Value, a, "{\"dense_embeddings\":\"vector_store\"}", .{}));
        try catalog.publish(table.*);
    }
    var reopened = try LocalStandaloneMetadata.init(alloc, 1, 1, "http://localhost", ".", path, backend.ptr(), null, .local);
    defer reopened.deinit();
    try std.testing.expectEqual(.vector_store, reopened.findTableByNameLocked("docs").?.storage.dense_embeddings);
    try std.testing.expect(reopened.system_catalog_state.?.index.find(.database, 0, "preserved") != null);
}
