// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const Allocator = std.mem.Allocator;

/// Process-wide count of query-time embeds currently executing (a public
/// query whose text is being embedded into a vector right now). The query
/// layers increment it around the embed call itself — not the whole query —
/// so BM25-only traffic never sets it. The enrichment embed loop briefly
/// defers the next embed batch while it is non-zero so interactive embeds
/// get the embedder first. Lives here so the embed loop has no dependency
/// on HTTP wiring.
pub const interactive_embed_inflight = InteractiveCounter{ .kind = 0 };

/// Process-wide count of interactive generation requests. Background asset
/// producers (including GLiNER extraction) yield between batches while this is
/// non-zero so long-running backfills do not contend with user-facing answers.
pub const interactive_generate_inflight = InteractiveCounter{ .kind = 1 };

var local_interactive_counters = [_]std.atomic.Value(u32){ .init(0), .init(0) };

const InteractiveCounter = struct {
    kind: u32,

    pub fn fetchAdd(self: @This(), value: u32, comptime order: std.builtin.AtomicOrder) u32 {
        if (comptime @import("storage_source_options").control_only)
            return @import("kernel_owner_abi").antfly_storage_interactive_activity(self.kind, @intCast(value));
        return local_interactive_counters[self.kind].fetchAdd(value, order);
    }

    pub fn fetchSub(self: @This(), value: u32, comptime order: std.builtin.AtomicOrder) u32 {
        if (comptime @import("storage_source_options").control_only)
            return @import("kernel_owner_abi").antfly_storage_interactive_activity(self.kind, -@as(i32, @intCast(value)));
        return local_interactive_counters[self.kind].fetchSub(value, order);
    }

    pub fn load(self: @This(), comptime order: std.builtin.AtomicOrder) u32 {
        if (comptime @import("storage_source_options").control_only)
            return @import("kernel_owner_abi").antfly_storage_interactive_activity(self.kind, 0);
        return local_interactive_counters[self.kind].load(order);
    }
};

/// Called only by the physical storage archive. Returns the previous count.
/// One call surrounds an interactive inference operation, never a record loop.
pub fn interactiveActivity(kind: u32, delta: i32) callconv(.c) u32 {
    std.debug.assert(kind < local_interactive_counters.len);
    const counter = &local_interactive_counters[kind];
    if (delta == 0) return counter.load(.monotonic);
    if (delta > 0) return counter.fetchAdd(@intCast(delta), .monotonic);
    const amount: u32 = @intCast(-@as(i64, delta));
    const previous = counter.fetchSub(amount, .monotonic);
    std.debug.assert(previous >= amount);
    return previous;
}

pub const ExecutionPolicy = struct {
    batch_items: ?usize = null,
    batch_bytes: ?usize = null,
    max_document_pages: ?usize = null,
};

pub fn parseExecutionPolicyJson(alloc: Allocator, execution_json: []const u8) !ExecutionPolicy {
    if (execution_json.len == 0) return .{};
    const parsed = try std.json.parseFromSlice(std.json.Value, alloc, execution_json, .{});
    defer parsed.deinit();
    return try parseExecutionPolicyValue(parsed.value);
}

pub fn parseExecutionPolicyValue(value: std.json.Value) !ExecutionPolicy {
    if (value != .object) return error.InvalidEnrichmentExecutionConfig;
    var out = ExecutionPolicy{};
    var iter = value.object.iterator();
    while (iter.next()) |entry| {
        if (std.mem.eql(u8, entry.key_ptr.*, "batch_items")) {
            if (entry.value_ptr.* == .null) continue;
            out.batch_items = try parsePositiveExecutionInteger(entry.value_ptr.*);
        } else if (std.mem.eql(u8, entry.key_ptr.*, "batch_bytes")) {
            if (entry.value_ptr.* == .null) continue;
            out.batch_bytes = try parsePositiveExecutionInteger(entry.value_ptr.*);
        } else if (std.mem.eql(u8, entry.key_ptr.*, "max_document_pages")) {
            if (entry.value_ptr.* == .null) continue;
            out.max_document_pages = try parsePositiveExecutionInteger(entry.value_ptr.*);
        } else {
            return error.InvalidEnrichmentExecutionConfig;
        }
    }
    return out;
}

fn parsePositiveExecutionInteger(value: std.json.Value) !usize {
    const raw = switch (value) {
        .integer => |n| n,
        else => return error.InvalidEnrichmentExecutionConfig,
    };
    if (raw <= 0) return error.InvalidEnrichmentExecutionConfig;
    return std.math.cast(usize, raw) orelse error.InvalidEnrichmentExecutionConfig;
}

pub fn executionBatchItemsOrDefault(alloc: Allocator, execution_json: []const u8, default_value: usize) usize {
    const policy = parseExecutionPolicyJson(alloc, execution_json) catch return default_value;
    return policy.batch_items orelse default_value;
}

pub fn executionBatchBytesOrDefault(alloc: Allocator, execution_json: []const u8, default_value: usize) usize {
    const policy = parseExecutionPolicyJson(alloc, execution_json) catch return default_value;
    return policy.batch_bytes orelse default_value;
}

test "execution policy admits a positive PDF document page ceiling" {
    const policy = try parseExecutionPolicyJson(
        std.testing.allocator,
        "{\"batch_items\":4,\"batch_bytes\":1024,\"max_document_pages\":200}",
    );
    try std.testing.expectEqual(@as(?usize, 200), policy.max_document_pages);
    try std.testing.expectError(
        error.InvalidEnrichmentExecutionConfig,
        parseExecutionPolicyJson(std.testing.allocator, "{\"max_document_pages\":0}"),
    );
}

pub const GeneratedEnrichmentKind = enum {
    dense_embedding,
    sparse_embedding,
    chunk_text,
    asset,
};

pub const EmbeddingInput = enum {
    text,
    pdf_page_images,
};

pub const EmbeddingInputKind = enum {
    document,
    inline_chunks,
    materialized_chunks,
};

pub const GeneratedEnrichmentRequest = struct {
    kind: GeneratedEnrichmentKind,
    index_name: []const u8,
    artifact_name: []const u8 = "",
    embedding_name: []const u8 = "",
    embedding_input: EmbeddingInput = .text,
    /// Exact semantic input owned by this embedding request. `artifact_name`
    /// names an embedding output for document requests and a chunk input for
    /// chunk requests, so its presence cannot safely answer this question.
    input_kind: EmbeddingInputKind = .document,
    doc_key: []const u8,
    source_field: []const u8,
    /// Handlebars template to render document fields for embedding.
    /// When non-empty, the full document is rendered through this template
    /// instead of extracting a single source_field.
    source_template: []const u8 = "",
    expected_dims: u32 = 0,
    chunk_size: u32 = 0,
    chunk_overlap: u32 = 0,
    chunker_json: []const u8 = "",
    full_text_index: bool = false,
    /// Persist generated chunk records even when no text index consumes them.
    /// Graph indexes read their source payloads from the artifact store, while
    /// embedding-only consumers can usually reuse the in-flight chunk cache.
    persist_artifact: bool = false,
    content_type: []const u8 = "",
    producer_json: []const u8 = "",
    execution_json: []const u8 = "",
    /// Upstream materialized asset for a chunk-backed request, pinned with the
    /// same catalog generation as the rest of the plan.
    upstream_artifact_name: []const u8 = "",
    /// Immutable, request-owned projection of the index catalog. Provider and
    /// chunking work consumes these names without borrowing live IndexManager
    /// arrays after the write-plan generation fence is released.
    consumer_indexes: [][]u8 = &.{},
    /// Replay sequence of the source document change. This is attached when a
    /// cached request plan is instantiated for a pending journal group.
    sequence: u64 = 0,
};

pub const GeneratedEnrichmentRef = struct {
    kind: GeneratedEnrichmentKind,
    index_name: []const u8,
    artifact_name: []const u8 = "",
    embedding_name: []const u8 = "",
    doc_key: []const u8,
};

pub const GeneratedEnrichmentRequestList = []const GeneratedEnrichmentRequest;

pub const LeaseRecord = struct {
    owner_id: []const u8,
    expires_at_ms: u64,
};

pub fn freeGeneratedRequest(alloc: Allocator, request: GeneratedEnrichmentRequest) void {
    alloc.free(request.index_name);
    if (request.artifact_name.len > 0) alloc.free(request.artifact_name);
    if (request.embedding_name.len > 0) alloc.free(request.embedding_name);
    alloc.free(request.doc_key);
    alloc.free(request.source_field);
    if (request.source_template.len > 0) alloc.free(request.source_template);
    if (request.chunker_json.len > 0) alloc.free(request.chunker_json);
    if (request.content_type.len > 0) alloc.free(request.content_type);
    if (request.producer_json.len > 0) alloc.free(request.producer_json);
    if (request.execution_json.len > 0) alloc.free(request.execution_json);
    if (request.upstream_artifact_name.len > 0) alloc.free(request.upstream_artifact_name);
    for (request.consumer_indexes) |name| alloc.free(name);
    if (request.consumer_indexes.len > 0) alloc.free(request.consumer_indexes);
}

pub fn cloneGeneratedRequest(alloc: Allocator, request: GeneratedEnrichmentRequest) !GeneratedEnrichmentRequest {
    const index_name = try alloc.dupe(u8, request.index_name);
    errdefer alloc.free(index_name);
    const artifact_name = if (request.artifact_name.len > 0) try alloc.dupe(u8, request.artifact_name) else "";
    errdefer if (artifact_name.len > 0) alloc.free(artifact_name);
    const embedding_name = if (request.embedding_name.len > 0) try alloc.dupe(u8, request.embedding_name) else "";
    errdefer if (embedding_name.len > 0) alloc.free(embedding_name);
    const doc_key = try alloc.dupe(u8, request.doc_key);
    errdefer alloc.free(doc_key);
    const source_field = try alloc.dupe(u8, request.source_field);
    errdefer alloc.free(source_field);
    const source_template = if (request.source_template.len > 0) try alloc.dupe(u8, request.source_template) else "";
    errdefer if (source_template.len > 0) alloc.free(source_template);
    const chunker_json = if (request.chunker_json.len > 0) try alloc.dupe(u8, request.chunker_json) else "";
    errdefer if (chunker_json.len > 0) alloc.free(chunker_json);
    const content_type = if (request.content_type.len > 0) try alloc.dupe(u8, request.content_type) else "";
    errdefer if (content_type.len > 0) alloc.free(content_type);
    const producer_json = if (request.producer_json.len > 0) try alloc.dupe(u8, request.producer_json) else "";
    errdefer if (producer_json.len > 0) alloc.free(producer_json);
    const execution_json = if (request.execution_json.len > 0) try alloc.dupe(u8, request.execution_json) else "";
    errdefer if (execution_json.len > 0) alloc.free(execution_json);
    const upstream_artifact_name = if (request.upstream_artifact_name.len > 0) try alloc.dupe(u8, request.upstream_artifact_name) else "";
    errdefer if (upstream_artifact_name.len > 0) alloc.free(upstream_artifact_name);
    const consumer_indexes = try alloc.alloc([]u8, request.consumer_indexes.len);
    var consumer_indexes_initialized: usize = 0;
    errdefer {
        for (consumer_indexes[0..consumer_indexes_initialized]) |name| alloc.free(name);
        if (consumer_indexes.len > 0) alloc.free(consumer_indexes);
    }
    for (request.consumer_indexes, 0..) |name, i| {
        consumer_indexes[i] = try alloc.dupe(u8, name);
        consumer_indexes_initialized += 1;
    }
    return .{
        .kind = request.kind,
        .index_name = index_name,
        .artifact_name = artifact_name,
        .embedding_name = embedding_name,
        .embedding_input = request.embedding_input,
        .input_kind = request.input_kind,
        .doc_key = doc_key,
        .source_field = source_field,
        .source_template = source_template,
        .expected_dims = request.expected_dims,
        .chunk_size = request.chunk_size,
        .chunk_overlap = request.chunk_overlap,
        .chunker_json = chunker_json,
        .full_text_index = request.full_text_index,
        .persist_artifact = request.persist_artifact,
        .content_type = content_type,
        .producer_json = producer_json,
        .execution_json = execution_json,
        .upstream_artifact_name = upstream_artifact_name,
        .consumer_indexes = consumer_indexes,
        .sequence = request.sequence,
    };
}

pub fn deinitGeneratedRequests(alloc: Allocator, requests: []const GeneratedEnrichmentRequest) void {
    for (requests) |request| freeGeneratedRequest(alloc, request);
    if (requests.len > 0) alloc.free(requests);
}

pub fn freeGeneratedRef(alloc: Allocator, request: GeneratedEnrichmentRef) void {
    alloc.free(request.index_name);
    if (request.artifact_name.len > 0) alloc.free(request.artifact_name);
    if (request.embedding_name.len > 0) alloc.free(request.embedding_name);
    alloc.free(request.doc_key);
}

pub fn cloneGeneratedRef(alloc: Allocator, request: GeneratedEnrichmentRef) !GeneratedEnrichmentRef {
    const index_name = try alloc.dupe(u8, request.index_name);
    errdefer alloc.free(index_name);
    const artifact_name = if (request.artifact_name.len > 0) try alloc.dupe(u8, request.artifact_name) else "";
    errdefer if (artifact_name.len > 0) alloc.free(artifact_name);
    const embedding_name = if (request.embedding_name.len > 0) try alloc.dupe(u8, request.embedding_name) else "";
    errdefer if (embedding_name.len > 0) alloc.free(embedding_name);
    const doc_key = try alloc.dupe(u8, request.doc_key);
    return .{
        .kind = request.kind,
        .index_name = index_name,
        .artifact_name = artifact_name,
        .embedding_name = embedding_name,
        .doc_key = doc_key,
    };
}

pub fn deinitGeneratedRefs(alloc: Allocator, requests: []const GeneratedEnrichmentRef) void {
    for (requests) |request| freeGeneratedRef(alloc, request);
    alloc.free(requests);
}

pub fn cloneGeneratedRequests(alloc: Allocator, requests: []const GeneratedEnrichmentRequest) ![]GeneratedEnrichmentRequest {
    const cloned = try alloc.alloc(GeneratedEnrichmentRequest, requests.len);
    var initialized: usize = 0;
    errdefer {
        for (cloned[0..initialized]) |request| freeGeneratedRequest(alloc, request);
        if (cloned.len > 0) alloc.free(cloned);
    }

    for (requests, 0..) |request, i| {
        cloned[i] = try cloneGeneratedRequest(alloc, request);
        initialized += 1;
    }
    return cloned;
}

pub fn cloneGeneratedRefs(alloc: Allocator, requests: []const GeneratedEnrichmentRef) ![]GeneratedEnrichmentRef {
    const cloned = try alloc.alloc(GeneratedEnrichmentRef, requests.len);
    var initialized: usize = 0;
    errdefer {
        for (cloned[0..initialized]) |request| freeGeneratedRef(alloc, request);
        if (cloned.len > 0) alloc.free(cloned);
    }

    for (requests, 0..) |request, i| {
        cloned[i] = try cloneGeneratedRef(alloc, request);
        initialized += 1;
    }
    return cloned;
}

pub fn requestToRef(alloc: Allocator, request: GeneratedEnrichmentRequest) !GeneratedEnrichmentRef {
    return cloneGeneratedRef(alloc, .{
        .kind = request.kind,
        .index_name = request.index_name,
        .artifact_name = request.artifact_name,
        .embedding_name = request.embedding_name,
        .doc_key = request.doc_key,
    });
}

pub fn requestArtifactName(request: GeneratedEnrichmentRequest) []const u8 {
    return if (request.artifact_name.len > 0) request.artifact_name else request.index_name;
}

pub fn requestEmbeddingName(request: GeneratedEnrichmentRequest) []const u8 {
    return if (request.embedding_name.len > 0) request.embedding_name else request.index_name;
}

pub fn refArtifactName(request: GeneratedEnrichmentRef) []const u8 {
    return if (request.artifact_name.len > 0) request.artifact_name else request.index_name;
}

pub fn refEmbeddingName(request: GeneratedEnrichmentRef) []const u8 {
    return if (request.embedding_name.len > 0) request.embedding_name else request.index_name;
}

pub fn requestMatchesRef(request: GeneratedEnrichmentRequest, ref: GeneratedEnrichmentRef) bool {
    if (request.kind != ref.kind) return false;
    if (!std.mem.eql(u8, request.doc_key, ref.doc_key)) return false;
    if (!std.mem.eql(u8, requestArtifactName(request), refArtifactName(ref))) return false;
    return switch (request.kind) {
        .chunk_text, .asset => true,
        .dense_embedding, .sparse_embedding => std.mem.eql(u8, requestEmbeddingName(request), refEmbeddingName(ref)),
    };
}

pub fn cloneLeaseRecord(alloc: Allocator, record: LeaseRecord) !LeaseRecord {
    return .{
        .owner_id = try alloc.dupe(u8, record.owner_id),
        .expires_at_ms = record.expires_at_ms,
    };
}

pub fn deinitLeaseRecord(alloc: Allocator, record: *LeaseRecord) void {
    alloc.free(record.owner_id);
    record.* = undefined;
}

test "generated enrichment request clone round trip" {
    const alloc = std.testing.allocator;

    const cloned = try cloneGeneratedRequests(alloc, &.{
        .{
            .kind = .dense_embedding,
            .index_name = "dv_v1",
            .artifact_name = "body_chunks_v1",
            .embedding_name = "body_dense_v1",
            .input_kind = .materialized_chunks,
            .doc_key = "doc:a",
            .source_field = "body",
            .source_template = "{{title}} {{body}}",
            .expected_dims = 768,
            .chunk_size = 512,
            .chunk_overlap = 64,
            .chunker_json = "{\"provider\":\"antfly\",\"text\":{\"target_tokens\":512,\"overlap_tokens\":64}}",
            .persist_artifact = true,
        },
    });
    defer deinitGeneratedRequests(alloc, cloned);

    try std.testing.expectEqual(@as(usize, 1), cloned.len);
    try std.testing.expectEqualStrings("dv_v1", cloned[0].index_name);
    try std.testing.expectEqualStrings("body_chunks_v1", cloned[0].artifact_name);
    try std.testing.expectEqualStrings("body_dense_v1", cloned[0].embedding_name);
    try std.testing.expectEqual(EmbeddingInputKind.materialized_chunks, cloned[0].input_kind);
    try std.testing.expectEqualStrings("doc:a", cloned[0].doc_key);
    try std.testing.expectEqualStrings("body", cloned[0].source_field);
    try std.testing.expectEqualStrings("{{title}} {{body}}", cloned[0].source_template);
    try std.testing.expectEqual(@as(u32, 768), cloned[0].expected_dims);
    try std.testing.expectEqual(@as(u32, 512), cloned[0].chunk_size);
    try std.testing.expectEqual(@as(u32, 64), cloned[0].chunk_overlap);
    try std.testing.expectEqualStrings("{\"provider\":\"antfly\",\"text\":{\"target_tokens\":512,\"overlap_tokens\":64}}", cloned[0].chunker_json);
    try std.testing.expect(cloned[0].persist_artifact);
}

test "generated enrichment request clone without source_template" {
    const alloc = std.testing.allocator;

    const cloned = try cloneGeneratedRequests(alloc, &.{
        .{
            .kind = .dense_embedding,
            .index_name = "dv_v1",
            .embedding_input = .pdf_page_images,
            .doc_key = "doc:b",
            .source_field = "body",
            .expected_dims = 384,
        },
    });
    defer deinitGeneratedRequests(alloc, cloned);

    try std.testing.expectEqual(@as(usize, 1), cloned.len);
    try std.testing.expectEqualStrings("body", cloned[0].source_field);
    try std.testing.expectEqual(EmbeddingInput.pdf_page_images, cloned[0].embedding_input);
    try std.testing.expectEqual(EmbeddingInputKind.document, cloned[0].input_kind);
    try std.testing.expectEqual(@as(usize, 0), cloned[0].source_template.len);
}

test "generated enrichment request clone releases every partial allocation" {
    const source = [_]GeneratedEnrichmentRequest{
        .{
            .kind = .dense_embedding,
            .index_name = "dense_v1",
            .artifact_name = "chunks_v1",
            .embedding_name = "embedding_v1",
            .doc_key = "doc:a",
            .source_field = "body",
            .source_template = "{{title}} {{body}}",
            .expected_dims = 768,
            .chunk_size = 512,
            .chunk_overlap = 64,
            .chunker_json = "{\"provider\":\"antfly\"}",
            .content_type = "text/plain",
            .producer_json = "{\"type\":\"chunk\"}",
            .execution_json = "{\"batch_size\":32}",
        },
        .{
            .kind = .asset,
            .index_name = "assets_v1",
            .doc_key = "doc:b",
            .source_field = "url",
        },
    };

    try std.testing.checkAllAllocationFailures(
        std.testing.allocator,
        struct {
            fn run(alloc: Allocator, requests: []const GeneratedEnrichmentRequest) !void {
                const cloned = try cloneGeneratedRequests(alloc, requests);
                defer deinitGeneratedRequests(alloc, cloned);
                try std.testing.expectEqual(requests.len, cloned.len);
            }
        }.run,
        .{source[0..]},
    );
}
