// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const platform_sync = @import("antfly_platform").sync;
const builtin = @import("builtin");
const platform = @import("antfly_platform");
const byte_copy = @import("../../common/byte_copy.zig");
const fs_paths = @import("../../common/fs_paths.zig");
const threaded_io_limits = @import("../../common/threaded_io_limits.zig");

const Allocator = std.mem.Allocator;
const CounterU64 = platform.atomic.Value(u64);
const supports_native_storage = builtin.os.tag != .freestanding;
const supports_posix_fd_cache = supports_native_storage and
    builtin.os.tag != .windows and
    builtin.os.tag != .wasi and
    (builtin.os.tag == .linux or builtin.link_libc) and
    @hasDecl(std.posix.system, "pread");
// TODO: Re-enable Linux evented storage once std.Io.Evented/std.Io.Uring is
// stable enough for this code path. In Zig 0.16, instantiating std.Io.Uring
// trips stdlib error-set mismatches: std/Io/Uring.zig's dirOpenDir and
// dirRealPathFile propagate openat's error.ReadOnlyFileSystem into std/Io/Dir.zig
// error sets that do not include it.
const supports_evented_runtime = false;
// Standalone NativeStorage values used by focused tools/tests have no
// BackendRuntime handle, so keep their local cache conservative. Production
// runtimes inject handles to the single RLIMIT-derived process pool.
const fallback_cached_native_fds: usize = 64;
const unlimited_fd_admission_capacity: usize = 1024;
const fd_cache_shard_count: usize = if (builtin.os.tag == .freestanding) 1 else 16;
const max_posix_io_chunk: usize = 64 * 1024 * 1024;

pub const RuntimeKind = enum {
    threaded,
    evented,
};

pub const NativeStorageStats = struct {
    fd_cache_entries: usize = 0,
    fd_admitted_descriptors: usize = 0,
    fd_persistent_descriptors: usize = 0,
    fd_admission_capacity: usize = 0,
    fd_persistent_reserve: usize = 0,
    fd_admission_waiters: usize = 0,
    fd_admission_waits: u64 = 0,
    fd_persistent_admission_failures: u64 = 0,
};

pub fn nativeFdAdmissionCapacityForSoftLimit(soft_limit: u64) usize {
    if (soft_limit == std.math.maxInt(u64)) return unlimited_fd_admission_capacity;
    // Public inbound sockets independently receive at most one quarter of the
    // process table. Give native storage one half, leaving the final quarter
    // for listeners, Raft, outbound providers, logs, and operator recovery
    // traffic. A third was too conservative: at RLIMIT_NOFILE=256 the 85-slot
    // pool could not retain the lifetime locks required by a normal multi-shard
    // graph/full-text workload even though the process still had ample OS
    // descriptor headroom.
    const bounded = @max(@as(u64, 1), soft_limit / 2);
    return @intCast(@min(bounded, unlimited_fd_admission_capacity));
}

fn configuredNativeFdAdmissionCapacity() usize {
    if (comptime builtin.os.tag == .windows or builtin.os.tag == .freestanding or builtin.os.tag == .wasi)
        return fallback_cached_native_fds;
    const limit = std.posix.getrlimit(.NOFILE) catch return fallback_cached_native_fds;
    const raw: u64 = if (limit.cur == std.math.maxInt(@TypeOf(limit.cur)))
        std.math.maxInt(u64)
    else
        @intCast(limit.cur);
    return nativeFdAdmissionCapacityForSoftLimit(raw);
}

pub const AtomicWriteSink = struct {
    ptr: *anyopaque,
    vtable: *const VTable,

    pub const VTable = struct {
        len: *const fn (*anyopaque) usize,
        append_slice: *const fn (*anyopaque, []const u8) anyerror!void,
        write_at: *const fn (*anyopaque, usize, []const u8) anyerror!void,
        crc32_prefix: *const fn (*anyopaque, usize) anyerror!u32,
        crc32_range: *const fn (*anyopaque, usize, usize) anyerror!u32,
        finish: *const fn (*anyopaque) anyerror!void,
        abort: *const fn (*anyopaque) void,
    };

    pub fn len(self: *const AtomicWriteSink) usize {
        return self.vtable.len(self.ptr);
    }

    pub fn appendSlice(self: *AtomicWriteSink, bytes: []const u8) !void {
        try self.vtable.append_slice(self.ptr, bytes);
    }

    pub fn appendByte(self: *AtomicWriteSink, byte: u8) !void {
        const one = [1]u8{byte};
        try self.appendSlice(&one);
    }

    pub fn writeAt(self: *AtomicWriteSink, offset: usize, bytes: []const u8) !void {
        try self.vtable.write_at(self.ptr, offset, bytes);
    }

    pub fn crc32Prefix(self: *AtomicWriteSink, len_prefix: usize) !u32 {
        return try self.vtable.crc32_prefix(self.ptr, len_prefix);
    }

    pub fn crc32Range(self: *AtomicWriteSink, offset: usize, range_len: usize) !u32 {
        return try self.vtable.crc32_range(self.ptr, offset, range_len);
    }

    /// Atomically publish the written bytes at the requested destination.
    /// Consumes the sink whether publishing succeeds or fails.
    pub fn finish(self: *AtomicWriteSink) !void {
        try self.vtable.finish(self.ptr);
    }

    /// Discard all bytes and remove any temporary object.
    /// Consumes the sink.
    pub fn abort(self: *AtomicWriteSink) void {
        self.vtable.abort(self.ptr);
    }
};

pub const RangeReadFuture = struct {
    ptr: *anyopaque,
    vtable: *const VTable,

    pub const VTable = struct {
        wait: *const fn (*anyopaque) anyerror![]u8,
        cancel: *const fn (*anyopaque) void,
    };

    /// Complete this one-shot read and transfer the returned bytes to the
    /// caller. After wait or cancel, the future must not be used again.
    pub fn wait(self: *RangeReadFuture) ![]u8 {
        return try self.vtable.wait(self.ptr);
    }

    /// Best-effort cancellation. Synchronous/completed futures just release
    /// their owned bytes if the caller has not waited yet.
    pub fn cancel(self: *RangeReadFuture) void {
        self.vtable.cancel(self.ptr);
    }
};

pub const ReadRuntime = if (builtin.os.tag == .freestanding)
    struct {
        pub fn init(_: anytype) ReadRuntime {
            return .{};
        }
    }
else
    struct {
        io: std.Io,

        pub fn init(io: std.Io) ReadRuntime {
            return .{ .io = io };
        }
    };

const CompletedRangeReadFuture = struct {
    allocator: Allocator,
    bytes: ?[]u8 = null,
    err: ?anyerror = null,

    const vtable: RangeReadFuture.VTable = .{
        .wait = wait,
        .cancel = cancel,
    };

    fn create(storage: Storage, allocator: Allocator, path: []const u8, offset: u64, len: usize) !RangeReadFuture {
        const self = try allocator.create(CompletedRangeReadFuture);
        self.* = .{ .allocator = allocator };
        self.bytes = storage.readFileRangeAlloc(allocator, path, offset, len) catch |err| blk: {
            self.err = err;
            break :blk null;
        };
        return .{
            .ptr = self,
            .vtable = &vtable,
        };
    }

    fn wait(ptr: *anyopaque) ![]u8 {
        const self: *CompletedRangeReadFuture = @ptrCast(@alignCast(ptr));
        const allocator = self.allocator;
        defer allocator.destroy(self);
        if (self.err) |err| return err;
        const bytes = self.bytes orelse return error.CanceledRangeRead;
        self.bytes = null;
        return bytes;
    }

    fn cancel(ptr: *anyopaque) void {
        const self: *CompletedRangeReadFuture = @ptrCast(@alignCast(ptr));
        const allocator = self.allocator;
        if (self.bytes) |bytes| allocator.free(bytes);
        allocator.destroy(self);
    }
};

pub const NativePathLockMode = enum {
    shared,
    exclusive,
};

pub const NativePathLockOptions = struct {
    nonblocking: bool = false,
};

pub const NativePathLock = struct {
    lock_file: NativePathLockFile,

    pub fn release(self: *NativePathLock) void {
        self.lock_file.close();
        self.* = undefined;
    }
};

pub const NativePathLockFileOptions = struct {
    create_if_missing: bool = true,
};

pub const NativePathLockFile = struct {
    io_impl: std.Io.Threaded,
    file: std.Io.File,
    fd_cache: *FdCache,
    locked: bool = false,

    pub fn lock(self: *NativePathLockFile, mode: NativePathLockMode) !void {
        std.debug.assert(!self.locked);
        try self.file.lock(self.io_impl.io(), switch (mode) {
            .shared => .shared,
            .exclusive => .exclusive,
        });
        self.locked = true;
    }

    pub fn tryLock(self: *NativePathLockFile, mode: NativePathLockMode) !bool {
        std.debug.assert(!self.locked);
        const locked = try self.file.tryLock(self.io_impl.io(), switch (mode) {
            .shared => .shared,
            .exclusive => .exclusive,
        });
        self.locked = locked;
        return locked;
    }

    pub fn unlock(self: *NativePathLockFile) void {
        if (!self.locked) return;
        self.file.unlock(self.io_impl.io());
        self.locked = false;
    }

    pub fn close(self: *NativePathLockFile) void {
        self.unlock();
        self.file.close(self.io_impl.io());
        self.fd_cache.releasePersistentDescriptors(self.io_impl.io(), 1);
        self.io_impl.deinit();
        self.* = undefined;
    }
};

pub fn nativeRealPathAlloc(allocator: Allocator, path: []const u8) ![:0]u8 {
    var io_impl = std.Io.Threaded.init(allocator, .{});
    defer io_impl.deinit();

    if (std.fs.path.isAbsolute(path)) {
        return try std.Io.Dir.realPathFileAbsoluteAlloc(io_impl.io(), path, allocator);
    }
    return try std.Io.Dir.cwd().realPathFileAlloc(io_impl.io(), path, allocator);
}

fn nativeRootIdentityAlloc(_: *anyopaque, allocator: Allocator, root_dir: []const u8) ![]u8 {
    const canonical = nativeRealPathAlloc(allocator, root_dir) catch |err| switch (err) {
        error.FileNotFound => return try nativeMissingRootIdentityAlloc(allocator, root_dir),
        else => return err,
    };
    defer allocator.free(canonical);
    return try allocator.dupe(u8, canonical);
}

fn nativeMissingRootIdentityAlloc(allocator: Allocator, root_dir: []const u8) ![]u8 {
    if (std.fs.path.isAbsolute(root_dir)) {
        return try std.fs.path.resolve(allocator, &.{root_dir});
    }

    var io_impl = std.Io.Threaded.init(allocator, .{});
    defer io_impl.deinit();
    const cwd = try std.Io.Dir.cwd().realPathFileAlloc(io_impl.io(), ".", allocator);
    defer allocator.free(cwd);
    return try std.fs.path.resolve(allocator, &.{ cwd, root_dir });
}

fn openNativePathFile(io: std.Io, path: []const u8) !std.Io.File {
    if (std.fs.path.isAbsolute(path)) {
        return try std.Io.Dir.openFileAbsolute(io, path, .{});
    }
    return try std.Io.Dir.cwd().openFile(io, path, .{});
}

pub fn openNativePathLockFile(
    allocator: Allocator,
    path: []const u8,
    options: NativePathLockFileOptions,
) !NativePathLockFile {
    return try openNativePathLockFileWithPool(allocator, path, options, null);
}

pub fn openNativePathLockFileWithPool(
    allocator: Allocator,
    path: []const u8,
    options: NativePathLockFileOptions,
    pool: ?*NativeStoragePool,
) !NativePathLockFile {
    return try openNativePathLockFileWithCache(
        allocator,
        path,
        options,
        if (pool) |configured| configured.fd_cache else processNativeFdCache(),
    );
}

fn openNativePathLockFileWithCache(
    allocator: Allocator,
    path: []const u8,
    options: NativePathLockFileOptions,
    fd_cache: *FdCache,
) !NativePathLockFile {
    var io_impl = threaded_io_limits.initService(allocator);
    errdefer io_impl.deinit();

    const open_descriptor_count = createPathDescriptorCount(path);
    // Path locks live for the backend lifetime. They share the process budget,
    // but must never queue behind other lifetime descriptors: once that class
    // fills the budget, no waiter can make progress until a backend closes.
    // Reserved headroom protects opens from transient load; true persistent
    // exhaustion is reported instead of hanging startup or restore.
    try fd_cache.reservePersistentDescriptors(io_impl.io(), open_descriptor_count);
    var reserved_descriptor_count = open_descriptor_count;
    errdefer fd_cache.releasePersistentDescriptors(io_impl.io(), reserved_descriptor_count);

    const file = if (options.create_if_missing)
        try fs_paths.createFilePortable(io_impl.io(), path, .{ .read = true, .truncate = false })
    else
        try openNativePathFile(io_impl.io(), path);
    errdefer file.close(io_impl.io());
    if (reserved_descriptor_count > 1) {
        fd_cache.releasePersistentDescriptors(io_impl.io(), reserved_descriptor_count - 1);
        reserved_descriptor_count = 1;
    }

    return .{
        .io_impl = io_impl,
        .file = file,
        .fd_cache = fd_cache,
    };
}

fn createPathDescriptorCount(path: []const u8) usize {
    // fs_paths opens a parent directory and then the new file for absolute
    // paths and relative paths containing a directory component.
    return if (std.fs.path.isAbsolute(path) or std.fs.path.dirname(path) != null) 2 else 1;
}

pub fn acquireNativePathLock(
    allocator: Allocator,
    path: []const u8,
    mode: NativePathLockMode,
    options: NativePathLockOptions,
) !NativePathLock {
    return try acquireNativePathLockWithPool(allocator, path, mode, options, null);
}

pub fn acquireNativePathLockWithPool(
    allocator: Allocator,
    path: []const u8,
    mode: NativePathLockMode,
    options: NativePathLockOptions,
    pool: ?*NativeStoragePool,
) !NativePathLock {
    var lock_file = try openNativePathLockFileWithPool(allocator, path, .{ .create_if_missing = true }, pool);
    errdefer lock_file.close();

    if (options.nonblocking) {
        if (!try lock_file.tryLock(mode)) return error.WouldBlock;
    } else {
        try lock_file.lock(mode);
    }

    return .{
        .lock_file = lock_file,
    };
}

pub const FileTrailer = struct {
    /// Bytes and size must describe the same pinned storage generation.
    bytes: []u8,
    file_size: u64,

    pub fn deinit(self: *FileTrailer, allocator: Allocator) void {
        allocator.free(self.bytes);
        self.* = undefined;
    }
};

/// Borrowed, type-erased storage view. The provider which produced this value
/// must outlive every call through it. Callers that launch work beyond a call
/// boundary must use a provider operation whose returned handle owns its own
/// lifetime (for example RangeReadFuture or AtomicWriteSink). Native callers
/// that retain a Storage view itself must retain a NativeStorage.Lease and
/// obtain the view from that lease.
pub const Storage = struct {
    pub const Trailer = FileTrailer;

    ptr: *anyopaque,
    vtable: *const VTable,

    pub const VTable = struct {
        create_dir_path: *const fn (*anyopaque, []const u8) anyerror!void,
        read_file_alloc: *const fn (*anyopaque, Allocator, []const u8, usize) anyerror![]u8,
        read_file_range_alloc: *const fn (*anyopaque, Allocator, []const u8, u64, usize) anyerror![]u8,
        begin_read_file_range_alloc: ?*const fn (*anyopaque, Allocator, []const u8, u64, usize) anyerror!RangeReadFuture = null,
        begin_read_file_range_alloc_with_runtime: ?*const fn (*anyopaque, ?ReadRuntime, Allocator, []const u8, u64, usize) anyerror!RangeReadFuture = null,
        read_file_range_into: ?*const fn (*anyopaque, []const u8, u64, []u8) anyerror!void = null,
        read_file_range_at_most_into: ?*const fn (*anyopaque, []const u8, u64, []u8) anyerror!usize = null,
        file_size: *const fn (*anyopaque, []const u8) anyerror!u64,
        /// Reads an exact suffix and reports the size already observed while
        /// locating it, avoiding a separate metadata operation.
        read_file_trailer_alloc: ?*const fn (*anyopaque, Allocator, []const u8, usize) anyerror!FileTrailer = null,
        write_file_absolute: *const fn (*anyopaque, []const u8, []const u8) anyerror!void,
        append_file_absolute: ?*const fn (*anyopaque, []const u8, []const u8, bool) anyerror!void = null,
        begin_atomic_write: ?*const fn (*anyopaque, Allocator, []const u8) anyerror!AtomicWriteSink = null,
        /// Persists file contents without implying namespace durability.
        sync_contents_absolute: ?*const fn (*anyopaque, []const u8) anyerror!void = null,
        /// Persists the parent namespace entry for the supplied path.
        sync_parent_absolute: ?*const fn (*anyopaque, []const u8) anyerror!void = null,
        rename_absolute: *const fn (*anyopaque, []const u8, []const u8) anyerror!void,
        delete_file_absolute: *const fn (*anyopaque, []const u8) anyerror!void,
        delete_tree: *const fn (*anyopaque, []const u8) anyerror!void,
        now_ns: *const fn (*anyopaque) u64,
        root_identity_alloc: ?*const fn (*anyopaque, Allocator, []const u8) anyerror![]u8 = null,
        /// The host guarantees that rename_absolute is an atomic replacement
        /// when source and destination are siblings on the same storage.
        rename_is_atomic: bool = false,
        /// Logical storage paths share the process host namespace and may be
        /// captured and atomically published as a complete directory
        /// generation. This is intentionally distinct from path locking:
        /// locking alone does not prove that a host-directory rename publishes
        /// the bytes addressed through this storage implementation.
        supports_host_path_generation_publication: bool = false,
        supports_native_path_locks: bool = false,
    };

    pub fn createDirPath(self: Storage, path: []const u8) !void {
        return self.vtable.create_dir_path(self.ptr, path);
    }

    pub fn readFileAlloc(self: Storage, allocator: Allocator, path: []const u8, max_bytes: usize) ![]u8 {
        return self.vtable.read_file_alloc(self.ptr, allocator, path, max_bytes);
    }

    pub fn readFileRangeAlloc(self: Storage, allocator: Allocator, path: []const u8, offset: u64, len: usize) ![]u8 {
        return self.vtable.read_file_range_alloc(self.ptr, allocator, path, offset, len);
    }

    pub fn beginReadFileRangeAlloc(self: Storage, allocator: Allocator, path: []const u8, offset: u64, len: usize) !RangeReadFuture {
        if (self.vtable.begin_read_file_range_alloc) |begin_read_file_range_alloc| {
            return try begin_read_file_range_alloc(self.ptr, allocator, path, offset, len);
        }
        return try CompletedRangeReadFuture.create(self, allocator, path, offset, len);
    }

    pub fn beginReadFileRangeAllocWithRuntime(self: Storage, read_runtime: ?ReadRuntime, allocator: Allocator, path: []const u8, offset: u64, len: usize) !RangeReadFuture {
        if (self.vtable.begin_read_file_range_alloc_with_runtime) |begin_read_file_range_alloc_with_runtime| {
            return try begin_read_file_range_alloc_with_runtime(self.ptr, read_runtime, allocator, path, offset, len);
        }
        return try self.beginReadFileRangeAlloc(allocator, path, offset, len);
    }

    pub fn readFileRangeInto(self: Storage, allocator: Allocator, path: []const u8, offset: u64, out: []u8) !void {
        if (self.vtable.read_file_range_into) |read_file_range_into| {
            return read_file_range_into(self.ptr, path, offset, out);
        }
        const loaded = try self.readFileRangeAlloc(allocator, path, offset, out.len);
        defer allocator.free(loaded);
        if (loaded.len != out.len) return error.EndOfStream;
        @memcpy(out, loaded);
    }

    pub fn readFileRangeAtMostInto(self: Storage, allocator: Allocator, path: []const u8, offset: u64, out: []u8) !usize {
        if (self.vtable.read_file_range_at_most_into) |read_file_range_at_most_into| {
            return try read_file_range_at_most_into(self.ptr, path, offset, out);
        }
        const size = self.fileSize(path) catch |err| switch (err) {
            error.FileNotFound => return 0,
            else => return err,
        };
        if (offset >= size) return 0;
        const len: usize = @intCast(@min(size - offset, out.len));
        try self.readFileRangeInto(allocator, path, offset, out[0..len]);
        return len;
    }

    pub fn fileSize(self: Storage, path: []const u8) !u64 {
        return self.vtable.file_size(self.ptr, path);
    }

    pub fn readFileTrailerAlloc(self: Storage, allocator: Allocator, path: []const u8, len: usize) !FileTrailer {
        if (self.vtable.read_file_trailer_alloc) |read_file_trailer_alloc| {
            var trailer = try read_file_trailer_alloc(self.ptr, allocator, path, len);
            errdefer trailer.deinit(allocator);
            if (trailer.bytes.len != len or trailer.file_size < len)
                return error.EndOfStream;
            return trailer;
        }

        const size = try self.fileSize(path);
        if (size < len) return error.EndOfStream;
        return .{
            .bytes = try self.readFileRangeAlloc(allocator, path, size - len, len),
            .file_size = size,
        };
    }

    pub fn writeFileAbsolute(self: Storage, path: []const u8, contents: []const u8) !void {
        return self.vtable.write_file_absolute(self.ptr, path, contents);
    }

    pub fn appendFileAbsolute(self: Storage, allocator: Allocator, path: []const u8, contents: []const u8, sync: bool) !void {
        if (self.vtable.append_file_absolute) |append_file_absolute| {
            return append_file_absolute(self.ptr, path, contents, sync);
        }

        const existing = self.readFileAlloc(allocator, path, std.math.maxInt(usize)) catch |err| switch (err) {
            error.FileNotFound => {
                try self.writeFileAbsolute(path, contents);
                if (sync) try self.syncFileContentsAbsolute(path);
                return;
            },
            else => return err,
        };
        defer allocator.free(existing);

        const joined = try allocator.alloc(u8, existing.len + contents.len);
        defer allocator.free(joined);
        @memcpy(joined[0..existing.len], existing);
        @memcpy(joined[existing.len..], contents);
        try self.writeFileAbsolute(path, joined);
        if (sync) try self.syncFileContentsAbsolute(path);
    }

    pub fn beginAtomicWrite(self: Storage, allocator: Allocator, path: []const u8) !AtomicWriteSink {
        if (self.vtable.begin_atomic_write) |begin_atomic_write| {
            return try begin_atomic_write(self.ptr, allocator, path);
        }
        if (self.vtable.sync_contents_absolute == null or
            self.vtable.sync_parent_absolute == null or
            !self.vtable.rename_is_atomic)
        {
            return error.DurableAtomicWriteUnsupported;
        }
        return try BufferedAtomicWriteSink.create(allocator, self, path);
    }

    pub fn syncFileContentsAbsolute(self: Storage, path: []const u8) !void {
        if (self.vtable.sync_contents_absolute) |sync_contents_absolute| {
            return try sync_contents_absolute(self.ptr, path);
        }
        return error.DurableFileSyncUnsupported;
    }

    /// Persists creation or removal of `path` without resyncing its contents.
    pub fn syncParentAbsolute(self: Storage, path: []const u8) !void {
        if (self.vtable.sync_parent_absolute) |sync_parent_absolute| {
            return try sync_parent_absolute(self.ptr, path);
        }
        return error.DurableDirectorySyncUnsupported;
    }

    pub fn renameAbsolute(self: Storage, old_path: []const u8, new_path: []const u8) !void {
        return self.vtable.rename_absolute(self.ptr, old_path, new_path);
    }

    pub fn deleteFileAbsolute(self: Storage, path: []const u8) !void {
        return self.vtable.delete_file_absolute(self.ptr, path);
    }

    pub fn deleteTree(self: Storage, path: []const u8) !void {
        return self.vtable.delete_tree(self.ptr, path);
    }

    pub fn nowNs(self: Storage) u64 {
        return self.vtable.now_ns(self.ptr);
    }

    pub fn rootIdentityAlloc(self: Storage, allocator: Allocator, root_dir: []const u8) ![]u8 {
        if (self.vtable.root_identity_alloc) |root_identity_alloc| {
            return try root_identity_alloc(self.ptr, allocator, root_dir);
        }
        return try std.fmt.allocPrint(allocator, "storage:{x}:{x}:{s}", .{
            @intFromPtr(self.ptr),
            @intFromPtr(self.vtable),
            root_dir,
        });
    }

    pub fn supportsNativePathLocks(self: Storage) bool {
        return self.vtable.supports_native_path_locks;
    }

    pub fn supportsHostPathGenerationPublication(self: Storage) bool {
        return self.vtable.supports_host_path_generation_publication;
    }
};

pub fn createDirPathPortable(io: anytype, path: []const u8) !void {
    return fs_paths.createDirPathPortable(io, path);
}

/// Thin wrapper for host-provided storage callbacks.
/// Intended for embedders that want durable LSM semantics without native fs access,
/// such as wasm or foreign host runtimes. Durable writers require either a
/// host-provided atomic writer or an explicitly atomic rename plus separate
/// file-content and parent-namespace sync callbacks. A host with one combined
/// durability barrier may install it in both callback slots.
pub const HostStorage = struct {
    ptr: *anyopaque,
    vtable: *const Storage.VTable,

    pub fn init(ptr: *anyopaque, vtable: *const Storage.VTable) HostStorage {
        return .{
            .ptr = ptr,
            .vtable = vtable,
        };
    }

    pub fn storage(self: HostStorage) Storage {
        return .{
            .ptr = self.ptr,
            .vtable = self.vtable,
        };
    }
};

const BufferedAtomicWriteSink = struct {
    allocator: Allocator,
    storage: Storage,
    final_path: []u8,
    tmp_path: []u8,
    out: std.ArrayListUnmanaged(u8) = .empty,

    fn create(allocator: Allocator, storage: Storage, path: []const u8) !AtomicWriteSink {
        const self = try allocator.create(BufferedAtomicWriteSink);
        errdefer allocator.destroy(self);

        const final_path = try allocator.dupe(u8, path);
        errdefer allocator.free(final_path);

        const tmp_path = try tempSiblingPath(allocator, path);
        errdefer allocator.free(tmp_path);

        self.* = .{
            .allocator = allocator,
            .storage = storage,
            .final_path = final_path,
            .tmp_path = tmp_path,
        };
        return .{
            .ptr = self,
            .vtable = &buffered_atomic_write_sink_vtable,
        };
    }

    fn deinit(self: *BufferedAtomicWriteSink) void {
        self.out.deinit(self.allocator);
        self.allocator.free(self.final_path);
        self.allocator.free(self.tmp_path);
        self.allocator.destroy(self);
    }

    fn len(ptr: *anyopaque) usize {
        const self: *BufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        return self.out.items.len;
    }

    fn appendSlice(ptr: *anyopaque, bytes: []const u8) !void {
        const self: *BufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        try byte_copy.appendSlicePossiblyAliased(&self.out, self.allocator, bytes);
    }

    fn writeAt(ptr: *anyopaque, offset: usize, bytes: []const u8) !void {
        const self: *BufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (offset > self.out.items.len or bytes.len > self.out.items.len - offset) return error.InvalidAtomicWriteOffset;
        byte_copy.copyPossiblyAliased(self.out.items[offset..][0..bytes.len], bytes);
    }

    fn crc32Prefix(ptr: *anyopaque, len_prefix: usize) !u32 {
        const self: *BufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (len_prefix > self.out.items.len) return error.InvalidAtomicWriteOffset;
        return std.hash.Crc32.hash(self.out.items[0..len_prefix]);
    }

    fn crc32Range(ptr: *anyopaque, offset: usize, range_len: usize) !u32 {
        const self: *BufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (offset > self.out.items.len or range_len > self.out.items.len - offset) return error.InvalidAtomicWriteOffset;
        return std.hash.Crc32.hash(self.out.items[offset..][0..range_len]);
    }

    fn finish(ptr: *anyopaque) !void {
        const self: *BufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        defer self.deinit();

        self.storage.writeFileAbsolute(self.tmp_path, self.out.items) catch |err| {
            self.storage.deleteFileAbsolute(self.tmp_path) catch {};
            return err;
        };
        self.storage.syncFileContentsAbsolute(self.tmp_path) catch |err| {
            self.storage.deleteFileAbsolute(self.tmp_path) catch {};
            return err;
        };
        self.storage.renameAbsolute(self.tmp_path, self.final_path) catch |err| {
            self.storage.deleteFileAbsolute(self.tmp_path) catch {};
            return err;
        };
        try self.storage.syncParentAbsolute(self.final_path);
    }

    fn abort(ptr: *anyopaque) void {
        const self: *BufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        self.storage.deleteFileAbsolute(self.tmp_path) catch {};
        self.deinit();
    }
};

const buffered_atomic_write_sink_vtable: AtomicWriteSink.VTable = .{
    .len = BufferedAtomicWriteSink.len,
    .append_slice = BufferedAtomicWriteSink.appendSlice,
    .write_at = BufferedAtomicWriteSink.writeAt,
    .crc32_prefix = BufferedAtomicWriteSink.crc32Prefix,
    .crc32_range = BufferedAtomicWriteSink.crc32Range,
    .finish = BufferedAtomicWriteSink.finish,
    .abort = BufferedAtomicWriteSink.abort,
};

const FdCache = if (!supports_posix_fd_cache)
    struct {
        admission_epoch: std.atomic.Value(u32) = .init(0),

        pub fn init(_: Allocator, _: usize) FdCache {
            return .{};
        }

        pub fn deinit(_: *FdCache) void {}

        pub fn snapshotStats(_: *const FdCache) NativeStorageStats {
            return .{};
        }

        pub fn readRangeAlloc(_: *FdCache, _: u64, _: Allocator, _: []const u8, _: u64, _: usize) ![]u8 {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn fileSize(_: *FdCache, _: u64, _: []const u8) !u64 {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn readTrailerAlloc(_: *FdCache, _: u64, _: Allocator, _: []const u8, _: usize) !FileTrailer {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn invalidatePath(_: *FdCache, _: u64, _: []const u8) void {}
        pub fn invalidateTree(_: *FdCache, _: u64, _: []const u8) void {}
        pub fn invalidateRename(_: *FdCache, _: u64, _: []const u8, _: []const u8) void {}
        pub fn invalidateNamespace(_: *FdCache, _: u64) void {}
        pub fn reserveDescriptors(_: *FdCache, _: std.Io, _: usize) !void {}
        pub fn releaseDescriptors(_: *FdCache, _: std.Io, _: usize) void {}
        pub fn reservePersistentDescriptors(_: *FdCache, _: std.Io, _: usize) !void {}
        pub fn releasePersistentDescriptors(_: *FdCache, _: std.Io, _: usize) void {}
        pub fn signalAdmissionChanged(_: *FdCache, _: std.Io) void {}
        pub fn admissionEpoch(_: *const FdCache) u32 {
            return 0;
        }
    }
else
    struct {
        const BucketMap = std.AutoHashMapUnmanaged(u64, std.ArrayListUnmanaged(*Entry));

        const Entry = struct {
            namespace: u64,
            path_hash: u64,
            path: [:0]u8,
            fd: std.posix.fd_t,
            ref_count: usize = 0,
            last_access: u64 = 0,
            invalidated: bool = false,
            lru_prev: ?*Entry = null,
            lru_next: ?*Entry = null,

            fn deinit(self: *Entry, allocator: Allocator) void {
                closeFd(self.fd);
                allocator.free(self.path);
                self.* = undefined;
            }
        };

        const Shard = struct {
            mutex: std.atomic.Mutex = .unlocked,
            entries: BucketMap = .empty,
            lru_head: ?*Entry = null,
            lru_tail: ?*Entry = null,
            // Incremented before every mutation fence affecting this shard.
            // Cache misses open outside the shard lock, then compare the
            // observed epoch before insertion so an invalidate/rename cannot
            // be lost in that window.
            invalidation_epoch: u64 = 0,
        };

        const AdmissionWaiter = struct {
            previous: ?*AdmissionWaiter = null,
            next: ?*AdmissionWaiter = null,
            count: usize,
        };

        allocator: Allocator,
        shards: []Shard,
        capacity: usize,
        persistent_reserve: usize,
        persistent_open_headroom: usize,
        admitted_descriptors: std.atomic.Value(usize) = .init(0),
        persistent_descriptors: std.atomic.Value(usize) = .init(0),
        cache_entry_count: std.atomic.Value(usize) = .init(0),
        admission_waiters: std.atomic.Value(usize) = .init(0),
        admission_waits: CounterU64 = .init(0),
        persistent_admission_failures: CounterU64 = .init(0),
        admission_epoch: std.atomic.Value(u32) = .init(0),
        admission_mutex: std.Io.Mutex = .init,
        admission_changed: std.Io.Condition = .init,
        admission_head: ?*AdmissionWaiter = null,
        admission_tail: ?*AdmissionWaiter = null,
        access_clock: CounterU64 = .init(0),
        evict_cursor: std.atomic.Value(usize) = .init(0),
        evict_mutex: std.atomic.Mutex = .unlocked,

        fn init(allocator: Allocator, capacity: usize) FdCache {
            const shards = allocator.alloc(Shard, fd_cache_shard_count) catch @panic("OOM");
            @memset(shards, .{});
            return .{
                .allocator = allocator,
                .shards = shards,
                .capacity = @max(@as(usize, 1), capacity),
                // Keep a small slice unavailable to transient leases so a
                // backend can still acquire its lifetime lock files under
                // read/write pressure. Small test and emergency limits retain
                // their full capacity.
                .persistent_reserve = if (capacity < 4) 0 else @min(@as(usize, 64), @max(@as(usize, 2), capacity / 8)),
                // Creating one lifetime lock can transiently require both the
                // newly-created descriptor and the final reopened descriptor.
                .persistent_open_headroom = if (capacity < 4) 0 else 2,
            };
        }

        fn deinit(self: *FdCache) void {
            for (self.shards) |*shard| {
                var current = shard.lru_head;
                while (current) |entry| {
                    const next = entry.lru_next;
                    entry.deinit(self.allocator);
                    self.allocator.destroy(entry);
                    current = next;
                }

                var it = shard.entries.iterator();
                while (it.next()) |bucket| {
                    bucket.value_ptr.deinit(self.allocator);
                }
                shard.entries.deinit(self.allocator);
            }
            self.allocator.free(self.shards);
            self.* = undefined;
        }

        fn snapshotStats(self: *const FdCache) NativeStorageStats {
            return .{
                .fd_cache_entries = self.cache_entry_count.load(.monotonic),
                .fd_admitted_descriptors = self.admitted_descriptors.load(.monotonic),
                .fd_persistent_descriptors = self.persistent_descriptors.load(.monotonic),
                .fd_admission_capacity = self.capacity,
                .fd_persistent_reserve = self.persistent_reserve,
                .fd_admission_waiters = self.admission_waiters.load(.monotonic),
                .fd_admission_waits = self.admission_waits.load(.monotonic),
                .fd_persistent_admission_failures = self.persistent_admission_failures.load(.monotonic),
            };
        }

        fn readRangeAlloc(self: *FdCache, namespace: u64, io: std.Io, allocator: Allocator, path: []const u8, offset: u64, len: usize) ![]u8 {
            const entry = try self.retain(namespace, io, path);
            defer self.release(io, entry);

            const out = try allocator.alloc(u8, len);
            errdefer allocator.free(out);
            try readAllAtOffset(entry.fd, out, offset);
            return out;
        }

        fn readRangeInto(self: *FdCache, namespace: u64, io: std.Io, path: []const u8, offset: u64, out: []u8) !void {
            const entry = try self.retain(namespace, io, path);
            defer self.release(io, entry);
            try readAllAtOffset(entry.fd, out, offset);
        }

        fn readRangeAtMostInto(self: *FdCache, namespace: u64, io: std.Io, path: []const u8, offset: u64, out: []u8) !usize {
            const entry = try self.retain(namespace, io, path);
            defer self.release(io, entry);
            return try readAtMostAtOffset(entry.fd, out, offset);
        }

        fn fileSize(self: *FdCache, namespace: u64, io: std.Io, path: []const u8) !u64 {
            const entry = try self.retain(namespace, io, path);
            defer self.release(io, entry);
            return try fileSizeFromFd(entry.fd);
        }

        fn readTrailerAlloc(self: *FdCache, namespace: u64, io: std.Io, allocator: Allocator, path: []const u8, len: usize) !FileTrailer {
            const entry = try self.retain(namespace, io, path);
            defer self.release(io, entry);

            const size = try fileSizeFromFd(entry.fd);
            if (size < len) return error.EndOfStream;

            const out = try allocator.alloc(u8, len);
            errdefer allocator.free(out);
            try readAllAtOffset(entry.fd, out, size - len);
            return .{ .bytes = out, .file_size = size };
        }

        fn invalidatePath(self: *FdCache, namespace: u64, path: []const u8) void {
            const path_hash = namespacedPathHash(namespace, path);
            const shard = self.shardForHash(path_hash);
            const locked = lockAtomic(&shard.mutex);
            defer if (locked) shard.mutex.unlock();

            shard.invalidation_epoch +%= 1;

            var matched_any = false;
            if (shard.entries.getPtr(path_hash)) |bucket| {
                for (bucket.items) |entry| {
                    if (entry.namespace != namespace or !std.mem.eql(u8, entry.path, path)) continue;
                    entry.invalidated = true;
                    matched_any = true;
                }
            }

            if (!matched_any) return;

            while (self.findInvalidatedEntryLocked(shard, namespace, path_hash, path)) |entry| {
                if (entry.ref_count != 0) break;
                self.removeEntryLocked(shard, entry);
            }
        }

        fn invalidateTree(self: *FdCache, namespace: u64, path: []const u8) void {
            for (self.shards) |*shard| {
                const locked = lockAtomic(&shard.mutex);
                shard.invalidation_epoch +%= 1;

                var current = shard.lru_head;
                while (current) |entry| {
                    const next = entry.lru_next;
                    if (entry.namespace == namespace and pathContains(path, entry.path)) {
                        entry.invalidated = true;
                        if (entry.ref_count == 0) self.removeEntryLocked(shard, entry);
                    }
                    current = next;
                }
                if (locked) shard.mutex.unlock();
            }
        }

        fn invalidateRename(self: *FdCache, namespace: u64, old_path: []const u8, new_path: []const u8) void {
            self.invalidatePath(namespace, old_path);
            self.invalidatePath(namespace, new_path);
        }

        fn invalidateNamespace(self: *FdCache, namespace: u64) void {
            for (self.shards) |*shard| {
                const locked = lockAtomic(&shard.mutex);
                shard.invalidation_epoch +%= 1;
                var current = shard.lru_head;
                while (current) |entry| {
                    const next = entry.lru_next;
                    if (entry.namespace == namespace) {
                        entry.invalidated = true;
                        if (entry.ref_count == 0) self.removeEntryLocked(shard, entry);
                    }
                    current = next;
                }
                if (locked) shard.mutex.unlock();
            }
        }

        fn retain(self: *FdCache, namespace: u64, io: std.Io, path: []const u8) !*Entry {
            const path_hash = namespacedPathHash(namespace, path);
            const shard = self.shardForHash(path_hash);
            const owned_path = try self.allocator.dupeZ(u8, path);
            var owned_path_active = true;
            errdefer if (owned_path_active) self.allocator.free(owned_path);

            while (true) {
                var observed_invalidation_epoch: u64 = 0;
                const locked = lockAtomic(&shard.mutex);
                if (self.findEntryLocked(shard, namespace, path_hash, path)) |existing| {
                    existing.ref_count += 1;
                    existing.last_access = self.nextAccessLocked();
                    self.touchEntryLocked(shard, existing);
                    if (locked) shard.mutex.unlock();
                    self.allocator.free(owned_path);
                    owned_path_active = false;
                    return existing;
                }
                observed_invalidation_epoch = shard.invalidation_epoch;
                if (locked) shard.mutex.unlock();

                try self.reserveDescriptors(io, 1);
                var descriptor_reserved = true;
                errdefer if (descriptor_reserved) self.releaseDescriptors(io, 1);

                const fd = try std.posix.openatZ(std.posix.AT.FDCWD, owned_path, .{
                    .ACCMODE = .RDONLY,
                    .CLOEXEC = true,
                }, 0);
                var fd_open = true;
                errdefer if (fd_open) closeFd(fd);

                if (builtin.is_test and test_fd_cache_pause_after_open.load(.acquire)) {
                    test_fd_cache_open_paused.store(true, .release);
                    while (!test_fd_cache_release_after_open.load(.acquire)) {
                        io.sleep(.fromMilliseconds(1), .awake) catch {};
                    }
                }

                const entry = try self.allocator.create(Entry);
                var entry_active = true;
                errdefer if (entry_active) self.allocator.destroy(entry);
                entry.* = .{
                    .namespace = namespace,
                    .path_hash = path_hash,
                    .path = owned_path,
                    .fd = fd,
                    .ref_count = 1,
                    .last_access = self.nextAccessLocked(),
                };

                const insertion_locked = lockAtomic(&shard.mutex);
                errdefer if (insertion_locked) shard.mutex.unlock();

                if (self.findEntryLocked(shard, namespace, path_hash, owned_path)) |existing| {
                    existing.ref_count += 1;
                    existing.last_access = self.nextAccessLocked();
                    self.touchEntryLocked(shard, existing);
                    if (insertion_locked) shard.mutex.unlock();
                    closeFd(fd);
                    fd_open = false;
                    self.allocator.destroy(entry);
                    entry_active = false;
                    self.allocator.free(owned_path);
                    owned_path_active = false;
                    descriptor_reserved = false;
                    self.releaseDescriptors(io, 1);
                    return existing;
                }

                if (shard.invalidation_epoch != observed_invalidation_epoch) {
                    if (insertion_locked) shard.mutex.unlock();
                    closeFd(fd);
                    fd_open = false;
                    self.allocator.destroy(entry);
                    entry_active = false;
                    descriptor_reserved = false;
                    self.releaseDescriptors(io, 1);
                    continue;
                }

                const bucket = try shard.entries.getOrPut(self.allocator, path_hash);
                if (!bucket.found_existing) bucket.value_ptr.* = .empty;
                try bucket.value_ptr.append(self.allocator, entry);
                self.linkEntryLocked(shard, entry);
                _ = self.cache_entry_count.fetchAdd(1, .monotonic);
                if (insertion_locked) shard.mutex.unlock();

                entry_active = false;
                fd_open = false;
                owned_path_active = false;
                descriptor_reserved = false;
                return entry;
            }
        }

        fn release(self: *FdCache, io: std.Io, entry: *Entry) void {
            const shard = self.shardForHash(entry.path_hash);
            const locked = lockAtomic(&shard.mutex);
            var removed = false;

            std.debug.assert(entry.ref_count > 0);
            entry.ref_count -= 1;
            entry.last_access = self.nextAccessLocked();
            self.touchEntryLocked(shard, entry);
            if (entry.ref_count == 0 and entry.invalidated) {
                self.removeEntryLocked(shard, entry);
                removed = true;
                if (locked) shard.mutex.unlock();
                self.signalAdmissionChanged(io);
                return;
            }
            if (entry.ref_count == 0 and self.admission_waiters.load(.acquire) > 0) {
                self.removeEntryLocked(shard, entry);
                removed = true;
            }
            if (locked) shard.mutex.unlock();
            if (removed) self.signalAdmissionChanged(io);
        }

        fn reserveDescriptors(self: *FdCache, io: std.Io, count: usize) !void {
            if (count == 0) return;
            if (count > self.capacity - self.persistent_reserve) return error.DescriptorAdmissionCapacityTooSmall;
            self.admission_mutex.lockUncancelable(io);
            defer self.admission_mutex.unlock(io);

            if (self.admission_head == null and self.makeCapacityAvailable(count)) {
                _ = self.admitted_descriptors.fetchAdd(count, .acq_rel);
                return;
            }
            if (self.admission_head == null and self.persistentDescriptorsPreventTransientProgress(count)) {
                _ = self.persistent_admission_failures.fetchAdd(1, .monotonic);
                return error.PersistentDescriptorAdmissionExhausted;
            }

            var waiter = AdmissionWaiter{ .count = count };
            self.enqueueAdmissionWaiter(&waiter);
            _ = self.admission_waiters.fetchAdd(1, .acq_rel);
            _ = self.admission_waits.fetchAdd(1, .monotonic);
            errdefer {
                self.removeAdmissionWaiter(&waiter);
                _ = self.admission_waiters.fetchSub(1, .acq_rel);
                self.admission_changed.broadcast(io);
            }
            while (true) {
                if (self.admission_head == &waiter and self.makeCapacityAvailable(count)) {
                    self.removeAdmissionWaiter(&waiter);
                    _ = self.admission_waiters.fetchSub(1, .acq_rel);
                    _ = self.admitted_descriptors.fetchAdd(count, .acq_rel);
                    self.admission_changed.broadcast(io);
                    return;
                }
                // A transient holder can eventually release and wake us. A
                // pool occupied only by lifetime descriptors cannot make
                // progress until a backend is closed, and the caller may own
                // the cache-open lock required to choose that victim. Surface
                // the persistent-pressure error instead of sleeping forever.
                if (self.admission_head == &waiter and self.persistentDescriptorsPreventTransientProgress(count)) {
                    _ = self.persistent_admission_failures.fetchAdd(1, .monotonic);
                    return error.PersistentDescriptorAdmissionExhausted;
                }
                try self.admission_changed.wait(io, &self.admission_mutex);
            }
        }

        fn persistentDescriptorsPreventTransientProgress(self: *FdCache, count: usize) bool {
            const persistent = self.persistent_descriptors.load(.acquire);
            const admitted = self.admitted_descriptors.load(.acquire);
            if (admitted != persistent) return false;

            const unused_reserve = self.persistent_reserve -| persistent;
            const remaining_capacity = self.capacity -| persistent;
            const open_headroom = if (remaining_capacity >= self.persistent_open_headroom)
                self.persistent_open_headroom
            else
                0;
            const transient_capacity = self.capacity - @max(unused_reserve, open_headroom);
            return count > transient_capacity -| admitted;
        }

        fn makeCapacityAvailable(self: *FdCache, count: usize) bool {
            // The reserve is startup headroom, not a permanent tax. Once
            // lifetime locks consume it, transient leases may use the freed
            // portion while retaining the two-descriptor peak needed by the
            // next create/openat. Without this dynamic accounting, enough
            // open backends can make transient admission wait forever even
            // though the aggregate pool still has unused descriptors.
            const persistent = self.persistent_descriptors.load(.acquire);
            const unused_reserve = self.persistent_reserve -| persistent;
            const remaining_capacity = self.capacity -| persistent;
            const open_headroom = if (remaining_capacity >= self.persistent_open_headroom)
                self.persistent_open_headroom
            else
                0;
            const held_headroom = @max(unused_reserve, open_headroom);
            const transient_capacity = self.capacity - held_headroom;
            if (count > transient_capacity) return false;
            const available_at = transient_capacity - count;
            while (self.admitted_descriptors.load(.acquire) > available_at and self.evictOne()) {}
            return self.admitted_descriptors.load(.acquire) <= available_at;
        }

        /// Admit a descriptor that remains open for a backend lifetime. This
        /// path is deliberately non-blocking: if retained descriptors truly
        /// fill the process storage budget, waiting cannot guarantee progress.
        /// Callers receive an actionable resource error instead of deadlocking
        /// table startup, restore, or shutdown coordination.
        fn reservePersistentDescriptors(self: *FdCache, io: std.Io, count: usize) !void {
            if (count == 0) return;
            if (count > self.capacity) return error.DescriptorAdmissionCapacityTooSmall;

            self.admission_mutex.lockUncancelable(io);
            defer self.admission_mutex.unlock(io);
            const available_at = self.capacity - count;
            while (self.admitted_descriptors.load(.acquire) > available_at and self.evictOne()) {}
            if (self.admitted_descriptors.load(.acquire) > available_at) {
                _ = self.persistent_admission_failures.fetchAdd(1, .monotonic);
                return error.PersistentDescriptorAdmissionExhausted;
            }
            _ = self.admitted_descriptors.fetchAdd(count, .acq_rel);
            _ = self.persistent_descriptors.fetchAdd(count, .acq_rel);
        }

        fn enqueueAdmissionWaiter(self: *FdCache, waiter: *AdmissionWaiter) void {
            waiter.previous = self.admission_tail;
            waiter.next = null;
            if (self.admission_tail) |tail| tail.next = waiter else self.admission_head = waiter;
            self.admission_tail = waiter;
        }

        fn removeAdmissionWaiter(self: *FdCache, waiter: *AdmissionWaiter) void {
            if (waiter.previous) |previous| previous.next = waiter.next else self.admission_head = waiter.next;
            if (waiter.next) |next| next.previous = waiter.previous else self.admission_tail = waiter.previous;
            waiter.previous = null;
            waiter.next = null;
        }

        fn releaseDescriptors(self: *FdCache, io: std.Io, count: usize) void {
            if (count == 0) return;
            const previous = self.admitted_descriptors.fetchSub(count, .acq_rel);
            std.debug.assert(previous >= count);
            _ = self.admission_epoch.fetchAdd(1, .release);
            self.signalAdmissionChanged(io);
        }

        fn releasePersistentDescriptors(self: *FdCache, io: std.Io, count: usize) void {
            if (count == 0) return;
            const previous_persistent = self.persistent_descriptors.fetchSub(count, .acq_rel);
            std.debug.assert(previous_persistent >= count);
            self.releaseDescriptors(io, count);
        }

        fn signalAdmissionChanged(self: *FdCache, io: std.Io) void {
            std.Io.futexWake(io, u32, &self.admission_epoch.raw, std.math.maxInt(u32));
            // The registering waiter always performs a final capacity/eviction
            // check, so skipping the uncontended mutex on the hot release path
            // cannot lose progress.
            if (self.admission_waiters.load(.acquire) == 0) return;
            self.admission_mutex.lockUncancelable(io);
            // Requests have different weights. The FIFO head is the only
            // request allowed to claim newly available capacity.
            self.admission_changed.broadcast(io);
            self.admission_mutex.unlock(io);
        }

        fn admissionEpoch(self: *const FdCache) u32 {
            return self.admission_epoch.load(.acquire);
        }

        fn evictOne(self: *FdCache) bool {
            const start = self.evict_cursor.fetchAdd(1, .monotonic);
            for (0..self.shards.len) |offset| {
                const shard = &self.shards[(start + offset) % self.shards.len];
                const locked = lockAtomic(&shard.mutex);

                var current = shard.lru_head;
                while (current) |entry| {
                    if (entry.ref_count == 0) {
                        self.removeEntryLocked(shard, entry);
                        if (locked) shard.mutex.unlock();
                        return true;
                    }
                    current = entry.lru_next;
                }
                if (locked) shard.mutex.unlock();
            }
            return false;
        }

        fn findEntryLocked(self: *FdCache, shard: *Shard, namespace: u64, path_hash: u64, path: []const u8) ?*Entry {
            _ = self;
            const bucket = shard.entries.getPtr(path_hash) orelse return null;
            var i = bucket.items.len;
            while (i > 0) {
                i -= 1;
                const entry = bucket.items[i];
                if (!entry.invalidated and entry.namespace == namespace and std.mem.eql(u8, entry.path, path)) return entry;
            }
            return null;
        }

        fn findInvalidatedEntryLocked(self: *FdCache, shard: *Shard, namespace: u64, path_hash: u64, path: []const u8) ?*Entry {
            _ = self;
            const bucket = shard.entries.getPtr(path_hash) orelse return null;
            var i = bucket.items.len;
            while (i > 0) {
                i -= 1;
                const entry = bucket.items[i];
                if (entry.invalidated and entry.namespace == namespace and std.mem.eql(u8, entry.path, path)) return entry;
            }
            return null;
        }

        fn removeEntryLocked(self: *FdCache, shard: *Shard, entry: *Entry) void {
            const bucket = shard.entries.getPtr(entry.path_hash) orelse unreachable;
            for (bucket.items, 0..) |bucket_entry, i| {
                if (bucket_entry != entry) continue;
                _ = bucket.orderedRemove(i);
                if (bucket.items.len == 0) {
                    var removed = shard.entries.fetchRemove(entry.path_hash) orelse unreachable;
                    removed.value.deinit(self.allocator);
                }
                self.unlinkEntryLocked(shard, entry);
                std.debug.assert(entry.ref_count == 0);
                _ = self.cache_entry_count.fetchSub(1, .monotonic);
                _ = self.admitted_descriptors.fetchSub(1, .monotonic);
                _ = self.admission_epoch.fetchAdd(1, .release);
                entry.deinit(self.allocator);
                self.allocator.destroy(entry);
                return;
            }
            unreachable;
        }

        fn shardForHash(self: *FdCache, path_hash: u64) *Shard {
            return &self.shards[@intCast(path_hash % self.shards.len)];
        }

        fn linkEntryLocked(self: *FdCache, shard: *Shard, entry: *Entry) void {
            _ = self;
            entry.lru_prev = shard.lru_tail;
            entry.lru_next = null;
            if (entry.lru_prev) |prev| {
                prev.lru_next = entry;
            } else {
                shard.lru_head = entry;
            }
            shard.lru_tail = entry;
        }

        fn unlinkEntryLocked(self: *FdCache, shard: *Shard, entry: *Entry) void {
            _ = self;
            if (entry.lru_prev) |prev| {
                prev.lru_next = entry.lru_next;
            } else {
                shard.lru_head = entry.lru_next;
            }
            if (entry.lru_next) |next| {
                next.lru_prev = entry.lru_prev;
            } else {
                shard.lru_tail = entry.lru_prev;
            }
            entry.lru_prev = null;
            entry.lru_next = null;
        }

        fn touchEntryLocked(self: *FdCache, shard: *Shard, entry: *Entry) void {
            if (shard.lru_tail == entry) return;
            self.unlinkEntryLocked(shard, entry);
            self.linkEntryLocked(shard, entry);
        }

        fn nextAccessLocked(self: *FdCache) u64 {
            return self.access_clock.fetchAdd(1, .monotonic) + 1;
        }
    };

var process_native_storage_pool_mutex: std.atomic.Mutex = .unlocked;
var process_native_fd_cache: ?*FdCache = null;
var native_storage_cache_namespace: std.atomic.Value(u64) = .init(1);

fn processNativeFdCache() *FdCache {
    const locked = lockAtomic(&process_native_storage_pool_mutex);
    defer if (locked) process_native_storage_pool_mutex.unlock();
    if (process_native_fd_cache) |cache| return cache;

    // The process admission domain intentionally lives for the process
    // lifetime. This prevents one BackendRuntime from invalidating permits or
    // cached descriptors still owned by another runtime during shutdown.
    const cache = std.heap.page_allocator.create(FdCache) catch @panic("OOM");
    cache.* = FdCache.init(std.heap.page_allocator, configuredNativeFdAdmissionCapacity());
    process_native_fd_cache = cache;
    return cache;
}

/// Handle to the process-wide descriptor admission domain shared by every
/// BackendRuntime and all of their native stores. Cached LSM entries and
/// transient opens consume the same aggregate, RLIMIT-derived budget.
pub const NativeStoragePool = struct {
    fd_cache: *FdCache,
    owned_allocator: ?Allocator = null,

    pub fn init(_: Allocator) NativeStoragePool {
        return .{ .fd_cache = processNativeFdCache() };
    }

    pub fn initWithCapacityForTest(allocator: Allocator, capacity: usize) NativeStoragePool {
        const cache = allocator.create(FdCache) catch @panic("OOM");
        cache.* = FdCache.init(allocator, capacity);
        return .{ .fd_cache = cache, .owned_allocator = allocator };
    }

    pub fn deinit(self: *NativeStoragePool) void {
        if (self.owned_allocator) |allocator| {
            self.fd_cache.deinit();
            allocator.destroy(self.fd_cache);
        }
        self.* = undefined;
    }

    pub fn snapshotStats(self: *const NativeStoragePool) NativeStorageStats {
        return self.fd_cache.snapshotStats();
    }

    pub fn admissionEpoch(self: *const NativeStoragePool) u32 {
        return self.fd_cache.admissionEpoch();
    }

    pub fn waitForAdmissionChange(self: *const NativeStoragePool, io: std.Io, observed_epoch: u32) !void {
        if (self.admissionEpoch() != observed_epoch) return;
        try std.Io.futexWait(io, u32, &self.fd_cache.admission_epoch.raw, observed_epoch);
    }

    pub fn signalAdmissionChanged(self: *NativeStoragePool, io: std.Io) void {
        self.fd_cache.signalAdmissionChanged(io);
    }

    pub fn reserveDescriptorsForTest(self: *NativeStoragePool, io: std.Io, count: usize) !void {
        if (!builtin.is_test) unreachable;
        try self.fd_cache.reserveDescriptors(io, count);
    }

    pub fn releaseDescriptorsForTest(self: *NativeStoragePool, io: std.Io, count: usize) void {
        if (!builtin.is_test) unreachable;
        self.fd_cache.releaseDescriptors(io, count);
    }
};

const NativeStorageState = struct {
    // Storage values returned by NativeStorage.storage() borrow their owner and
    // must not outlive it. Operations which do outlive a call (range futures,
    // descriptor permits, and atomic writers) explicitly retain this state.
    // Reclaim it once the owner and all such operations release their refs.
    allocator: Allocator,
    refs: std.atomic.Value(usize) = .init(1),
    closing: std.atomic.Value(bool) = .init(false),
    threaded: std.Io.Threaded,
    fd_cache: *FdCache,
    cache_namespace: u64,

    fn create(allocator: Allocator, kind: RuntimeKind, pool: ?*NativeStoragePool) !*NativeStorageState {
        if (kind != .threaded) return error.UnsupportedEventedIoRuntime;
        const state = try allocator.create(NativeStorageState);
        errdefer allocator.destroy(state);
        state.* = undefined;
        state.allocator = allocator;
        state.refs = .init(1);
        state.closing = .init(false);
        // Native storage state may outlive its owning DB while range futures
        // drain. Prevent that retained runtime from growing without a bound.
        state.threaded = threaded_io_limits.initService(allocator);
        errdefer state.threaded.deinit();
        // NativeStorage.init is used by repository, recovery, and status
        // helpers as well as BackendRuntime-owned stores. All production
        // handles must join the same process admission domain; isolated caches
        // are available only through an explicit test pool.
        state.fd_cache = if (pool) |shared| shared.fd_cache else processNativeFdCache();
        state.cache_namespace = native_storage_cache_namespace.fetchAdd(1, .monotonic);
        return state;
    }

    fn fdCache(self: *NativeStorageState) *FdCache {
        return self.fd_cache;
    }

    fn fdCacheConst(self: *const NativeStorageState) *const FdCache {
        return self.fd_cache;
    }

    fn acquireFdPermit(self: *NativeStorageState) !NativeFdPermit {
        const retained = try self.retain();
        errdefer retained.release();
        const io = retained.threaded.io();
        const cache = retained.fdCache();
        try cache.reserveDescriptors(io, 1);
        return .{
            .state = retained,
            .cache = cache,
            .io = io,
            .count = 1,
        };
    }

    fn acquireFdPermits(self: *NativeStorageState, count: usize) !NativeFdPermit {
        const retained = try self.retain();
        errdefer retained.release();
        const io = retained.threaded.io();
        const cache = retained.fdCache();
        try cache.reserveDescriptors(io, count);
        return .{
            .state = retained,
            .cache = cache,
            .io = io,
            .count = count,
        };
    }

    fn retain(self: *NativeStorageState) !*NativeStorageState {
        while (true) {
            const current = self.refs.load(.acquire);
            if (current == 0) return error.StorageClosed;
            if (self.refs.cmpxchgWeak(current, current + 1, .acq_rel, .acquire)) |_| {
                continue;
            }
            return self;
        }
    }

    fn acquireLease(self: *NativeStorageState) !*NativeStorageState {
        if (self.closing.load(.acquire)) return error.StorageClosed;
        const retained = try self.retain();
        if (self.closing.load(.acquire)) {
            retained.release();
            return error.StorageClosed;
        }
        return retained;
    }

    fn closeStorageRef(self: *NativeStorageState) void {
        self.closing.store(true, .release);
        self.release();
    }

    fn release(self: *NativeStorageState) void {
        if (self.refs.fetchSub(1, .acq_rel) != 1) return;
        // Cached descriptors are shared for aggregate eviction, but file
        // identity is scoped to this storage generation. Invalidate the
        // namespace before destroying the I/O runtime so a restore or reopen
        // at the same pathname can never observe the old inode.
        self.fd_cache.invalidateNamespace(self.cache_namespace);
        self.fd_cache.signalAdmissionChanged(self.threaded.io());
        self.threaded.deinit();
        const allocator = self.allocator;
        allocator.destroy(self);
    }

    /// Mutation paths call invalidation before and after the filesystem
    /// operation. The first pass retires cached entries; the second changes
    /// the epoch for a miss that opened the old inode inside the mutation
    /// window but had not inserted it yet.
    fn invalidatePath(self: *NativeStorageState, path: []const u8) void {
        self.fdCache().invalidatePath(self.cache_namespace, path);
        self.fdCache().signalAdmissionChanged(self.threaded.io());
    }

    fn invalidateRename(self: *NativeStorageState, old_path: []const u8, new_path: []const u8) void {
        self.fdCache().invalidateRename(self.cache_namespace, old_path, new_path);
        self.fdCache().signalAdmissionChanged(self.threaded.io());
    }

    fn invalidateTree(self: *NativeStorageState, path: []const u8) void {
        self.fdCache().invalidateTree(self.cache_namespace, path);
        self.fdCache().signalAdmissionChanged(self.threaded.io());
    }
};

/// A permit from the runtime-wide storage descriptor budget. Cached LSM files
/// retain one for the cache entry lifetime; short-lived consumers such as
/// full-text segment mmap release theirs as soon as the source fd is closed.
pub const NativeFdPermit = struct {
    state: *NativeStorageState,
    cache: *FdCache,
    io: std.Io,
    count: usize,
    active: bool = true,

    pub fn release(self: *NativeFdPermit) void {
        if (!self.active) return;
        self.active = false;
        self.cache.releaseDescriptors(self.io, self.count);
        self.state.release();
    }

    fn take(self: *NativeFdPermit) NativeFdPermit {
        std.debug.assert(self.active);
        std.debug.assert(self.count == 1);
        const owned = self.*;
        self.active = false;
        return owned;
    }

    pub fn splitOne(self: *NativeFdPermit) !NativeFdPermit {
        if (!self.active or self.count <= 1) return error.InvalidNativeFdPermit;
        const retained = try self.state.retain();
        self.count -= 1;
        return .{
            .state = retained,
            .cache = self.cache,
            .io = self.io,
            .count = 1,
        };
    }
};

const NativeRangeReadFuture = if (!supports_native_storage or builtin.os.tag == .freestanding)
    struct {}
else
    struct {
        allocator: Allocator,
        state: *NativeStorageState,
        path: []u8,
        offset: u64,
        len: usize,
        io: std.Io,
        future: std.Io.Future(void) = undefined,
        bytes: ?[]u8 = null,
        err: ?anyerror = null,

        const vtable: RangeReadFuture.VTable = .{
            .wait = wait,
            .cancel = cancel,
        };

        fn create(read_runtime: ReadRuntime, state: *NativeStorageState, allocator: Allocator, path: []const u8, offset: u64, len: usize) !RangeReadFuture {
            const retained = try state.retain();
            errdefer retained.release();

            const self = try allocator.create(NativeRangeReadFuture);
            errdefer allocator.destroy(self);

            const path_copy = try allocator.dupe(u8, path);
            errdefer allocator.free(path_copy);

            self.* = .{
                .allocator = allocator,
                .state = retained,
                .path = path_copy,
                .offset = offset,
                .len = len,
                .io = read_runtime.io,
            };
            self.future = try read_runtime.io.concurrent(run, .{self});
            return .{
                .ptr = self,
                .vtable = &vtable,
            };
        }

        fn run(self: *NativeRangeReadFuture) void {
            self.bytes = read(self) catch |err| {
                self.err = err;
                return;
            };
        }

        fn read(self: *NativeRangeReadFuture) ![]u8 {
            if (comptime supports_posix_fd_cache) {
                return try self.state.fdCache().readRangeAlloc(self.state.cache_namespace, self.io, self.allocator, self.path, self.offset, self.len);
            }
            return try readFileRangeWithIo(self.io, self.allocator, self.path, self.offset, self.len);
        }

        fn wait(ptr: *anyopaque) ![]u8 {
            const self: *NativeRangeReadFuture = @ptrCast(@alignCast(ptr));
            self.future.await(self.io);
            const allocator = self.allocator;
            defer {
                allocator.free(self.path);
                self.state.release();
                allocator.destroy(self);
            }
            if (self.err) |err| return err;
            const bytes = self.bytes orelse return error.CanceledRangeRead;
            self.bytes = null;
            return bytes;
        }

        fn cancel(ptr: *anyopaque) void {
            const self: *NativeRangeReadFuture = @ptrCast(@alignCast(ptr));
            self.future.cancel(self.io);
            const allocator = self.allocator;
            if (self.bytes) |bytes| allocator.free(bytes);
            allocator.free(self.path);
            self.state.release();
            allocator.destroy(self);
        }
    };

pub const NativeStorage = if (!supports_native_storage)
    struct {
        pub const Lease = struct {
            pub fn deinit(_: *Lease) void {}
            pub fn storage(_: *Lease) Storage {
                @panic("native storage is unavailable on this target");
            }
        };

        pub fn init(_: Allocator, _: RuntimeKind) !NativeStorage {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn initWithPool(_: Allocator, _: RuntimeKind, _: ?*NativeStoragePool) !NativeStorage {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn deinit(_: *NativeStorage) void {}

        pub fn snapshotStats(_: *const NativeStorage) NativeStorageStats {
            return .{};
        }

        pub fn acquireFdPermit(_: *NativeStorage) !NativeFdPermit {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn acquireFdPermits(_: *NativeStorage, _: usize) !NativeFdPermit {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn acquireLease(_: *NativeStorage) !Lease {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn beginAtomicWriteWithPermit(_: *NativeStorage, _: Allocator, _: []const u8, _: *NativeFdPermit) !AtomicWriteSink {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn validateFdPermit(_: *NativeStorage, _: *const NativeFdPermit) !void {
            return error.UnsupportedNativeStorageRuntime;
        }

        pub fn storage(_: *NativeStorage) Storage {
            @panic("native storage is unavailable on this target");
        }
    }
else blk: {
    if (supports_evented_runtime) {
        break :blk struct {
            runtime: union(RuntimeKind) {
                threaded: std.Io.Threaded,
                evented: std.Io.Evented,
            },
            state: *NativeStorageState,

            const native_vtable: Storage.VTable = .{
                .create_dir_path = createDirPath,
                .read_file_alloc = readFileAlloc,
                .read_file_range_alloc = readFileRangeAlloc,
                .begin_read_file_range_alloc_with_runtime = beginReadFileRangeAllocWithRuntime,
                .read_file_range_into = readFileRangeInto,
                .read_file_range_at_most_into = readFileRangeAtMostInto,
                .file_size = fileSize,
                .read_file_trailer_alloc = readFileTrailerAlloc,
                .write_file_absolute = writeFileAbsolute,
                .append_file_absolute = appendFileAbsolute,
                .begin_atomic_write = beginAtomicWrite,
                .sync_contents_absolute = syncFileContentsAbsolute,
                .sync_parent_absolute = syncParentAbsolute,
                .rename_absolute = renameAbsolute,
                .delete_file_absolute = deleteFileAbsolute,
                .delete_tree = deleteTree,
                .now_ns = nowNs,
                .root_identity_alloc = nativeRootIdentityAlloc,
                .rename_is_atomic = true,
                .supports_host_path_generation_publication = true,
                .supports_native_path_locks = true,
            };

            pub fn init(allocator: Allocator, kind: RuntimeKind) !NativeStorage {
                return try initWithPool(allocator, kind, null);
            }

            pub fn initWithPool(allocator: Allocator, kind: RuntimeKind, pool: ?*NativeStoragePool) !NativeStorage {
                var runtime = switch (kind) {
                    .threaded => .{ .threaded = threaded_io_limits.initService(allocator) },
                    .evented => blk2: {
                        var evented: std.Io.Evented = undefined;
                        try std.Io.Evented.init(&evented, allocator, .{});
                        break :blk2 .{ .evented = evented };
                    },
                };
                errdefer switch (runtime) {
                    .threaded => |*threaded| threaded.deinit(),
                    .evented => |*evented| std.Io.Evented.deinit(evented),
                };
                return .{
                    .runtime = runtime,
                    .state = try NativeStorageState.create(allocator, kind, pool),
                };
            }

            pub fn deinit(self: *NativeStorage) void {
                self.state.closeStorageRef();
                switch (self.runtime) {
                    .threaded => |*threaded| threaded.deinit(),
                    .evented => |*evented| std.Io.Evented.deinit(evented),
                }
                self.* = undefined;
            }

            pub fn snapshotStats(self: *const NativeStorage) NativeStorageStats {
                return self.state.fdCacheConst().snapshotStats();
            }

            pub fn acquireFdPermit(self: *NativeStorage) !NativeFdPermit {
                return try self.state.acquireFdPermit();
            }

            pub fn storage(self: *NativeStorage) Storage {
                return .{
                    .ptr = self,
                    .vtable = &native_vtable,
                };
            }

            fn createDirPath(ptr: *anyopaque, path: []const u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                switch (self.runtime) {
                    .threaded => |*threaded| try createDirPathPortable(threaded.io(), path),
                    .evented => |*evented| try createDirPathPortable(evented.io(), path),
                }
            }

            fn readFileAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, max_bytes: usize) ![]u8 {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                return switch (self.runtime) {
                    .threaded => |*threaded| try std.Io.Dir.cwd().readFileAlloc(threaded.io(), path, allocator, .limited(max_bytes)),
                    .evented => |*evented| try std.Io.Dir.cwd().readFileAlloc(evented.io(), path, allocator, .limited(max_bytes)),
                };
            }

            fn readFileRangeAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, offset: u64, len: usize) ![]u8 {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                if (comptime supports_posix_fd_cache) {
                    const state = try self.state.retain();
                    defer state.release();
                    return try state.fdCache().readRangeAlloc(state.cache_namespace, state.threaded.io(), allocator, path, offset, len);
                }
                return switch (self.runtime) {
                    .threaded => |*threaded| try readFileRangeWithIo(threaded.io(), allocator, path, offset, len),
                    .evented => |*evented| try readFileRangeWithIo(evented.io(), allocator, path, offset, len),
                };
            }

            fn beginReadFileRangeAllocWithRuntime(ptr: *anyopaque, read_runtime: ?ReadRuntime, allocator: Allocator, path: []const u8, offset: u64, len: usize) !RangeReadFuture {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                const runtime = read_runtime orelse {
                    const sync_storage: Storage = .{ .ptr = ptr, .vtable = &native_vtable };
                    return try CompletedRangeReadFuture.create(sync_storage, allocator, path, offset, len);
                };
                return try NativeRangeReadFuture.create(runtime, self.state, allocator, path, offset, len);
            }

            fn readFileRangeInto(ptr: *anyopaque, path: []const u8, offset: u64, out: []u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                if (comptime supports_posix_fd_cache) {
                    const state = try self.state.retain();
                    defer state.release();
                    return try state.fdCache().readRangeInto(state.cache_namespace, state.threaded.io(), path, offset, out);
                }
                return switch (self.runtime) {
                    .threaded => |*threaded| try readFileRangeWithIoInto(threaded.io(), path, offset, out),
                    .evented => |*evented| try readFileRangeWithIoInto(evented.io(), path, offset, out),
                };
            }

            fn readFileRangeAtMostInto(ptr: *anyopaque, path: []const u8, offset: u64, out: []u8) !usize {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                if (comptime supports_posix_fd_cache) {
                    const state = try self.state.retain();
                    defer state.release();
                    return try state.fdCache().readRangeAtMostInto(state.cache_namespace, state.threaded.io(), path, offset, out);
                }
                return switch (self.runtime) {
                    .threaded => |*threaded| try readFileRangeWithIoAtMostInto(threaded.io(), path, offset, out),
                    .evented => |*evented| try readFileRangeWithIoAtMostInto(evented.io(), path, offset, out),
                };
            }

            fn fileSize(ptr: *anyopaque, path: []const u8) !u64 {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                if (comptime supports_posix_fd_cache) {
                    const state = try self.state.retain();
                    defer state.release();
                    return try state.fdCache().fileSize(state.cache_namespace, state.threaded.io(), path);
                }
                return switch (self.runtime) {
                    .threaded => |*threaded| try fileSizeWithIo(threaded.io(), path),
                    .evented => |*evented| try fileSizeWithIo(evented.io(), path),
                };
            }

            fn readFileTrailerAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, len: usize) !FileTrailer {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                if (comptime supports_posix_fd_cache) {
                    const state = try self.state.retain();
                    defer state.release();
                    return try state.fdCache().readTrailerAlloc(state.cache_namespace, state.threaded.io(), allocator, path, len);
                }
                return switch (self.runtime) {
                    .threaded => |*threaded| try readFileTrailerWithIo(threaded.io(), allocator, path, len),
                    .evented => |*evented| try readFileTrailerWithIo(evented.io(), allocator, path, len),
                };
            }

            fn writeFileAbsolute(ptr: *anyopaque, path: []const u8, contents: []const u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                self.state.invalidatePath(path);
                defer self.state.invalidatePath(path);
                switch (self.runtime) {
                    .threaded => |*threaded| try writeFileAbsoluteWithIo(threaded.io(), path, contents),
                    .evented => |*evented| try writeFileAbsoluteWithIo(evented.io(), path, contents),
                }
            }

            fn appendFileAbsolute(ptr: *anyopaque, path: []const u8, contents: []const u8, sync: bool) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                self.state.invalidatePath(path);
                defer self.state.invalidatePath(path);
                switch (self.runtime) {
                    .threaded => |*threaded| try appendFileAbsoluteWithIo(threaded.io(), path, contents, sync),
                    .evented => |*evented| try appendFileAbsoluteWithIo(evented.io(), path, contents, sync),
                }
            }

            fn beginAtomicWrite(ptr: *anyopaque, allocator: Allocator, path: []const u8) !AtomicWriteSink {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                return try NativeAtomicWriteSink.create(allocator, path, self.state);
            }

            fn syncFileContentsAbsolute(ptr: *anyopaque, path: []const u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                switch (self.runtime) {
                    .threaded => |*threaded| try syncFileContentsPathWithIo(threaded.io(), path),
                    .evented => |*evented| try syncFileContentsPathWithIo(evented.io(), path),
                }
            }

            fn syncParentAbsolute(ptr: *anyopaque, path: []const u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                switch (self.runtime) {
                    .threaded => |*threaded| try syncParentPathWithIo(threaded.io(), path),
                    .evented => |*evented| try syncParentPathWithIo(evented.io(), path),
                }
            }

            fn renameAbsolute(ptr: *anyopaque, old_path: []const u8, new_path: []const u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                self.state.invalidateRename(old_path, new_path);
                defer self.state.invalidateRename(old_path, new_path);
                switch (self.runtime) {
                    .threaded => |*threaded| try renamePathWithIo(threaded.io(), old_path, new_path),
                    .evented => |*evented| try renamePathWithIo(evented.io(), old_path, new_path),
                }
            }

            fn deleteFileAbsolute(ptr: *anyopaque, path: []const u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                self.state.invalidatePath(path);
                defer self.state.invalidatePath(path);
                switch (self.runtime) {
                    .threaded => |*threaded| try deleteFilePathWithIo(threaded.io(), path),
                    .evented => |*evented| try deleteFilePathWithIo(evented.io(), path),
                }
            }

            fn deleteTree(ptr: *anyopaque, path: []const u8) !void {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                self.state.invalidateTree(path);
                defer self.state.invalidateTree(path);
                switch (self.runtime) {
                    .threaded => |*threaded| try std.Io.Dir.cwd().deleteTree(threaded.io(), path),
                    .evented => |*evented| try std.Io.Dir.cwd().deleteTree(evented.io(), path),
                }
            }

            fn nowNs(ptr: *anyopaque) u64 {
                const self: *NativeStorage = @ptrCast(@alignCast(ptr));
                return switch (self.runtime) {
                    .threaded => |*threaded| blk2: {
                        const now = std.Io.Timestamp.now(threaded.io(), .awake);
                        break :blk2 @intCast(now.toNanoseconds());
                    },
                    .evented => |*evented| blk2: {
                        const now = std.Io.Timestamp.now(evented.io(), .awake);
                        break :blk2 @intCast(now.toNanoseconds());
                    },
                };
            }
        };
    }

    break :blk struct {
        state: *NativeStorageState,

        pub const Lease = struct {
            state: *NativeStorageState,
            active: bool = true,

            /// Returns a borrowed view whose lifetime is bounded by this lease.
            pub fn storage(self: *Lease) Storage {
                std.debug.assert(self.active);
                return .{
                    .ptr = self.state,
                    .vtable = &threaded_only_vtable,
                };
            }

            pub fn deinit(self: *Lease) void {
                if (!self.active) return;
                self.active = false;
                self.state.release();
                self.* = undefined;
            }
        };

        const threaded_only_vtable: Storage.VTable = .{
            .create_dir_path = createDirPath,
            .read_file_alloc = readFileAlloc,
            .read_file_range_alloc = readFileRangeAlloc,
            .begin_read_file_range_alloc_with_runtime = beginReadFileRangeAllocWithRuntime,
            .read_file_range_into = readFileRangeInto,
            .read_file_range_at_most_into = readFileRangeAtMostInto,
            .file_size = fileSize,
            .read_file_trailer_alloc = readFileTrailerAlloc,
            .write_file_absolute = writeFileAbsolute,
            .append_file_absolute = appendFileAbsolute,
            .begin_atomic_write = beginAtomicWrite,
            .sync_contents_absolute = syncFileContentsAbsolute,
            .sync_parent_absolute = syncParentAbsolute,
            .rename_absolute = renameAbsolute,
            .delete_file_absolute = deleteFileAbsolute,
            .delete_tree = deleteTree,
            .now_ns = nowNs,
            .root_identity_alloc = rootIdentityAlloc,
            .rename_is_atomic = true,
            .supports_host_path_generation_publication = true,
            .supports_native_path_locks = true,
        };

        pub fn init(allocator: Allocator, kind: RuntimeKind) !NativeStorage {
            return try initWithPool(allocator, kind, null);
        }

        pub fn initWithPool(allocator: Allocator, kind: RuntimeKind, pool: ?*NativeStoragePool) !NativeStorage {
            return .{ .state = try NativeStorageState.create(allocator, kind, pool) };
        }

        pub fn deinit(self: *NativeStorage) void {
            self.state.closeStorageRef();
            self.* = undefined;
        }

        pub fn snapshotStats(self: *const NativeStorage) NativeStorageStats {
            return self.state.fdCacheConst().snapshotStats();
        }

        pub fn acquireFdPermit(self: *NativeStorage) !NativeFdPermit {
            return try self.state.acquireFdPermit();
        }

        pub fn acquireFdPermits(self: *NativeStorage, count: usize) !NativeFdPermit {
            if (count == 0) return error.InvalidNativeFdPermit;
            return try self.state.acquireFdPermits(count);
        }

        /// Keeps the native state and its threaded I/O runtime alive after the
        /// owning NativeStorage begins shutdown. No new lease may be acquired
        /// once shutdown has started.
        pub fn acquireLease(self: *NativeStorage) !Lease {
            return .{ .state = try self.state.acquireLease() };
        }

        pub fn beginAtomicWriteWithPermit(
            self: *NativeStorage,
            allocator: Allocator,
            path: []const u8,
            permit: *NativeFdPermit,
        ) !AtomicWriteSink {
            if (!permit.active or permit.count != 1 or permit.state != self.state) return error.InvalidNativeFdPermit;
            return try NativeAtomicWriteSink.createWithPermit(allocator, path, self.state, permit);
        }

        pub fn validateFdPermit(self: *NativeStorage, permit: *const NativeFdPermit) !void {
            if (!permit.active or permit.count != 1 or permit.state != self.state) return error.InvalidNativeFdPermit;
        }

        pub fn storage(self: *NativeStorage) Storage {
            // The returned view borrows self.state; retained child operations
            // keep the state alive independently until their own deinit.
            return .{
                .ptr = self.state,
                .vtable = &threaded_only_vtable,
            };
        }

        fn createDirPath(ptr: *anyopaque, path: []const u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            // Recursive directory creation opens the next directory before
            // closing its parent.
            var permit = try state.acquireFdPermits(2);
            defer permit.release();
            try createDirPathPortable(permit.io, path);
        }

        fn readFileAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, max_bytes: usize) ![]u8 {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            var permit = try state.acquireFdPermit();
            defer permit.release();
            return try std.Io.Dir.cwd().readFileAlloc(permit.io, path, allocator, .limited(max_bytes));
        }

        fn readFileRangeAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, offset: u64, len: usize) ![]u8 {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = try state.retain();
            defer retained.release();
            if (comptime supports_posix_fd_cache) {
                return try retained.fdCache().readRangeAlloc(retained.cache_namespace, retained.threaded.io(), allocator, path, offset, len);
            }
            return try readFileRangeWithIo(retained.threaded.io(), allocator, path, offset, len);
        }

        fn beginReadFileRangeAllocWithRuntime(ptr: *anyopaque, read_runtime: ?ReadRuntime, allocator: Allocator, path: []const u8, offset: u64, len: usize) !RangeReadFuture {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const runtime = read_runtime orelse {
                const sync_storage: Storage = .{ .ptr = ptr, .vtable = &threaded_only_vtable };
                return try CompletedRangeReadFuture.create(sync_storage, allocator, path, offset, len);
            };
            return try NativeRangeReadFuture.create(runtime, state, allocator, path, offset, len);
        }

        fn readFileRangeInto(ptr: *anyopaque, path: []const u8, offset: u64, out: []u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = try state.retain();
            defer retained.release();
            if (comptime supports_posix_fd_cache) {
                return try retained.fdCache().readRangeInto(retained.cache_namespace, retained.threaded.io(), path, offset, out);
            }
            return try readFileRangeWithIoInto(retained.threaded.io(), path, offset, out);
        }

        fn readFileRangeAtMostInto(ptr: *anyopaque, path: []const u8, offset: u64, out: []u8) !usize {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = try state.retain();
            defer retained.release();
            if (comptime supports_posix_fd_cache) {
                return try retained.fdCache().readRangeAtMostInto(retained.cache_namespace, retained.threaded.io(), path, offset, out);
            }
            return try readFileRangeWithIoAtMostInto(retained.threaded.io(), path, offset, out);
        }

        fn fileSize(ptr: *anyopaque, path: []const u8) !u64 {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = try state.retain();
            defer retained.release();
            if (comptime supports_posix_fd_cache) {
                return try retained.fdCache().fileSize(retained.cache_namespace, retained.threaded.io(), path);
            }
            return try fileSizeWithIo(retained.threaded.io(), path);
        }

        fn readFileTrailerAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, len: usize) !FileTrailer {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = try state.retain();
            defer retained.release();
            if (comptime supports_posix_fd_cache) {
                return try retained.fdCache().readTrailerAlloc(retained.cache_namespace, retained.threaded.io(), allocator, path, len);
            }
            return try readFileTrailerWithIo(retained.threaded.io(), allocator, path, len);
        }

        fn writeFileAbsolute(ptr: *anyopaque, path: []const u8, contents: []const u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            var permit = try state.acquireFdPermits(createPathDescriptorCount(path));
            defer permit.release();
            permit.state.invalidatePath(path);
            defer permit.state.invalidatePath(path);
            try writeFileAbsoluteWithIo(permit.io, path, contents);
        }

        fn appendFileAbsolute(ptr: *anyopaque, path: []const u8, contents: []const u8, sync: bool) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            var permit = try state.acquireFdPermits(createPathDescriptorCount(path));
            defer permit.release();
            permit.state.invalidatePath(path);
            defer permit.state.invalidatePath(path);
            try appendFileAbsoluteWithIo(permit.io, path, contents, sync);
        }

        fn beginAtomicWrite(ptr: *anyopaque, allocator: Allocator, path: []const u8) !AtomicWriteSink {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            return try NativeAtomicWriteSink.create(allocator, path, state);
        }

        fn syncFileContentsAbsolute(ptr: *anyopaque, path: []const u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            var permit = try state.acquireFdPermit();
            defer permit.release();
            try syncFileContentsPathWithIo(permit.io, path);
        }

        fn syncParentAbsolute(ptr: *anyopaque, path: []const u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            var permit = try state.acquireFdPermit();
            defer permit.release();
            try syncParentPathWithIo(permit.io, path);
        }

        fn renameAbsolute(ptr: *anyopaque, old_path: []const u8, new_path: []const u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = try state.retain();
            defer retained.release();
            retained.invalidateRename(old_path, new_path);
            defer retained.invalidateRename(old_path, new_path);
            try renamePathWithIo(retained.threaded.io(), old_path, new_path);
        }

        fn deleteFileAbsolute(ptr: *anyopaque, path: []const u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = try state.retain();
            defer retained.release();
            retained.invalidatePath(path);
            defer retained.invalidatePath(path);
            if (comptime supports_posix_fd_cache) {
                // unlink(2) does not open a descriptor. Keeping rollback
                // deletion outside admission guarantees an unpublished file
                // can be removed even while the FD budget is saturated.
                try deleteFilePathPosix(path);
            } else {
                try deleteFilePathWithIo(retained.threaded.io(), path);
            }
        }

        fn deleteTree(ptr: *anyopaque, path: []const u8) !void {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            // The O(1)-memory traversal holds at most the active directory,
            // its parent, and the next directory while descending.
            var permit = try state.acquireFdPermits(3);
            defer permit.release();
            permit.state.invalidateTree(path);
            defer permit.state.invalidateTree(path);
            try std.Io.Dir.cwd().deleteTreeMinStackSize(permit.io, path);
        }

        fn rootIdentityAlloc(ptr: *anyopaque, allocator: Allocator, root_dir: []const u8) ![]u8 {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            var permit = try state.acquireFdPermit();
            defer permit.release();
            return try nativeRootIdentityAlloc(ptr, allocator, root_dir);
        }

        fn nowNs(ptr: *anyopaque) u64 {
            const state: *NativeStorageState = @ptrCast(@alignCast(ptr));
            const retained = state.retain() catch return 0;
            defer retained.release();
            const now = std.Io.Timestamp.now(retained.threaded.io(), .awake);
            return @intCast(now.toNanoseconds());
        }
    };
};

fn writeFileAbsoluteWithIo(io: anytype, path: []const u8, contents: []const u8) !void {
    var file = openFilePathForWriteWithIo(io, path, .{ .truncate = true }) catch |err| {
        std.log.err("lsm writeFileAbsolute create failed path={s} bytes={} err={s}", .{ path, contents.len, @errorName(err) });
        return err;
    };
    defer file.close(io);

    var file_buf: [4096]u8 = undefined;
    var writer = file.writer(io, &file_buf);
    writer.interface.writeAll(contents) catch |err| {
        const actual = if (err == error.WriteFailed) writer.err orelse err else err;
        std.log.err("lsm writeFileAbsolute write failed path={s} bytes={} err={s}", .{ path, contents.len, @errorName(actual) });
        return actual;
    };
    writer.end() catch |err| {
        const actual = if (err == error.WriteFailed) writer.err orelse err else err;
        std.log.err("lsm writeFileAbsolute finish failed path={s} bytes={} err={s}", .{ path, contents.len, @errorName(actual) });
        return actual;
    };
}

fn appendFileAbsoluteWithIo(io: anytype, path: []const u8, contents: []const u8, sync: bool) !void {
    var file = openFilePathForWriteWithIo(io, path, .{ .truncate = false }) catch |err| {
        std.log.err("lsm appendFileAbsolute create failed path={s} bytes={} err={s}", .{ path, contents.len, @errorName(err) });
        return err;
    };
    defer file.close(io);

    const size = (try file.stat(io)).size;
    var file_buf: [4096]u8 = undefined;
    var writer = file.writer(io, &file_buf);
    writer.seekTo(size) catch |err| {
        const actual = if (err == error.WriteFailed) writer.err orelse err else err;
        std.log.err("lsm appendFileAbsolute seek failed path={s} offset={} err={s}", .{ path, size, @errorName(actual) });
        return actual;
    };
    writer.interface.writeAll(contents) catch |err| {
        const actual = if (err == error.WriteFailed) writer.err orelse err else err;
        std.log.err("lsm appendFileAbsolute write failed path={s} bytes={} err={s}", .{ path, contents.len, @errorName(actual) });
        return actual;
    };
    writer.end() catch |err| {
        const actual = if (err == error.WriteFailed) writer.err orelse err else err;
        std.log.err("lsm appendFileAbsolute finish failed path={s} bytes={} err={s}", .{ path, contents.len, @errorName(actual) });
        return actual;
    };
    if (sync) file.sync(io) catch |err| {
        std.log.err("lsm appendFileAbsolute sync failed path={s} bytes={} err={s}", .{ path, contents.len, @errorName(err) });
        return err;
    };
}

fn syncFileContentsPathWithIo(io: anytype, path: []const u8) !void {
    try fs_paths.syncFilePortable(io, path);
}

fn syncParentPathWithIo(io: anytype, path: []const u8) !void {
    const parent = std.fs.path.dirname(path) orelse if (std.fs.path.isAbsolute(path)) "/" else ".";
    try fs_paths.syncDirPortable(io, parent);
}

fn openFilePathForWriteWithIo(io: anytype, path: []const u8, flags: std.Io.Dir.CreateFileOptions) !std.Io.File {
    return try fs_paths.createFilePortable(io, path, flags);
}

fn readFileRangeWithIo(io: anytype, allocator: Allocator, path: []const u8, offset: u64, len: usize) ![]u8 {
    const file = if (std.fs.path.isAbsolute(path))
        try std.Io.Dir.openFileAbsolute(io, path, .{})
    else
        try std.Io.Dir.cwd().openFile(io, path, .{});
    defer file.close(io);

    var reader = file.reader(io, &.{});
    try reader.seekTo(offset);
    const out = try allocator.alloc(u8, len);
    errdefer allocator.free(out);
    try reader.interface.readSliceAll(out);
    return out;
}

fn readFileRangeWithIoInto(io: anytype, path: []const u8, offset: u64, out: []u8) !void {
    const file = if (std.fs.path.isAbsolute(path))
        try std.Io.Dir.openFileAbsolute(io, path, .{})
    else
        try std.Io.Dir.cwd().openFile(io, path, .{});
    defer file.close(io);

    var reader = file.reader(io, &.{});
    try reader.seekTo(offset);
    try reader.interface.readSliceAll(out);
}

fn readFileRangeWithIoAtMostInto(io: anytype, path: []const u8, offset: u64, out: []u8) !usize {
    const file = if (std.fs.path.isAbsolute(path))
        try std.Io.Dir.openFileAbsolute(io, path, .{})
    else
        try std.Io.Dir.cwd().openFile(io, path, .{});
    defer file.close(io);

    var reader = file.reader(io, &.{});
    try reader.seekTo(offset);
    return try reader.interface.readSliceShort(out);
}

fn fileSizeWithIo(io: anytype, path: []const u8) !u64 {
    const file = if (std.fs.path.isAbsolute(path))
        try std.Io.Dir.openFileAbsolute(io, path, .{})
    else
        try std.Io.Dir.cwd().openFile(io, path, .{});
    defer file.close(io);
    return (try file.stat(io)).size;
}

fn readFileTrailerWithIo(io: anytype, allocator: Allocator, path: []const u8, len: usize) !FileTrailer {
    const file = if (std.fs.path.isAbsolute(path))
        try std.Io.Dir.openFileAbsolute(io, path, .{})
    else
        try std.Io.Dir.cwd().openFile(io, path, .{});
    defer file.close(io);

    const size = (try file.stat(io)).size;
    if (size < len) return error.EndOfStream;

    var reader = file.reader(io, &.{});
    try reader.seekTo(size - len);
    const out = try allocator.alloc(u8, len);
    errdefer allocator.free(out);
    try reader.interface.readSliceAll(out);
    return .{ .bytes = out, .file_size = size };
}

fn renamePathWithIo(io: anytype, old_path: []const u8, new_path: []const u8) !void {
    if (std.fs.path.isAbsolute(old_path) and std.fs.path.isAbsolute(new_path)) {
        try renameAbsoluteWithIo(io, old_path, new_path);
        return;
    }
    try std.Io.Dir.rename(std.Io.Dir.cwd(), old_path, std.Io.Dir.cwd(), new_path, io);
}

fn renameAbsoluteWithIo(io: anytype, old_path: []const u8, new_path: []const u8) !void {
    if (builtin.os.tag != .windows and builtin.os.tag != .wasi and builtin.os.tag != .freestanding) {
        // The direct syscall is atomic and does not need two temporary parent
        // directory descriptors, so it cannot oversubscribe FD admission.
        return try renameAbsoluteDirectPosix(old_path, new_path);
    }

    const old_parent_path = std.fs.path.dirname(old_path) orelse return error.FileNotFound;
    const new_parent_path = std.fs.path.dirname(new_path) orelse return error.FileNotFound;
    const old_base_name = std.fs.path.basename(old_path);
    const new_base_name = std.fs.path.basename(new_path);

    var old_parent = try std.Io.Dir.openDirAbsolute(io, old_parent_path, .{});
    defer old_parent.close(io);
    var new_parent = try std.Io.Dir.openDirAbsolute(io, new_parent_path, .{});
    defer new_parent.close(io);

    try std.Io.Dir.rename(old_parent, old_base_name, new_parent, new_base_name, io);
}

fn renameAbsolutePosix(old_path: []const u8, new_path: []const u8) !void {
    const old_parent_path = std.fs.path.dirname(old_path) orelse return error.FileNotFound;
    const new_parent_path = std.fs.path.dirname(new_path) orelse return error.FileNotFound;
    const old_base_name = std.fs.path.basename(old_path);
    const new_base_name = std.fs.path.basename(new_path);

    const old_parent_fd = try std.posix.openat(std.posix.AT.FDCWD, old_parent_path, .{
        .ACCMODE = .RDONLY,
        .DIRECTORY = true,
        .CLOEXEC = true,
    }, 0);
    defer closeFd(old_parent_fd);
    const new_parent_fd = try std.posix.openat(std.posix.AT.FDCWD, new_parent_path, .{
        .ACCMODE = .RDONLY,
        .DIRECTORY = true,
        .CLOEXEC = true,
    }, 0);
    defer closeFd(new_parent_fd);

    const allocator = std.heap.page_allocator;
    const old_base_name_z = try allocator.dupeZ(u8, old_base_name);
    defer allocator.free(old_base_name_z);
    const new_base_name_z = try allocator.dupeZ(u8, new_base_name);
    defer allocator.free(new_base_name_z);

    while (true) {
        const rc = std.posix.system.renameat(old_parent_fd, old_base_name_z, new_parent_fd, new_base_name_z);
        switch (std.posix.errno(rc)) {
            .SUCCESS => return,
            .INTR => continue,
            .ACCES, .PERM, .ROFS => return error.AccessDenied,
            .BUSY => return error.FileBusy,
            .IO => return error.InputOutput,
            .ISDIR => return error.IsDir,
            .LOOP => return error.SymLinkLoop,
            .MLINK => return error.LinkQuotaExceeded,
            .NAMETOOLONG => return error.NameTooLong,
            .NOENT => return error.FileNotFound,
            .NOTDIR => return error.NotDir,
            .NOMEM => return error.SystemResources,
            .NOSPC => return error.NoSpaceLeft,
            .NOTEMPTY, .EXIST => return error.PathAlreadyExists,
            .XDEV => return error.RenameAcrossMountPoints,
            .INVAL => return renameAbsoluteDirectPosix(old_path, new_path),
            else => |err| return std.posix.unexpectedErrno(err),
        }
    }
}

fn renameAbsoluteDirectPosix(old_path: []const u8, new_path: []const u8) !void {
    const allocator = std.heap.page_allocator;
    const old_path_z = try allocator.dupeZ(u8, old_path);
    defer allocator.free(old_path_z);
    const new_path_z = try allocator.dupeZ(u8, new_path);
    defer allocator.free(new_path_z);

    while (true) {
        const rc = std.posix.system.rename(old_path_z, new_path_z);
        switch (std.posix.errno(rc)) {
            .SUCCESS => return,
            .INTR => continue,
            .ACCES, .PERM, .ROFS => return error.AccessDenied,
            .BUSY => return error.FileBusy,
            .IO => return error.InputOutput,
            .ISDIR => return error.IsDir,
            .LOOP => return error.SymLinkLoop,
            .MLINK => return error.LinkQuotaExceeded,
            .NAMETOOLONG => return error.NameTooLong,
            .NOENT => return error.FileNotFound,
            .NOTDIR => return error.NotDir,
            .NOMEM => return error.SystemResources,
            .NOSPC => return error.NoSpaceLeft,
            .NOTEMPTY, .EXIST => return error.PathAlreadyExists,
            .XDEV => return error.RenameAcrossMountPoints,
            .INVAL => {
                std.log.err("lsm rename invalid old={s} new={s}", .{ old_path, new_path });
                return error.InvalidArgument;
            },
            else => |err| return std.posix.unexpectedErrno(err),
        }
    }
}

fn deleteFilePathWithIo(io: anytype, path: []const u8) !void {
    if (!std.fs.path.isAbsolute(path)) {
        try std.Io.Dir.cwd().deleteFile(io, path);
        return;
    }

    const parent_path = std.fs.path.dirname(path) orelse return error.FileNotFound;
    const base_name = std.fs.path.basename(path);
    var parent = try std.Io.Dir.openDirAbsolute(io, parent_path, .{});
    defer parent.close(io);
    try parent.deleteFile(io, base_name);
}

fn createAtomicWriteFdPosix(path: []const u8) !std.posix.fd_t {
    return try std.posix.openat(std.posix.AT.FDCWD, path, .{
        .ACCMODE = .RDWR,
        .CREAT = true,
        .CLOEXEC = true,
        .TRUNC = true,
    }, std.Io.File.Permissions.default_file.toMode());
}

fn deleteFilePathPosix(path: []const u8) !void {
    const allocator = std.heap.page_allocator;
    const path_z = try allocator.dupeZ(u8, path);
    defer allocator.free(path_z);

    while (true) {
        const rc = std.posix.system.unlink(path_z);
        switch (std.posix.errno(rc)) {
            .SUCCESS => return,
            .INTR => continue,
            .ACCES, .PERM, .ROFS => return error.AccessDenied,
            .BUSY => return error.FileBusy,
            .IO => return error.InputOutput,
            .ISDIR => return error.IsDir,
            .LOOP => return error.SymLinkLoop,
            .NAMETOOLONG => return error.NameTooLong,
            .NOENT => return error.FileNotFound,
            .NOTDIR => return error.NotDir,
            else => |err| return std.posix.unexpectedErrno(err),
        }
    }
}

fn readAllAtOffset(fd: std.posix.fd_t, bytes: []u8, offset: u64) !void {
    var read_len: usize = 0;
    while (read_len < bytes.len) {
        const chunk_len = @min(max_posix_io_chunk, bytes.len - read_len);
        const rc = std.posix.system.pread(fd, bytes.ptr + read_len, chunk_len, @intCast(offset + read_len));
        switch (std.posix.errno(rc)) {
            .SUCCESS => {
                const n: usize = @intCast(rc);
                if (n == 0) return error.EndOfStream;
                read_len += n;
            },
            .INTR => continue,
            else => |err| return posixReadError(err),
        }
    }
}

fn readAtMostAtOffset(fd: std.posix.fd_t, bytes: []u8, offset: u64) !usize {
    var read_len: usize = 0;
    while (read_len < bytes.len) {
        const chunk_len = @min(max_posix_io_chunk, bytes.len - read_len);
        const rc = std.posix.system.pread(fd, bytes.ptr + read_len, chunk_len, @intCast(offset + read_len));
        switch (std.posix.errno(rc)) {
            .SUCCESS => {
                const n: usize = @intCast(rc);
                if (n == 0) return read_len;
                read_len += n;
            },
            .INTR => continue,
            else => |err| return posixReadError(err),
        }
    }
    return read_len;
}

fn writeAllAtOffset(fd: std.posix.fd_t, bytes: []const u8, offset: u64) !void {
    var written: usize = 0;
    while (written < bytes.len) {
        const chunk_len = @min(max_posix_io_chunk, bytes.len - written);
        const rc = std.posix.system.pwrite(fd, bytes.ptr + written, chunk_len, @intCast(offset + written));
        switch (std.posix.errno(rc)) {
            .SUCCESS => {
                const n: usize = @intCast(rc);
                if (n == 0) return error.WriteZero;
                written += n;
            },
            .INTR => continue,
            else => |err| return posixWriteError(err),
        }
    }
}

fn closeFd(fd: std.posix.fd_t) void {
    _ = std.posix.system.close(fd);
}

fn fileSizeFromFd(fd: std.posix.fd_t) !u64 {
    if (builtin.os.tag == .linux) {
        const linux = std.os.linux;
        const empty_path: [*:0]const u8 = "";
        while (true) {
            var statx = std.mem.zeroes(linux.Statx);
            switch (linux.errno(linux.statx(fd, empty_path, linux.AT.EMPTY_PATH, .{ .SIZE = true }, &statx))) {
                .SUCCESS => {
                    if (!statx.mask.SIZE) return error.Unexpected;
                    return statx.size;
                },
                .INTR => continue,
                else => |err| return posixStatError(err),
            }
        }
    } else {
        var stat: std.posix.Stat = undefined;
        while (true) {
            const rc = std.posix.system.fstat(fd, &stat);
            switch (std.posix.errno(rc)) {
                .SUCCESS => return @intCast(stat.size),
                .INTR => continue,
                else => |err| return posixStatError(err),
            }
        }
    }
}

fn seekFd(fd: std.posix.fd_t, offset: i64, whence: usize) !u64 {
    while (true) {
        const rc = std.posix.system.lseek(fd, offset, @intCast(whence));
        switch (std.posix.errno(rc)) {
            .SUCCESS => return @intCast(rc),
            .INTR => continue,
            else => |err| return posixStatError(err),
        }
    }
}

fn posixReadError(err: std.posix.E) anyerror {
    return switch (err) {
        .AGAIN => error.WouldBlock,
        .BADF => error.InvalidFileDescriptor,
        .FAULT => error.InvalidAddress,
        .INVAL => error.InvalidArgument,
        .IO => error.InputOutput,
        .NXIO => error.NoDevice,
        .OVERFLOW => error.FileTooBig,
        else => std.posix.unexpectedErrno(err),
    };
}

fn posixWriteError(err: std.posix.E) anyerror {
    return switch (err) {
        .ACCES, .PERM, .ROFS => error.AccessDenied,
        .AGAIN => error.WouldBlock,
        .BADF => error.InvalidFileDescriptor,
        .DQUOT => error.DiskQuota,
        .FBIG, .OVERFLOW => error.FileTooBig,
        .FAULT => error.InvalidAddress,
        .INVAL => error.InvalidArgument,
        .IO => error.InputOutput,
        .NOSPC => error.NoSpaceLeft,
        .NXIO => error.NoDevice,
        .PIPE => error.BrokenPipe,
        else => std.posix.unexpectedErrno(err),
    };
}

fn posixStatError(err: std.posix.E) anyerror {
    return switch (err) {
        .ACCES, .PERM => error.AccessDenied,
        .BADF => error.InvalidFileDescriptor,
        .FAULT => error.InvalidAddress,
        .IO => error.InputOutput,
        .LOOP => error.SymLinkLoop,
        .NAMETOOLONG => error.NameTooLong,
        .NOENT => error.FileNotFound,
        .NOMEM => error.SystemResources,
        .NOTDIR => error.NotDir,
        .OVERFLOW => error.FileTooBig,
        else => std.posix.unexpectedErrno(err),
    };
}

const NativeBufferedAtomicWriteSink = struct {
    allocator: Allocator,
    state: *NativeStorageState,
    final_path: []u8,
    tmp_path: []u8,
    out: std.ArrayListUnmanaged(u8) = .empty,

    fn create(allocator: Allocator, path: []const u8, state: *NativeStorageState) !AtomicWriteSink {
        const retained_state = try state.retain();
        errdefer retained_state.release();

        const self = try allocator.create(NativeBufferedAtomicWriteSink);
        errdefer allocator.destroy(self);

        const final_path = try allocator.dupe(u8, path);
        errdefer allocator.free(final_path);

        const tmp_path = try tempSiblingPath(allocator, path);
        errdefer allocator.free(tmp_path);

        self.* = .{
            .allocator = allocator,
            .state = retained_state,
            .final_path = final_path,
            .tmp_path = tmp_path,
        };
        return .{
            .ptr = self,
            .vtable = &native_buffered_atomic_write_sink_vtable,
        };
    }

    fn deinit(self: *NativeBufferedAtomicWriteSink) void {
        self.state.release();
        self.out.deinit(self.allocator);
        self.allocator.free(self.final_path);
        self.allocator.free(self.tmp_path);
        self.allocator.destroy(self);
    }

    fn len(ptr: *anyopaque) usize {
        const self: *NativeBufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        return self.out.items.len;
    }

    fn appendSlice(ptr: *anyopaque, bytes: []const u8) !void {
        const self: *NativeBufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        try byte_copy.appendSlicePossiblyAliased(&self.out, self.allocator, bytes);
    }

    fn writeAt(ptr: *anyopaque, offset: usize, bytes: []const u8) !void {
        const self: *NativeBufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (offset > self.out.items.len or bytes.len > self.out.items.len - offset) return error.InvalidAtomicWriteOffset;
        byte_copy.copyPossiblyAliased(self.out.items[offset..][0..bytes.len], bytes);
    }

    fn crc32Prefix(ptr: *anyopaque, len_prefix: usize) !u32 {
        const self: *NativeBufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (len_prefix > self.out.items.len) return error.InvalidAtomicWriteOffset;
        return std.hash.Crc32.hash(self.out.items[0..len_prefix]);
    }

    fn crc32Range(ptr: *anyopaque, offset: usize, range_len: usize) !u32 {
        const self: *NativeBufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (offset > self.out.items.len or range_len > self.out.items.len - offset) return error.InvalidAtomicWriteOffset;
        return std.hash.Crc32.hash(self.out.items[offset..][0..range_len]);
    }

    fn finish(ptr: *anyopaque) !void {
        const self: *NativeBufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        defer self.deinit();

        const io = self.state.threaded.io();
        self.state.invalidatePath(self.tmp_path);
        writeFileAbsoluteWithIo(io, self.tmp_path, self.out.items) catch |err| {
            deleteFilePathWithIo(io, self.tmp_path) catch {};
            self.state.invalidatePath(self.tmp_path);
            return err;
        };
        syncFileContentsPathWithIo(io, self.tmp_path) catch |err| {
            deleteFilePathWithIo(io, self.tmp_path) catch {};
            self.state.invalidatePath(self.tmp_path);
            return err;
        };
        self.state.invalidateRename(self.tmp_path, self.final_path);
        defer self.state.invalidateRename(self.tmp_path, self.final_path);
        renamePathWithIo(io, self.tmp_path, self.final_path) catch |err| {
            deleteFilePathWithIo(io, self.tmp_path) catch {};
            return err;
        };
        try syncParentPathWithIo(io, self.final_path);
    }

    fn abort(ptr: *anyopaque) void {
        const self: *NativeBufferedAtomicWriteSink = @ptrCast(@alignCast(ptr));
        self.state.invalidatePath(self.tmp_path);
        deleteFilePathWithIo(self.state.threaded.io(), self.tmp_path) catch {};
        self.state.invalidatePath(self.tmp_path);
        self.deinit();
    }
};

const native_buffered_atomic_write_sink_vtable: AtomicWriteSink.VTable = .{
    .len = NativeBufferedAtomicWriteSink.len,
    .append_slice = NativeBufferedAtomicWriteSink.appendSlice,
    .write_at = NativeBufferedAtomicWriteSink.writeAt,
    .crc32_prefix = NativeBufferedAtomicWriteSink.crc32Prefix,
    .crc32_range = NativeBufferedAtomicWriteSink.crc32Range,
    .finish = NativeBufferedAtomicWriteSink.finish,
    .abort = NativeBufferedAtomicWriteSink.abort,
};

const NativeAtomicWriteSink = struct {
    allocator: Allocator,
    state: *NativeStorageState,
    fd_permit: NativeFdPermit,
    final_path: []u8,
    tmp_path: []u8,
    fd: std.posix.fd_t,
    bytes_written: usize = 0,

    fn create(allocator: Allocator, path: []const u8, state: *NativeStorageState) !AtomicWriteSink {
        if (builtin.os.tag == .windows or builtin.os.tag == .wasi or builtin.os.tag == .freestanding) {
            // std.Io rename does not expose a write-through durability contract
            // on these targets. Fail before creating the temporary file rather
            // than acknowledge an atomic write whose namespace may be lost.
            return error.DurableAtomicRenameUnsupported;
        }

        var fd_permit = try state.acquireFdPermit();
        errdefer fd_permit.release();
        return try createWithPermit(allocator, path, state, &fd_permit);
    }

    fn createWithPermit(
        allocator: Allocator,
        path: []const u8,
        state: *NativeStorageState,
        permit: *NativeFdPermit,
    ) !AtomicWriteSink {
        if (builtin.os.tag == .windows or builtin.os.tag == .wasi or builtin.os.tag == .freestanding) {
            return error.DurableAtomicRenameUnsupported;
        }
        if (!permit.active or permit.count != 1 or permit.state != state) return error.InvalidNativeFdPermit;

        const final_path = try allocator.dupe(u8, path);
        errdefer allocator.free(final_path);

        const tmp_path = try tempSiblingPath(allocator, path);
        errdefer allocator.free(tmp_path);

        const retained = try state.retain();
        errdefer retained.release();
        const fd = try createAtomicWriteFdPosix(tmp_path);
        errdefer closeFd(fd);

        const self = try allocator.create(NativeAtomicWriteSink);
        errdefer allocator.destroy(self);
        self.* = .{
            .allocator = allocator,
            .state = retained,
            .fd_permit = permit.take(),
            .final_path = final_path,
            .tmp_path = tmp_path,
            .fd = fd,
        };
        return .{
            .ptr = self,
            .vtable = &native_atomic_write_sink_vtable,
        };
    }

    fn deinit(self: *NativeAtomicWriteSink) void {
        if (self.fd >= 0) closeFd(self.fd);
        self.fd_permit.release();
        self.state.release();
        self.allocator.free(self.final_path);
        self.allocator.free(self.tmp_path);
        self.allocator.destroy(self);
    }

    fn len(ptr: *anyopaque) usize {
        const self: *NativeAtomicWriteSink = @ptrCast(@alignCast(ptr));
        return self.bytes_written;
    }

    fn appendSlice(ptr: *anyopaque, bytes: []const u8) !void {
        const self: *NativeAtomicWriteSink = @ptrCast(@alignCast(ptr));
        try writeAllAtOffset(self.fd, bytes, @intCast(self.bytes_written));
        self.bytes_written += bytes.len;
    }

    fn writeAt(ptr: *anyopaque, offset: usize, bytes: []const u8) !void {
        const self: *NativeAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (offset > self.bytes_written or bytes.len > self.bytes_written - offset) return error.InvalidAtomicWriteOffset;
        try writeAllAtOffset(self.fd, bytes, @intCast(offset));
    }

    fn crc32Prefix(ptr: *anyopaque, len_prefix: usize) !u32 {
        return crc32Range(ptr, 0, len_prefix);
    }

    fn crc32Range(ptr: *anyopaque, range_offset: usize, range_len: usize) !u32 {
        const self: *NativeAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (range_offset > self.bytes_written or range_len > self.bytes_written - range_offset) return error.InvalidAtomicWriteOffset;

        var crc = std.hash.Crc32.init();
        var offset: usize = 0;
        var buf: [64 * 1024]u8 = undefined;
        while (offset < range_len) {
            const n = @min(buf.len, range_len - offset);
            try readAllAtOffset(self.fd, buf[0..n], @intCast(range_offset + offset));
            crc.update(buf[0..n]);
            offset += n;
        }
        return crc.final();
    }

    fn finish(ptr: *anyopaque) !void {
        const self: *NativeAtomicWriteSink = @ptrCast(@alignCast(ptr));
        defer self.deinit();

        fs_paths.syncFileFdPortable(self.fd) catch |err| {
            closeFd(self.fd);
            self.fd = -1;
            self.state.invalidatePath(self.tmp_path);
            deleteFilePathPosix(self.tmp_path) catch {};
            self.state.invalidatePath(self.tmp_path);
            return err;
        };
        closeFd(self.fd);
        self.fd = -1;
        // The data-file descriptor is closed, so its existing admission slot
        // can be transferred directly to the parent-directory descriptor.
        // Do not relinquish and reacquire it here: another opener could claim
        // the slot and make finish fail after the rename has already published
        // the destination.

        self.state.invalidateRename(self.tmp_path, self.final_path);
        defer self.state.invalidateRename(self.tmp_path, self.final_path);
        renameAbsoluteDirectPosix(self.tmp_path, self.final_path) catch |err| {
            deleteFilePathPosix(self.tmp_path) catch {};
            return err;
        };
        try syncParentDirectoryPosix(self.final_path);
    }

    fn abort(ptr: *anyopaque) void {
        const self: *NativeAtomicWriteSink = @ptrCast(@alignCast(ptr));
        if (self.fd >= 0) {
            closeFd(self.fd);
            self.fd = -1;
        }
        self.state.invalidatePath(self.tmp_path);
        deleteFilePathPosix(self.tmp_path) catch {};
        self.state.invalidatePath(self.tmp_path);
        self.deinit();
    }
};

const native_atomic_write_sink_vtable: AtomicWriteSink.VTable = .{
    .len = NativeAtomicWriteSink.len,
    .append_slice = NativeAtomicWriteSink.appendSlice,
    .write_at = NativeAtomicWriteSink.writeAt,
    .crc32_prefix = NativeAtomicWriteSink.crc32Prefix,
    .crc32_range = NativeAtomicWriteSink.crc32Range,
    .finish = NativeAtomicWriteSink.finish,
    .abort = NativeAtomicWriteSink.abort,
};

fn syncParentDirectoryPosix(path: []const u8) !void {
    const parent_path = std.fs.path.dirname(path) orelse if (std.fs.path.isAbsolute(path)) "/" else ".";
    const parent_fd = try std.posix.openat(std.posix.AT.FDCWD, parent_path, .{
        .ACCMODE = .RDONLY,
        .DIRECTORY = true,
        .CLOEXEC = true,
    }, 0);
    defer closeFd(parent_fd);
    try fs_paths.syncDirectoryFdPortable(parent_fd);
}

pub const MemoryStorage = struct {
    allocator: Allocator,
    mutex: std.atomic.Mutex = .unlocked,
    files: std.StringHashMapUnmanaged([]u8) = .empty,
    tick: u64 = 1,

    pub fn init(allocator: Allocator) MemoryStorage {
        return .{
            .allocator = allocator,
        };
    }

    pub fn deinit(self: *MemoryStorage) void {
        var it = self.files.iterator();
        while (it.next()) |entry| {
            self.allocator.free(entry.key_ptr.*);
            self.allocator.free(entry.value_ptr.*);
        }
        self.files.deinit(self.allocator);
        self.* = undefined;
    }

    pub fn storage(self: *MemoryStorage) Storage {
        return .{
            .ptr = self,
            .vtable = &memory_vtable,
        };
    }
};

fn lockAtomic(mutex: *std.atomic.Mutex) bool {
    if (builtin.os.tag == .freestanding) return false;
    platform_sync.lockYielding(mutex);
    return true;
}

const memory_vtable: Storage.VTable = .{
    .create_dir_path = memoryCreateDirPath,
    .read_file_alloc = memoryReadFileAlloc,
    .read_file_range_alloc = memoryReadFileRangeAlloc,
    .file_size = memoryFileSize,
    .read_file_trailer_alloc = memoryReadFileTrailerAlloc,
    .write_file_absolute = memoryWriteFileAbsolute,
    .append_file_absolute = memoryAppendFileAbsolute,
    .sync_contents_absolute = memorySyncFileContentsAbsolute,
    .sync_parent_absolute = memorySyncParentAbsolute,
    .rename_absolute = memoryRenameAbsolute,
    .delete_file_absolute = memoryDeleteFileAbsolute,
    .delete_tree = memoryDeleteTree,
    .now_ns = memoryNowNs,
    .rename_is_atomic = true,
};

test "native path locking does not imply host generation publication" {
    var lock_only_vtable = memory_vtable;
    lock_only_vtable.supports_native_path_locks = true;
    var memory = MemoryStorage.init(std.testing.allocator);
    defer memory.deinit();
    const lock_only: Storage = .{ .ptr = &memory, .vtable = &lock_only_vtable };

    try std.testing.expect(lock_only.supportsNativePathLocks());
    try std.testing.expect(!lock_only.supportsHostPathGenerationPublication());
}

fn memoryCreateDirPath(_: *anyopaque, _: []const u8) !void {}

fn memoryReadFileAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, max_bytes: usize) ![]u8 {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    const stored = self.files.get(path) orelse return error.FileNotFound;
    if (stored.len > max_bytes) return error.FileTooBig;
    return try allocator.dupe(u8, stored);
}

fn memoryReadFileRangeAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, offset: u64, len: usize) ![]u8 {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    const stored = self.files.get(path) orelse return error.FileNotFound;
    const start: usize = @intCast(offset);
    if (start > stored.len or stored.len - start < len) return error.EndOfStream;
    return try allocator.dupe(u8, stored[start .. start + len]);
}

fn memoryFileSize(ptr: *anyopaque, path: []const u8) !u64 {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    const stored = self.files.get(path) orelse return error.FileNotFound;
    return stored.len;
}

fn memoryReadFileTrailerAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, len: usize) !FileTrailer {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    const stored = self.files.get(path) orelse return error.FileNotFound;
    if (stored.len < len) return error.EndOfStream;
    return .{
        .bytes = try allocator.dupe(u8, stored[stored.len - len ..]),
        .file_size = stored.len,
    };
}

fn memoryWriteFileAbsolute(ptr: *anyopaque, path: []const u8, contents: []const u8) !void {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    const owned_path = try self.allocator.dupe(u8, path);
    errdefer self.allocator.free(owned_path);
    const owned_contents = try self.allocator.dupe(u8, contents);
    errdefer self.allocator.free(owned_contents);

    const gop = try self.files.getOrPut(self.allocator, owned_path);
    if (gop.found_existing) {
        self.allocator.free(owned_path);
        self.allocator.free(gop.value_ptr.*);
        gop.value_ptr.* = owned_contents;
    } else {
        gop.value_ptr.* = owned_contents;
    }
}

fn memoryAppendFileAbsolute(ptr: *anyopaque, path: []const u8, contents: []const u8, sync: bool) !void {
    _ = sync;
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    if (self.files.getPtr(path)) |value_ptr| {
        const old = value_ptr.*;
        const joined = try self.allocator.alloc(u8, old.len + contents.len);
        errdefer self.allocator.free(joined);
        @memcpy(joined[0..old.len], old);
        @memcpy(joined[old.len..], contents);
        self.allocator.free(old);
        value_ptr.* = joined;
        return;
    }

    const owned_path = try self.allocator.dupe(u8, path);
    errdefer self.allocator.free(owned_path);
    const owned_contents = try self.allocator.dupe(u8, contents);
    errdefer self.allocator.free(owned_contents);
    try self.files.putNoClobber(self.allocator, owned_path, owned_contents);
}

fn memorySyncFileContentsAbsolute(_: *anyopaque, _: []const u8) !void {}

fn memorySyncParentAbsolute(_: *anyopaque, _: []const u8) !void {}

fn memoryRenameAbsolute(ptr: *anyopaque, old_path: []const u8, new_path: []const u8) !void {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    const removed = self.files.fetchRemove(old_path) orelse return error.FileNotFound;
    const old_key = removed.key;
    const value = removed.value;

    const new_key = try self.allocator.dupe(u8, new_path);
    errdefer self.allocator.free(new_key);
    const gop = try self.files.getOrPut(self.allocator, new_key);
    if (gop.found_existing) {
        self.allocator.free(new_key);
        self.allocator.free(gop.value_ptr.*);
        gop.value_ptr.* = value;
    } else {
        gop.value_ptr.* = value;
    }
    self.allocator.free(old_key);
}

fn memoryDeleteFileAbsolute(ptr: *anyopaque, path: []const u8) !void {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    const removed = self.files.fetchRemove(path) orelse return error.FileNotFound;
    self.allocator.free(removed.key);
    self.allocator.free(removed.value);
}

fn memoryDeleteTree(ptr: *anyopaque, path: []const u8) !void {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();

    var doomed = std.ArrayListUnmanaged([]const u8).empty;
    defer doomed.deinit(self.allocator);

    var it = self.files.iterator();
    while (it.next()) |entry| {
        if (!pathContains(path, entry.key_ptr.*)) continue;
        try doomed.append(self.allocator, entry.key_ptr.*);
    }

    for (doomed.items) |doomed_key| {
        const removed = self.files.fetchRemove(doomed_key) orelse continue;
        self.allocator.free(removed.key);
        self.allocator.free(removed.value);
    }
}

fn memoryNowNs(ptr: *anyopaque) u64 {
    const self: *MemoryStorage = @ptrCast(@alignCast(ptr));
    const locked = lockAtomic(&self.mutex);
    defer if (locked) self.mutex.unlock();
    const current = self.tick;
    self.tick += 1;
    return current;
}

fn pathContains(prefix: []const u8, path: []const u8) bool {
    // Preserve the storage namespace's empty spelling for the absolute root.
    if (prefix.len == 0) return path.len == 0 or path[0] == '/';

    // Storage paths use '/' on every backend. Treat redundant trailing
    // separators as spelling aliases without allocating a normalized copy.
    // Keep one separator for the root so "/" contains every absolute path.
    var prefix_len = prefix.len;
    while (prefix_len > 1 and prefix[prefix_len - 1] == '/') prefix_len -= 1;
    const normalized = prefix[0..prefix_len];
    if (normalized.len == 1 and normalized[0] == '/') {
        return path.len > 0 and path[0] == '/';
    }
    if (!std.mem.startsWith(u8, path, normalized)) return false;
    if (path.len == normalized.len) return true;
    return path[normalized.len] == '/';
}

fn tempSiblingPath(allocator: Allocator, path: []const u8) ![]u8 {
    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    return try std.fmt.allocPrint(allocator, "{s}.tmp-{d}", .{ path, nonce });
}

var atomic_write_nonce: CounterU64 = .init(0);
var test_fd_cache_pause_after_open: std.atomic.Value(bool) = .init(false);
var test_fd_cache_open_paused: std.atomic.Value(bool) = .init(false);
var test_fd_cache_release_after_open: std.atomic.Value(bool) = .init(false);

fn hashPath(path: []const u8) u64 {
    return std.hash.Wyhash.hash(0x6d3b7a1db6f9c24f, path);
}

fn namespacedPathHash(namespace: u64, path: []const u8) u64 {
    return std.hash.Wyhash.hash(namespace ^ 0x6d3b7a1db6f9c24f, path);
}

test "host storage delegates through callbacks" {
    var backing = MemoryStorage.init(std.testing.allocator);
    defer backing.deinit();

    const HostContext = struct {
        backing: *MemoryStorage,
        trailer_reads: usize = 0,
        content_syncs: usize = 0,
        parent_syncs: usize = 0,

        fn createDirPath(ptr: *anyopaque, path: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().createDirPath(path);
        }

        fn readFileAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, max_bytes: usize) ![]u8 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().readFileAlloc(allocator, path, max_bytes);
        }

        fn readFileRangeAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, offset: u64, len: usize) ![]u8 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().readFileRangeAlloc(allocator, path, offset, len);
        }

        fn fileSize(ptr: *anyopaque, path: []const u8) !u64 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().fileSize(path);
        }

        fn readFileTrailerAlloc(ptr: *anyopaque, allocator: Allocator, path: []const u8, len: usize) !FileTrailer {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.trailer_reads += 1;
            return self.backing.storage().readFileTrailerAlloc(allocator, path, len);
        }

        fn writeFileAbsolute(ptr: *anyopaque, path: []const u8, contents: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().writeFileAbsolute(path, contents);
        }

        fn syncFileContentsAbsolute(ptr: *anyopaque, path: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.content_syncs += 1;
            return self.backing.storage().syncFileContentsAbsolute(path);
        }

        fn syncParentAbsolute(ptr: *anyopaque, path: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.parent_syncs += 1;
            return self.backing.storage().syncParentAbsolute(path);
        }

        fn renameAbsolute(ptr: *anyopaque, old_path: []const u8, new_path: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().renameAbsolute(old_path, new_path);
        }

        fn deleteFileAbsolute(ptr: *anyopaque, path: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().deleteFileAbsolute(path);
        }

        fn deleteTree(ptr: *anyopaque, path: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().deleteTree(path);
        }

        fn nowNs(ptr: *anyopaque) u64 {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            return self.backing.storage().nowNs();
        }
    };

    const host_vtable: Storage.VTable = .{
        .create_dir_path = HostContext.createDirPath,
        .read_file_alloc = HostContext.readFileAlloc,
        .read_file_range_alloc = HostContext.readFileRangeAlloc,
        .file_size = HostContext.fileSize,
        .read_file_trailer_alloc = HostContext.readFileTrailerAlloc,
        .write_file_absolute = HostContext.writeFileAbsolute,
        .rename_absolute = HostContext.renameAbsolute,
        .delete_file_absolute = HostContext.deleteFileAbsolute,
        .delete_tree = HostContext.deleteTree,
        .now_ns = HostContext.nowNs,
    };

    var host_ctx = HostContext{ .backing = &backing };
    const host = HostStorage.init(&host_ctx, &host_vtable).storage();

    try host.createDirPath("/host");
    try host.writeFileAbsolute("/host/a.txt", "hello");
    const hello = try host.readFileAlloc(std.testing.allocator, "/host/a.txt", 32);
    defer std.testing.allocator.free(hello);
    try std.testing.expectEqualStrings("hello", hello);
    const ell = try host.readFileRangeAlloc(std.testing.allocator, "/host/a.txt", 1, 3);
    defer std.testing.allocator.free(ell);
    try std.testing.expectEqualStrings("ell", ell);
    var llo = try host.readFileTrailerAlloc(std.testing.allocator, "/host/a.txt", 3);
    defer llo.deinit(std.testing.allocator);
    try std.testing.expectEqualStrings("llo", llo.bytes);
    try std.testing.expectEqual(@as(u64, 5), llo.file_size);
    try std.testing.expectEqual(@as(usize, 1), host_ctx.trailer_reads);

    try host.renameAbsolute("/host/a.txt", "/host/b.txt");
    try std.testing.expectError(error.FileNotFound, host.readFileAlloc(std.testing.allocator, "/host/a.txt", 32));
    const renamed = try host.readFileAlloc(std.testing.allocator, "/host/b.txt", 32);
    defer std.testing.allocator.free(renamed);
    try std.testing.expectEqualStrings("hello", renamed);

    try host.writeFileAbsolute("/host/sub/c.txt", "world");
    try host.deleteTree("/host/sub");
    try std.testing.expectError(error.FileNotFound, host.readFileAlloc(std.testing.allocator, "/host/sub/c.txt", 32));

    try host.deleteFileAbsolute("/host/b.txt");
    try std.testing.expectError(error.FileNotFound, host.readFileAlloc(std.testing.allocator, "/host/b.txt", 32));

    const t0 = host.nowNs();
    const t1 = host.nowNs();
    try std.testing.expect(t1 > t0);
    try std.testing.expectError(
        error.DurableAtomicWriteUnsupported,
        host.beginAtomicWrite(std.testing.allocator, "/host/not-durable"),
    );
    try std.testing.expectError(
        error.DurableFileSyncUnsupported,
        host.syncFileContentsAbsolute("/host/not-durable"),
    );
    try std.testing.expectError(
        error.DurableDirectorySyncUnsupported,
        host.syncParentAbsolute("/host/not-durable"),
    );

    var durable_host_vtable = host_vtable;
    durable_host_vtable.sync_contents_absolute = HostContext.syncFileContentsAbsolute;
    const durable_host = HostStorage.init(&host_ctx, &durable_host_vtable).storage();
    try std.testing.expectError(
        error.DurableAtomicWriteUnsupported,
        durable_host.beginAtomicWrite(std.testing.allocator, "/host/not-atomic"),
    );
    try std.testing.expectError(
        error.DurableDirectorySyncUnsupported,
        durable_host.syncParentAbsolute("/host/not-parent-durable"),
    );
    try durable_host.appendFileAbsolute(std.testing.allocator, "/host/durable.log", "a", true);
    try durable_host.appendFileAbsolute(std.testing.allocator, "/host/durable.log", "b", true);
    try std.testing.expectEqual(@as(usize, 2), host_ctx.content_syncs);
    const durable_contents = try durable_host.readFileAlloc(std.testing.allocator, "/host/durable.log", 32);
    defer std.testing.allocator.free(durable_contents);
    try std.testing.expectEqualStrings("ab", durable_contents);

    var atomic_host_vtable = durable_host_vtable;
    atomic_host_vtable.sync_parent_absolute = HostContext.syncParentAbsolute;
    atomic_host_vtable.rename_is_atomic = true;
    const atomic_host = HostStorage.init(&host_ctx, &atomic_host_vtable).storage();
    var atomic_writer = try atomic_host.beginAtomicWrite(std.testing.allocator, "/host/atomic.bin");
    try atomic_writer.appendSlice("durable");
    try atomic_writer.finish();
    try std.testing.expectEqual(@as(usize, 3), host_ctx.content_syncs);
    try std.testing.expectEqual(@as(usize, 1), host_ctx.parent_syncs);
    const atomic_contents = try atomic_host.readFileAlloc(std.testing.allocator, "/host/atomic.bin", 32);
    defer std.testing.allocator.free(atomic_contents);
    try std.testing.expectEqualStrings("durable", atomic_contents);
}

test "storage range read future fallback waits and cancels" {
    var backing = MemoryStorage.init(std.testing.allocator);
    defer backing.deinit();

    const storage = backing.storage();
    try storage.writeFileAbsolute("/future/a.txt", "hello");

    var future = try storage.beginReadFileRangeAlloc(std.testing.allocator, "/future/a.txt", 1, 3);
    const bytes = try future.wait();
    defer std.testing.allocator.free(bytes);
    try std.testing.expectEqualStrings("ell", bytes);

    var canceled = try storage.beginReadFileRangeAlloc(std.testing.allocator, "/future/a.txt", 0, 5);
    canceled.cancel();
}

test "native atomic write sink supports patching and crc before finish" {
    if (!supports_native_storage) return error.SkipZigTest;

    var native = try NativeStorage.init(std.testing.allocator, .threaded);
    defer native.deinit();

    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-atomic-{d}", .{atomic_write_nonce.fetchAdd(1, .monotonic)});
    defer native.storage().deleteFileAbsolute(path) catch {};

    var writer = try native.storage().beginAtomicWrite(std.testing.allocator, path);
    var active = true;
    defer if (active) writer.abort();
    if (supports_posix_fd_cache) {
        try std.testing.expectEqual(@as(usize, 1), native.snapshotStats().fd_admitted_descriptors);
    }

    try writer.appendSlice("hello _____");
    try writer.writeAt(6, "world");
    try std.testing.expectEqual(std.hash.Crc32.hash("hello world"), try writer.crc32Prefix(writer.len()));

    active = false;
    try writer.finish();
    if (supports_posix_fd_cache) {
        try std.testing.expectEqual(@as(usize, 0), native.snapshotStats().fd_admitted_descriptors);
    }

    const written = try native.storage().readFileAlloc(std.testing.allocator, path, 64);
    defer std.testing.allocator.free(written);
    try std.testing.expectEqualStrings("hello world", written);
}

test "native storage retained runtime has a finite worker ceiling" {
    if (!supports_native_storage) return error.SkipZigTest;

    var native = try NativeStorage.init(std.testing.allocator, .threaded);
    defer native.deinit();
    try std.testing.expectEqual(
        std.Io.Limit.limited(threaded_io_limits.service),
        native.state.threaded.concurrent_limit,
    );
}

test "native fd cache retries an open that straddles a mutation fence" {
    if (!supports_posix_fd_cache or builtin.single_threaded) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 4);
    defer pool.deinit();
    var native = try NativeStorage.initWithPool(std.testing.allocator, .threaded, &pool);
    defer native.deinit();

    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();

    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-invalidation-race-{d}", .{atomic_write_nonce.fetchAdd(1, .monotonic)});
    defer native.storage().deleteFileAbsolute(path) catch {};
    try native.storage().writeFileAbsolute(path, "old");

    var replacement_buf: [288]u8 = undefined;
    const replacement_path = try std.fmt.bufPrint(&replacement_buf, "{s}.replacement", .{path});
    defer native.storage().deleteFileAbsolute(replacement_path) catch {};
    try native.storage().writeFileAbsolute(replacement_path, "new");

    // Model the first half of the mutation fence before the reader opens the
    // old inode. Only the matching post-rename fence can make this miss retry.
    native.state.invalidateRename(replacement_path, path);

    test_fd_cache_pause_after_open.store(true, .release);
    test_fd_cache_open_paused.store(false, .release);
    test_fd_cache_release_after_open.store(false, .release);
    defer {
        test_fd_cache_pause_after_open.store(false, .release);
        test_fd_cache_open_paused.store(false, .release);
        test_fd_cache_release_after_open.store(true, .release);
    }

    const Reader = struct {
        storage: Storage,
        path: []const u8,
        bytes: [3]u8 = undefined,
        err: ?anyerror = null,

        fn run(self: *@This()) void {
            self.storage.readFileRangeInto(std.heap.page_allocator, self.path, 0, &self.bytes) catch |err| {
                self.err = err;
            };
        }
    };
    var reader = Reader{ .storage = native.storage(), .path = path };
    var reader_future = std.Io.async(io, Reader.run, .{&reader});
    var reader_awaited = false;
    defer if (!reader_awaited) {
        test_fd_cache_release_after_open.store(true, .release);
        reader_future.await(io);
    };

    for (0..10_000) |_| {
        if (test_fd_cache_open_paused.load(.acquire)) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    try std.testing.expect(test_fd_cache_open_paused.load(.acquire));

    try renameAbsoluteDirectPosix(replacement_path, path);
    native.state.invalidateRename(replacement_path, path);

    test_fd_cache_release_after_open.store(true, .release);
    reader_future.await(io);
    reader_awaited = true;
    try std.testing.expect(reader.err == null);
    try std.testing.expectEqualStrings("new", reader.bytes[0..]);
}

test "native fd cache invalidates a deleted tree spelled with trailing separators" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 8);
    defer pool.deinit();
    var native = try NativeStorage.initWithPool(std.testing.allocator, .threaded, &pool);
    defer native.deinit();

    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    var root_buf: [256]u8 = undefined;
    const root = try std.fmt.bufPrint(&root_buf, "/tmp/antfly-storage-tree-invalidation-{d}", .{nonce});
    defer native.storage().deleteTree(root) catch {};
    try native.storage().createDirPath(root);

    var path_buf: [288]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "{s}/cached.bin", .{root});
    try native.storage().writeFileAbsolute(path, "old");
    const cached = try native.storage().readFileRangeAlloc(std.testing.allocator, path, 0, 3);
    defer std.testing.allocator.free(cached);
    try std.testing.expectEqualStrings("old", cached);
    try std.testing.expectEqual(@as(usize, 1), pool.snapshotStats().fd_cache_entries);

    var alias_buf: [288]u8 = undefined;
    const trailing_alias = try std.fmt.bufPrint(&alias_buf, "{s}///", .{root});
    try native.storage().deleteTree(trailing_alias);
    try std.testing.expectEqual(@as(usize, 0), pool.snapshotStats().fd_cache_entries);
    try std.testing.expectError(
        error.FileNotFound,
        native.storage().readFileRangeAlloc(std.testing.allocator, path, 0, 3),
    );
}

test "path containment handles root and trailing separators" {
    try std.testing.expect(pathContains("/", "/tree/file"));
    try std.testing.expect(pathContains("/tree///", "/tree/file"));
    try std.testing.expect(pathContains("/tree///", "/tree"));
    try std.testing.expect(!pathContains("/tree///", "/treehouse/file"));
    try std.testing.expect(pathContains("", "/tree/file"));
    try std.testing.expect(!pathContains("", "relative/file"));
}

test "native atomic write finish retains admission through parent sync" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 4);
    defer pool.deinit();
    var native = try NativeStorage.initWithPool(std.testing.allocator, .threaded, &pool);
    defer native.deinit();

    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-atomic-admission-handoff-{d}", .{atomic_write_nonce.fetchAdd(1, .monotonic)});
    defer native.storage().deleteFileAbsolute(path) catch {};

    var writer = try native.storage().beginAtomicWrite(std.testing.allocator, path);
    var writer_active = true;
    defer if (writer_active) writer.abort();
    try writer.appendSlice("durable");

    // Fill the persistent class to the point where relinquishing the writer's
    // transient slot would make reacquisition fail. The closed data file and
    // parent-directory fsync are sequential users of that same admitted slot.
    const io = native.state.threaded.io();
    try pool.fd_cache.reservePersistentDescriptors(io, 2);
    var persistent_held = true;
    defer if (persistent_held) pool.fd_cache.releasePersistentDescriptors(io, 2);

    writer_active = false;
    try writer.finish();
    try std.testing.expectEqual(@as(usize, 2), pool.snapshotStats().fd_admitted_descriptors);

    pool.fd_cache.releasePersistentDescriptors(io, 2);
    persistent_held = false;
    const written = try native.storage().readFileAlloc(std.testing.allocator, path, 64);
    defer std.testing.allocator.free(written);
    try std.testing.expectEqualStrings("durable", written);
}

test "native atomic write sink cleans temporary file when content sync fails" {
    if (!supports_native_storage) return error.SkipZigTest;
    if (builtin.os.tag == .windows or builtin.os.tag == .wasi or builtin.os.tag == .freestanding) return error.SkipZigTest;

    var native = try NativeStorage.init(std.testing.allocator, .threaded);
    defer native.deinit();

    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-atomic-sync-failure-{d}", .{atomic_write_nonce.fetchAdd(1, .monotonic)});
    defer native.storage().deleteFileAbsolute(path) catch {};

    var writer = try native.storage().beginAtomicWrite(std.testing.allocator, path);
    const impl: *NativeAtomicWriteSink = @ptrCast(@alignCast(writer.ptr));
    const tmp_path = try std.testing.allocator.dupe(u8, impl.tmp_path);
    defer std.testing.allocator.free(tmp_path);
    try writer.appendSlice("uncommitted");

    closeFd(impl.fd);
    impl.fd = -1;
    try std.testing.expectError(error.InvalidFileDescriptor, writer.finish());

    try std.testing.expectError(
        error.FileNotFound,
        native.storage().fileSize(tmp_path),
    );
    try std.testing.expectError(
        error.FileNotFound,
        native.storage().fileSize(path),
    );
}

test "buffered atomic write sink supports overlapping writes and appends" {
    var backing = MemoryStorage.init(std.testing.allocator);
    defer backing.deinit();

    var writer = try backing.storage().beginAtomicWrite(std.testing.allocator, "/alias-safe.bin");
    var active = true;
    defer if (active) writer.abort();

    try writer.appendSlice("abcdef");
    const impl: *BufferedAtomicWriteSink = @ptrCast(@alignCast(writer.ptr));
    try writer.writeAt(2, impl.out.items[0..4]);
    try std.testing.expectEqualStrings("ababcd", impl.out.items);

    try writer.appendSlice(impl.out.items[1..5]);
    try std.testing.expectEqualStrings("ababcdbabc", impl.out.items);

    active = false;
    try writer.finish();

    const written = try backing.storage().readFileAlloc(std.testing.allocator, "/alias-safe.bin", 64);
    defer std.testing.allocator.free(written);
    try std.testing.expectEqualStrings("ababcdbabc", written);
}

test "native fd cache evicts to per-store budget" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, fallback_cached_native_fds);
    defer pool.deinit();
    var native = try NativeStorage.initWithPool(std.testing.allocator, .threaded, &pool);
    defer native.deinit();

    const base_nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    for (0..fallback_cached_native_fds + 8) |i| {
        var path_buf: [256]u8 = undefined;
        const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-fd-cache-{d}-{d}", .{ base_nonce, i });
        try native.storage().writeFileAbsolute(path, "x");
        defer native.storage().deleteFileAbsolute(path) catch {};

        const bytes = try native.storage().readFileAlloc(std.testing.allocator, path, 8);
        defer std.testing.allocator.free(bytes);
        try std.testing.expectEqualStrings("x", bytes);
    }

    const stats = native.snapshotStats();
    try std.testing.expect(stats.fd_cache_entries <= stats.fd_admission_capacity);
    try std.testing.expectEqual(fallback_cached_native_fds, stats.fd_admission_capacity);
}

test "native fd admission reserves non-storage process capacity" {
    try std.testing.expectEqual(@as(usize, 4), nativeFdAdmissionCapacityForSoftLimit(8));
    try std.testing.expectEqual(@as(usize, 32), nativeFdAdmissionCapacityForSoftLimit(64));
    try std.testing.expectEqual(@as(usize, 128), nativeFdAdmissionCapacityForSoftLimit(256));
    try std.testing.expectEqual(@as(usize, 512), nativeFdAdmissionCapacityForSoftLimit(1024));
    try std.testing.expectEqual(unlimited_fd_admission_capacity, nativeFdAdmissionCapacityForSoftLimit(std.math.maxInt(u64)));
    try std.testing.expectEqual(unlimited_fd_admission_capacity, nativeFdAdmissionCapacityForSoftLimit(1_048_576));
}

test "backend runtime native fd pools share one process admission domain" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var first = NativeStoragePool.init(std.testing.allocator);
    defer first.deinit();
    var second = NativeStoragePool.init(std.testing.allocator);
    defer second.deinit();

    try std.testing.expectEqual(first.fd_cache, second.fd_cache);
    try std.testing.expectEqual(configuredNativeFdAdmissionCapacity(), first.snapshotStats().fd_admission_capacity);

    var first_storage = try NativeStorage.init(std.testing.allocator, .threaded);
    defer first_storage.deinit();
    var second_storage = try NativeStorage.init(std.testing.allocator, .threaded);
    defer second_storage.deinit();
    try std.testing.expectEqual(first_storage.state.fdCache(), second_storage.state.fdCache());
    try std.testing.expectEqual(first.fd_cache, first_storage.state.fdCache());
}

test "transient native writes wait before opening at descriptor capacity" {
    if (!supports_posix_fd_cache or builtin.single_threaded) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 2);
    defer pool.deinit();
    var native = try NativeStorage.initWithPool(std.testing.allocator, .threaded, &pool);
    defer native.deinit();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();

    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-transient-admission-{d}", .{nonce});
    defer native.storage().deleteFileAbsolute(path) catch {};

    var occupying_permit = try native.acquireFdPermit();
    var permit_active = true;
    defer if (permit_active) occupying_permit.release();

    const Worker = struct {
        storage: Storage,
        path: []const u8,
        failed: *std.atomic.Value(bool),

        fn run(self: *@This()) void {
            self.storage.writeFileAbsolute(self.path, "bounded") catch {
                self.failed.store(true, .release);
            };
        }
    };
    var failed = std.atomic.Value(bool).init(false);
    var worker = Worker{ .storage = native.storage(), .path = path, .failed = &failed };
    var future = std.Io.async(io, Worker.run, .{&worker});
    var future_awaited = false;
    defer if (!future_awaited) {
        if (permit_active) {
            occupying_permit.release();
            permit_active = false;
        }
        future.await(io);
    };

    for (0..10_000) |_| {
        if (pool.snapshotStats().fd_admission_waiters > 0) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    try std.testing.expectEqual(@as(usize, 1), pool.snapshotStats().fd_admitted_descriptors);
    try std.testing.expectEqual(@as(usize, 1), pool.snapshotStats().fd_admission_waiters);

    occupying_permit.release();
    permit_active = false;
    future.await(io);
    future_awaited = true;
    try std.testing.expect(!failed.load(.acquire));
    try std.testing.expectEqual(@as(usize, 0), pool.snapshotStats().fd_admitted_descriptors);
}

test "persistent path locks use reserved headroom under transient saturation" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 8);
    defer pool.deinit();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();

    try pool.fd_cache.reserveDescriptors(io, 6);
    const held_descriptors: usize = 6;
    defer pool.fd_cache.releaseDescriptors(io, held_descriptors);

    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-path-lock-admission-{d}", .{nonce});
    defer deleteFilePathPosix(path) catch {};

    var lock_file = try openNativePathLockFileWithCache(
        std.testing.allocator,
        path,
        .{ .create_if_missing = true },
        pool.fd_cache,
    );
    defer lock_file.close();
    const stats = pool.snapshotStats();
    try std.testing.expectEqual(@as(usize, 7), stats.fd_admitted_descriptors);
    try std.testing.expectEqual(@as(usize, 1), stats.fd_persistent_descriptors);
    try std.testing.expectEqual(@as(usize, 2), stats.fd_persistent_reserve);
}

test "persistent path lock exhaustion fails without waiting" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 2);
    defer pool.deinit();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    try pool.fd_cache.reservePersistentDescriptors(io, 2);
    defer pool.fd_cache.releasePersistentDescriptors(io, 2);

    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-path-lock-exhaustion-{d}", .{nonce});
    defer deleteFilePathPosix(path) catch {};

    try std.testing.expectError(
        error.PersistentDescriptorAdmissionExhausted,
        openNativePathLockFileWithCache(std.testing.allocator, path, .{ .create_if_missing = true }, pool.fd_cache),
    );
    const stats = pool.snapshotStats();
    try std.testing.expectEqual(@as(usize, 0), stats.fd_admission_waiters);
    try std.testing.expectEqual(@as(u64, 1), stats.fd_persistent_admission_failures);
}

test "transient admission fails fast when only persistent descriptors prevent progress" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 4);
    defer pool.deinit();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();

    // Two lifetime descriptors consume the transient ceiling after open
    // headroom is retained. No short-lived holder can wake an admission wait,
    // so a cache owner must be allowed to close an idle backend instead.
    try pool.fd_cache.reservePersistentDescriptors(io, 2);
    var persistent_held = true;
    defer if (persistent_held) pool.fd_cache.releasePersistentDescriptors(io, 2);
    try std.testing.expectError(
        error.PersistentDescriptorAdmissionExhausted,
        pool.fd_cache.reserveDescriptors(io, 1),
    );
    try std.testing.expectEqual(@as(usize, 0), pool.snapshotStats().fd_admission_waiters);

    pool.fd_cache.releasePersistentDescriptors(io, 2);
    persistent_held = false;
    try pool.fd_cache.reserveDescriptors(io, 1);
    defer pool.fd_cache.releaseDescriptors(io, 1);
}

test "persistent path locks honor an explicitly configured pool" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 2);
    defer pool.deinit();
    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    var first_buf: [256]u8 = undefined;
    const first_path = try std.fmt.bufPrint(&first_buf, "/tmp/antfly-storage-configured-lock-pool-{d}-first", .{nonce});
    defer deleteFilePathPosix(first_path) catch {};
    var second_buf: [256]u8 = undefined;
    const second_path = try std.fmt.bufPrint(&second_buf, "/tmp/antfly-storage-configured-lock-pool-{d}-second", .{nonce});
    defer deleteFilePathPosix(second_path) catch {};

    var first = try openNativePathLockFileWithPool(
        std.testing.allocator,
        first_path,
        .{ .create_if_missing = true },
        &pool,
    );
    defer first.close();
    try std.testing.expectEqual(@as(usize, 1), pool.snapshotStats().fd_persistent_descriptors);
    try std.testing.expectError(
        error.PersistentDescriptorAdmissionExhausted,
        openNativePathLockFileWithPool(
            std.testing.allocator,
            second_path,
            .{ .create_if_missing = true },
            &pool,
        ),
    );
}

test "persistent locks consume reserved headroom without stranding transient capacity" {
    if (!supports_posix_fd_cache) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 42);
    defer pool.deinit();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();

    // Model nineteen open backends (root + WAL locks each). The original
    // fixed-reserve calculation capped transient admission at 37 and would
    // wait forever here despite four unused aggregate slots.
    try pool.fd_cache.reservePersistentDescriptors(io, 38);
    defer pool.fd_cache.releasePersistentDescriptors(io, 38);
    try std.testing.expect(pool.fd_cache.makeCapacityAvailable(2));
    try pool.fd_cache.reserveDescriptors(io, 2);
    defer pool.fd_cache.releaseDescriptors(io, 2);

    // The dynamic transient ceiling still preserves the exact two-descriptor
    // peak required to open the next lifetime path lock.
    try pool.fd_cache.reservePersistentDescriptors(io, 2);
    defer pool.fd_cache.releasePersistentDescriptors(io, 2);
    const stats = pool.snapshotStats();
    try std.testing.expectEqual(@as(usize, 42), stats.fd_admitted_descriptors);
    try std.testing.expectEqual(@as(usize, 40), stats.fd_persistent_descriptors);
    try std.testing.expectEqual(@as(usize, 5), stats.fd_persistent_reserve);
}

test "shared native fd cache blocks before opening more than 64 files across stores" {
    if (!supports_posix_fd_cache or builtin.single_threaded) return error.SkipZigTest;

    const capacity = 64;
    const worker_count = capacity + 8;
    const store_count = 3;
    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, capacity);
    defer pool.deinit();
    const transient_capacity = capacity - pool.snapshotStats().fd_persistent_reserve;
    var stores: [store_count]NativeStorage = undefined;
    var stores_initialized: usize = 0;
    defer while (stores_initialized > 0) {
        stores_initialized -= 1;
        stores[stores_initialized].deinit();
    };
    for (&stores) |*native| {
        native.* = try NativeStorage.initWithPool(std.testing.allocator, .threaded, &pool);
        stores_initialized += 1;
    }

    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    var paths: [worker_count][256]u8 = undefined;
    var path_lens: [worker_count]usize = undefined;
    for (0..worker_count) |i| {
        const path = try std.fmt.bufPrint(&paths[i], "/tmp/antfly-storage-shared-fd-cache-{d}-{d}", .{ nonce, i });
        path_lens[i] = path.len;
        try stores[i % store_count].storage().writeFileAbsolute(path, "x");
    }
    defer for (0..worker_count) |i| stores[i % store_count].storage().deleteFileAbsolute(paths[i][0..path_lens[i]]) catch {};

    const Worker = struct {
        cache: *FdCache,
        namespace: u64,
        io: std.Io,
        path: []const u8,
        release_all: *std.Io.Event,
        acquired: *std.atomic.Value(usize),
        failures: *std.atomic.Value(usize),

        fn run(self: *@This()) void {
            const entry = self.cache.retain(self.namespace, self.io, self.path) catch {
                _ = self.failures.fetchAdd(1, .monotonic);
                return;
            };
            _ = self.acquired.fetchAdd(1, .release);
            self.release_all.waitUncancelable(self.io);
            self.cache.release(self.io, entry);
        }
    };

    // Every task intentionally holds a lease or waits in descriptor admission.
    // Give the threaded test executor one slot per task so the harness itself
    // cannot become the bottleneck before the >64 contention point is reached.
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{
        .async_limit = .limited(worker_count),
        .stack_size = 512 * 1024,
    });
    defer io_impl.deinit();
    const io = io_impl.io();
    var release_all: std.Io.Event = .unset;
    var acquired = std.atomic.Value(usize).init(0);
    var failures = std.atomic.Value(usize).init(0);
    var workers: [worker_count]Worker = undefined;
    var group = std.Io.Group.init;
    var group_awaited = false;
    defer if (!group_awaited) {
        release_all.set(io);
        group.await(io) catch {};
    };
    for (0..worker_count) |i| {
        workers[i] = .{
            .cache = stores[i % store_count].state.fdCache(),
            .namespace = stores[i % store_count].state.cache_namespace,
            .io = io,
            .path = paths[i][0..path_lens[i]],
            .release_all = &release_all,
            .acquired = &acquired,
            .failures = &failures,
        };
        group.async(io, Worker.run, .{&workers[i]});
    }

    for (0..10_000) |_| {
        if (acquired.load(.acquire) == transient_capacity and pool.snapshotStats().fd_admission_waiters > 0) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    const blocked_stats = pool.snapshotStats();
    try std.testing.expectEqual(transient_capacity, acquired.load(.acquire));
    try std.testing.expectEqual(transient_capacity, blocked_stats.fd_cache_entries);
    try std.testing.expectEqual(transient_capacity, blocked_stats.fd_admitted_descriptors);
    try std.testing.expect(blocked_stats.fd_admission_waiters > 0);
    try std.testing.expect(blocked_stats.fd_admission_waits > 0);

    release_all.set(io);
    try group.await(io);
    group_awaited = true;
    try std.testing.expectEqual(@as(usize, 0), failures.load(.acquire));
    try std.testing.expect(pool.snapshotStats().fd_cache_entries <= capacity);
}

test "weighted native fd admission preserves FIFO progress" {
    if (!supports_posix_fd_cache or builtin.single_threaded) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 3);
    defer pool.deinit();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    try pool.fd_cache.reserveDescriptors(io, 3);
    var held_descriptors: usize = 3;
    defer pool.fd_cache.releaseDescriptors(io, held_descriptors);

    const Worker = struct {
        cache: *FdCache,
        io: std.Io,
        count: usize,
        acquired: *std.atomic.Value(bool),
        release_gate: ?*std.Io.Event,

        fn run(self: *@This()) void {
            self.cache.reserveDescriptors(self.io, self.count) catch return;
            self.acquired.store(true, .release);
            if (self.release_gate) |gate| gate.waitUncancelable(self.io);
            self.cache.releaseDescriptors(self.io, self.count);
        }
    };

    var large_acquired = std.atomic.Value(bool).init(false);
    var small_acquired = std.atomic.Value(bool).init(false);
    var release_large: std.Io.Event = .unset;
    var large = Worker{
        .cache = pool.fd_cache,
        .io = io,
        .count = 2,
        .acquired = &large_acquired,
        .release_gate = &release_large,
    };
    var large_future = std.Io.async(io, Worker.run, .{&large});
    var large_awaited = false;
    defer if (!large_awaited) {
        release_large.set(io);
        large_future.await(io);
    };
    for (0..10_000) |_| {
        if (pool.snapshotStats().fd_admission_waiters >= 1) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    try std.testing.expect(pool.snapshotStats().fd_admission_waiters >= 1);

    var small = Worker{
        .cache = pool.fd_cache,
        .io = io,
        .count = 1,
        .acquired = &small_acquired,
        .release_gate = null,
    };
    var small_future = std.Io.async(io, Worker.run, .{&small});
    var small_awaited = false;
    defer if (!small_awaited) {
        // The small request is queued behind the large request. Unblock the
        // older request first on every error path so cleanup cannot deadlock
        // while awaiting the younger future.
        release_large.set(io);
        small_future.await(io);
    };
    for (0..10_000) |_| {
        if (pool.snapshotStats().fd_admission_waiters >= 2) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    try std.testing.expect(pool.snapshotStats().fd_admission_waiters >= 2);

    // One released descriptor would admit the younger one-fd request, but it
    // must remain queued so capacity can accumulate for the older request.
    pool.fd_cache.releaseDescriptors(io, 1);
    held_descriptors -= 1;
    try io.sleep(.fromMilliseconds(10), .awake);
    try std.testing.expect(!large_acquired.load(.acquire));
    try std.testing.expect(!small_acquired.load(.acquire));

    pool.fd_cache.releaseDescriptors(io, 1);
    held_descriptors -= 1;
    for (0..10_000) |_| {
        if (large_acquired.load(.acquire)) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    try std.testing.expect(large_acquired.load(.acquire));
    try std.testing.expect(!small_acquired.load(.acquire));
    release_large.set(io);
    for (0..10_000) |_| {
        if (small_acquired.load(.acquire)) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    try std.testing.expect(small_acquired.load(.acquire));
    large_future.await(io);
    large_awaited = true;
    small_future.await(io);
    small_awaited = true;
}

test "shared native fd admission wait is cancellation aware" {
    if (!supports_posix_fd_cache or builtin.single_threaded) return error.SkipZigTest;

    var pool = NativeStoragePool.initWithCapacityForTest(std.testing.allocator, 2);
    defer pool.deinit();
    var native = try NativeStorage.initWithPool(std.testing.allocator, .threaded, &pool);
    defer native.deinit();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    const nonce = atomic_write_nonce.fetchAdd(1, .monotonic);
    var second_path_buf: [256]u8 = undefined;
    const second_path = try std.fmt.bufPrint(&second_path_buf, "/tmp/antfly-storage-fd-cancel-{d}-waiting", .{nonce});
    try native.storage().writeFileAbsolute(second_path, "b");
    defer native.storage().deleteFileAbsolute(second_path) catch {};

    // Model concurrent short-lived permits held by full-text mmap and an
    // atomic writer. The cached LSM read must share this same budget.
    var transient_a = try native.acquireFdPermit();
    defer transient_a.release();
    var transient_b = try native.acquireFdPermit();
    defer transient_b.release();

    var future = try native.storage().beginReadFileRangeAllocWithRuntime(
        ReadRuntime.init(io),
        std.testing.allocator,
        second_path,
        0,
        1,
    );
    var future_active = true;
    defer if (future_active) future.cancel();

    for (0..10_000) |_| {
        if (pool.snapshotStats().fd_admission_waiters > 0) break;
        try io.sleep(.fromMilliseconds(1), .awake);
    }
    try std.testing.expectEqual(@as(usize, 1), pool.snapshotStats().fd_admission_waiters);
    future.cancel();
    future_active = false;

    const stats = pool.snapshotStats();
    try std.testing.expectEqual(@as(usize, 0), stats.fd_admission_waiters);
    try std.testing.expectEqual(@as(usize, 0), stats.fd_cache_entries);
    try std.testing.expectEqual(@as(usize, 2), stats.fd_admitted_descriptors);
}

test "native storage state is reclaimed after owner deinit" {
    if (!supports_native_storage) return error.SkipZigTest;

    // std.testing.allocator reports any state or runtime allocation left by
    // these repeated complete lifecycles.
    for (0..64) |_| {
        var native = try NativeStorage.init(std.testing.allocator, .threaded);
        native.deinit();
    }
}

test "native storage lease owns state past owner deinit" {
    if (!supports_native_storage) return error.SkipZigTest;

    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-owned-lease-{d}", .{atomic_write_nonce.fetchAdd(1, .monotonic)});

    var native = try NativeStorage.init(std.testing.allocator, .threaded);
    var lease = try native.acquireLease();
    defer lease.deinit();
    native.deinit();

    const storage_view = lease.storage();
    try storage_view.writeFileAbsolute(path, "owned lease");
    defer storage_view.deleteFileAbsolute(path) catch {};
    const loaded = try storage_view.readFileAlloc(std.testing.allocator, path, 64);
    defer std.testing.allocator.free(loaded);
    try std.testing.expectEqualStrings("owned lease", loaded);
}

test "native atomic write sink retains invalidation state past storage deinit" {
    if (!supports_native_storage) return error.SkipZigTest;
    if (builtin.os.tag == .windows or builtin.os.tag == .wasi or builtin.os.tag == .freestanding) return error.SkipZigTest;

    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-atomic-lease-{d}", .{atomic_write_nonce.fetchAdd(1, .monotonic)});

    var native = try NativeStorage.init(std.testing.allocator, .threaded);
    var writer = try native.storage().beginAtomicWrite(std.testing.allocator, path);
    var active = true;
    defer if (active) writer.abort();

    try writer.appendSlice("leased");
    native.deinit();

    active = false;
    try writer.finish();

    var verifier = try NativeStorage.init(std.testing.allocator, .threaded);
    defer verifier.deinit();
    defer verifier.storage().deleteFileAbsolute(path) catch {};

    const written = try verifier.storage().readFileAlloc(std.testing.allocator, path, 64);
    defer std.testing.allocator.free(written);
    try std.testing.expectEqualStrings("leased", written);
}

test "native buffered atomic write sink retains invalidation state past storage deinit" {
    if (!supports_native_storage) return error.SkipZigTest;
    if (builtin.os.tag == .windows or builtin.os.tag == .wasi or builtin.os.tag == .freestanding) return error.SkipZigTest;

    var path_buf: [256]u8 = undefined;
    const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-storage-buffered-atomic-lease-{d}", .{atomic_write_nonce.fetchAdd(1, .monotonic)});

    var native = try NativeStorage.init(std.testing.allocator, .threaded);
    var writer = try NativeBufferedAtomicWriteSink.create(std.testing.allocator, path, native.state);
    var active = true;
    defer if (active) writer.abort();

    try writer.appendSlice("buffered _____");
    try writer.writeAt(9, "lease");
    try std.testing.expectEqual(std.hash.Crc32.hash("buffered lease"), try writer.crc32Prefix(writer.len()));
    native.deinit();

    active = false;
    try writer.finish();

    var verifier = try NativeStorage.init(std.testing.allocator, .threaded);
    defer verifier.deinit();
    defer verifier.storage().deleteFileAbsolute(path) catch {};

    const written = try verifier.storage().readFileAlloc(std.testing.allocator, path, 64);
    defer std.testing.allocator.free(written);
    try std.testing.expectEqualStrings("buffered lease", written);
}
