// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

//! Storage-backed shell for the HBC (Hierarchical Balanced Clustering) vector
//! index engine in `lib/vectorindex`.
//!
//! The HBC engine itself now lives in `antfly_vectorindex`; this module owns:
//!   - backend opening/ownership
//!   - backend-neutral runtime transaction/store wiring
//!   - persistence layout for HBC namespaces
//!   - a thin storage facade over the library-owned engine
//!
//! HBC namespaces:
//!   "hbc_nodes"  - packed tree nodes
//!   "hbc_meta"   - index metadata
//!   "hbc_quant"  - node search payloads: raw leaf/root vector sets and RaBitQ internal-node sets
//!   "hbc_vecs"   - vector metadata and legacy/index-local raw vectors keyed by vector ID

const std = @import("std");
const builtin = @import("builtin");
const build_options = @import("build_options");
const platform = @import("antfly_platform");
const Allocator = std.mem.Allocator;
const AtomicU64 = platform.atomic.Value(u64);
const backend_erased = @import("backend_erased.zig");
const backend_types = @import("backend_types.zig");
const hbc_backend = @import("hbc_backend.zig");
const resource_manager_mod = @import("resource_manager.zig");
const apply_rw_lock_mod = @import("db/apply_rw_lock.zig");
const supports_lmdb = builtin.os.tag != .freestanding and build_options.lmdb_enabled;
const lmdb = if (supports_lmdb) @import("lmdb.zig") else struct {
    pub const Error = error{NotFound};
};
const lsm_backend = @import("lsm_backend/mod.zig");
const platform_time = @import("antfly_platform").time;
const vec = @import("antfly_vector").vector;
const proto = @import("antfly_vector").proto;
const quantizer_mod = @import("antfly_vector").quantizer;
const rabitq = @import("antfly_vector").rabitq;
const go_rand = @import("antfly_vector").go_rand;
const vectorindex_types = @import("antfly_vectorindex").types;
const vectorindex_bulk_build = @import("antfly_vectorindex").bulk_build;
const vectorindex_search_results = @import("antfly_vectorindex").search_results;
const vectorindex_search_types = @import("antfly_vectorindex").search_types;
const vectorindex_search = @import("antfly_vectorindex").search;
const vectorindex_search_runtime = @import("antfly_vectorindex").search_runtime;
const vectorindex_store = @import("antfly_vectorindex").store;
const vectorindex_hbc_runtime = @import("antfly_vectorindex").hbc_runtime;
const vectorindex_hbc = @import("antfly_vectorindex").hbc;
const vectorindex_hbc_index = @import("antfly_vectorindex").hbc_index;
const vectorindex_posting = @import("antfly_vectorindex").posting;
const vectorindex_spfresh_index = @import("antfly_vectorindex").spfresh_index;
const vectorindex_hbc_transfer = @import("antfly_vectorindex").hbc_transfer;
const vectorindex_hbc_debug = @import("antfly_vectorindex").hbc_debug;

var temp_path_nonce: u64 = 0;
const default_deferred_hbc_leaf_splits_per_publish: usize = 256;
const default_bulk_split_vector_workspace_budget_bytes: u64 = 256 * 1024 * 1024;

pub const TestGetVectorViewOrScratchHook = *const fn (?*anyopaque, *HBCIndex, u64) void;
var test_get_vector_view_or_scratch_ctx: ?*anyopaque = null;
var test_get_vector_view_or_scratch_hook: ?TestGetVectorViewOrScratchHook = null;

pub fn setTestGetVectorViewOrScratchHook(ctx: ?*anyopaque, hook: ?TestGetVectorViewOrScratchHook) void {
    if (!builtin.is_test) return;
    test_get_vector_view_or_scratch_ctx = ctx;
    test_get_vector_view_or_scratch_hook = hook;
}

const TestCompleteSnapshotCaptureHook = *const fn (?*anyopaque, *HBCIndex) void;
var test_complete_snapshot_capture_ctx: ?*anyopaque = null;
var test_complete_snapshot_capture_hook: ?TestCompleteSnapshotCaptureHook = null;
const TestBeforeDurableSnapshotCaptureHook = *const fn (?*anyopaque, *HBCIndex) void;
var test_before_durable_snapshot_capture_ctx: ?*anyopaque = null;
var test_before_durable_snapshot_capture_hook: ?TestBeforeDurableSnapshotCaptureHook = null;

// ============================================================================
// Configuration
// ============================================================================

pub const HBCConfig = vectorindex_types.HBCConfig;
pub const StorageBackend = vectorindex_types.StorageBackend;
pub const BulkBuildAlgo = vectorindex_types.BulkBuildAlgo;
pub const LsmWriteStats = lsm_backend.Backend.WriteStats;
pub const LsmMaintenanceStats = lsm_backend.Backend.MaintenanceStats;
pub const LsmOpenStats = lsm_backend.Backend.OpenStats;

fn lockAtomic(mutex: *std.atomic.Mutex) void {
    while (!mutex.tryLock()) {
        if (builtin.os.tag == .freestanding) {
            std.atomic.spinLoopHint();
        } else {
            std.Thread.yield() catch {};
        }
    }
}

fn hbcRuntimeBatchMode(in_bulk_session: bool, lsm_direct_bulk_ingest_enabled: ?bool) vectorindex_store.BatchMode {
    if (!in_bulk_session) return .default;
    const direct_bulk_ingest_enabled = lsm_direct_bulk_ingest_enabled orelse return .default;
    return if (direct_bulk_ingest_enabled) .default else .bulk_ingest;
}

// ============================================================================
// Index metadata (serialized to LMDB)
// ============================================================================

const meta_key = vectorindex_hbc.meta_key;
const bulk_publish_state_key = "__bulk_publish_state";
const bulk_publish_state_value = "incomplete";
pub const WriteSessionKind = enum {
    streaming_replay,
    bulk_publication,
};
const hbc_index_version = vectorindex_hbc.hbc_index_version;
const IndexMetadata = vectorindex_hbc.IndexMetadata;
pub const ProjectionCheckpointMetadata = vectorindex_hbc.ProjectionCheckpointMetadata;

// ============================================================================
// Node representation
// ============================================================================

pub const Node = vectorindex_types.Node;

// ============================================================================
// Priority queue item for search
// ============================================================================

pub const PriorityItem = vectorindex_types.PriorityItem;

const candidateLessThan = vectorindex_search_types.candidateLessThan;

fn nowNs() u64 {
    return platform_time.monotonicNs();
}

fn elapsedSince(start_ns: u64) u64 {
    const end_ns = nowNs();
    if (end_ns <= start_ns) return 0;
    return end_ns - start_ns;
}

fn elapsedSinceNs(start_ns: i128) u64 {
    const end_ns = nowNsI128();
    return @intCast(@max(end_ns - start_ns, 0));
}

fn nowNsI128() i128 {
    return @intCast(platform_time.monotonicNs());
}

fn isNotFound(err: anyerror) bool {
    return err == error.NotFound or (supports_lmdb and err == lmdb.Error.NotFound);
}

// ============================================================================
// Node key encoding
// ============================================================================

const Suffix = vectorindex_hbc.Suffix;
const encodeNodeKey = vectorindex_hbc.encodeNodeKey;
const encodeVecKey = vectorindex_hbc.encodeVecKey;
const encodeVecLeafKey = vectorindex_hbc.encodeVecLeafKey;
const encodeVecMetaKey = vectorindex_hbc.encodeVecMetaKey;
const encodeQuantKey = vectorindex_hbc.encodeQuantKey;

// ============================================================================
// Node header
// ============================================================================

const NodeHeader = vectorindex_hbc.NodeHeader;

pub const NodeSplitClass = vectorindex_types.NodeSplitClass;
pub const NodeSplitRange = vectorindex_types.NodeSplitRange;
pub const SplitPlanningStats = vectorindex_types.SplitPlanningStats;
pub const SplitReusePlan = vectorindex_types.SplitReusePlan;
pub const SplitRebuildWork = vectorindex_types.SplitRebuildWork;

const DeferredNodeValue = struct {
    value: ?[]u8 = null,

    fn deinit(self: *DeferredNodeValue, alloc: Allocator) void {
        if (self.value) |value| alloc.free(value);
        self.* = undefined;
    }
};

pub const SplitMemberPlan = struct {
    right_only_members: []u64,
    mixed_right_members: []u64,

    pub fn deinit(self: *SplitMemberPlan, alloc: Allocator) void {
        alloc.free(self.right_only_members);
        alloc.free(self.mixed_right_members);
        self.* = undefined;
    }
};

const LeafKeyEntry = vectorindex_hbc_index.LeafKeyEntry;

const initNodeSplitRangeFromInput = vectorindex_bulk_build.initNodeSplitRangeFromInput;
const extendNodeSplitRangeFromInput = vectorindex_bulk_build.extendNodeSplitRangeFromInput;
const mergeNodeSplitRanges = vectorindex_bulk_build.mergeNodeSplitRanges;
const planBalancedGroupSizes = vectorindex_bulk_build.planBalancedGroupSizes;
const encodeNodeRange = vectorindex_bulk_build.encodeNodeRange;
const decodeNodeRange = vectorindex_bulk_build.decodeNodeRange;

const QuantizedSet = vectorindex_hbc_runtime.QuantizedSet;

const HbcCacheKind = enum {
    node,
    quantized,
    vector,
    metadata,
};

const hbc_cache_kind_count: usize = 4;
const vector_cache_fill_stripe_count: usize = 4096;
const vector_cache_fill_dirty_word_count: usize = vector_cache_fill_stripe_count / 64;
const shared_vector_fill_stripe_count: usize = 256;

pub const HbcCacheKindStats = struct {
    used_bytes: u64 = 0,
    peak_bytes: u64 = 0,
    hits: u64 = 0,
    misses: u64 = 0,
    insertions: u64 = 0,
    replacements: u64 = 0,
    sampled_admissions: u64 = 0,
    admission_skips: u64 = 0,
    evictions: u64 = 0,
};

pub const HbcCacheStats = struct {
    total_bytes: u64 = 0,
    accounted_bytes: u64 = 0,
    pinned_bytes: u64 = 0,
    node: HbcCacheKindStats = .{},
    quantized: HbcCacheKindStats = .{},
    vector: HbcCacheKindStats = .{},
    metadata: HbcCacheKindStats = .{},
};

const HbcPhysicalAccounting = struct {
    mutex: std.atomic.Mutex = .unlocked,
    resource_manager: ?*resource_manager_mod.ResourceManager = null,
    current_bytes: u64 = 0,
    published_bytes: std.atomic.Value(u64) = .init(0),
    pinned_bytes: std.atomic.Value(u64) = .init(0),

    fn attach(self: *HbcPhysicalAccounting, manager: *resource_manager_mod.ResourceManager) void {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (self.resource_manager == manager) return;
        const bytes = self.current_bytes;
        if (self.resource_manager) |old| {
            old.observeUsage(.hbc_node_metadata_cache, &self.current_bytes, 0);
        } else {
            self.current_bytes = 0;
        }
        self.resource_manager = manager;
        manager.observeUsage(.hbc_node_metadata_cache, &self.current_bytes, bytes);
        self.published_bytes.store(self.current_bytes, .release);
    }

    fn reserve(self: *HbcPhysicalAccounting, bytes: u64) bool {
        if (bytes == 0) return true;
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const next = std.math.add(u64, self.current_bytes, bytes) catch return false;
        if (self.resource_manager) |manager| {
            manager.adjustUsage(.hbc_node_metadata_cache, &self.current_bytes, next) catch return false;
        } else {
            self.current_bytes = next;
        }
        self.published_bytes.store(self.current_bytes, .release);
        return true;
    }

    fn release(self: *HbcPhysicalAccounting, bytes: u64, was_pinned: bool) void {
        if (bytes == 0) return;
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        if (bytes > self.current_bytes) {
            if (self.resource_manager) |manager| manager.recordAccountingError();
            return;
        }
        if (self.resource_manager) |manager| {
            manager.adjustUsage(.hbc_node_metadata_cache, &self.current_bytes, self.current_bytes - bytes) catch return;
        } else {
            self.current_bytes -= bytes;
        }
        self.published_bytes.store(self.current_bytes, .release);
        if (was_pinned) _ = self.pinned_bytes.fetchSub(bytes, .monotonic);
    }

    fn observeAllocate(self: *HbcPhysicalAccounting, bytes: u64) void {
        if (bytes == 0) return;
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const next = self.current_bytes +| bytes;
        if (self.resource_manager) |manager| {
            manager.observeUsage(.hbc_node_metadata_cache, &self.current_bytes, next);
        } else {
            self.current_bytes = next;
        }
        self.published_bytes.store(self.current_bytes, .release);
    }

    fn markPinned(self: *HbcPhysicalAccounting, bytes: u64) void {
        _ = self.pinned_bytes.fetchAdd(bytes, .monotonic);
    }

    fn current(self: *const HbcPhysicalAccounting) u64 {
        return self.published_bytes.load(.acquire);
    }
};

/// Detached shared-cache leases need namespace-local telemetry even though
/// their physical bytes remain in one process-wide ResourceManager slice.
/// This ledger is diagnostic only; HbcPhysicalAccounting remains the
/// authoritative admission and release owner.
const HbcNamespacePinnedAccounting = struct {
    alloc: Allocator,
    mutex: std.atomic.Mutex = .unlocked,
    bytes: std.AutoHashMapUnmanaged(u64, u64) = .empty,

    fn init(alloc: Allocator) HbcNamespacePinnedAccounting {
        return .{ .alloc = alloc };
    }

    fn deinit(self: *HbcNamespacePinnedAccounting) void {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        self.bytes.deinit(self.alloc);
    }

    fn mark(self: *HbcNamespacePinnedAccounting, namespace: u64, amount: u64) void {
        if (amount == 0) return;
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const entry = self.bytes.getOrPut(self.alloc, namespace) catch return;
        if (!entry.found_existing) entry.value_ptr.* = 0;
        entry.value_ptr.* +|= amount;
    }

    fn release(self: *HbcNamespacePinnedAccounting, namespace: u64, amount: u64) void {
        if (amount == 0) return;
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        const namespace_bytes = self.bytes.getPtr(namespace) orelse return;
        namespace_bytes.* -|= amount;
        if (namespace_bytes.* == 0) _ = self.bytes.remove(namespace);
    }

    fn current(self: *HbcNamespacePinnedAccounting, namespace: u64) u64 {
        lockAtomic(&self.mutex);
        defer self.mutex.unlock();
        return self.bytes.get(namespace) orelse 0;
    }
};

const NodeCacheEntry = struct {
    refs: std.atomic.Value(u32) = .init(1),
    accounting: ?*HbcPhysicalAccounting = null,
    namespace_pinned_accounting: ?*HbcNamespacePinnedAccounting = null,
    namespace: u64 = 0,
    accounted_bytes: u64 = 0,
    detached_pinned: std.atomic.Value(bool) = .init(false),
    node: Node,
};

const QuantizedCacheEntry = struct {
    refs: std.atomic.Value(u32) = .init(1),
    accounting: ?*HbcPhysicalAccounting = null,
    namespace_pinned_accounting: ?*HbcNamespacePinnedAccounting = null,
    namespace: u64 = 0,
    accounted_bytes: u64 = 0,
    detached_pinned: std.atomic.Value(bool) = .init(false),
    quantized: QuantizedSet,
};

const VectorCacheEntry = struct {
    refs: std.atomic.Value(u32) = .init(1),
    accounting: ?*HbcPhysicalAccounting = null,
    namespace_pinned_accounting: ?*HbcNamespacePinnedAccounting = null,
    namespace: u64 = 0,
    accounted_bytes: u64 = 0,
    detached_pinned: std.atomic.Value(bool) = .init(false),
    vector: []f32,
};

const MetadataCacheEntry = struct {
    refs: std.atomic.Value(u32) = .init(1),
    accounting: ?*HbcPhysicalAccounting = null,
    namespace_pinned_accounting: ?*HbcNamespacePinnedAccounting = null,
    namespace: u64 = 0,
    accounted_bytes: u64 = 0,
    detached_pinned: std.atomic.Value(bool) = .init(false),
    metadata: []u8,
};

fn detachCacheEntry(
    accounting: ?*HbcPhysicalAccounting,
    namespace_accounting: ?*HbcNamespacePinnedAccounting,
    namespace: u64,
    bytes: u64,
    refs: u32,
    detached: *std.atomic.Value(bool),
) void {
    if (accounting == null or refs <= 1) return;
    if (!detached.swap(true, .acq_rel)) {
        accounting.?.markPinned(bytes);
        if (namespace_accounting) |tracker| tracker.mark(namespace, bytes);
    }
}

fn retainNodeCacheEntry(entry: *NodeCacheEntry) void {
    _ = entry.refs.fetchAdd(1, .acq_rel);
}

fn releaseNodeCacheEntry(alloc: Allocator, entry: *NodeCacheEntry) void {
    if (entry.refs.fetchSub(1, .acq_rel) == 1) {
        const was_pinned = entry.detached_pinned.load(.acquire);
        if (entry.accounting) |accounting| accounting.release(entry.accounted_bytes, was_pinned);
        if (was_pinned) if (entry.namespace_pinned_accounting) |tracker| tracker.release(entry.namespace, entry.accounted_bytes);
        var node = entry.node;
        node.deinit(alloc);
        alloc.destroy(entry);
    }
}

fn retainQuantizedCacheEntry(entry: *QuantizedCacheEntry) void {
    _ = entry.refs.fetchAdd(1, .acq_rel);
}

fn releaseQuantizedCacheEntry(alloc: Allocator, entry: *QuantizedCacheEntry) void {
    if (entry.refs.fetchSub(1, .acq_rel) == 1) {
        const was_pinned = entry.detached_pinned.load(.acquire);
        if (entry.accounting) |accounting| accounting.release(entry.accounted_bytes, was_pinned);
        if (was_pinned) if (entry.namespace_pinned_accounting) |tracker| tracker.release(entry.namespace, entry.accounted_bytes);
        var quantized = entry.quantized;
        quantized.deinit(alloc);
        alloc.destroy(entry);
    }
}

fn retainVectorCacheEntry(entry: *VectorCacheEntry) void {
    _ = entry.refs.fetchAdd(1, .acq_rel);
}

fn releaseVectorCacheEntry(alloc: Allocator, entry: *VectorCacheEntry) void {
    if (entry.refs.fetchSub(1, .acq_rel) == 1) {
        const was_pinned = entry.detached_pinned.load(.acquire);
        if (entry.accounting) |accounting| accounting.release(entry.accounted_bytes, was_pinned);
        if (was_pinned) if (entry.namespace_pinned_accounting) |tracker| tracker.release(entry.namespace, entry.accounted_bytes);
        alloc.free(entry.vector);
        alloc.destroy(entry);
    }
}

fn retainMetadataCacheEntry(entry: *MetadataCacheEntry) void {
    _ = entry.refs.fetchAdd(1, .acq_rel);
}

fn releaseMetadataCacheEntry(alloc: Allocator, entry: *MetadataCacheEntry) void {
    if (entry.refs.fetchSub(1, .acq_rel) == 1) {
        const was_pinned = entry.detached_pinned.load(.acquire);
        if (entry.accounting) |accounting| accounting.release(entry.accounted_bytes, was_pinned);
        if (was_pinned) if (entry.namespace_pinned_accounting) |tracker| tracker.release(entry.namespace, entry.accounted_bytes);
        alloc.free(entry.metadata);
        alloc.destroy(entry);
    }
}

const BorrowedNodeLease = union(enum) {
    locked: struct {
        lock: *apply_rw_lock_mod.ApplyRwLock,
        node: *const Node,
    },
    retained: struct {
        alloc: Allocator,
        entry: *NodeCacheEntry,
    },

    pub fn ptr(self: *const BorrowedNodeLease) *const Node {
        return switch (self.*) {
            .locked => |lease| lease.node,
            .retained => |lease| &lease.entry.node,
        };
    }

    pub fn deinit(self: *BorrowedNodeLease) void {
        switch (self.*) {
            .locked => |lease| lease.lock.unlockShared(),
            .retained => |lease| releaseNodeCacheEntry(lease.alloc, lease.entry),
        }
        self.* = undefined;
    }
};

const BorrowedQuantizedLease = union(enum) {
    locked: struct {
        lock: *apply_rw_lock_mod.ApplyRwLock,
        quantized: *const QuantizedSet,
    },
    retained: struct {
        alloc: Allocator,
        entry: *QuantizedCacheEntry,
    },

    pub fn ptr(self: *const BorrowedQuantizedLease) *const QuantizedSet {
        return switch (self.*) {
            .locked => |lease| lease.quantized,
            .retained => |lease| &lease.entry.quantized,
        };
    }

    pub fn deinit(self: *BorrowedQuantizedLease) void {
        switch (self.*) {
            .locked => |lease| lease.lock.unlockShared(),
            .retained => |lease| releaseQuantizedCacheEntry(lease.alloc, lease.entry),
        }
        self.* = undefined;
    }
};

const BorrowedVectorLease = union(enum) {
    locked: struct {
        lock: *apply_rw_lock_mod.ApplyRwLock,
        vector: []const f32,
    },
    retained: struct {
        alloc: Allocator,
        entry: *VectorCacheEntry,
    },

    pub fn view(self: *const BorrowedVectorLease) []const f32 {
        return switch (self.*) {
            .locked => |lease| lease.vector,
            .retained => |lease| lease.entry.vector,
        };
    }

    pub fn deinit(self: *BorrowedVectorLease) void {
        switch (self.*) {
            .locked => |lease| lease.lock.unlockShared(),
            .retained => |lease| releaseVectorCacheEntry(lease.alloc, lease.entry),
        }
        self.* = undefined;
    }
};

const BorrowedMetadataLease = union(enum) {
    locked: struct {
        lock: *apply_rw_lock_mod.ApplyRwLock,
        metadata: []const u8,
    },
    retained: struct {
        alloc: Allocator,
        entry: *MetadataCacheEntry,
    },

    pub fn view(self: *const BorrowedMetadataLease) []const u8 {
        return switch (self.*) {
            .locked => |lease| lease.metadata,
            .retained => |lease| lease.entry.metadata,
        };
    }

    pub fn deinit(self: *BorrowedMetadataLease) void {
        switch (self.*) {
            .locked => |lease| lease.lock.unlockShared(),
            .retained => |lease| releaseMetadataCacheEntry(lease.alloc, lease.entry),
        }
        self.* = undefined;
    }
};

const HbcSharedCacheKey = struct {
    namespace: u64,
    id: u64,
};

const HbcSharedClockEntry = struct {
    key: HbcSharedCacheKey = .{ .namespace = 0, .id = 0 },
    // Shared-cache reads hold only the shared map lock. An atomic reference
    // bit lets those hits refresh CLOCK recency without upgrading every hit to
    // the exclusive admission/eviction lock.
    referenced: std.atomic.Value(bool) = .init(false),
};

const HbcSharedAdmission = struct {
    bytes: u64 = 0,
    reserved: bool = false,
    precharged: bool = false,
    overcommitted: bool = false,
};

const HbcVectorLookupStats = struct {
    hits: u64 = 0,
    misses: u64 = 0,
};

fn sharedVectorFillStripe(namespace: u64, vector_id: u64) usize {
    var value = namespace ^ std.math.rotl(u64, vector_id, 29);
    value ^= value >> 33;
    value *%= 0xff51afd7ed558ccd;
    value ^= value >> 33;
    return @intCast(value & (shared_vector_fill_stripe_count - 1));
}

fn mulDivU64(value: u64, numerator: u64, denominator: u64) u64 {
    if (value == 0 or numerator == 0 or denominator == 0) return 0;
    return @intCast(@min(@as(u128, std.math.maxInt(u64)), (@as(u128, value) * numerator) / denominator));
}

fn firstNamespaceKey(map: anytype, namespace: u64, protected: HbcSharedCacheKey) ?HbcSharedCacheKey {
    var it = map.keyIterator();
    while (it.next()) |key| {
        if (key.namespace != namespace) continue;
        if (key.namespace == protected.namespace and key.id == protected.id) continue;
        return key.*;
    }
    return null;
}

fn hbcCacheNamespace(path: []const u8) u64 {
    const hash = std.hash.Wyhash.hash(0xa6f9_19e5_cace_f00d, path);
    return if (hash == 0) 1 else hash;
}

fn hbcCacheStablePathAlloc(alloc: Allocator, path: []const u8) ![]u8 {
    if (comptime builtin.os.tag == .freestanding) {
        return try alloc.dupe(u8, path);
    } else {
        var io_impl = std.Io.Threaded.init(alloc, .{});
        defer io_impl.deinit();

        const absolute_path = if (std.fs.path.isAbsolute(path))
            try alloc.dupe(u8, path)
        else blk: {
            const cwd = try std.process.currentPathAlloc(io_impl.io(), alloc);
            defer alloc.free(cwd);
            break :blk try std.fs.path.resolve(alloc, &.{ cwd, path });
        };
        errdefer alloc.free(absolute_path);
        if (!std.fs.path.isAbsolute(absolute_path)) return absolute_path;

        const canonical_z = std.Io.Dir.realPathFileAbsoluteAlloc(io_impl.io(), absolute_path, alloc) catch return absolute_path;
        defer alloc.free(canonical_z);
        const canonical = try alloc.dupe(u8, canonical_z);
        alloc.free(absolute_path);
        return canonical;
    }
}

fn hbcCacheNamespaceStable(alloc: Allocator, path: []const u8) u64 {
    const stable_path = hbcCacheStablePathAlloc(alloc, path) catch return hbcCacheNamespace(path);
    defer alloc.free(stable_path);
    return hbcCacheNamespace(stable_path);
}

const HbcCacheIdentity = struct {
    namespace: u64,
    stable_path: []u8,
};

const HbcNamespacePathRegistration = struct {
    path: []u8,
    active_owners: usize,
    weight: u32 = 1,
};

fn hbcCacheIdentityAlloc(alloc: Allocator, path: []const u8) !HbcCacheIdentity {
    const stable_path = hbcCacheStablePathAlloc(alloc, path) catch try alloc.dupe(u8, path);
    return .{
        .namespace = hbcCacheNamespace(stable_path),
        .stable_path = stable_path,
    };
}

fn hbcPathHasPrefixBoundary(path: []const u8, prefix: []const u8) bool {
    if (std.mem.eql(u8, path, prefix)) return true;
    if (!std.mem.startsWith(u8, path, prefix)) return false;
    if (path.len <= prefix.len) return false;
    return path[prefix.len] == std.fs.path.sep;
}

fn hbcKindStats(stats: *HbcCacheStats, kind: HbcCacheKind) *HbcCacheKindStats {
    return switch (kind) {
        .node => &stats.node,
        .quantized => &stats.quantized,
        .vector => &stats.vector,
        .metadata => &stats.metadata,
    };
}

fn snapshotHbcKindStats(stored: *HbcCacheKindStats) HbcCacheKindStats {
    // The cache map lock stabilizes structural counters. Hits and misses are
    // intentionally updated while readers share that lock, so they must be
    // loaded atomically instead of copying the containing struct.
    return .{
        .used_bytes = stored.used_bytes,
        .peak_bytes = stored.peak_bytes,
        .hits = @atomicLoad(u64, &stored.hits, .monotonic),
        .misses = @atomicLoad(u64, &stored.misses, .monotonic),
        .insertions = stored.insertions,
        .replacements = stored.replacements,
        .sampled_admissions = stored.sampled_admissions,
        .admission_skips = stored.admission_skips,
        .evictions = stored.evictions,
    };
}

fn snapshotHbcCacheStats(stored: *HbcCacheStats) HbcCacheStats {
    return .{
        .total_bytes = stored.total_bytes,
        .accounted_bytes = stored.accounted_bytes,
        .pinned_bytes = stored.pinned_bytes,
        .node = snapshotHbcKindStats(&stored.node),
        .quantized = snapshotHbcKindStats(&stored.quantized),
        .vector = snapshotHbcKindStats(&stored.vector),
        .metadata = snapshotHbcKindStats(&stored.metadata),
    };
}

fn hbcResourceCacheClass(kind: HbcCacheKind) resource_manager_mod.HbcCacheClass {
    return switch (kind) {
        .node => .node,
        .quantized => .quantized,
        .vector => .vector,
        .metadata => .metadata,
    };
}

fn addHbcKindBytes(stats: *HbcCacheStats, kind: HbcCacheKind, bytes: u64) void {
    if (bytes == 0) return;
    stats.total_bytes +|= bytes;
    const kind_stats = hbcKindStats(stats, kind);
    kind_stats.used_bytes +|= bytes;
    kind_stats.peak_bytes = @max(kind_stats.peak_bytes, kind_stats.used_bytes);
}

fn removeHbcKindBytes(stats: *HbcCacheStats, kind: HbcCacheKind, bytes: u64) void {
    if (bytes == 0) return;
    stats.total_bytes -|= bytes;
    hbcKindStats(stats, kind).used_bytes -|= bytes;
}

fn noteHbcKindInsertion(stats: *HbcCacheStats, kind: HbcCacheKind) void {
    hbcKindStats(stats, kind).insertions += 1;
}

fn noteHbcKindReplacement(stats: *HbcCacheStats, kind: HbcCacheKind) void {
    hbcKindStats(stats, kind).replacements += 1;
}

fn noteHbcKindSampledAdmission(stats: *HbcCacheStats, kind: HbcCacheKind) void {
    hbcKindStats(stats, kind).sampled_admissions += 1;
}

fn noteHbcKindAdmissionSkip(stats: *HbcCacheStats, kind: HbcCacheKind) void {
    hbcKindStats(stats, kind).admission_skips += 1;
}

fn cacheFillEpochCurrent(fill_epoch: ?*const std.atomic.Value(u64), expected_epoch: u64) bool {
    const epoch = fill_epoch orelse return true;
    return expected_epoch & 1 == 0 and epoch.load(.acquire) == expected_epoch;
}

fn noteHbcKindEviction(stats: *HbcCacheStats, kind: HbcCacheKind) void {
    hbcKindStats(stats, kind).evictions += 1;
}

/// Read-optimized ownership fence for the process-wide HBC cache maps.
///
/// `ApplyRwLock` intentionally provides a writer-preferring service fence for
/// apply/runtime coordination, but its shared path takes two mutexes and
/// updates several global telemetry atomics. A vector query can perform
/// hundreds of cache lookups, so that design turns read-only hits into one
/// process-wide serialization point. Cache readers need only stabilize the
/// maps long enough to retain an entry; entry lifetime is then ref-counted.
///
/// This lock therefore uses one atomic word for ownership and a writer-intent
/// counter to bound writer starvation. A separate writer-admission gate keeps
/// nonblocking reclaim from bypassing an already queued structural writer. The
/// read hot path performs one CAS on acquire and one subtraction on release,
/// with no telemetry writes. Writers retain exclusive map/invalidation
/// semantics.
const CacheRwLock = struct {
    const vector_read_stripe_count = 64;
    const writer_bit: usize = @as(usize, 1) << (@bitSizeOf(usize) - 1);
    const reader_mask: usize = writer_bit - 1;

    state: std.atomic.Value(usize) = .init(0),
    writers_waiting: std.atomic.Value(u32) = .init(0),
    writer_gate: std.atomic.Mutex = .unlocked,
    vector_fence_pending: std.atomic.Value(bool) = .init(false),
    // Vector lookups dominate the shared-cache hot path. Key-striped reader
    // ownership lets independent lookups proceed without modifying the same
    // global reader-count word. Structural writers fence every stripe before
    // mutating the authoritative hash map.
    vector_read_stripes: [vector_read_stripe_count]std.atomic.Mutex = .{.unlocked} ** vector_read_stripe_count,
    exclusive_lock_calls: AtomicU64 = .init(0),
    exclusive_contended_calls: AtomicU64 = .init(0),
    exclusive_wait_ns: AtomicU64 = .init(0),
    exclusive_max_wait_ns: AtomicU64 = .init(0),

    fn backoff(attempts: usize) void {
        if (builtin.os.tag == .freestanding or builtin.single_threaded or attempts < 64) {
            std.atomic.spinLoopHint();
        } else {
            std.Thread.yield() catch {};
        }
    }

    fn lockShared(self: *@This()) void {
        var attempts: usize = 0;
        while (true) : (attempts += 1) {
            if (self.writers_waiting.load(.acquire) != 0) {
                backoff(attempts);
                continue;
            }
            const observed = self.state.load(.monotonic);
            std.debug.assert(observed & reader_mask != reader_mask);
            if (observed & writer_bit != 0 or
                self.state.cmpxchgWeak(observed, observed + 1, .acquire, .monotonic) != null)
            {
                backoff(attempts);
                continue;
            }
            return;
        }
    }

    fn tryLockShared(self: *@This()) bool {
        if (self.writers_waiting.load(.acquire) != 0) return false;
        const observed = self.state.load(.monotonic);
        if (observed & writer_bit != 0 or observed & reader_mask == reader_mask) return false;
        return self.state.cmpxchgStrong(observed, observed + 1, .acquire, .monotonic) == null;
    }

    fn unlockShared(self: *@This()) void {
        const previous = self.state.fetchSub(1, .release);
        std.debug.assert(previous & writer_bit == 0 and previous & reader_mask != 0);
    }

    fn vectorReadStripe(namespace: u64, vector_id: u64) usize {
        return sharedVectorFillStripe(namespace, vector_id) & (vector_read_stripe_count - 1);
    }

    fn lockVectorShared(self: *@This(), namespace: u64, vector_id: u64) usize {
        const stripe = vectorReadStripe(namespace, vector_id);
        var attempts: usize = 0;
        while (true) : (attempts += 1) {
            // Once a structural writer publishes intent, stop admitting new
            // striped readers so it can fence the finite set already active.
            if (!self.vector_fence_pending.load(.acquire) and
                self.vector_read_stripes[stripe].tryLock()) break;
            backoff(attempts);
        }
        return stripe;
    }

    fn unlockVectorShared(self: *@This(), stripe: usize) void {
        self.vector_read_stripes[stripe].unlock();
    }

    fn lockVectorStripes(self: *@This()) bool {
        var contended = false;
        for (&self.vector_read_stripes) |*stripe| {
            var attempts: usize = 0;
            while (!stripe.tryLock()) : (attempts += 1) {
                contended = true;
                backoff(attempts);
            }
        }
        return contended;
    }

    fn tryLockVectorStripes(self: *@This()) ?usize {
        for (&self.vector_read_stripes, 0..) |*stripe, index| {
            if (!stripe.tryLock()) return index;
        }
        return vector_read_stripe_count;
    }

    fn unlockVectorStripes(self: *@This(), count: usize) void {
        var remaining = count;
        while (remaining != 0) {
            remaining -= 1;
            self.vector_read_stripes[remaining].unlock();
        }
    }

    fn tryLockExclusive(self: *@This()) bool {
        _ = self.exclusive_lock_calls.fetchAdd(1, .monotonic);

        // The first check avoids competing for the gate when a blocking writer
        // has already announced intent. The second closes the race where that
        // announcement occurs between the first check and gate acquisition.
        if (self.writers_waiting.load(.acquire) != 0) return false;
        if (!self.writer_gate.tryLock()) return false;
        if (self.writers_waiting.load(.acquire) != 0) {
            self.writer_gate.unlock();
            return false;
        }

        // Fence striped readers before testing the global ownership word. A
        // failed nonblocking acquisition rolls the fence back immediately.
        self.vector_fence_pending.store(true, .release);
        if (self.state.cmpxchgStrong(0, writer_bit, .acquire, .monotonic) != null) {
            self.vector_fence_pending.store(false, .release);
            self.writer_gate.unlock();
            return false;
        }
        const locked = self.tryLockVectorStripes() orelse unreachable;
        if (locked != vector_read_stripe_count) {
            self.unlockVectorStripes(locked);
            self.state.store(0, .release);
            self.vector_fence_pending.store(false, .release);
            self.writer_gate.unlock();
            return false;
        }
        return true;
    }

    fn lockExclusive(self: *@This()) void {
        const started_ns = nowNs();
        _ = self.exclusive_lock_calls.fetchAdd(1, .monotonic);
        _ = self.writers_waiting.fetchAdd(1, .acq_rel);

        var gate_attempts: usize = 0;
        while (!self.writer_gate.tryLock()) : (gate_attempts += 1) {
            backoff(gate_attempts);
        }

        // Publishing this fence while holding the admission gate turns the
        // striped-reader population into a finite set before the writer drains
        // either ownership domain.
        self.vector_fence_pending.store(true, .release);

        var state_attempts: usize = 0;
        while (self.state.cmpxchgWeak(0, writer_bit, .acquire, .monotonic) != null) : (state_attempts += 1) {
            backoff(state_attempts);
        }
        const stripes_contended = self.lockVectorStripes();
        _ = self.writers_waiting.fetchSub(1, .release);

        if (gate_attempts != 0 or state_attempts != 0 or stripes_contended) {
            const waited_ns = elapsedSince(started_ns);
            _ = self.exclusive_contended_calls.fetchAdd(1, .monotonic);
            _ = self.exclusive_wait_ns.fetchAdd(waited_ns, .monotonic);
            var maximum = self.exclusive_max_wait_ns.load(.monotonic);
            while (waited_ns > maximum) {
                maximum = self.exclusive_max_wait_ns.cmpxchgWeak(maximum, waited_ns, .monotonic, .monotonic) orelse break;
            }
        }
    }

    fn unlockExclusive(self: *@This()) void {
        self.unlockVectorStripes(vector_read_stripe_count);
        const previous = self.state.swap(0, .release);
        std.debug.assert(previous == writer_bit);
        self.vector_fence_pending.store(false, .release);
        self.writer_gate.unlock();
    }

    fn snapshot(self: *const @This()) apply_rw_lock_mod.ApplyRwLock.Stats {
        return .{
            // Deliberately zero: exact shared telemetry would reintroduce a
            // globally written cache line on every vector hit.
            .shared_lock_calls = 0,
            .shared_contended_calls = 0,
            .shared_wait_ns = 0,
            .shared_max_wait_ns = 0,
            .exclusive_lock_calls = self.exclusive_lock_calls.load(.monotonic),
            .exclusive_contended_calls = self.exclusive_contended_calls.load(.monotonic),
            .exclusive_wait_ns = self.exclusive_wait_ns.load(.monotonic),
            .exclusive_max_wait_ns = self.exclusive_max_wait_ns.load(.monotonic),
        };
    }
};

pub const Cache = struct {
    alloc: Allocator,
    mutex: CacheRwLock = .{},
    resource_manager: ?*resource_manager_mod.ResourceManager = null,
    reclaimer_identity: u64 = 0,
    physical_accounting: HbcPhysicalAccounting = .{},
    namespace_pinned_accounting: HbcNamespacePinnedAccounting,
    admission_target_bytes: std.atomic.Value(u64) = .init(0),
    concurrent_vector_admission_stride: std.atomic.Value(u32) = .init(1),
    concurrent_vector_admission_counter: std.atomic.Value(u64) = .init(0),
    // Counts live query-level decoded-residency leases across namespaces.
    // Serial cold starts fill eagerly; only genuinely overlapping fills use
    // the normal-pressure sampling doorkeeper.
    decoded_query_active_leases: std.atomic.Value(u64) = .init(0),
    decoded_query_reserved_bytes: std.atomic.Value(u64) = .init(0),
    // Query leases claim logical capacity before their primary-store batch is
    // read, then atomically transfer that entitlement to physical precharge.
    // This prevents concurrent cold-start requests from all observing the
    // same free bytes without charging the full request up front.
    decoded_query_entitled_bytes: std.atomic.Value(u64) = .init(0),
    decoded_query_replacement_entitled_bytes: std.atomic.Value(u64) = .init(0),
    /// Coalesce duplicate exact-vector publication and keep cloning outside
    /// the global map/admission lock. These locks do not guard visibility;
    /// the map lock plus HBC's mutation epoch remain authoritative.
    vector_fill_mutexes: [shared_vector_fill_stripe_count]std.atomic.Mutex = .{.unlocked} ** shared_vector_fill_stripe_count,
    global_stats: HbcCacheStats = .{},
    namespace_stats: std.AutoHashMapUnmanaged(u64, HbcCacheStats) = .empty,
    node_cache: std.AutoHashMapUnmanaged(HbcSharedCacheKey, *NodeCacheEntry) = .empty,
    node_slots: std.AutoHashMapUnmanaged(HbcSharedCacheKey, usize) = .empty,
    node_clock: std.ArrayListUnmanaged(HbcSharedClockEntry) = .empty,
    node_hand: usize = 0,
    quantized_cache: std.AutoHashMapUnmanaged(HbcSharedCacheKey, *QuantizedCacheEntry) = .empty,
    quantized_slots: std.AutoHashMapUnmanaged(HbcSharedCacheKey, usize) = .empty,
    quantized_clock: std.ArrayListUnmanaged(HbcSharedClockEntry) = .empty,
    quantized_hand: usize = 0,
    vector_cache: std.AutoHashMapUnmanaged(HbcSharedCacheKey, *VectorCacheEntry) = .empty,
    vector_slots: std.AutoHashMapUnmanaged(HbcSharedCacheKey, usize) = .empty,
    vector_clock: std.ArrayListUnmanaged(HbcSharedClockEntry) = .empty,
    vector_hand: usize = 0,
    // Lookup counters follow the same ownership stripes as vector reads. This
    // avoids recreating one globally written cache line solely for telemetry.
    vector_lookup_stats: [CacheRwLock.vector_read_stripe_count]std.AutoHashMapUnmanaged(u64, HbcVectorLookupStats) =
        .{std.AutoHashMapUnmanaged(u64, HbcVectorLookupStats).empty} ** CacheRwLock.vector_read_stripe_count,
    metadata_cache: std.AutoHashMapUnmanaged(HbcSharedCacheKey, *MetadataCacheEntry) = .empty,
    metadata_slots: std.AutoHashMapUnmanaged(HbcSharedCacheKey, usize) = .empty,
    metadata_clock: std.ArrayListUnmanaged(HbcSharedClockEntry) = .empty,
    metadata_hand: usize = 0,
    namespace_paths: std.AutoHashMapUnmanaged(u64, HbcNamespacePathRegistration) = .empty,

    pub fn init(alloc: Allocator) Cache {
        return .{
            .alloc = alloc,
            .namespace_pinned_accounting = HbcNamespacePinnedAccounting.init(alloc),
        };
    }

    pub fn deinit(self: *Cache) void {
        if (self.resource_manager) |manager| {
            manager.unregisterReclaimer(self.reclaimer_identity);
            self.reclaimer_identity = 0;
        }
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();

        self.clearAllLocked();
        self.namespace_stats.deinit(self.alloc);
        self.node_cache.deinit(self.alloc);
        self.node_slots.deinit(self.alloc);
        self.node_clock.deinit(self.alloc);
        self.quantized_cache.deinit(self.alloc);
        self.quantized_slots.deinit(self.alloc);
        self.quantized_clock.deinit(self.alloc);
        self.vector_cache.deinit(self.alloc);
        self.vector_slots.deinit(self.alloc);
        self.vector_clock.deinit(self.alloc);
        for (&self.vector_lookup_stats) |*stats| stats.deinit(self.alloc);
        self.metadata_cache.deinit(self.alloc);
        self.metadata_slots.deinit(self.alloc);
        self.metadata_clock.deinit(self.alloc);
        self.namespace_pinned_accounting.deinit();
        var path_it = self.namespace_paths.valueIterator();
        while (path_it.next()) |registration| self.alloc.free(registration.path);
        self.namespace_paths.deinit(self.alloc);
    }

    pub fn attachResourceManager(self: *Cache, resource_manager: *resource_manager_mod.ResourceManager) void {
        self.mutex.lockExclusive();
        if (self.resource_manager == resource_manager and self.reclaimer_identity != 0) {
            self.mutex.unlockExclusive();
            return;
        }
        const old_manager = self.resource_manager;
        const old_reclaimer = self.reclaimer_identity;
        self.resource_manager = resource_manager;
        self.reclaimer_identity = 0;
        self.physical_accounting.attach(resource_manager);
        self.refreshAdmissionPolicy(resource_manager);
        self.mutex.unlockExclusive();

        if (old_manager) |manager| manager.unregisterReclaimer(old_reclaimer);
        const identity = resource_manager.registerReclaimer(
            .hbc_node_metadata_cache,
            self,
            reclaimForResourceManager,
        ) catch |err| {
            std.log.err("failed to register shared HBC cache reclaimer: {s}", .{@errorName(err)});
            return;
        };
        self.mutex.lockExclusive();
        if (self.resource_manager == resource_manager and self.reclaimer_identity == 0) {
            self.reclaimer_identity = identity;
            self.mutex.unlockExclusive();
        } else {
            self.mutex.unlockExclusive();
            resource_manager.unregisterReclaimer(identity);
        }
    }

    fn refreshAdmissionPolicy(self: *Cache, manager: *resource_manager_mod.ResourceManager) void {
        const policy = manager.hbcCachePolicy();
        self.admission_target_bytes.store(policy.target_bytes, .release);
        self.concurrent_vector_admission_stride.store(policy.concurrent_vector_admission_stride, .release);
    }

    fn concurrentVectorAdmissionStride(self: *Cache) u32 {
        var stride = self.concurrent_vector_admission_stride.load(.acquire);
        const target = self.admission_target_bytes.load(.acquire);
        // At a steady full target, pressure may read as "normal" immediately
        // after synchronous eviction. Keep a doorkeeper active based on
        // fullness itself so unique misses cannot serialize every query on
        // clone/insert/evict churn.
        const committed = self.physical_accounting.current() +| self.decoded_query_entitled_bytes.load(.acquire);
        if (target > 0 and committed >= target) stride = @max(stride, 8);
        return stride;
    }

    fn admitConcurrentVectorTicket(self: *Cache, namespace: u64, stride: u32) bool {
        if (stride == 0) return false;
        if (stride == 1) return true;
        const ticket = self.concurrent_vector_admission_counter.fetchAdd(1, .monotonic);
        if (ticket % stride != 0) return false;
        self.mutex.lockExclusive();
        noteHbcKindSampledAdmission(&self.global_stats, .vector);
        if (self.namespace_stats.getPtr(namespace)) |stats| noteHbcKindSampledAdmission(stats, .vector);
        self.mutex.unlockExclusive();
        return true;
    }

    pub fn shouldAdmitConcurrentVector(self: *Cache, namespace: u64) bool {
        return self.admitConcurrentVectorTicket(namespace, self.concurrentVectorAdmissionStride());
    }

    fn tryReserveDecodedQuery(
        self: *Cache,
        namespace: u64,
        requested_bytes: u64,
        vector_bytes: u64,
    ) ?DecodedVectorResidencyLease {
        if (self.resource_manager) |manager| self.refreshAdmissionPolicy(manager);
        if (requested_bytes == 0 or vector_bytes == 0) return null;
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();

        const target = self.admission_target_bytes.load(.acquire);
        const current = self.physical_accounting.current();
        const entitled = self.decoded_query_entitled_bytes.load(.acquire);
        const committed = current +| entitled;
        const free_bytes = if (target > 0) target -| @min(target, committed) else std.math.maxInt(u64);
        var stride = self.concurrent_vector_admission_stride.load(.acquire);
        if (stride != 0) {
            if (self.decoded_query_active_leases.load(.acquire) != 0) stride = @max(stride, 8);
            if (requested_bytes > free_bytes) stride = @max(stride, 8);
        }
        if (stride == 0) return null;

        // Under saturation, admit a bounded replacement window for one sampled
        // request instead of freezing the decoded resident set or letting one
        // large exact request rotate the entire cache. The session hands off to
        // retained LSM ownership at a batch boundary when this entitlement is
        // exhausted.
        const capacity_bytes = if (target > 0 and stride > 1)
            @min(requested_bytes, @max(vector_bytes, target / 8))
        else
            requested_bytes;
        if (target > 0 and capacity_bytes > target) return null;

        const replacement = target > 0 and capacity_bytes > free_bytes;
        if (replacement) {
            // Logical cold-start claims are not evictable and therefore cannot
            // back a replacement window. Require enough physical residency and
            // permit only one untransferred replacement window at a time.
            if (capacity_bytes > current) return null;
            if (self.decoded_query_replacement_entitled_bytes.load(.acquire) != 0) return null;
        }
        if (stride > 1) {
            const ticket = self.concurrent_vector_admission_counter.fetchAdd(1, .monotonic);
            if (ticket % stride != 0) return null;
        }

        self.decoded_query_entitled_bytes.store(entitled +| capacity_bytes, .release);
        if (replacement) self.decoded_query_replacement_entitled_bytes.store(capacity_bytes, .release);
        if (stride > 1) {
            noteHbcKindSampledAdmission(&self.global_stats, .vector);
            if (self.namespace_stats.getPtr(namespace)) |stats| noteHbcKindSampledAdmission(stats, .vector);
        }
        const previous_active = self.decoded_query_active_leases.fetchAdd(1, .acq_rel);
        std.debug.assert(previous_active != std.math.maxInt(u64));
        return .{
            .cache = self,
            .namespace = namespace,
            .capacity_bytes = capacity_bytes,
            .entitled_bytes = capacity_bytes,
            .replacement_entitlement = replacement,
        };
    }

    fn reserveDecodedQueryCapacity(self: *Cache, lease: *DecodedVectorResidencyLease, requested_bytes: u64) bool {
        if (requested_bytes == 0) return true;
        if (!lease.active or lease.cache != self) return false;
        if (lease.reserved_bytes >= requested_bytes) return true;
        const additional_bytes = requested_bytes - lease.reserved_bytes;
        const committed_bytes = lease.published_bytes +| lease.reserved_bytes;
        if (committed_bytes > lease.capacity_bytes or additional_bytes > lease.capacity_bytes - committed_bytes) return false;
        if (additional_bytes > lease.entitled_bytes) return false;

        // Capacity is charged before the primary-store read. Consequently all
        // ordinary cache admissions and other ResourceManager users see these
        // bytes as unavailable, and publication can atomically transfer the
        // charge to the retained entry without a second reservation.
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        const target = self.admission_target_bytes.load(.acquire);
        if (target > 0) {
            while (self.physical_accounting.current() > target -| @min(target, additional_bytes)) {
                if (!self.evictOneLocked(.{ .namespace = 0, .id = 0 })) return false;
            }
        }
        if (!self.physical_accounting.reserve(additional_bytes)) return false;
        const entitled = self.decoded_query_entitled_bytes.load(.acquire);
        std.debug.assert(entitled >= additional_bytes);
        self.decoded_query_entitled_bytes.store(entitled - additional_bytes, .release);
        if (lease.replacement_entitlement) {
            const replacement_entitled = self.decoded_query_replacement_entitled_bytes.load(.acquire);
            std.debug.assert(replacement_entitled >= additional_bytes);
            self.decoded_query_replacement_entitled_bytes.store(replacement_entitled - additional_bytes, .release);
        }
        lease.entitled_bytes -= additional_bytes;
        _ = self.decoded_query_reserved_bytes.fetchAdd(additional_bytes, .acq_rel);
        lease.reserved_bytes += additional_bytes;
        return true;
    }

    fn releaseDecodedQueryEntitlement(self: *Cache, bytes: u64, replacement: bool) void {
        if (bytes == 0) return;
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        const entitled = self.decoded_query_entitled_bytes.load(.acquire);
        std.debug.assert(entitled >= bytes);
        self.decoded_query_entitled_bytes.store(entitled - bytes, .release);
        if (replacement) {
            const replacement_entitled = self.decoded_query_replacement_entitled_bytes.load(.acquire);
            std.debug.assert(replacement_entitled >= bytes);
            self.decoded_query_replacement_entitled_bytes.store(replacement_entitled - bytes, .release);
        }
    }

    fn releaseDecodedQueryReservation(self: *Cache, bytes: u64) void {
        if (bytes == 0) return;
        const previous = self.decoded_query_reserved_bytes.fetchSub(bytes, .acq_rel);
        std.debug.assert(previous >= bytes);
        self.physical_accounting.release(bytes, false);
    }

    fn consumeDecodedQueryReservation(self: *Cache, bytes: u64) void {
        if (bytes == 0) return;
        const previous = self.decoded_query_reserved_bytes.fetchSub(bytes, .acq_rel);
        std.debug.assert(previous >= bytes);
    }

    fn releaseDecodedQueryLease(self: *Cache, reserved_bytes: u64, entitled_bytes: u64, replacement: bool) void {
        self.releaseDecodedQueryReservation(reserved_bytes);
        self.releaseDecodedQueryEntitlement(entitled_bytes, replacement);
        // Publish the lease retirement only after all of its physical and
        // logical capacity is returned. A concurrent acquirer may sample once
        // unnecessarily, but can never observe false serial availability.
        const previous = self.decoded_query_active_leases.fetchSub(1, .acq_rel);
        std.debug.assert(previous != 0);
    }

    fn reclaimForResourceManager(context: *anyopaque, target_bytes: u64) u64 {
        const self: *Cache = @ptrCast(@alignCast(context));
        if (target_bytes == 0 or !self.mutex.tryLockExclusive()) return 0;
        defer self.mutex.unlockExclusive();
        const before = self.physical_accounting.current();
        while (before -| self.physical_accounting.current() < target_bytes) {
            if (!self.evictOneLocked(.{ .namespace = 0, .id = 0 })) break;
        }
        return before -| self.physical_accounting.current();
    }

    pub fn namespaceStats(self: *Cache, namespace: u64) HbcCacheStats {
        self.mutex.lockShared();
        defer self.mutex.unlockShared();
        var stats = if (self.namespace_stats.getPtr(namespace)) |stored|
            snapshotHbcCacheStats(stored)
        else
            HbcCacheStats{};
        // Shared vector-cache lookups are tracked per read stripe. Structural
        // map changes require exclusive ownership, so the global shared lock
        // stabilizes these maps while their counters are loaded atomically.
        stats.vector.hits = 0;
        stats.vector.misses = 0;
        for (&self.vector_lookup_stats) |*lookup_stats| {
            if (lookup_stats.getPtr(namespace)) |stored| {
                stats.vector.hits +|= @atomicLoad(u64, &stored.hits, .monotonic);
                stats.vector.misses +|= @atomicLoad(u64, &stored.misses, .monotonic);
            }
        }
        stats.pinned_bytes = self.namespace_pinned_accounting.current(namespace);
        stats.accounted_bytes = stats.total_bytes +| stats.pinned_bytes;
        return stats;
    }

    fn noteLookupLocked(self: *Cache, kind: HbcCacheKind, namespace: u64, hit: bool) void {
        const global = hbcKindStats(&self.global_stats, kind);
        if (hit) {
            _ = @atomicRmw(u64, &global.hits, .Add, 1, .monotonic);
        } else {
            _ = @atomicRmw(u64, &global.misses, .Add, 1, .monotonic);
        }
        if (self.namespace_stats.getPtr(namespace)) |stats| {
            const counters = hbcKindStats(stats, kind);
            if (hit) {
                _ = @atomicRmw(u64, &counters.hits, .Add, 1, .monotonic);
            } else {
                _ = @atomicRmw(u64, &counters.misses, .Add, 1, .monotonic);
            }
        }
    }

    fn noteVectorLookupStriped(self: *Cache, stripe: usize, namespace: u64, hit: bool) void {
        const stats = self.vector_lookup_stats[stripe].getPtr(namespace) orelse return;
        if (hit) {
            _ = @atomicRmw(u64, &stats.hits, .Add, 1, .monotonic);
        } else {
            _ = @atomicRmw(u64, &stats.misses, .Add, 1, .monotonic);
        }
    }

    fn ensureVectorLookupStatsLocked(self: *Cache, namespace: u64) !void {
        // Stripe entries are installed and removed as one namespace-wide set,
        // so stripe zero is the allocation-free fast-path sentinel after the
        // first registration or successful vector admission.
        if (self.vector_lookup_stats[0].contains(namespace)) return;
        var created: [CacheRwLock.vector_read_stripe_count]bool =
            .{false} ** CacheRwLock.vector_read_stripe_count;
        errdefer for (&self.vector_lookup_stats, 0..) |*lookup_stats, stripe| {
            if (created[stripe]) std.debug.assert(lookup_stats.remove(namespace));
        };

        for (&self.vector_lookup_stats, 0..) |*lookup_stats, stripe| {
            const entry = try lookup_stats.getOrPut(self.alloc, namespace);
            if (!entry.found_existing) {
                entry.value_ptr.* = .{};
                created[stripe] = true;
            }
        }
    }

    pub fn invalidateNamespace(self: *Cache, namespace: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        while (self.evictNamespaceEntryLocked(namespace, .vector)) {}
        while (self.evictNamespaceEntryLocked(namespace, .metadata)) {}
        while (self.evictNamespaceEntryLocked(namespace, .quantized)) {}
        while (self.evictNamespaceEntryLocked(namespace, .node)) {}
    }

    pub fn registerNamespacePath(self: *Cache, namespace: u64, path: []const u8) bool {
        return self.registerNamespacePathWithWeight(namespace, path, 1);
    }

    pub fn registerNamespacePathWithWeight(self: *Cache, namespace: u64, path: []const u8, requested_weight: u32) bool {
        const weight = @max(@as(u32, 1), requested_weight);
        const stable_path = hbcCacheStablePathAlloc(self.alloc, path) catch return false;
        errdefer self.alloc.free(stable_path);

        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        self.pruneUnusedNamespacePathsLocked();

        // Registered namespaces always have an accounting slot. This makes
        // retained-entry detection O(1) without adding cache hot-path state.
        const stats_entry = self.namespace_stats.getOrPut(self.alloc, namespace) catch {
            self.alloc.free(stable_path);
            return false;
        };
        if (!stats_entry.found_existing) stats_entry.value_ptr.* = .{};
        self.ensureVectorLookupStatsLocked(namespace) catch {
            if (!stats_entry.found_existing) _ = self.namespace_stats.remove(namespace);
            self.alloc.free(stable_path);
            return false;
        };

        const entry = self.namespace_paths.getOrPut(self.alloc, namespace) catch {
            if (!stats_entry.found_existing) self.removeNamespaceStateIfUnusedLocked(namespace);
            self.alloc.free(stable_path);
            return false;
        };
        if (!entry.found_existing) {
            entry.value_ptr.* = .{ .path = stable_path, .active_owners = 1, .weight = weight };
            return true;
        }
        if (std.mem.eql(u8, entry.value_ptr.path, stable_path) and entry.value_ptr.weight == weight) {
            entry.value_ptr.active_owners += 1;
            self.alloc.free(stable_path);
            return true;
        }

        self.alloc.free(stable_path);
        return false;
    }

    pub fn unregisterNamespacePath(self: *Cache, namespace: u64, stable_path: []const u8) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();

        const registration = self.namespace_paths.getPtr(namespace) orelse return;
        if (!std.mem.eql(u8, registration.path, stable_path) or registration.active_owners == 0) return;
        registration.active_owners -= 1;
        self.removeNamespaceStateIfUnusedLocked(namespace);
    }

    pub fn invalidatePath(self: *Cache, path: []const u8) void {
        const stable_prefix = hbcCacheStablePathAlloc(self.alloc, path) catch {
            self.clear();
            return;
        };
        defer self.alloc.free(stable_prefix);

        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();

        while (self.evictPathPrefixEntryLocked(stable_prefix, .vector)) {}
        while (self.evictPathPrefixEntryLocked(stable_prefix, .metadata)) {}
        while (self.evictPathPrefixEntryLocked(stable_prefix, .quantized)) {}
        while (self.evictPathPrefixEntryLocked(stable_prefix, .node)) {}
        self.pruneUnusedNamespacePathsLocked();
    }

    pub fn clear(self: *Cache) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        self.clearAllLocked();
        self.pruneUnusedNamespacePathsLocked();
    }

    pub fn clearNodeNamespace(self: *Cache, namespace: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        while (self.evictNamespaceEntryLocked(namespace, .node)) {}
    }

    pub fn clearQuantizedNamespace(self: *Cache, namespace: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        while (self.evictNamespaceEntryLocked(namespace, .quantized)) {}
    }

    pub fn clearVectorNamespace(self: *Cache, namespace: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        while (self.evictNamespaceEntryLocked(namespace, .vector)) {}
    }

    pub fn clearMetadataNamespace(self: *Cache, namespace: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        while (self.evictNamespaceEntryLocked(namespace, .metadata)) {}
    }

    pub fn invalidateNode(self: *Cache, namespace: u64, node_id: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        _ = self.removeNodeLocked(.{ .namespace = namespace, .id = node_id }, false);
    }

    pub fn invalidateQuantized(self: *Cache, namespace: u64, node_id: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        _ = self.removeQuantizedLocked(.{ .namespace = namespace, .id = node_id }, false);
    }

    pub fn invalidateVector(self: *Cache, namespace: u64, vector_id: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        _ = self.removeVectorLocked(.{ .namespace = namespace, .id = vector_id }, false);
    }

    pub fn invalidateMetadata(self: *Cache, namespace: u64, vector_id: u64) void {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        _ = self.removeMetadataLocked(.{ .namespace = namespace, .id = vector_id }, false);
    }

    pub fn borrowNode(self: *Cache, namespace: u64, node_id: u64) ?BorrowedNodeLease {
        self.mutex.lockShared();
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = node_id };
        if (self.node_cache.get(key)) |entry| {
            self.noteLookupLocked(.node, namespace, true);
            self.touchSlot(&self.node_clock, self.node_slots.get(key));
            retainNodeCacheEntry(entry);
            self.mutex.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteLookupLocked(.node, namespace, false);
        self.mutex.unlockShared();
        return null;
    }

    pub fn cloneNode(self: *Cache, namespace: u64, node_id: u64) !?Node {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = node_id };
        if (self.node_cache.get(key)) |entry| {
            self.touchSlot(&self.node_clock, self.node_slots.get(key));
            return try entry.node.clone(self.alloc);
        }
        return null;
    }

    pub fn borrowQuantized(self: *Cache, namespace: u64, node_id: u64) ?BorrowedQuantizedLease {
        self.mutex.lockShared();
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = node_id };
        if (self.quantized_cache.get(key)) |entry| {
            self.noteLookupLocked(.quantized, namespace, true);
            self.touchSlot(&self.quantized_clock, self.quantized_slots.get(key));
            retainQuantizedCacheEntry(entry);
            self.mutex.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteLookupLocked(.quantized, namespace, false);
        self.mutex.unlockShared();
        return null;
    }

    pub fn cloneQuantized(self: *Cache, namespace: u64, node_id: u64) !?QuantizedSet {
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = node_id };
        if (self.quantized_cache.get(key)) |entry| {
            self.touchSlot(&self.quantized_clock, self.quantized_slots.get(key));
            return try entry.quantized.clone(self.alloc);
        }
        return null;
    }

    pub fn borrowVector(self: *Cache, namespace: u64, vector_id: u64) ?BorrowedVectorLease {
        const read_stripe = self.mutex.lockVectorShared(namespace, vector_id);
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = vector_id };
        if (self.vector_cache.get(key)) |entry| {
            self.noteVectorLookupStriped(read_stripe, namespace, true);
            self.touchSlot(&self.vector_clock, self.vector_slots.get(key));
            retainVectorCacheEntry(entry);
            self.mutex.unlockVectorShared(read_stripe);
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteVectorLookupStriped(read_stripe, namespace, false);
        self.mutex.unlockVectorShared(read_stripe);
        return null;
    }

    pub fn borrowMetadata(self: *Cache, namespace: u64, vector_id: u64) ?BorrowedMetadataLease {
        self.mutex.lockShared();
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = vector_id };
        if (self.metadata_cache.get(key)) |entry| {
            self.noteLookupLocked(.metadata, namespace, true);
            self.touchSlot(&self.metadata_clock, self.metadata_slots.get(key));
            retainMetadataCacheEntry(entry);
            self.mutex.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteLookupLocked(.metadata, namespace, false);
        self.mutex.unlockShared();
        return null;
    }

    pub fn cacheNode(self: *Cache, namespace: u64, node: *const Node) !bool {
        return try self.cacheNodeGuarded(namespace, node, null, 0);
    }

    fn cacheNodeGuarded(
        self: *Cache,
        namespace: u64,
        node: *const Node,
        fill_epoch: ?*const std.atomic.Value(u64),
        expected_epoch: u64,
    ) !bool {
        const cloned = try node.clone(self.alloc);
        var cloned_active = true;
        defer if (cloned_active) {
            var owned = cloned;
            owned.deinit(self.alloc);
        };
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        if (!cacheFillEpochCurrent(fill_epoch, expected_epoch)) return false;
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = node.id };
        _ = self.removeNodeLocked(key, false);
        const bytes = estimateNodeCacheBytes(node);
        const admission = self.admitLocked(.node, namespace, key, bytes, false, false) orelse {
            self.noteAdmissionSkipLocked(.node, namespace);
            return false;
        };
        errdefer self.rollbackAdmissionLocked(admission);
        const entry = try self.alloc.create(NodeCacheEntry);
        entry.* = .{ .node = cloned };
        cloned_active = false;
        errdefer releaseNodeCacheEntry(self.alloc, entry);
        try self.recordClockSlot(&self.node_clock, &self.node_slots, key);
        errdefer removeSlot(&self.node_clock, &self.node_slots, key);
        try self.node_cache.put(self.alloc, key, entry);
        if (admission.overcommitted) self.physical_accounting.observeAllocate(bytes);
        entry.accounting = &self.physical_accounting;
        entry.namespace_pinned_accounting = &self.namespace_pinned_accounting;
        entry.namespace = namespace;
        entry.accounted_bytes = bytes;
        self.finishInsertLocked(.node, key, bytes, admission);
        return true;
    }

    pub fn cacheQuantized(self: *Cache, namespace: u64, node_id: u64, qs: *const QuantizedSet) !bool {
        return try self.cacheQuantizedGuarded(namespace, node_id, qs, null, 0);
    }

    fn cacheQuantizedGuarded(
        self: *Cache,
        namespace: u64,
        node_id: u64,
        qs: *const QuantizedSet,
        fill_epoch: ?*const std.atomic.Value(u64),
        expected_epoch: u64,
    ) !bool {
        var cloned = try qs.clone(self.alloc);
        var cloned_active = true;
        defer if (cloned_active) cloned.deinit(self.alloc);
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        if (!cacheFillEpochCurrent(fill_epoch, expected_epoch)) return false;
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = node_id };
        _ = self.removeQuantizedLocked(key, false);
        const bytes = estimateQuantizedCacheBytes(qs);
        const admission = self.admitLocked(.quantized, namespace, key, bytes, false, false) orelse {
            self.noteAdmissionSkipLocked(.quantized, namespace);
            return false;
        };
        errdefer self.rollbackAdmissionLocked(admission);
        const entry = try self.alloc.create(QuantizedCacheEntry);
        entry.* = .{ .quantized = cloned };
        cloned_active = false;
        errdefer releaseQuantizedCacheEntry(self.alloc, entry);
        try self.recordClockSlot(&self.quantized_clock, &self.quantized_slots, key);
        errdefer removeSlot(&self.quantized_clock, &self.quantized_slots, key);
        try self.quantized_cache.put(self.alloc, key, entry);
        if (admission.overcommitted) self.physical_accounting.observeAllocate(bytes);
        entry.accounting = &self.physical_accounting;
        entry.namespace_pinned_accounting = &self.namespace_pinned_accounting;
        entry.namespace = namespace;
        entry.accounted_bytes = bytes;
        self.finishInsertLocked(.quantized, key, bytes, admission);
        return true;
    }

    pub fn cacheQuantizedOwned(self: *Cache, namespace: u64, node_id: u64, qs: QuantizedSet) !void {
        var owned = qs;
        var owned_active = true;
        errdefer if (owned_active) owned.deinit(self.alloc);
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = node_id };
        _ = self.removeQuantizedLocked(key, false);
        const bytes = estimateQuantizedCacheBytes(&owned);
        const admission = self.admitLocked(.quantized, namespace, key, bytes, true, false) orelse unreachable;
        errdefer self.rollbackAdmissionLocked(admission);
        const entry = try self.alloc.create(QuantizedCacheEntry);
        entry.* = .{ .quantized = owned };
        owned_active = false;
        errdefer releaseQuantizedCacheEntry(self.alloc, entry);
        try self.recordClockSlot(&self.quantized_clock, &self.quantized_slots, key);
        errdefer removeSlot(&self.quantized_clock, &self.quantized_slots, key);
        try self.quantized_cache.put(self.alloc, key, entry);
        if (admission.overcommitted) self.physical_accounting.observeAllocate(bytes);
        entry.accounting = &self.physical_accounting;
        entry.namespace_pinned_accounting = &self.namespace_pinned_accounting;
        entry.namespace = namespace;
        entry.accounted_bytes = bytes;
        self.finishInsertLocked(.quantized, key, bytes, admission);
    }

    pub fn cacheVector(self: *Cache, namespace: u64, vector_id: u64, vector_data: []const f32) ![]const f32 {
        return try self.cacheVectorGuarded(namespace, vector_id, vector_data, null, 0, false, false, null);
    }

    fn cacheVectorGuarded(
        self: *Cache,
        namespace: u64,
        vector_id: u64,
        vector_data: []const f32,
        fill_epoch: ?*const std.atomic.Value(u64),
        expected_epoch: u64,
        must_cache: bool,
        precharged: bool,
        inserted_out: ?*bool,
    ) ![]const f32 {
        if (inserted_out) |inserted| inserted.* = false;
        const fill_stripe = sharedVectorFillStripe(namespace, vector_id);
        lockAtomic(&self.vector_fill_mutexes[fill_stripe]);
        defer self.vector_fill_mutexes[fill_stripe].unlock();

        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = vector_id };
        const read_stripe = self.mutex.lockVectorShared(namespace, vector_id);
        if (self.vector_cache.get(key)) |existing| {
            if (std.mem.eql(f32, existing.vector, vector_data)) {
                self.touchSlot(&self.vector_clock, self.vector_slots.get(key));
                self.mutex.unlockVectorShared(read_stripe);
                return vector_data;
            }
        }
        self.mutex.unlockVectorShared(read_stripe);

        const copied = try self.alloc.dupe(f32, vector_data);
        const entry = self.alloc.create(VectorCacheEntry) catch |err| {
            self.alloc.free(copied);
            return err;
        };
        entry.* = .{ .vector = copied };
        var entry_active = true;
        defer if (entry_active) releaseVectorCacheEntry(self.alloc, entry);

        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        // Admission and the epoch check share this lock with invalidation.
        // A writer either marks the stripe before this check (so the fill is
        // rejected), or invalidates the newly inserted entry afterwards.
        if (fill_epoch) |epoch| {
            if (expected_epoch & 1 != 0 or epoch.load(.acquire) != expected_epoch) return vector_data;
        }
        var replaced = false;
        if (self.vector_cache.get(key)) |existing| {
            // Duplicate read misses converge without allocating, but a writer
            // is allowed to publish a new value for an existing vector id.
            // Mutation-boundary invalidation is the primary coherence rule;
            // this comparison is the fail-safe for adapters that seed an
            // externally-owned vector directly.
            if (std.mem.eql(f32, existing.vector, vector_data)) {
                self.touchSlot(&self.vector_clock, self.vector_slots.get(key));
                return vector_data;
            }
            replaced = true;
            _ = self.removeVectorLocked(key, false);
        }
        const bytes = estimateVectorCacheBytes(vector_data);
        const admission = self.admitLocked(.vector, namespace, key, bytes, must_cache, precharged) orelse {
            self.noteAdmissionSkipLocked(.vector, namespace);
            return vector_data;
        };
        errdefer self.rollbackAdmissionLocked(admission);
        try self.ensureVectorLookupStatsLocked(namespace);
        try self.recordClockSlot(&self.vector_clock, &self.vector_slots, key);
        errdefer removeSlot(&self.vector_clock, &self.vector_slots, key);
        try self.vector_cache.put(self.alloc, key, entry);
        entry_active = false;
        if (admission.overcommitted) self.physical_accounting.observeAllocate(bytes);
        entry.accounting = &self.physical_accounting;
        entry.namespace_pinned_accounting = &self.namespace_pinned_accounting;
        entry.namespace = namespace;
        entry.accounted_bytes = bytes;
        self.finishInsertLocked(.vector, key, bytes, admission);
        if (inserted_out) |inserted| inserted.* = true;
        if (replaced) {
            noteHbcKindReplacement(&self.global_stats, .vector);
            if (self.namespace_stats.getPtr(namespace)) |stats| noteHbcKindReplacement(stats, .vector);
        }
        // The cache owns its copy. Returning the request-owned view avoids
        // exposing an unretained cache allocation after the lock is dropped.
        return vector_data;
    }

    pub fn cacheMetadata(self: *Cache, namespace: u64, vector_id: u64, metadata: []const u8) ![]const u8 {
        return try self.cacheMetadataGuarded(namespace, vector_id, metadata, null, 0);
    }

    fn cacheMetadataGuarded(
        self: *Cache,
        namespace: u64,
        vector_id: u64,
        metadata: []const u8,
        fill_epoch: ?*const std.atomic.Value(u64),
        expected_epoch: u64,
    ) ![]const u8 {
        const copied = try self.alloc.dupe(u8, metadata);
        const entry = self.alloc.create(MetadataCacheEntry) catch |err| {
            self.alloc.free(copied);
            return err;
        };
        entry.* = .{ .metadata = copied };
        var entry_active = true;
        defer if (entry_active) releaseMetadataCacheEntry(self.alloc, entry);
        self.mutex.lockExclusive();
        defer self.mutex.unlockExclusive();
        if (!cacheFillEpochCurrent(fill_epoch, expected_epoch)) return metadata;
        const key: HbcSharedCacheKey = .{ .namespace = namespace, .id = vector_id };
        _ = self.removeMetadataLocked(key, false);
        const bytes = estimateMetadataCacheBytes(metadata);
        const admission = self.admitLocked(.metadata, namespace, key, bytes, false, false) orelse {
            self.noteAdmissionSkipLocked(.metadata, namespace);
            return metadata;
        };
        errdefer self.rollbackAdmissionLocked(admission);
        try self.recordClockSlot(&self.metadata_clock, &self.metadata_slots, key);
        errdefer removeSlot(&self.metadata_clock, &self.metadata_slots, key);
        try self.metadata_cache.put(self.alloc, key, entry);
        entry_active = false;
        if (admission.overcommitted) self.physical_accounting.observeAllocate(bytes);
        entry.accounting = &self.physical_accounting;
        entry.namespace_pinned_accounting = &self.namespace_pinned_accounting;
        entry.namespace = namespace;
        entry.accounted_bytes = bytes;
        self.finishInsertLocked(.metadata, key, bytes, admission);
        // The cache owns its copy. Returning the request/transaction-owned
        // view avoids exposing an unretained allocation after unlock.
        return metadata;
    }

    fn namespaceStatsPtrLocked(self: *Cache, namespace: u64) !*HbcCacheStats {
        const entry = try self.namespace_stats.getOrPut(self.alloc, namespace);
        if (!entry.found_existing) entry.value_ptr.* = .{};
        return entry.value_ptr;
    }

    fn addStatsLocked(self: *Cache, kind: HbcCacheKind, namespace: u64, bytes: u64) !void {
        const stats = try self.namespaceStatsPtrLocked(namespace);
        addHbcKindBytes(&self.global_stats, kind, bytes);
        addHbcKindBytes(stats, kind, bytes);
    }

    fn removeStatsLocked(self: *Cache, kind: HbcCacheKind, namespace: u64, bytes: u64) void {
        removeHbcKindBytes(&self.global_stats, kind, bytes);
        if (self.namespace_stats.getPtr(namespace)) |stats| {
            removeHbcKindBytes(stats, kind, bytes);
            if (stats.total_bytes == 0) self.removeNamespaceStateIfUnusedLocked(namespace);
        }
    }

    fn noteInsertionLocked(self: *Cache, kind: HbcCacheKind, namespace: u64) void {
        noteHbcKindInsertion(&self.global_stats, kind);
        if (self.namespace_stats.getPtr(namespace)) |stats| noteHbcKindInsertion(stats, kind);
    }

    fn noteAdmissionSkipLocked(self: *Cache, kind: HbcCacheKind, namespace: u64) void {
        noteHbcKindAdmissionSkip(&self.global_stats, kind);
        if (self.namespace_stats.getPtr(namespace)) |stats| noteHbcKindAdmissionSkip(stats, kind);
    }

    fn noteEvictionLocked(self: *Cache, kind: HbcCacheKind, namespace: u64) void {
        noteHbcKindEviction(&self.global_stats, kind);
        if (self.namespace_stats.getPtr(namespace)) |stats| noteHbcKindEviction(stats, kind);
    }

    fn reserveLocked(self: *Cache, bytes: u64) bool {
        return self.physical_accounting.reserve(bytes);
    }

    fn releaseLocked(self: *Cache, bytes: u64) void {
        self.physical_accounting.release(bytes, false);
    }

    fn observeLocked(self: *Cache) void {
        _ = self;
    }

    fn admitLocked(
        self: *Cache,
        kind: HbcCacheKind,
        namespace: u64,
        protected: HbcSharedCacheKey,
        bytes: u64,
        must_cache: bool,
        precharged: bool,
    ) ?HbcSharedAdmission {
        if (bytes == 0) return .{};
        if (precharged) return .{ .bytes = bytes, .precharged = true };
        if (self.hasUnentitledCapacityLocked(bytes) and self.reserveLocked(bytes)) return .{ .bytes = bytes, .reserved = true };
        _ = kind;
        _ = namespace;
        while (self.evictOneLocked(protected)) {
            if (self.hasUnentitledCapacityLocked(bytes) and self.reserveLocked(bytes)) return .{ .bytes = bytes, .reserved = true };
        }
        if (must_cache) return .{ .bytes = bytes, .overcommitted = true };
        return null;
    }

    fn hasUnentitledCapacityLocked(self: *Cache, bytes: u64) bool {
        const target = self.admission_target_bytes.load(.acquire);
        if (target == 0) return true;
        const committed = self.physical_accounting.current() +| self.decoded_query_entitled_bytes.load(.acquire);
        return bytes <= target -| @min(target, committed);
    }

    fn rollbackAdmissionLocked(self: *Cache, admission: HbcSharedAdmission) void {
        if (admission.precharged) return;
        if (admission.reserved) self.releaseLocked(admission.bytes);
    }

    fn finishInsertLocked(self: *Cache, kind: HbcCacheKind, key: HbcSharedCacheKey, bytes: u64, admission: HbcSharedAdmission) void {
        self.addStatsLocked(kind, key.namespace, bytes) catch {};
        self.noteInsertionLocked(kind, key.namespace);
        if (admission.overcommitted) self.observeLocked();
        if (self.resource_manager) |manager| self.refreshAdmissionPolicy(manager);
        self.enforceBudgetLocked(key);
    }

    fn enforceBudgetLocked(self: *Cache, protected: HbcSharedCacheKey) void {
        const manager = self.resource_manager orelse return;
        const stats = manager.sliceStats(.hbc_node_metadata_cache);
        const action = switch (stats.pressure) {
            .normal => return,
            .soft => stats.soft_action,
            .hard => stats.hard_action,
        };
        if (action != .shrink_cache) return;
        const target_bytes = if (stats.soft_limit_bytes > 0) stats.soft_limit_bytes else stats.hard_limit_bytes;
        if (target_bytes == 0) return;
        while (self.physical_accounting.current() > target_bytes) {
            if (!self.evictOneLocked(protected)) break;
        }
    }

    fn touchSlot(_: *Cache, clock: *std.ArrayListUnmanaged(HbcSharedClockEntry), maybe_slot: ?usize) void {
        const slot = maybe_slot orelse return;
        if (slot < clock.items.len) clock.items[slot].referenced.store(true, .release);
    }

    fn recordClockSlot(
        self: *Cache,
        clock: *std.ArrayListUnmanaged(HbcSharedClockEntry),
        slots: *std.AutoHashMapUnmanaged(HbcSharedCacheKey, usize),
        key: HbcSharedCacheKey,
    ) !void {
        for (clock.items, 0..) |entry, i| {
            if (entry.key.namespace == 0) {
                try slots.put(self.alloc, key, i);
                clock.items[i] = .{ .key = key, .referenced = .init(true) };
                return;
            }
        }
        const slot = clock.items.len;
        try slots.put(self.alloc, key, slot);
        errdefer _ = slots.remove(key);
        try clock.append(self.alloc, .{ .key = key, .referenced = .init(true) });
    }

    fn nextVictim(clock: *std.ArrayListUnmanaged(HbcSharedClockEntry), hand: *usize, protected: HbcSharedCacheKey) ?HbcSharedCacheKey {
        if (clock.items.len == 0) return null;
        var scanned: usize = 0;
        const limit = clock.items.len * 2;
        while (scanned < limit) : (scanned += 1) {
            const slot = hand.* % clock.items.len;
            const entry = &clock.items[slot];
            if (entry.key.namespace != 0 and !(entry.key.namespace == protected.namespace and entry.key.id == protected.id)) {
                if (!entry.referenced.swap(false, .acq_rel)) {
                    hand.* = (slot + 1) % clock.items.len;
                    return entry.key;
                }
            }
            hand.* = (slot + 1) % clock.items.len;
        }
        return null;
    }

    fn evictOneLocked(self: *Cache, protected: HbcSharedCacheKey) bool {
        if (self.resource_manager) |manager| {
            if (self.mostOverQuotaNamespaceLocked(manager.hbcCachePolicy().target_bytes)) |namespace| {
                if (self.evictOneFromNamespaceLocked(namespace, protected)) return true;
            }
        }
        // First reclaim classes consuming borrowed capacity. Exact vectors are
        // intentionally elastic; routing nodes and quantized payloads keep
        // ResourceManager-derived working-set targets whenever another class
        // can satisfy pressure.
        if (self.resource_manager) |manager| {
            const policy = manager.hbcCachePolicy();
            inline for (.{ HbcCacheKind.vector, HbcCacheKind.metadata, HbcCacheKind.quantized, HbcCacheKind.node }) |kind| {
                const used = hbcKindStats(&self.global_stats, kind).used_bytes;
                if (used > policy.protectedBytes(hbcResourceCacheClass(kind)) and
                    self.evictOneKindLocked(kind, protected)) return true;
            }
        }

        // Protected targets are preferences, never unreclaimable reservations.
        // Aggregate hard pressure must always be able to drain the whole pool.
        inline for (.{ HbcCacheKind.vector, HbcCacheKind.metadata, HbcCacheKind.quantized, HbcCacheKind.node }) |kind| {
            if (self.evictOneKindLocked(kind, protected)) return true;
        }
        return false;
    }

    fn mostOverQuotaNamespaceLocked(self: *Cache, target_bytes: u64) ?u64 {
        if (target_bytes == 0) return null;
        var total_weight: u64 = 0;
        var registrations = self.namespace_paths.iterator();
        while (registrations.next()) |entry| {
            if (entry.value_ptr.active_owners != 0) total_weight +|= entry.value_ptr.weight;
        }
        if (total_weight == 0) return null;

        var selected: ?u64 = null;
        var selected_excess: u64 = 0;
        var stats_it = self.namespace_stats.iterator();
        while (stats_it.next()) |entry| {
            const registration = self.namespace_paths.get(entry.key_ptr.*) orelse continue;
            if (registration.active_owners == 0) continue;
            const share = mulDivU64(target_bytes, registration.weight, total_weight);
            const excess = entry.value_ptr.total_bytes -| share;
            if (excess > selected_excess) {
                selected = entry.key_ptr.*;
                selected_excess = excess;
            }
        }
        return selected;
    }

    fn evictOneFromNamespaceLocked(self: *Cache, namespace: u64, protected: HbcSharedCacheKey) bool {
        inline for (.{ HbcCacheKind.vector, HbcCacheKind.metadata, HbcCacheKind.quantized, HbcCacheKind.node }) |kind| {
            const maybe_key: ?HbcSharedCacheKey = switch (kind) {
                .vector => firstNamespaceKey(&self.vector_cache, namespace, protected),
                .metadata => firstNamespaceKey(&self.metadata_cache, namespace, protected),
                .quantized => firstNamespaceKey(&self.quantized_cache, namespace, protected),
                .node => firstNamespaceKey(&self.node_cache, namespace, protected),
            };
            if (maybe_key) |key| if (self.removeKindLocked(kind, key, true)) return true;
        }
        return false;
    }

    fn removeKindLocked(self: *Cache, kind: HbcCacheKind, key: HbcSharedCacheKey, evicted: bool) bool {
        return switch (kind) {
            .node => self.removeNodeLocked(key, evicted),
            .quantized => self.removeQuantizedLocked(key, evicted),
            .vector => self.removeVectorLocked(key, evicted),
            .metadata => self.removeMetadataLocked(key, evicted),
        };
    }

    fn evictOneKindLocked(self: *Cache, kind: HbcCacheKind, protected: HbcSharedCacheKey) bool {
        return switch (kind) {
            .vector => if (nextVictim(&self.vector_clock, &self.vector_hand, protected)) |key|
                self.removeVectorLocked(key, true)
            else
                false,
            .metadata => if (nextVictim(&self.metadata_clock, &self.metadata_hand, protected)) |key|
                self.removeMetadataLocked(key, true)
            else
                false,
            .quantized => if (nextVictim(&self.quantized_clock, &self.quantized_hand, protected)) |key|
                self.removeQuantizedLocked(key, true)
            else
                false,
            .node => if (nextVictim(&self.node_clock, &self.node_hand, protected)) |key|
                self.removeNodeLocked(key, true)
            else
                false,
        };
    }

    fn evictNamespaceEntryLocked(self: *Cache, namespace: u64, kind: HbcCacheKind) bool {
        switch (kind) {
            .vector => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.vector_cache.keyIterator();
                    while (it.next()) |key| if (key.namespace == namespace) {
                        victim = key.*;
                        break;
                    };
                }
                if (victim) |key| return self.removeVectorLocked(key, false);
            },
            .metadata => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.metadata_cache.keyIterator();
                    while (it.next()) |key| if (key.namespace == namespace) {
                        victim = key.*;
                        break;
                    };
                }
                if (victim) |key| return self.removeMetadataLocked(key, false);
            },
            .quantized => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.quantized_cache.keyIterator();
                    while (it.next()) |key| if (key.namespace == namespace) {
                        victim = key.*;
                        break;
                    };
                }
                if (victim) |key| return self.removeQuantizedLocked(key, false);
            },
            .node => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.node_cache.keyIterator();
                    while (it.next()) |key| if (key.namespace == namespace) {
                        victim = key.*;
                        break;
                    };
                }
                if (victim) |key| return self.removeNodeLocked(key, false);
            },
        }
        return false;
    }

    fn evictPathPrefixEntryLocked(self: *Cache, stable_prefix: []const u8, kind: HbcCacheKind) bool {
        switch (kind) {
            .vector => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.vector_cache.keyIterator();
                    while (it.next()) |key| {
                        const registration = self.namespace_paths.get(key.namespace) orelse continue;
                        if (hbcPathHasPrefixBoundary(registration.path, stable_prefix)) {
                            victim = key.*;
                            break;
                        }
                    }
                }
                if (victim) |key| return self.removeVectorLocked(key, false);
            },
            .metadata => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.metadata_cache.keyIterator();
                    while (it.next()) |key| {
                        const registration = self.namespace_paths.get(key.namespace) orelse continue;
                        if (hbcPathHasPrefixBoundary(registration.path, stable_prefix)) {
                            victim = key.*;
                            break;
                        }
                    }
                }
                if (victim) |key| return self.removeMetadataLocked(key, false);
            },
            .quantized => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.quantized_cache.keyIterator();
                    while (it.next()) |key| {
                        const registration = self.namespace_paths.get(key.namespace) orelse continue;
                        if (hbcPathHasPrefixBoundary(registration.path, stable_prefix)) {
                            victim = key.*;
                            break;
                        }
                    }
                }
                if (victim) |key| return self.removeQuantizedLocked(key, false);
            },
            .node => {
                var victim: ?HbcSharedCacheKey = null;
                {
                    var it = self.node_cache.keyIterator();
                    while (it.next()) |key| {
                        const registration = self.namespace_paths.get(key.namespace) orelse continue;
                        if (hbcPathHasPrefixBoundary(registration.path, stable_prefix)) {
                            victim = key.*;
                            break;
                        }
                    }
                }
                if (victim) |key| return self.removeNodeLocked(key, false);
            },
        }
        return false;
    }

    fn removeSlot(clock: *std.ArrayListUnmanaged(HbcSharedClockEntry), slots: *std.AutoHashMapUnmanaged(HbcSharedCacheKey, usize), key: HbcSharedCacheKey) void {
        if (slots.fetchRemove(key)) |removed| {
            if (removed.value < clock.items.len) clock.items[removed.value] = .{};
        }
    }

    fn removeNodeLocked(self: *Cache, key: HbcSharedCacheKey, evicted: bool) bool {
        removeSlot(&self.node_clock, &self.node_slots, key);
        if (self.node_cache.fetchRemove(key)) |removed| {
            const bytes = estimateNodeCacheBytes(&removed.value.node);
            detachCacheEntry(removed.value.accounting, removed.value.namespace_pinned_accounting, removed.value.namespace, bytes, removed.value.refs.load(.acquire), &removed.value.detached_pinned);
            releaseNodeCacheEntry(self.alloc, removed.value);
            self.removeStatsLocked(.node, key.namespace, bytes);
            if (evicted) self.noteEvictionLocked(.node, key.namespace);
            return true;
        }
        return false;
    }

    fn removeQuantizedLocked(self: *Cache, key: HbcSharedCacheKey, evicted: bool) bool {
        removeSlot(&self.quantized_clock, &self.quantized_slots, key);
        if (self.quantized_cache.fetchRemove(key)) |removed| {
            const bytes = estimateQuantizedCacheBytes(&removed.value.quantized);
            detachCacheEntry(removed.value.accounting, removed.value.namespace_pinned_accounting, removed.value.namespace, bytes, removed.value.refs.load(.acquire), &removed.value.detached_pinned);
            releaseQuantizedCacheEntry(self.alloc, removed.value);
            self.removeStatsLocked(.quantized, key.namespace, bytes);
            if (evicted) self.noteEvictionLocked(.quantized, key.namespace);
            return true;
        }
        return false;
    }

    fn removeVectorLocked(self: *Cache, key: HbcSharedCacheKey, evicted: bool) bool {
        removeSlot(&self.vector_clock, &self.vector_slots, key);
        if (self.vector_cache.fetchRemove(key)) |removed| {
            const bytes = estimateVectorCacheBytes(removed.value.vector);
            detachCacheEntry(removed.value.accounting, removed.value.namespace_pinned_accounting, removed.value.namespace, bytes, removed.value.refs.load(.acquire), &removed.value.detached_pinned);
            releaseVectorCacheEntry(self.alloc, removed.value);
            self.removeStatsLocked(.vector, key.namespace, bytes);
            if (evicted) self.noteEvictionLocked(.vector, key.namespace);
            return true;
        }
        return false;
    }

    fn removeMetadataLocked(self: *Cache, key: HbcSharedCacheKey, evicted: bool) bool {
        removeSlot(&self.metadata_clock, &self.metadata_slots, key);
        if (self.metadata_cache.fetchRemove(key)) |removed| {
            const bytes = estimateMetadataCacheBytes(removed.value.metadata);
            detachCacheEntry(removed.value.accounting, removed.value.namespace_pinned_accounting, removed.value.namespace, bytes, removed.value.refs.load(.acquire), &removed.value.detached_pinned);
            releaseMetadataCacheEntry(self.alloc, removed.value);
            self.removeStatsLocked(.metadata, key.namespace, bytes);
            if (evicted) self.noteEvictionLocked(.metadata, key.namespace);
            return true;
        }
        return false;
    }

    fn clearAllLocked(self: *Cache) void {
        while (self.evictOneLocked(.{ .namespace = 0, .id = 0 })) {}
        self.observeLocked();
    }

    fn namespaceHasEntriesLocked(self: *Cache, namespace: u64) bool {
        const stats = self.namespace_stats.get(namespace) orelse return false;
        return stats.total_bytes != 0;
    }

    fn removeNamespaceStateIfUnusedLocked(self: *Cache, namespace: u64) void {
        if (self.namespaceHasEntriesLocked(namespace)) return;
        if (self.namespace_paths.get(namespace)) |registration| {
            if (registration.active_owners != 0) return;
            const removed = self.namespace_paths.fetchRemove(namespace) orelse return;
            self.alloc.free(removed.value.path);
        }
        for (&self.vector_lookup_stats) |*lookup_stats| _ = lookup_stats.remove(namespace);
        _ = self.namespace_stats.remove(namespace);
    }

    fn pruneUnusedNamespacePathsLocked(self: *Cache) void {
        while (true) {
            var victim: ?u64 = null;
            var it = self.namespace_paths.iterator();
            while (it.next()) |entry| {
                if (entry.value_ptr.active_owners == 0 and !self.namespaceHasEntriesLocked(entry.key_ptr.*)) {
                    victim = entry.key_ptr.*;
                    break;
                }
            }
            const namespace = victim orelse return;
            self.removeNamespaceStateIfUnusedLocked(namespace);
        }
    }
};

/// A bounded query-level ownership decision for externally stored vectors.
/// While live, the primary-store transaction uses transient block admission
/// and every decoded miss is eligible for retained vector publication. If the
/// reservation cannot be made, the caller retains LSM blocks and suppresses
/// decoded-cache writes for the whole request instead.
pub const DecodedVectorResidencyLease = struct {
    cache: *Cache,
    namespace: u64,
    capacity_bytes: u64,
    entitled_bytes: u64,
    reserved_bytes: u64 = 0,
    published_bytes: u64 = 0,
    replacement_entitlement: bool = false,
    active: bool = true,

    pub fn deinit(self: *@This()) void {
        if (!self.active) return;
        self.cache.releaseDecodedQueryLease(self.reserved_bytes, self.entitled_bytes, self.replacement_entitlement);
        self.active = false;
    }

    pub fn remainingBytes(self: *const @This()) u64 {
        return if (self.active) self.reserved_bytes else 0;
    }

    pub fn ensureCapacity(self: *@This(), bytes: u64) bool {
        if (!self.active) return false;
        return self.cache.reserveDecodedQueryCapacity(self, bytes);
    }

    fn consume(self: *@This(), bytes: u64) void {
        if (!self.active or self.reserved_bytes == 0) return;
        std.debug.assert(bytes <= self.reserved_bytes);
        const consumed = @min(bytes, self.reserved_bytes);
        self.cache.consumeDecodedQueryReservation(consumed);
        self.reserved_bytes -= consumed;
        self.published_bytes +|= consumed;
    }

    fn belongsTo(self: *const @This(), cache: *const Cache, namespace: u64) bool {
        return self.active and self.cache == cache and self.namespace == namespace;
    }
};

const HbcCacheProtection = struct {
    kind: ?HbcCacheKind = null,
    key: u64 = 0,

    fn none() HbcCacheProtection {
        return .{};
    }

    fn one(kind: HbcCacheKind, key: u64) HbcCacheProtection {
        return .{ .kind = kind, .key = key };
    }

    fn protects(self: HbcCacheProtection, kind: HbcCacheKind, key: u64) bool {
        return self.kind == kind and self.key == key;
    }
};

fn estimateNodeCacheBytes(node: *const Node) u64 {
    var total: u64 = @sizeOf(Node);
    if (node.backing.len > 0) {
        total +|= @intCast(node.backing.len);
    } else {
        total +|= @intCast(node.centroid.len * @sizeOf(f32));
        total +|= @intCast(node.children.len * @sizeOf(u64));
        total +|= @intCast(node.members.len * @sizeOf(u64));
    }
    return total;
}

fn estimateQuantizedCacheBytes(qs: *const QuantizedSet) u64 {
    return @sizeOf(QuantizedSet) + switch (qs.*) {
        .rabit => |*set| @as(u64, @intCast(set.centroid.len * @sizeOf(f32))) +|
            @as(u64, @intCast(set.codes.data.len * @sizeOf(u64))) +|
            @as(u64, @intCast(set.code_counts.len * @sizeOf(u32))) +|
            @as(u64, @intCast(set.centroid_distances.len * @sizeOf(f32))) +|
            @as(u64, @intCast(set.quantized_dot_products.len * @sizeOf(f32))) +|
            @as(u64, @intCast(set.centroid_dot_products.len * @sizeOf(f32))),
        .nonquant => |*set| @as(u64, @intCast(set.vectors.data.len * @sizeOf(f32))),
    };
}

fn estimateVectorCacheBytes(vector: []const f32) u64 {
    return @sizeOf(VectorCacheEntry) +
        @as(u64, @intCast(vector.len * @sizeOf(f32))) +
        @sizeOf(HbcSharedClockEntry) +
        @sizeOf(HbcSharedCacheKey) +
        @sizeOf(usize);
}

pub fn estimateDecodedVectorResidencyBytes(dims: usize) u64 {
    return @sizeOf(VectorCacheEntry) +
        @as(u64, @intCast(dims)) *| @sizeOf(f32) +
        @sizeOf(HbcSharedClockEntry) +
        @sizeOf(HbcSharedCacheKey) +
        @sizeOf(usize);
}

fn estimateMetadataCacheBytes(metadata: []const u8) u64 {
    return @sizeOf(MetadataCacheEntry) +
        @as(u64, @intCast(metadata.len)) +
        @sizeOf(HbcSharedClockEntry) +
        @sizeOf(HbcSharedCacheKey) +
        @sizeOf(usize);
}

fn nextHbcClockVictim(
    clock_keys: []u64,
    clock_refs: []bool,
    hand: *usize,
    protection: HbcCacheProtection,
    kind: HbcCacheKind,
) ?u64 {
    if (clock_keys.len == 0) return null;
    var scanned: usize = 0;
    const limit = clock_keys.len * 2;
    while (scanned < limit) : (scanned += 1) {
        const slot = hand.*;
        const key = clock_keys[slot];
        if (key != 0 and !protection.protects(kind, key)) {
            if (clock_refs[slot]) {
                clock_refs[slot] = false;
            } else {
                hand.* = (slot + 1) % clock_keys.len;
                return key;
            }
        }
        hand.* = (slot + 1) % clock_keys.len;
    }
    return null;
}

fn claimLocalClockSlot(clock_keys: []u64, start_slot: usize, key: u64) ?usize {
    if (clock_keys.len == 0) return null;
    for (0..clock_keys.len) |offset| {
        const slot = (start_slot + offset) % clock_keys.len;
        if (clock_keys[slot] == 0) {
            clock_keys[slot] = key;
            return slot;
        }
    }
    return null;
}

// ============================================================================
// HBC Index
// ============================================================================

const CompleteCoverageOutcome = enum {
    retry,
    validated,
    incomplete,
    runtime_canceled,
};

const CompleteCoverageFlight = struct {
    generation: u64,
    io: std.Io,
    ready: std.Io.Event = .unset,
    refs: usize = 1,
    outcome: CompleteCoverageOutcome = .retry,
    next: ?*CompleteCoverageFlight = null,
};

const FlatCentroidBuildFlight = struct {
    generation: u64,
    io: std.Io,
    ready: std.Io.Event = .unset,
    refs: usize = 1,
    outcome: vectorindex_spfresh_index.FlatCentroidBuildOutcome = .retry,
    next: ?*FlatCentroidBuildFlight = null,
};

const PublishedSearchStateFlight = struct {
    generation: u64 = 0,
    io: std.Io = undefined,
    ready: std.Io.Event = .unset,
    refs: usize = 0,
};

pub const HBCIndex = struct {
    alloc: Allocator,
    env_owner: EnvOwner,
    store: vectorindex_store.NamespaceStore,
    config: HBCConfig,
    metadata: IndexMetadata,
    published_root_node: AtomicU64,
    published_active_count: AtomicU64,
    published_node_count: AtomicU64,
    published_generation: AtomicU64,
    // Publishers take the exclusive side before mutating topology or caches.
    // Complete searches retry optimistically; only a conflicting retry holds
    // the shared side for progress. Shared acquisition and odd-generation
    // waits are cooperative through std.Io. Best-effort searches remain
    // lock-free.
    published_snapshot_mu: apply_rw_lock_mod.ApplyRwLock = .{},
    published_mutation_active: std.atomic.Value(bool) = std.atomic.Value(bool).init(false),
    /// Seqlock-style epoch for optimistic complete-snapshot searches. Every
    /// mutation, including an aborted one that leaves the durable generation
    /// unchanged, advances this from even -> odd -> even.
    published_mutation_epoch: std.atomic.Value(u64) = std.atomic.Value(u64).init(0),
    /// Publication commits may include durable I/O. Readers of an odd
    /// generation retain the active flight and sleep on its runtime event
    /// instead of occupying an OS thread with an unbounded seqlock spin.
    published_flight_mu: std.atomic.Mutex = .unlocked,
    published_flight: ?*PublishedSearchStateFlight = null,
    published_spare_flight: ?*PublishedSearchStateFlight = null,
    /// Exact reachable-vector coverage is immutable within a published
    /// generation, so only the first complete search needs to validate it.
    complete_coverage_generation: std.atomic.Value(u64) = std.atomic.Value(u64).init(std.math.maxInt(u64)),
    /// Short state lock for the generation validation flight. Long waits use
    /// CompleteCoverageFlight.ready on the backend runtime's std.Io; they never
    /// spin on an OS-thread mutex or retain a search transaction/workspace.
    complete_coverage_state_mu: std.atomic.Mutex = .unlocked,
    complete_coverage_flight: ?*CompleteCoverageFlight = null,
    runtime_io: ?std.Io = null,
    rng: go_rand.GoPcg,
    // Set when a write path observes a tree-link inconsistency (stale parent
    // pointer, dangling node reference); background maintenance runs a
    // bounded repairTreeLinks sweep and clears it on completion.
    link_repair_pending: std.atomic.Value(bool) = std.atomic.Value(bool).init(false),
    // A complete-snapshot query found that the published generation cannot
    // prove coverage. Link repair may make traversal safer, but only a shadow
    // generation rebuild can recover orphaned or duplicate membership.
    generation_repair_pending_generation: AtomicU64 = .init(std.math.maxInt(u64)),
    quantizer: quantizer_mod.RaBitQuantizer,
    rot: vec.RandomOrthogonalTransformer,
    node_cache: std.AutoHashMapUnmanaged(u64, *NodeCacheEntry),
    node_cache_slots: std.AutoHashMapUnmanaged(u64, usize),
    node_clock_keys: []u64,
    node_clock_refs: []bool,
    node_clock_hand: usize,
    pinned_node_cache: std.AutoHashMapUnmanaged(u64, *NodeCacheEntry),
    quantized_cache: std.AutoHashMapUnmanaged(u64, *QuantizedCacheEntry),
    quantized_cache_slots: std.AutoHashMapUnmanaged(u64, usize),
    quantized_clock_keys: []u64,
    quantized_clock_refs: []bool,
    quantized_clock_hand: usize,
    pinned_quantized_cache: std.AutoHashMapUnmanaged(u64, *QuantizedCacheEntry),
    vector_cache: std.AutoHashMapUnmanaged(u64, *VectorCacheEntry),
    vector_cache_slots: std.AutoHashMapUnmanaged(u64, usize),
    vector_clock_keys: []u64,
    vector_clock_refs: []bool,
    vector_clock_hand: usize,
    metadata_cache: std.AutoHashMapUnmanaged(u64, *MetadataCacheEntry),
    metadata_cache_slots: std.AutoHashMapUnmanaged(u64, usize),
    metadata_clock_keys: []u64,
    metadata_clock_refs: []bool,
    metadata_clock_hand: usize,
    resource_manager: ?*resource_manager_mod.ResourceManager = null,
    local_reclaimer_identity: u64 = 0,
    search_workspace_reclaimer_identity: u64 = 0,
    flat_centroid_reclaimer_identity: u64 = 0,
    bind_shared_cache_resource_manager: bool = true,
    shared_cache: ?*Cache = null,
    shared_cache_registered: bool = false,
    cache_namespace: u64 = 0,
    cache_path: []u8 = &.{},
    cache_enabled: bool = true,
    retained_vector_cache_enabled: bool = true,
    bypass_external_vector_cache: bool = false,
    cache_mu: apply_rw_lock_mod.ApplyRwLock,
    active_searches: std.atomic.Value(u32),
    // A striped seqlock fences cache fills against uncommitted vector writes
    // without retaining one version record per vector. A dirty stripe is odd;
    // commit/abort publication returns it to even. Existing keys in the same
    // stripe remain usable because only miss admission consults this fence.
    vector_cache_fill_epochs: [vector_cache_fill_stripe_count]std.atomic.Value(u64) = .{std.atomic.Value(u64).init(0)} ** vector_cache_fill_stripe_count,
    vector_cache_fill_dirty: [vector_cache_fill_dirty_word_count]std.atomic.Value(u64) = .{std.atomic.Value(u64).init(0)} ** vector_cache_fill_dirty_word_count,
    hbc_cache_bytes_accounted: u64 = 0,
    detached_hbc_accounting: HbcPhysicalAccounting = .{},
    search_workspace_bytes_accounted: u64 = 0,
    flat_centroid_directory_bytes_accounted: u64 = 0,
    flat_centroid_build_bytes_accounted: u64 = 0,
    flat_centroid_retained_reservation_bytes_accounted: u64 = 0,
    routing_scratch_bytes_accounted: u64 = 0,
    apply_workspace_bytes_accounted: u64 = 0,
    apply_workspace_split_bytes: u64 = 0,
    deferred_node_key_value_bytes: u64 = 0,
    deferred_oversized_leaves_peak: u64 = 0,
    bulk_split_vector_workspace: SplitVectorWorkspace = .{},
    hbc_cache_kind_stats: [hbc_cache_kind_count]HbcCacheKindStats = .{HbcCacheKindStats{}} ** hbc_cache_kind_count,
    deferred_quantized_nodes: std.AutoHashMapUnmanaged(u64, void),
    deferred_node_keys: std.AutoHashMapUnmanaged(u128, DeferredNodeValue),
    deferred_oversized_leaves: std.AutoHashMapUnmanaged(u64, void),
    write_session_depth: usize = 0,
    write_session_kind: ?WriteSessionKind = null,
    bulk_publication_may_have_mutated: bool = false,
    hilbert: ?vec.Hilbert,
    scratch_mu: std.atomic.Mutex,
    cached_scratch: ?SearchScratch,
    routing_scratch_mu: std.atomic.Mutex,
    cached_routing_scratch: ?RoutingScratch,
    flat_centroid_mu: std.atomic.Mutex,
    flat_centroid_directory: ?*vectorindex_spfresh_index.FlatCentroidDirectory,
    flat_centroid_build_mu: std.atomic.Mutex = .unlocked,
    flat_centroid_build_flight: ?*FlatCentroidBuildFlight = null,
    flat_centroid_build_accounting_mu: std.atomic.Mutex = .unlocked,
    flat_centroid_accounting_mu: std.atomic.Mutex = .unlocked,
    dense_route_cost_mu: std.atomic.Mutex = .unlocked,
    dense_route_cost: DenseRouteCostSnapshot = .{},
    write_profile: WriteProfile = .{},
    external_vector_ctx: ?*anyopaque = null,
    external_vector_loader: ?ExternalVectorLoader = null,
    external_vector_scratch_loader: ?ExternalVectorScratchLoader = null,
    external_vector_batch_scratch_loader: ?ExternalVectorBatchScratchLoader = null,
    external_vector_batch_transformed_matrix_loader: ?ExternalVectorBatchTransformedMatrixLoader = null,
    external_vector_batch_distance_loader: ?ExternalVectorBatchDistanceLoader = null,

    const EnvOwner = hbc_backend.OpenedBackend;
    pub const ExternalVectorLoader = *const fn (ctx: *anyopaque, alloc: Allocator, vector_id: u64, metadata: []const u8) anyerror![]f32;
    pub const ExternalVectorScratchLoader = *const fn (ctx: *anyopaque, vector_id: u64, metadata: []const u8, scratch: []f32) anyerror![]const f32;
    pub const ExternalVectorBatchScratchLoader = *const fn (ctx: *anyopaque, vector_ids: []const u64, metadata: []const ?[]const u8, vector_views: [][]const f32, batch_scratch: []f32, dims: usize) anyerror!void;
    pub const ExternalVectorTransformFn = *const fn (index: *HBCIndex, original: []const f32, transformed: []f32) []const f32;
    pub const ExternalVectorBatchTransformedMatrixLoader = *const fn (ctx: *anyopaque, vector_ids: []const u64, metadata: []const ?[]const u8, matrix_positions: []const usize, matrix: []f32, scratch: []f32, dims: usize, index: *HBCIndex, transform: ExternalVectorTransformFn) anyerror!void;
    pub const ExternalVectorBatchDistanceScratch = struct {
        artifact_keys: [][]const u8,
        raw_values: []?[]const u8,
    };
    pub const ExternalVectorBatchDistanceLoader = *const fn (
        ctx: *anyopaque,
        vector_ids: []const u64,
        metadata: []const ?[]const u8,
        query: []const f32,
        query_measure: f32,
        metric: vec.DistanceMetric,
        distances: []f32,
        batch_scratch: []f32,
        dims: usize,
        scratch: ExternalVectorBatchDistanceScratch,
        profile: ?*vectorindex_search_types.SearchProfile,
    ) anyerror!void;
    pub const BorrowedNode = BorrowedNodeLease;
    pub const BorrowedQuantized = BorrowedQuantizedLease;
    pub const BorrowedVector = BorrowedVectorLease;
    pub const BorrowedMetadata = BorrowedMetadataLease;
    pub const NodeRead = vectorindex_hbc_index.CachedNodeReadHandle(*HBCIndex);

    fn PublishedTxn(comptime Inner: type) type {
        return struct {
            const Self = @This();

            owner: *HBCIndex,
            inner: Inner,
            active: bool = true,

            pub fn abort(self: *Self) void {
                if (!self.active) return;
                self.inner.abort();
                self.owner.abortPublishedSearchStateRefresh();
                self.active = false;
            }

            pub fn commit(self: *Self) !void {
                if (!self.active) return error.TransactionClosed;
                self.owner.markPublishedSearchStateCommitting() catch |err| {
                    self.inner.abort();
                    self.owner.abortPublishedSearchStateRefresh();
                    self.active = false;
                    return err;
                };
                self.inner.commit() catch |err| {
                    self.inner.abort();
                    self.owner.abortPublishedSearchStateRefresh();
                    self.active = false;
                    return err;
                };
                self.owner.finishPublishedSearchStateRefresh();
                self.active = false;
            }

            pub fn get(self: *Self, namespace: Namespace, key: []const u8) ![]const u8 {
                return try self.inner.get(namespace, key);
            }

            pub fn getManySorted(self: *Self, namespace: Namespace, keys: []const []const u8, values: []?[]const u8) !void {
                return try self.inner.getManySorted(namespace, keys, values);
            }

            pub fn put(self: *Self, namespace: Namespace, key: []const u8, value: []const u8) !void {
                return try self.inner.put(namespace, key, value);
            }

            pub fn appendPut(self: *Self, namespace: Namespace, key: []const u8, value: []const u8) !void {
                return try self.inner.appendPut(namespace, key, value);
            }

            pub fn delete(self: *Self, namespace: Namespace, key: []const u8) !void {
                return try self.inner.delete(namespace, key);
            }

            pub fn openCursor(self: *Self, namespace: Namespace) !vectorindex_store.Cursor {
                return try self.inner.openCursor(namespace);
            }
        };
    }

    pub const PublishedWriteTxn = PublishedTxn(vectorindex_store.NamespaceWriteTxn);
    pub const PublishedBatchTxn = PublishedTxn(vectorindex_store.NamespaceBatch);

    pub const DenseRoute = enum(u8) { unknown, exact, hbc };

    pub const DenseRouteCostSnapshot = struct {
        filter_scan_ns_per_candidate: u64 = 0,
        quantized_score_ns_per_candidate: u64 = 0,
        artifact_read_decode_ns_per_vector: u64 = 0,
        exact_distance_ns_per_vector: u64 = 0,
        rerank_cache_hit_permille: u64 = 0,
        rerank_cache_observations: u64 = 0,
        exact_observations: u64 = 0,
        hbc_observations: u64 = 0,
        last_route: DenseRoute = .unknown,
    };

    pub const ExactRouteCostObservation = struct {
        candidates: u64,
        metadata_ns: u64,
        artifact_read_decode_ns: u64,
        distance_ns: u64,
        artifact_cache_hits: u64,
        artifact_vectors_loaded: u64,
    };
    pub const QuantizedRead = vectorindex_hbc_index.CachedQuantizedReadHandle(*HBCIndex);

    pub const Namespace = vectorindex_store.Namespace;

    const SplitVectorWorkspace = struct {
        active: bool = false,
        map: std.AutoHashMapUnmanaged(u64, usize) = .empty,
        vectors: std.ArrayListUnmanaged(f32) = .empty,
        accounted_bytes: u64 = 0,

        fn bytes(self: *const SplitVectorWorkspace) u64 {
            return @as(u64, @intCast(self.vectors.capacity)) * @sizeOf(f32) +
                @as(u64, @intCast(self.map.capacity())) * (@sizeOf(u64) + @sizeOf(usize));
        }

        fn clearRetainingCapacity(self: *SplitVectorWorkspace) void {
            self.map.clearRetainingCapacity();
            self.vectors.clearRetainingCapacity();
        }

        fn deinit(self: *SplitVectorWorkspace, alloc: Allocator) void {
            self.map.deinit(alloc);
            self.vectors.deinit(alloc);
            self.* = .{};
        }
    };

    const HbcCacheAdmission = struct {
        index: *HBCIndex,
        reserved_bytes: u64 = 0,
        active: bool = false,

        fn none(index: *HBCIndex) HbcCacheAdmission {
            return .{ .index = index };
        }

        fn commit(self: *HbcCacheAdmission) void {
            self.active = false;
        }

        fn rollback(self: *HbcCacheAdmission) void {
            if (!self.active or self.reserved_bytes == 0) return;
            if (self.reserved_bytes > self.index.hbc_cache_bytes_accounted) {
                if (self.index.resource_manager) |manager| manager.recordAccountingError();
                self.active = false;
                return;
            }
            if (self.index.resource_manager) |manager| {
                manager.adjustUsage(
                    .hbc_node_metadata_cache,
                    &self.index.hbc_cache_bytes_accounted,
                    self.index.hbc_cache_bytes_accounted - self.reserved_bytes,
                ) catch {
                    self.active = false;
                    return;
                };
            } else {
                self.index.hbc_cache_bytes_accounted -= self.reserved_bytes;
            }
            self.active = false;
        }
    };

    const RoutingScratch = struct {
        estimate: quantizer_mod.RaBitQuantizer.EstimateScratch,
        child_ids: []u64,
        distances: []f32,
        error_bounds: []f32,
        competitive: []vectorindex_types.PriorityItem,

        fn init(alloc: Allocator, dims: usize, initial_capacity: usize) !@This() {
            const capacity = @max(initial_capacity, 1);
            const estimate = try quantizer_mod.RaBitQuantizer.EstimateScratch.init(alloc, dims);
            errdefer {
                var tmp = estimate;
                tmp.deinit(alloc);
            }
            const child_ids = try alloc.alloc(u64, capacity);
            errdefer alloc.free(child_ids);
            const distances = try alloc.alloc(f32, capacity);
            errdefer alloc.free(distances);
            const error_bounds = try alloc.alloc(f32, capacity);
            errdefer alloc.free(error_bounds);
            const competitive = try alloc.alloc(vectorindex_types.PriorityItem, capacity);
            return .{
                .estimate = estimate,
                .child_ids = child_ids,
                .distances = distances,
                .error_bounds = error_bounds,
                .competitive = competitive,
            };
        }

        pub fn ensureCapacity(self: *@This(), alloc: Allocator, needed: usize) !void {
            const capacity = @max(needed, 1);
            if (self.child_ids.len < capacity) self.child_ids = try alloc.realloc(self.child_ids, capacity);
            if (self.distances.len < capacity) self.distances = try alloc.realloc(self.distances, capacity);
            if (self.error_bounds.len < capacity) self.error_bounds = try alloc.realloc(self.error_bounds, capacity);
            if (self.competitive.len < capacity) self.competitive = try alloc.realloc(self.competitive, capacity);
        }

        fn deinit(self: *@This(), alloc: Allocator) void {
            self.estimate.deinit(alloc);
            alloc.free(self.child_ids);
            alloc.free(self.distances);
            alloc.free(self.error_bounds);
            alloc.free(self.competitive);
            self.* = undefined;
        }

        fn bytes(self: *const @This()) u64 {
            return @as(u64, @intCast(self.estimate.query_diff.len * @sizeOf(f32))) +
                @as(u64, @intCast(self.estimate.q1.len * @sizeOf(u64))) +
                @as(u64, @intCast(self.estimate.q2.len * @sizeOf(u64))) +
                @as(u64, @intCast(self.estimate.q3.len * @sizeOf(u64))) +
                @as(u64, @intCast(self.estimate.q4.len * @sizeOf(u64))) +
                @as(u64, @intCast(self.child_ids.len * @sizeOf(u64))) +
                @as(u64, @intCast(self.distances.len * @sizeOf(f32))) +
                @as(u64, @intCast(self.error_bounds.len * @sizeOf(f32))) +
                @as(u64, @intCast(self.competitive.len * @sizeOf(vectorindex_types.PriorityItem)));
        }
    };

    const RoutingScratchHandle = struct {
        scratch: RoutingScratch,
        from_cache: bool = false,
    };

    fn runtimeNamespace(namespace: Namespace) backend_types.Namespace {
        return switch (namespace) {
            .nodes => .{ .name = "hbc_nodes" },
            .meta => .{ .name = "hbc_meta" },
            .quant => .{ .name = "hbc_quant" },
            .vecs => .{ .name = "hbc_vecs" },
            .vecs_transient => .{
                .name = "hbc_vecs",
                .block_cache_admission = .transient,
            },
        };
    }

    fn mapBackendNamespace(namespace: vectorindex_store.Namespace) !backend_types.Namespace {
        return runtimeNamespace(namespace);
    }

    fn openVectorIndexStore(allocator: Allocator, opened: hbc_backend.OpenedBackend) !vectorindex_store.NamespaceStore {
        var backend_store: ?backend_erased.NamespaceStore = try opened.runtimeNamespaceStore(allocator);
        errdefer if (backend_store) |*owned| owned.deinit();

        const store = try vectorindex_store.namespaceStoreFrom(
            allocator,
            backend_store.?,
            backend_types.Namespace,
            mapBackendNamespace,
        );
        backend_store = null;
        return store;
    }

    pub fn runtimeNamespaceStore(self: *HBCIndex, allocator: Allocator) !vectorindex_store.NamespaceStore {
        return try openVectorIndexStore(allocator, self.env_owner);
    }

    pub fn snapshotLsmWriteStats(self: *const HBCIndex) ?LsmWriteStats {
        return switch (self.env_owner) {
            .lsm => |handle| handle.backend.snapshotWriteStats(),
            .lmdb => null,
        };
    }

    pub fn snapshotLsmMaintenanceStats(self: *const HBCIndex) ?LsmMaintenanceStats {
        return switch (self.env_owner) {
            .lsm => |handle| handle.backend.snapshotMaintenanceStats(),
            .lmdb => null,
        };
    }

    pub fn snapshotLsmOpenStats(self: *const HBCIndex) ?LsmOpenStats {
        return switch (self.env_owner) {
            .lsm => |handle| handle.backend.snapshotOpenStats(),
            .lmdb => null,
        };
    }

    pub fn checkpointLsmWalAfterDurableBoundary(self: *HBCIndex) !void {
        switch (self.env_owner) {
            .lsm => |handle| try handle.backend.checkpointWalAfterDurableBoundary(),
            .lmdb => {},
        }
    }

    pub fn pinNativeCheckpoint(self: *HBCIndex) !lsm_backend.Backend.NativeCheckpoint {
        return switch (self.env_owner) {
            .lsm => |*handle| try handle.backend.pinNativeCheckpoint(),
            .lmdb => error.Unsupported,
        };
    }

    pub fn snapshotLsmNativeStorageStats(self: *const HBCIndex) ?lsm_backend.NativeStorageStats {
        return switch (self.env_owner) {
            .lsm => |handle| handle.backend.snapshotNativeStorageStats(),
            .lmdb => null,
        };
    }

    pub fn lsmMaintenanceScore(self: *const HBCIndex) u64 {
        return switch (self.env_owner) {
            .lsm => |handle| handle.backend.maintenanceScore(),
            .lmdb => 0,
        };
    }

    pub fn lsmMaintenanceDebtHint(self: *const HBCIndex) u64 {
        return switch (self.env_owner) {
            .lsm => |handle| handle.backend.maintenanceDebtHint(),
            .lmdb => 0,
        };
    }

    pub fn nextLsmMaintenanceWakeDelayNsBestEffort(self: *const HBCIndex) ?u64 {
        return switch (self.env_owner) {
            .lsm => |handle| handle.backend.nextMaintenanceWakeDelayNsBestEffort(),
            .lmdb => null,
        };
    }

    pub fn refreshLsmMaintenanceDebtHint(self: *HBCIndex) void {
        switch (self.env_owner) {
            .lsm => |handle| handle.backend.refreshMaintenanceDebtHint(),
            .lmdb => {},
        }
    }

    pub fn runLsmMaintenanceStep(self: *HBCIndex) !bool {
        return switch (self.env_owner) {
            .lsm => |handle| try handle.backend.runMaintenanceStep(),
            .lmdb => false,
        };
    }

    pub fn runLsmMaintenanceStepBestEffort(self: *HBCIndex) !bool {
        return switch (self.env_owner) {
            .lsm => |handle| blk: {
                // An open HBC publication session suppresses compaction and
                // ordinary flush work, but never a due WAL resource/durability
                // checkpoint. Backend bulk-mode maintenance is pressure-only.
                if (self.shouldSuppressRoutineMaintenance()) {
                    const due = if (handle.backend.nextMaintenanceWakeDelayNsBestEffort()) |delay_ns|
                        delay_ns == 0
                    else
                        false;
                    if (!due) break :blk false;
                }
                break :blk try handle.backend.runMaintenanceStepBestEffort();
            },
            .lmdb => false,
        };
    }

    pub fn lsmSessionBatchingActive(self: *const HBCIndex) bool {
        return self.write_session_depth != 0;
    }

    pub fn crossBatchPublicationActive(self: *const HBCIndex) bool {
        return self.write_session_depth != 0 and self.write_session_kind == .bulk_publication;
    }

    pub fn mustPublishMetadataPerBatch(self: *const HBCIndex) bool {
        return !self.crossBatchPublicationActive();
    }

    pub fn shouldPublishSearchStatePerBatch(self: *const HBCIndex) bool {
        return !self.crossBatchPublicationActive();
    }

    pub fn shouldSuppressRoutineMaintenance(self: *const HBCIndex) bool {
        return self.lsmSessionBatchingActive();
    }

    fn writeSessionFinishNeedsExplicitDurableSync(self: *const HBCIndex) bool {
        return switch (self.env_owner) {
            .lmdb => self.config.no_sync or self.config.no_meta_sync,
            .lsm => |handle| handle.backend.options.backend.durability != .full,
        };
    }

    fn beginWriteSession(self: *HBCIndex, kind: WriteSessionKind) !void {
        if (self.write_session_depth != 0 and self.write_session_kind != kind) {
            return error.MixedWriteSessionKinds;
        }
        switch (self.env_owner) {
            .lsm => |handle| handle.backend.beginBulkIngestSession() catch |err| {
                return err;
            },
            .lmdb => {},
        }
        const opening_outermost = self.write_session_depth == 0;
        if (opening_outermost) {
            self.deferred_quantized_nodes.clearRetainingCapacity();
            self.clearDeferredNodeKeys();
            self.deferred_oversized_leaves.clearRetainingCapacity();
            self.apply_workspace_split_bytes = 0;
            self.deferred_node_key_value_bytes = 0;
            self.observeApplyWorkspaceBytes();
            self.write_session_kind = kind;
            self.bulk_publication_may_have_mutated = false;
        }
        self.write_session_depth += 1;
        if (opening_outermost and kind == .bulk_publication) {
            self.persistBulkPublishState() catch |err| {
                self.write_session_depth -= 1;
                self.write_session_kind = null;
                switch (self.env_owner) {
                    .lsm => |handle| handle.backend.abortBulkIngestSession(),
                    .lmdb => {},
                }
                return err;
            };
        }
    }

    pub fn beginStreamingReplaySession(self: *HBCIndex) !void {
        try self.beginWriteSession(.streaming_replay);
    }

    pub fn beginBulkIngestSession(self: *HBCIndex) !void {
        try self.beginWriteSession(.bulk_publication);
    }

    fn finishWriteSessionWithOptions(
        self: *HBCIndex,
        expected_kind: WriteSessionKind,
        options: backend_types.BulkIngestFinishOptions,
    ) !void {
        if (self.write_session_depth == 0) return error.NoActiveWriteSession;
        if (self.write_session_kind != expected_kind) return error.WriteSessionKindMismatch;
        const finishing_outermost = self.write_session_depth == 1;
        if (finishing_outermost and expected_kind == .bulk_publication) {
            if (options.progress_fn) |progress| if (options.progress_ctx) |progress_ctx| {
                progress(progress_ctx, .{
                    .phase = .begin,
                    .deferred_leaf_splits = @intCast(self.deferred_oversized_leaves.count()),
                });
            };
            self.beginBulkSplitVectorWorkspace();
            errdefer self.endBulkSplitVectorWorkspace();
            var publish_window: u64 = 0;
            while (true) {
                try options.checkAdmission();
                publish_window += 1;
                const window_start_ns = nowNs();
                try self.beginPublishedSearchStateRefreshIo();
                errdefer self.abortPublishedSearchStateRefresh();
                var batch = try self.store.beginBatch();
                errdefer batch.abort();
                const split_calls_before = self.write_profile.split_leaf_calls;
                const has_more_deferred_splits = try self.normalizeDeferredOversizedLeavesForBulkFinishTxn(&batch, options);
                const split_steps = self.write_profile.split_leaf_calls - split_calls_before;
                const split_elapsed_ns = elapsedSince(window_start_ns);
                if (options.progress_fn) |progress| if (options.progress_ctx) |progress_ctx| {
                    progress(progress_ctx, .{
                        .phase = .split,
                        .publish_window = publish_window,
                        .split_steps = @intCast(split_steps),
                        .deferred_leaf_splits = @intCast(self.deferred_oversized_leaves.count()),
                        .elapsed_ns = split_elapsed_ns,
                    });
                };
                if (split_steps > 0) {
                    self.write_profile.deferred_leaf_split_publish_windows += 1;
                    self.write_profile.deferred_leaf_split_steps += split_steps;
                    self.write_profile.deferred_leaf_split_window_max_steps = @max(
                        self.write_profile.deferred_leaf_split_window_max_steps,
                        split_steps,
                    );
                }
                try self.publishDeferredNodeKeysForBulkFinishTxn(&batch);
                try self.publishDeferredQuantizedNodesForBulkFinishTxn(&batch);
                try self.flushMetadataNow(&batch);
                if (!has_more_deferred_splits) try self.clearBulkPublishStateTxn(&batch);
                const commit_start = nowNs();
                try self.markPublishedSearchStateCommitting();
                try batch.commit();
                self.write_profile.insert_commit_ns += elapsedSince(commit_start);
                self.finishPublishedSearchStateRefresh();
                if (options.progress_fn) |progress| if (options.progress_ctx) |progress_ctx| {
                    progress(progress_ctx, .{
                        .phase = .publish,
                        .publish_window = publish_window,
                        .split_steps = @intCast(split_steps),
                        .deferred_leaf_splits = @intCast(self.deferred_oversized_leaves.count()),
                        .elapsed_ns = elapsedSince(window_start_ns),
                    });
                };
                if (!has_more_deferred_splits) break;
            }
            self.endBulkSplitVectorWorkspace();
            if (options.progress_fn) |progress| if (options.progress_ctx) |progress_ctx| {
                progress(progress_ctx, .{
                    .phase = .complete,
                    .publish_window = publish_window,
                    .deferred_leaf_splits = @intCast(self.deferred_oversized_leaves.count()),
                });
            };
        }
        var finish_options = options;
        if (finishing_outermost) {
            // Both session kinds require a durable LSM boundary before their
            // caller may publish an applied sequence. Bulk publication also
            // publishes deferred HBC state above before reaching this point.
            finish_options.flush = true;
        }
        switch (self.env_owner) {
            .lsm => |handle| handle.backend.finishBulkIngestSessionWithOptions(finish_options) catch |err| {
                if (finishing_outermost and expected_kind == .bulk_publication) {
                    self.persistBulkPublishState() catch {};
                }
                return err;
            },
            .lmdb => {},
        }
        self.write_session_depth -= 1;
        if (finishing_outermost) try self.refreshPublishedSearchStateIo();
        if (self.write_session_depth == 0) {
            self.write_session_kind = null;
            self.bulk_publication_may_have_mutated = false;
            self.releaseDeferredBulkWorkspaceCapacity();
        }
    }

    pub fn finishStreamingReplaySessionWithOptions(
        self: *HBCIndex,
        options: backend_types.BulkIngestFinishOptions,
    ) !void {
        const finishing_outermost = self.write_session_depth == 1 and self.write_session_kind == .streaming_replay;
        const needs_explicit_sync = finishing_outermost and self.writeSessionFinishNeedsExplicitDurableSync();
        try self.finishWriteSessionWithOptions(.streaming_replay, options);
        if (needs_explicit_sync) try self.sync(true);
    }

    pub fn finishBulkIngestSessionWithOptions(self: *HBCIndex, options: backend_types.BulkIngestFinishOptions) !void {
        const finishing_outermost = self.write_session_depth == 1 and self.write_session_kind == .bulk_publication;
        const needs_explicit_sync = finishing_outermost and self.writeSessionFinishNeedsExplicitDurableSync();
        try self.finishWriteSessionWithOptions(.bulk_publication, options);
        if (needs_explicit_sync) try self.sync(true);
    }

    fn abortWriteSession(self: *HBCIndex, expected_kind: WriteSessionKind) void {
        if (self.write_session_depth == 0 or self.write_session_kind != expected_kind) return;
        switch (self.env_owner) {
            .lsm => |handle| handle.backend.abortBulkIngestSession(),
            .lmdb => {},
        }
        self.write_session_depth -= 1;
        if (self.write_session_depth == 0) {
            if (expected_kind == .bulk_publication and !self.bulk_publication_may_have_mutated) {
                self.clearBulkPublishStateBestEffort();
            }
            self.write_session_kind = null;
            self.bulk_publication_may_have_mutated = false;
            // Abort is a no-fail cleanup API, but it can still run on a
            // backend-runtime worker while a complete-snapshot reader owns the
            // fence. Yield cooperatively; cancellation here means the executor
            // itself is shutting down, where cache republication is moot.
            self.refreshPublishedSearchStateIo() catch {};
            self.releaseDeferredBulkWorkspaceCapacity();
        }
    }

    pub fn abortStreamingReplaySession(self: *HBCIndex) void {
        self.abortWriteSession(.streaming_replay);
    }

    pub fn abortBulkIngestSession(self: *HBCIndex) void {
        self.abortWriteSession(.bulk_publication);
    }

    const SplitResult = vectorindex_hbc_index.SplitResult;

    /// Open or create an HBC index at the given path.
    pub fn open(alloc: Allocator, path: [*:0]const u8, config: HBCConfig) !HBCIndex {
        return try openWithLsmStorage(alloc, path, config, null);
    }

    pub fn openWithLsmStorage(alloc: Allocator, path: [*:0]const u8, config: HBCConfig, lsm_storage: ?lsm_backend.Storage) !HBCIndex {
        return try openWithLsmOptions(alloc, path, config, .{ .storage = lsm_storage });
    }

    pub fn openWithLsmOptions(alloc: Allocator, path: [*:0]const u8, config: HBCConfig, lsm_options: hbc_backend.LsmOptions) !HBCIndex {
        var opened = try hbc_backend.openBackendWithLsmOptions(alloc, path, config, lsm_options);
        errdefer opened.close(alloc);

        var store = try openVectorIndexStore(alloc, opened);
        errdefer store.deinit();

        const metadata = if (lsm_options.backend_options.backend.read_only) blk: {
            var txn = try store.beginRead();
            defer txn.abort();
            const existing = txn.get(.meta, meta_key) catch |err| switch (err) {
                error.NotFound => return error.NotFound,
                else => return err,
            };
            break :blk IndexMetadata.decode(existing);
        } else blk: {
            var txn = try store.beginWrite();
            var txn_active = true;
            errdefer if (txn_active) txn.abort();

            const loaded = meta_blk: {
                const existing = txn.get(.meta, meta_key) catch |err| switch (err) {
                    error.NotFound => {
                        const meta = IndexMetadata{
                            .dims = config.dims,
                            .branching_factor = config.branching_factor,
                            .leaf_size = config.leaf_size,
                            .use_quantization = config.use_quantization,
                            .quantizer_seed = config.quantizer_seed,
                            .metric = @as(u8, @intCast(@intFromEnum(config.metric))),
                        };

                        var meta_buf: [IndexMetadata.encoded_size]u8 = undefined;
                        try txn.put(.meta, meta_key, meta.encode(&meta_buf));

                        var key_buf: [12]u8 = undefined;
                        var packed_buf: [vectorindex_hbc.packed_node_header_size]u8 = undefined;
                        const header = NodeHeader{ .is_leaf = true, .level = 0, .parent = 0 };
                        const packed_node = try vectorindex_hbc.encodePackedNodeValue(&packed_buf, header, 0, &.{}, &.{});
                        try txn.put(.nodes, encodeNodeKey(&key_buf, 1, .packed_node), packed_node);

                        break :meta_blk meta;
                    },
                    else => return err,
                };
                break :meta_blk IndexMetadata.decode(existing);
            };
            if (txn.get(.meta, bulk_publish_state_key)) |_| {
                return error.IncompleteBulkPublish;
            } else |err| switch (err) {
                error.NotFound => {},
                else => return err,
            }

            try txn.commit();
            txn_active = false;
            break :blk loaded;
        };

        var effective_config = config;
        if (metadata.version != hbc_index_version) return error.UnsupportedVersion;
        if (metadata.dims != config.dims) return error.DimensionMismatch;

        const stored_metric: vec.DistanceMetric = switch (metadata.metric) {
            @intCast(@intFromEnum(vec.DistanceMetric.l2_squared)) => .l2_squared,
            @intCast(@intFromEnum(vec.DistanceMetric.inner_product)) => .inner_product,
            @intCast(@intFromEnum(vec.DistanceMetric.cosine)) => .cosine,
            else => return error.Corrupted,
        };
        if (stored_metric != config.metric) return error.DistanceMetricMismatch;

        effective_config.metric = stored_metric;
        effective_config.branching_factor = metadata.branching_factor;
        effective_config.leaf_size = metadata.leaf_size;
        effective_config.use_quantization = metadata.use_quantization;
        effective_config.quantizer_seed = metadata.quantizer_seed;

        const env_owner: EnvOwner = opened;

        var quantizer = try quantizer_mod.RaBitQuantizer.init(
            alloc,
            effective_config.dims,
            effective_config.quantizer_seed,
            effective_config.metric,
        );
        errdefer quantizer.deinit();

        var rot = try vec.RandomOrthogonalTransformer.init(
            alloc,
            if (effective_config.use_random_ortho_trans) .givens else .none,
            effective_config.dims,
            effective_config.quantizer_seed,
        );
        errdefer rot.deinit();

        const node_clock_keys = try alloc.alloc(u64, effective_config.max_cached_nodes);
        errdefer alloc.free(node_clock_keys);
        const node_clock_refs = try alloc.alloc(bool, effective_config.max_cached_nodes);
        errdefer alloc.free(node_clock_refs);
        const quantized_clock_keys = try alloc.alloc(u64, effective_config.max_cached_nodes);
        errdefer alloc.free(quantized_clock_keys);
        const quantized_clock_refs = try alloc.alloc(bool, effective_config.max_cached_nodes);
        errdefer alloc.free(quantized_clock_refs);
        const vector_clock_keys = try alloc.alloc(u64, effective_config.max_cached_vectors);
        errdefer alloc.free(vector_clock_keys);
        const vector_clock_refs = try alloc.alloc(bool, effective_config.max_cached_vectors);
        errdefer alloc.free(vector_clock_refs);
        const metadata_clock_keys = try alloc.alloc(u64, effective_config.max_cached_metadata);
        errdefer alloc.free(metadata_clock_keys);
        const metadata_clock_refs = try alloc.alloc(bool, effective_config.max_cached_metadata);
        errdefer alloc.free(metadata_clock_refs);
        @memset(node_clock_keys, 0);
        @memset(node_clock_refs, false);
        @memset(quantized_clock_keys, 0);
        @memset(quantized_clock_refs, false);
        @memset(vector_clock_keys, 0);
        @memset(vector_clock_refs, false);
        @memset(metadata_clock_keys, 0);
        @memset(metadata_clock_refs, false);

        const cache_identity = try hbcCacheIdentityAlloc(alloc, std.mem.span(path));
        errdefer alloc.free(cache_identity.stable_path);

        const published_spare_flight = try alloc.create(PublishedSearchStateFlight);
        errdefer alloc.destroy(published_spare_flight);
        published_spare_flight.* = .{};

        const idx = HBCIndex{
            .alloc = alloc,
            .env_owner = env_owner,
            .store = store,
            .config = effective_config,
            .metadata = metadata,
            .published_root_node = .init(metadata.root_node),
            .published_active_count = .init(metadata.active_count),
            .published_node_count = .init(metadata.node_count),
            .published_generation = .init(0),
            .published_spare_flight = published_spare_flight,
            .rng = go_rand.GoPcg.init(effective_config.quantizer_seed, 1024),
            .quantizer = quantizer,
            .rot = rot,
            .node_cache = .empty,
            .node_cache_slots = .empty,
            .node_clock_keys = node_clock_keys,
            .node_clock_refs = node_clock_refs,
            .node_clock_hand = 0,
            .pinned_node_cache = .empty,
            .quantized_cache = .empty,
            .quantized_cache_slots = .empty,
            .quantized_clock_keys = quantized_clock_keys,
            .quantized_clock_refs = quantized_clock_refs,
            .quantized_clock_hand = 0,
            .pinned_quantized_cache = .empty,
            .vector_cache = .empty,
            .vector_cache_slots = .empty,
            .vector_clock_keys = vector_clock_keys,
            .vector_clock_refs = vector_clock_refs,
            .vector_clock_hand = 0,
            .metadata_cache = .empty,
            .metadata_cache_slots = .empty,
            .metadata_clock_keys = metadata_clock_keys,
            .metadata_clock_refs = metadata_clock_refs,
            .metadata_clock_hand = 0,
            .resource_manager = null,
            .local_reclaimer_identity = 0,
            .search_workspace_reclaimer_identity = 0,
            .bind_shared_cache_resource_manager = true,
            .shared_cache = null,
            .shared_cache_registered = false,
            .cache_namespace = cache_identity.namespace,
            .cache_path = cache_identity.stable_path,
            .cache_enabled = true,
            // Cache policy is governed by ResourceManager byte admission and
            // pressure actions. There is deliberately no environment or
            // per-index switch for retained-vector caching.
            .retained_vector_cache_enabled = true,
            .cache_mu = .{},
            .active_searches = .init(0),
            .hbc_cache_bytes_accounted = 0,
            .detached_hbc_accounting = .{},
            .search_workspace_bytes_accounted = 0,
            .routing_scratch_bytes_accounted = 0,
            .apply_workspace_bytes_accounted = 0,
            .apply_workspace_split_bytes = 0,
            .deferred_node_key_value_bytes = 0,
            .deferred_oversized_leaves_peak = 0,
            .bulk_split_vector_workspace = .{},
            .hbc_cache_kind_stats = .{HbcCacheKindStats{}} ** hbc_cache_kind_count,
            .deferred_quantized_nodes = .empty,
            .deferred_node_keys = .empty,
            .deferred_oversized_leaves = .empty,
            .write_session_depth = 0,
            .write_session_kind = null,
            .bulk_publication_may_have_mutated = false,
            .hilbert = null,
            .scratch_mu = .unlocked,
            .cached_scratch = null,
            .routing_scratch_mu = .unlocked,
            .cached_routing_scratch = null,
            .flat_centroid_mu = .unlocked,
            .flat_centroid_directory = null,
        };
        return idx;
    }

    pub fn beginPublishedSearchStateRefresh(self: *HBCIndex) void {
        self.published_snapshot_mu.lockExclusive();
        self.beginPublishedSearchStateRefreshLocked();
    }

    pub fn beginPublishedSearchStateRefreshIo(self: *HBCIndex) !void {
        try self.published_snapshot_mu.lockExclusiveIo(
            self.runtimeIo(),
            @as(?vectorindex_search_types.CancellationToken, null),
        );
        self.beginPublishedSearchStateRefreshLocked();
    }

    fn beginPublishedSearchStateRefreshLocked(self: *HBCIndex) void {
        const previous = self.published_mutation_epoch.fetchAdd(1, .acq_rel);
        std.debug.assert((previous & 1) == 0);
        self.published_mutation_active.store(true, .release);
    }

    fn acquirePublishedSearchStateFlight(self: *HBCIndex) !*PublishedSearchStateFlight {
        lockAtomic(&self.published_flight_mu);
        if (self.published_spare_flight) |flight| {
            self.published_spare_flight = null;
            self.published_flight_mu.unlock();
            return flight;
        }
        self.published_flight_mu.unlock();
        const flight = try self.alloc.create(PublishedSearchStateFlight);
        flight.* = .{};
        return flight;
    }

    fn releasePublishedSearchStateFlightRef(self: *HBCIndex, flight: *PublishedSearchStateFlight) void {
        var destroy = false;
        lockAtomic(&self.published_flight_mu);
        std.debug.assert(flight.refs > 0);
        flight.refs -= 1;
        if (flight.refs == 0) {
            if (self.published_spare_flight == null) {
                self.published_spare_flight = flight;
            } else {
                destroy = true;
            }
        }
        self.published_flight_mu.unlock();
        if (destroy) self.alloc.destroy(flight);
    }

    fn wakePublishedSearchStateWaiters(self: *HBCIndex) void {
        lockAtomic(&self.published_flight_mu);
        const flight = self.published_flight;
        self.published_flight = null;
        self.published_flight_mu.unlock();
        if (flight) |active| {
            active.ready.set(active.io);
            self.releasePublishedSearchStateFlightRef(active);
        }
    }

    pub fn waitForPublishedSearchState(
        self: *HBCIndex,
        observed_generation: u64,
        cancellation: ?vectorindex_search_types.CancellationToken,
    ) !void {
        while (true) {
            if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;

            lockAtomic(&self.published_flight_mu);
            const current_generation = self.published_generation.load(.acquire);
            if (current_generation != observed_generation or (current_generation & 1) == 0) {
                self.published_flight_mu.unlock();
                return;
            }
            const flight = self.published_flight orelse {
                // In-memory refreshes have a deliberately tiny odd window and
                // do not allocate a flight. Yield through the backend runtime
                // rather than falling back to an OS-thread spin.
                self.published_flight_mu.unlock();
                self.runtimeIo().sleep(std.Io.Duration.fromMilliseconds(1), .awake) catch |err| switch (err) {
                    error.Canceled => return error.Canceled,
                };
                continue;
            };
            std.debug.assert(flight.generation == observed_generation);
            flight.refs += 1;
            self.published_flight_mu.unlock();
            defer self.releasePublishedSearchStateFlightRef(flight);

            while (!flight.ready.isSet()) {
                if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;
                flight.ready.waitTimeout(flight.io, .{
                    .duration = .{
                        .raw = std.Io.Duration.fromMilliseconds(5),
                        .clock = .awake,
                    },
                }) catch |err| switch (err) {
                    error.Timeout => continue,
                    error.Canceled => return error.Canceled,
                };
            }
            return;
        }
    }

    pub fn markPublishedSearchStateCommitting(self: *HBCIndex) !void {
        std.debug.assert(self.published_mutation_active.load(.acquire));
        const flight = try self.acquirePublishedSearchStateFlight();
        const generation = self.published_generation.load(.acquire);
        std.debug.assert((generation & 1) == 0);
        flight.ready.reset();
        flight.generation = generation +% 1;
        flight.io = self.runtimeIo();
        flight.refs = 1;

        // Install the flight before making the odd generation observable, so
        // every durable-commit waiter has a stable event to retain.
        lockAtomic(&self.published_flight_mu);
        std.debug.assert(self.published_flight == null);
        self.published_flight = flight;
        _ = self.published_generation.fetchAdd(1, .acq_rel);
        self.published_flight_mu.unlock();
        vectorindex_spfresh_index.clearFlatCentroidDirectory(self);
    }

    pub fn finishPublishedSearchStateRefresh(self: *HBCIndex) void {
        // Publication paths mark explicitly just before commit, after staging
        // is complete, so the odd generation covers only the durable commit
        // and final in-memory publication.
        std.debug.assert((self.published_generation.load(.acquire) & 1) != 0);
        self.published_root_node.store(self.metadata.root_node, .release);
        self.published_active_count.store(self.metadata.active_count, .release);
        self.published_node_count.store(self.metadata.node_count, .release);
        self.finishVectorCacheMutations();
        const previous_mutation = self.published_mutation_epoch.fetchAdd(1, .acq_rel);
        std.debug.assert((previous_mutation & 1) != 0);
        self.published_mutation_active.store(false, .release);
        _ = self.published_generation.fetchAdd(1, .acq_rel);
        self.wakePublishedSearchStateWaiters();
        self.published_snapshot_mu.unlockExclusive();
    }

    pub fn abortPublishedSearchStateRefresh(self: *HBCIndex) void {
        const marked_committing = (self.published_generation.load(.acquire) & 1) != 0;
        // Mutation helpers can populate caches before the storage commit so
        // later operations in the same transaction can reuse staged state.
        // None of those entries may escape an abort. Clearing transaction-
        // populated caches is an exceptional-path cost and restores the
        // published generation without penalizing successful writes.
        self.clearNodeCache();
        self.clearQuantizedCache();
        self.clearMetadataCache();
        self.metadata.root_node = self.published_root_node.load(.acquire);
        self.metadata.active_count = self.published_active_count.load(.acquire);
        self.metadata.node_count = self.published_node_count.load(.acquire);
        self.finishVectorCacheMutations();
        const previous_mutation = self.published_mutation_epoch.fetchAdd(1, .acq_rel);
        std.debug.assert((previous_mutation & 1) != 0);
        self.published_mutation_active.store(false, .release);
        if (marked_committing) {
            _ = self.published_generation.fetchAdd(1, .acq_rel);
            self.wakePublishedSearchStateWaiters();
        }
        self.published_snapshot_mu.unlockExclusive();
    }

    pub fn refreshPublishedSearchState(self: *HBCIndex) void {
        self.beginPublishedSearchStateRefresh();
        self.refreshPublishedSearchStateLocked();
    }

    pub fn refreshPublishedSearchStateIo(self: *HBCIndex) !void {
        try self.beginPublishedSearchStateRefreshIo();
        self.refreshPublishedSearchStateLocked();
    }

    fn refreshPublishedSearchStateLocked(self: *HBCIndex) void {
        // This path only republishes already-durable in-memory state. Its odd
        // window contains no storage I/O, so readers that observe it take the
        // cooperative no-flight wait in waitForPublishedSearchState.
        _ = self.published_generation.fetchAdd(1, .acq_rel);
        vectorindex_spfresh_index.clearFlatCentroidDirectory(self);
        self.published_root_node.store(self.metadata.root_node, .release);
        self.published_active_count.store(self.metadata.active_count, .release);
        self.published_node_count.store(self.metadata.node_count, .release);
        self.finishVectorCacheMutations();
        const previous_mutation = self.published_mutation_epoch.fetchAdd(1, .acq_rel);
        std.debug.assert((previous_mutation & 1) != 0);
        self.published_mutation_active.store(false, .release);
        _ = self.published_generation.fetchAdd(1, .acq_rel);
        self.published_snapshot_mu.unlockExclusive();
    }

    pub fn shouldPublishSearchStateAfterWrite(self: *const HBCIndex) bool {
        return self.shouldPublishSearchStatePerBatch();
    }

    pub fn publishedRootNode(self: *const HBCIndex) u64 {
        return self.published_root_node.load(.acquire);
    }

    pub fn publishedActiveCount(self: *const HBCIndex) u64 {
        return self.published_active_count.load(.acquire);
    }

    pub fn publishedNodeCount(self: *const HBCIndex) u64 {
        return self.published_node_count.load(.acquire);
    }

    pub fn publishedGeneration(self: *const HBCIndex) u64 {
        return self.published_generation.load(.acquire);
    }

    pub fn publishedMutationEpoch(self: *const HBCIndex) u64 {
        return self.published_mutation_epoch.load(.acquire);
    }

    pub fn completeCoverageAlreadyValidated(self: *const HBCIndex, generation: u64) bool {
        return self.complete_coverage_generation.load(.acquire) == generation;
    }

    pub fn setIo(self: *HBCIndex, io: ?std.Io) void {
        // IndexManager binds this during construction/startup, before requests
        // are admitted. Direct library users retain the threaded fallback.
        self.runtime_io = io;
    }

    fn runtimeIo(self: *const HBCIndex) std.Io {
        return self.runtime_io orelse std.Io.Threaded.global_single_threaded.io();
    }

    fn releaseCompleteCoverageFlightRef(self: *HBCIndex, flight: *CompleteCoverageFlight) void {
        lockAtomic(&self.complete_coverage_state_mu);
        std.debug.assert(flight.refs > 0);
        flight.refs -= 1;
        const destroy = flight.refs == 0;
        self.complete_coverage_state_mu.unlock();
        if (destroy) self.alloc.destroy(flight);
    }

    fn waitForCompleteCoverageFlight(
        self: *HBCIndex,
        flight: *CompleteCoverageFlight,
        cancellation: ?vectorindex_search_types.CancellationToken,
    ) !bool {
        defer self.releaseCompleteCoverageFlightRef(flight);
        while (!flight.ready.isSet()) {
            if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;
            flight.ready.waitTimeout(flight.io, .{
                .duration = .{
                    .raw = std.Io.Duration.fromMilliseconds(5),
                    .clock = .awake,
                },
            }) catch |err| switch (err) {
                error.Timeout => continue,
                error.Canceled => return error.Canceled,
            };
        }
        if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;
        lockAtomic(&self.complete_coverage_state_mu);
        defer self.complete_coverage_state_mu.unlock();
        return switch (flight.outcome) {
            .retry => false,
            .validated => true,
            .incomplete => return error.IncompletePublishedSnapshot,
            .runtime_canceled => return error.Canceled,
        };
    }

    /// Elects one validator per publication generation. Contending callers
    /// sleep on a runtime event, consume its generation-wide result, or
    /// re-elect after a request-local failure; cancellation only removes that
    /// waiter and never cancels the producer.
    pub fn beginCompleteCoverageValidation(
        self: *HBCIndex,
        generation: u64,
        cancellation: ?vectorindex_search_types.CancellationToken,
    ) !bool {
        election: while (true) {
            if (self.completeCoverageAlreadyValidated(generation)) return false;
            if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;

            lockAtomic(&self.complete_coverage_state_mu);
            if (self.completeCoverageAlreadyValidated(generation)) {
                self.complete_coverage_state_mu.unlock();
                return false;
            }
            var current_flight = self.complete_coverage_flight;
            while (current_flight) |flight| : (current_flight = flight.next) {
                if (flight.generation != generation) continue;
                flight.refs += 1;
                self.complete_coverage_state_mu.unlock();
                if (try self.waitForCompleteCoverageFlight(flight, cancellation)) return false;
                continue :election;
            }

            self.complete_coverage_state_mu.unlock();
            const candidate = try self.alloc.create(CompleteCoverageFlight);
            candidate.* = .{ .generation = generation, .io = self.runtimeIo() };
            if (cancellation) |token| if (token.isCancelled()) {
                self.alloc.destroy(candidate);
                return error.Cancelled;
            };

            // Allocation can involve an allocator lock or a system call, so do
            // it outside the atomic state lock and repeat election afterward.
            lockAtomic(&self.complete_coverage_state_mu);
            if (self.completeCoverageAlreadyValidated(generation)) {
                self.complete_coverage_state_mu.unlock();
                self.alloc.destroy(candidate);
                return false;
            }
            current_flight = self.complete_coverage_flight;
            while (current_flight) |flight| : (current_flight = flight.next) {
                if (flight.generation != generation) continue;
                flight.refs += 1;
                self.complete_coverage_state_mu.unlock();
                self.alloc.destroy(candidate);
                if (try self.waitForCompleteCoverageFlight(flight, cancellation)) return false;
                continue :election;
            }
            candidate.next = self.complete_coverage_flight;
            self.complete_coverage_flight = candidate;
            self.complete_coverage_state_mu.unlock();
            return true;
        }
    }

    fn publishCompleteCoverageValidationOutcome(
        self: *HBCIndex,
        generation: u64,
        outcome: CompleteCoverageOutcome,
    ) void {
        lockAtomic(&self.complete_coverage_state_mu);
        var link = &self.complete_coverage_flight;
        const flight = while (link.*) |candidate| {
            if (candidate.generation == generation) {
                link.* = candidate.next;
                break candidate;
            }
            link = &candidate.next;
        } else unreachable;
        flight.outcome = outcome;
        if (outcome == .validated) {
            var current = self.complete_coverage_generation.load(.acquire);
            while (current == std.math.maxInt(u64) or generation > current) {
                current = self.complete_coverage_generation.cmpxchgWeak(
                    current,
                    generation,
                    .acq_rel,
                    .acquire,
                ) orelse break;
            }
        }
        self.complete_coverage_state_mu.unlock();

        // Waking runtime waiters can schedule work; keep it outside the atomic
        // state lock. The producer's reference keeps the flight alive here.
        flight.ready.set(flight.io);
        self.releaseCompleteCoverageFlightRef(flight);
    }

    pub fn finishCompleteCoverageValidation(self: *HBCIndex, generation: u64, validated: bool) void {
        self.publishCompleteCoverageValidationOutcome(generation, if (validated) .validated else .retry);
    }

    pub fn failCompleteCoverageValidation(self: *HBCIndex, generation: u64, err: anyerror) void {
        const outcome: CompleteCoverageOutcome = switch (err) {
            // Only errors that are themselves generation-wide coverage
            // results may escape the flight. The elected validator also does
            // request-specific scoring, so allocator, loader, and filter
            // errors must never be broadcast to unrelated callers.
            error.IncompletePublishedSnapshot => .incomplete,
            // std.Io cancellation means the bound backend runtime is no
            // longer servicing work. Re-election on the same runtime would
            // only serialize the same shutdown failure across all waiters.
            error.Canceled => .runtime_canceled,
            else => .retry,
        };
        self.publishCompleteCoverageValidationOutcome(generation, outcome);
    }

    fn releaseFlatCentroidBuildFlightRef(self: *HBCIndex, flight: *FlatCentroidBuildFlight) void {
        lockAtomic(&self.flat_centroid_build_mu);
        std.debug.assert(flight.refs > 0);
        flight.refs -= 1;
        const destroy = flight.refs == 0;
        const directory = if (destroy) switch (flight.outcome) {
            .ready => |retained| retained,
            else => null,
        } else null;
        self.flat_centroid_build_mu.unlock();
        if (destroy) {
            self.alloc.destroy(flight);
            if (directory) |retained| retained.release(self.alloc);
        }
    }

    fn waitForFlatCentroidBuildFlight(
        self: *HBCIndex,
        flight: *FlatCentroidBuildFlight,
        cancellation: ?vectorindex_search_types.CancellationToken,
    ) !vectorindex_spfresh_index.FlatCentroidBuildClaim {
        defer self.releaseFlatCentroidBuildFlightRef(flight);
        while (!flight.ready.isSet()) {
            if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;
            flight.ready.waitTimeout(flight.io, .{
                .duration = .{
                    .raw = std.Io.Duration.fromMilliseconds(5),
                    .clock = .awake,
                },
            }) catch |err| switch (err) {
                error.Timeout => continue,
                error.Canceled => return error.Canceled,
            };
        }
        if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;
        lockAtomic(&self.flat_centroid_build_mu);
        defer self.flat_centroid_build_mu.unlock();
        return switch (flight.outcome) {
            .retry => .retry,
            .failed => |err| return err,
            .ready => |retained| blk: {
                retained.retain();
                break :blk .{ .ready = retained };
            },
        };
    }

    /// Elect one cold directory builder per generation. Same-generation
    /// waiters sleep through the backend runtime; a durable reader of an older
    /// MVCC generation cannot head-of-line block the current publication.
    pub fn beginFlatCentroidDirectoryBuild(
        self: *HBCIndex,
        generation: u64,
        cancellation: ?vectorindex_search_types.CancellationToken,
    ) !vectorindex_spfresh_index.FlatCentroidBuildClaim {
        if (cancellation) |token| if (token.isCancelled()) return error.Cancelled;
        lockAtomic(&self.flat_centroid_build_mu);
        var current_flight = self.flat_centroid_build_flight;
        while (current_flight) |flight| : (current_flight = flight.next) {
            if (flight.generation != generation) continue;
            flight.refs += 1;
            self.flat_centroid_build_mu.unlock();
            return try self.waitForFlatCentroidBuildFlight(flight, cancellation);
        }
        self.flat_centroid_build_mu.unlock();

        const candidate = try self.alloc.create(FlatCentroidBuildFlight);
        candidate.* = .{ .generation = generation, .io = self.runtimeIo() };
        if (cancellation) |token| if (token.isCancelled()) {
            self.alloc.destroy(candidate);
            return error.Cancelled;
        };

        lockAtomic(&self.flat_centroid_build_mu);
        current_flight = self.flat_centroid_build_flight;
        while (current_flight) |flight| : (current_flight = flight.next) {
            if (flight.generation != generation) continue;
            flight.refs += 1;
            self.flat_centroid_build_mu.unlock();
            self.alloc.destroy(candidate);
            return try self.waitForFlatCentroidBuildFlight(flight, cancellation);
        }
        candidate.next = self.flat_centroid_build_flight;
        self.flat_centroid_build_flight = candidate;
        self.flat_centroid_build_mu.unlock();
        return .owner;
    }

    pub fn finishFlatCentroidDirectoryBuild(
        self: *HBCIndex,
        generation: u64,
        outcome: vectorindex_spfresh_index.FlatCentroidBuildOutcome,
    ) void {
        lockAtomic(&self.flat_centroid_build_mu);
        var link = &self.flat_centroid_build_flight;
        const flight = while (link.*) |candidate| {
            if (candidate.generation == generation) {
                link.* = candidate.next;
                break candidate;
            }
            link = &candidate.next;
        } else unreachable;
        switch (outcome) {
            .ready => |retained| retained.retain(),
            else => {},
        }
        flight.outcome = outcome;
        self.flat_centroid_build_mu.unlock();
        flight.ready.set(flight.io);
        self.releaseFlatCentroidBuildFlightRef(flight);
    }

    pub fn reserveFlatCentroidDirectoryBuildBytes(
        self: *HBCIndex,
        reservation: vectorindex_spfresh_index.FlatCentroidBuildReservation,
    ) !vectorindex_spfresh_index.FlatCentroidBuildReservation {
        lockAtomic(&self.flat_centroid_build_accounting_mu);
        const transient_next = std.math.add(
            u64,
            self.flat_centroid_build_bytes_accounted,
            reservation.transient_bytes,
        ) catch {
            self.flat_centroid_build_accounting_mu.unlock();
            return error.ResourceBudgetExceeded;
        };
        if (self.resource_manager) |manager| {
            manager.adjustUsage(.dense_search_working_set, &self.flat_centroid_build_bytes_accounted, transient_next) catch |err| {
                self.flat_centroid_build_accounting_mu.unlock();
                return err;
            };
        } else {
            self.flat_centroid_build_bytes_accounted = transient_next;
        }
        self.flat_centroid_build_accounting_mu.unlock();
        errdefer self.releaseFlatCentroidDirectoryBuildBytes(.{
            .transient_bytes = reservation.transient_bytes,
        });

        lockAtomic(&self.flat_centroid_build_accounting_mu);
        const retained_next = std.math.add(
            u64,
            self.flat_centroid_retained_reservation_bytes_accounted,
            reservation.retained_bytes,
        ) catch {
            self.flat_centroid_build_accounting_mu.unlock();
            return error.ResourceBudgetExceeded;
        };
        if (self.resource_manager) |manager| {
            manager.adjustUsage(.hbc_node_metadata_cache, &self.flat_centroid_retained_reservation_bytes_accounted, retained_next) catch |err| {
                self.flat_centroid_build_accounting_mu.unlock();
                return err;
            };
        } else {
            self.flat_centroid_retained_reservation_bytes_accounted = retained_next;
        }
        self.flat_centroid_build_accounting_mu.unlock();
        return reservation;
    }

    pub fn releaseFlatCentroidDirectoryBuildBytes(
        self: *HBCIndex,
        reservation: vectorindex_spfresh_index.FlatCentroidBuildReservation,
    ) void {
        lockAtomic(&self.flat_centroid_build_accounting_mu);
        defer self.flat_centroid_build_accounting_mu.unlock();
        const transient_next = self.flat_centroid_build_bytes_accounted -| reservation.transient_bytes;
        const retained_next = self.flat_centroid_retained_reservation_bytes_accounted -| reservation.retained_bytes;
        if (self.resource_manager) |manager| {
            manager.observeUsage(.dense_search_working_set, &self.flat_centroid_build_bytes_accounted, transient_next);
            manager.observeUsage(.hbc_node_metadata_cache, &self.flat_centroid_retained_reservation_bytes_accounted, retained_next);
        } else {
            self.flat_centroid_build_bytes_accounted = transient_next;
            self.flat_centroid_retained_reservation_bytes_accounted = retained_next;
        }
    }

    fn releaseFlatCentroidDirectoryAccounting(context: *anyopaque, bytes: u64) void {
        const self: *HBCIndex = @ptrCast(@alignCast(context));
        lockAtomic(&self.flat_centroid_accounting_mu);
        defer self.flat_centroid_accounting_mu.unlock();
        const next = self.flat_centroid_directory_bytes_accounted -| bytes;
        if (self.resource_manager) |manager| {
            manager.observeUsage(.hbc_node_metadata_cache, &self.flat_centroid_directory_bytes_accounted, next);
        } else {
            self.flat_centroid_directory_bytes_accounted = next;
        }
    }

    pub fn accountFlatCentroidDirectory(
        self: *HBCIndex,
        directory: *vectorindex_spfresh_index.FlatCentroidDirectory,
        build_reservation: vectorindex_spfresh_index.FlatCentroidBuildReservation,
    ) !void {
        const bytes = directory.bytes();
        if (bytes > build_reservation.retained_bytes) return error.ResourceBudgetExceeded;
        lockAtomic(&self.flat_centroid_build_accounting_mu);
        lockAtomic(&self.flat_centroid_accounting_mu);
        errdefer self.flat_centroid_accounting_mu.unlock();
        errdefer self.flat_centroid_build_accounting_mu.unlock();
        if (build_reservation.retained_bytes > self.flat_centroid_retained_reservation_bytes_accounted)
            return error.ResourceAccountingMismatch;
        const reservation_next = self.flat_centroid_retained_reservation_bytes_accounted - build_reservation.retained_bytes;
        const directory_next = std.math.add(
            u64,
            self.flat_centroid_directory_bytes_accounted,
            bytes,
        ) catch return error.ResourceBudgetExceeded;
        if (self.resource_manager) |manager| {
            try manager.transferUsage(
                .hbc_node_metadata_cache,
                &self.flat_centroid_retained_reservation_bytes_accounted,
                reservation_next,
                &self.flat_centroid_directory_bytes_accounted,
                directory_next,
            );
        } else {
            self.flat_centroid_retained_reservation_bytes_accounted = reservation_next;
            self.flat_centroid_directory_bytes_accounted = directory_next;
        }
        directory.accounting_context = self;
        directory.release_accounting = releaseFlatCentroidDirectoryAccounting;
        directory.accounted_bytes = bytes;
        self.flat_centroid_accounting_mu.unlock();
        self.flat_centroid_build_accounting_mu.unlock();
        // Retained ownership moved atomically above; only transient workspace
        // remains in the build reservation.
        self.releaseFlatCentroidDirectoryBuildBytes(.{
            .transient_bytes = build_reservation.transient_bytes,
        });
    }

    pub fn noteCompleteCoverageValidated(self: *HBCIndex, generation: u64) void {
        self.complete_coverage_generation.store(generation, .release);
    }

    pub fn attachResourceManager(self: *HBCIndex, resource_manager: *resource_manager_mod.ResourceManager) void {
        self.attachResourceManagerWithSharedCacheBinding(resource_manager, true);
    }

    pub fn attachResourceManagerWithSharedCacheBinding(
        self: *HBCIndex,
        resource_manager: *resource_manager_mod.ResourceManager,
        bind_shared_cache_resource_manager: bool,
    ) void {
        // Reattachment is a configuration update, not an accounting reset.
        // In particular, observeUsage requires the observer ledger to retain
        // its previous value for the same manager.
        if (self.resource_manager == resource_manager) {
            self.bind_shared_cache_resource_manager = bind_shared_cache_resource_manager;
            self.ensureSearchWorkspaceReclaimer(resource_manager);
            self.ensureFlatCentroidReclaimer(resource_manager);
            if (self.shared_cache) |cache| {
                if (bind_shared_cache_resource_manager) cache.attachResourceManager(resource_manager);
                return;
            }
            if (self.local_reclaimer_identity == 0) {
                self.local_reclaimer_identity = resource_manager.registerReclaimer(
                    .hbc_node_metadata_cache,
                    self,
                    reclaimLocalForResourceManager,
                ) catch |err| blk: {
                    std.log.err("failed to register local HBC cache reclaimer: {s}", .{@errorName(err)});
                    break :blk 0;
                };
            }
            self.refreshAndEnforceHbcCacheUsage(.none());
            return;
        }

        const current_search_bytes = self.search_workspace_bytes_accounted;
        const current_flat_directory_bytes = self.flat_centroid_directory_bytes_accounted;
        const current_flat_build_bytes = self.flat_centroid_build_bytes_accounted;
        const current_flat_retained_reservation_bytes = self.flat_centroid_retained_reservation_bytes_accounted;
        const current_routing_bytes = self.routing_scratch_bytes_accounted;
        const current_apply_bytes = self.currentApplyWorkspaceBytes();
        const current_hbc_bytes = if (self.shared_cache == null) self.hbcCacheBytes() else 0;
        if (self.resource_manager) |old_manager| {
            old_manager.unregisterReclaimer(self.local_reclaimer_identity);
            self.local_reclaimer_identity = 0;
            old_manager.unregisterReclaimer(self.search_workspace_reclaimer_identity);
            self.search_workspace_reclaimer_identity = 0;
            old_manager.unregisterReclaimer(self.flat_centroid_reclaimer_identity);
            self.flat_centroid_reclaimer_identity = 0;
            old_manager.observeUsage(.dense_search_working_set, &self.search_workspace_bytes_accounted, 0);
            old_manager.observeUsage(.dense_search_working_set, &self.flat_centroid_build_bytes_accounted, 0);
            old_manager.observeUsage(.hbc_node_metadata_cache, &self.flat_centroid_directory_bytes_accounted, 0);
            old_manager.observeUsage(.hbc_node_metadata_cache, &self.flat_centroid_retained_reservation_bytes_accounted, 0);
            old_manager.observeUsage(.dense_routing_working_set, &self.routing_scratch_bytes_accounted, 0);
            old_manager.observeUsage(.dense_apply_working_set, &self.apply_workspace_bytes_accounted, 0);
            old_manager.observeUsage(.hbc_node_metadata_cache, &self.hbc_cache_bytes_accounted, 0);
        } else {
            self.search_workspace_bytes_accounted = 0;
            self.flat_centroid_build_bytes_accounted = 0;
            self.flat_centroid_directory_bytes_accounted = 0;
            self.flat_centroid_retained_reservation_bytes_accounted = 0;
            self.routing_scratch_bytes_accounted = 0;
            self.apply_workspace_bytes_accounted = 0;
            self.hbc_cache_bytes_accounted = 0;
        }
        self.resource_manager = resource_manager;
        self.bind_shared_cache_resource_manager = bind_shared_cache_resource_manager;
        resource_manager.observeUsage(.dense_search_working_set, &self.search_workspace_bytes_accounted, current_search_bytes);
        resource_manager.observeUsage(.dense_search_working_set, &self.flat_centroid_build_bytes_accounted, current_flat_build_bytes);
        resource_manager.observeUsage(.hbc_node_metadata_cache, &self.flat_centroid_directory_bytes_accounted, current_flat_directory_bytes);
        resource_manager.observeUsage(.hbc_node_metadata_cache, &self.flat_centroid_retained_reservation_bytes_accounted, current_flat_retained_reservation_bytes);
        resource_manager.observeUsage(.dense_routing_working_set, &self.routing_scratch_bytes_accounted, current_routing_bytes);
        resource_manager.observeUsage(.dense_apply_working_set, &self.apply_workspace_bytes_accounted, current_apply_bytes);
        self.detached_hbc_accounting.attach(resource_manager);
        self.ensureSearchWorkspaceReclaimer(resource_manager);
        self.ensureFlatCentroidReclaimer(resource_manager);
        if (self.shared_cache) |cache| {
            if (bind_shared_cache_resource_manager) cache.attachResourceManager(resource_manager);
            return;
        }
        resource_manager.observeUsage(.hbc_node_metadata_cache, &self.hbc_cache_bytes_accounted, current_hbc_bytes);
        self.local_reclaimer_identity = resource_manager.registerReclaimer(
            .hbc_node_metadata_cache,
            self,
            reclaimLocalForResourceManager,
        ) catch |err| blk: {
            std.log.err("failed to register local HBC cache reclaimer: {s}", .{@errorName(err)});
            break :blk 0;
        };
        self.enforceHbcCacheBudget(.none());
    }

    fn reclaimLocalForResourceManager(context: *anyopaque, target_bytes: u64) u64 {
        const self: *HBCIndex = @ptrCast(@alignCast(context));
        if (target_bytes == 0 or self.shared_cache != null or !self.cache_mu.tryLockExclusive()) return 0;
        defer self.cache_mu.unlockExclusive();
        const before = self.hbcCacheBytes() +| self.detached_hbc_accounting.current();
        while (before -| (self.hbcCacheBytes() +| self.detached_hbc_accounting.current()) < target_bytes) {
            if (!self.evictOneHbcCacheEntry(.none())) break;
        }
        self.refreshHbcCacheUsage();
        return before -| (self.hbcCacheBytes() +| self.detached_hbc_accounting.current());
    }

    fn ensureSearchWorkspaceReclaimer(
        self: *HBCIndex,
        manager: *resource_manager_mod.ResourceManager,
    ) void {
        if (self.search_workspace_reclaimer_identity != 0) return;
        self.search_workspace_reclaimer_identity = manager.registerReclaimer(
            .dense_search_working_set,
            self,
            reclaimSearchWorkspaceForResourceManager,
        ) catch |err| {
            std.log.err("failed to register HBC search workspace reclaimer: {s}", .{@errorName(err)});
            return;
        };
    }

    fn ensureFlatCentroidReclaimer(
        self: *HBCIndex,
        manager: *resource_manager_mod.ResourceManager,
    ) void {
        if (self.flat_centroid_reclaimer_identity != 0) return;
        self.flat_centroid_reclaimer_identity = manager.registerReclaimer(
            .hbc_node_metadata_cache,
            self,
            reclaimFlatCentroidDirectoryForResourceManager,
        ) catch |err| {
            std.log.err("failed to register HBC flat centroid directory reclaimer: {s}", .{@errorName(err)});
            return;
        };
    }

    fn flatCentroidDirectoryAccountedBytes(self: *HBCIndex) u64 {
        lockAtomic(&self.flat_centroid_accounting_mu);
        defer self.flat_centroid_accounting_mu.unlock();
        return self.flat_centroid_directory_bytes_accounted;
    }

    fn reclaimFlatCentroidDirectoryForResourceManager(context: *anyopaque, target_bytes: u64) u64 {
        const self: *HBCIndex = @ptrCast(@alignCast(context));
        if (target_bytes == 0 or !self.flat_centroid_mu.tryLock()) return 0;
        const before = self.flatCentroidDirectoryAccountedBytes();
        const stale = self.flat_centroid_directory;
        self.flat_centroid_directory = null;
        self.flat_centroid_mu.unlock();
        if (stale) |directory| directory.release(self.alloc);
        return before -| self.flatCentroidDirectoryAccountedBytes();
    }

    fn reclaimSearchWorkspaceForResourceManager(context: *anyopaque, target_bytes: u64) u64 {
        const self: *HBCIndex = @ptrCast(@alignCast(context));
        if (target_bytes == 0 or !self.scratch_mu.tryLock()) return 0;
        defer self.scratch_mu.unlock();
        const scratch = if (self.cached_scratch) |*cached| cached else return 0;
        const max_candidates = @max(
            @as(usize, @intCast(self.metadata.branching_factor)),
            @as(usize, @intCast(self.metadata.leaf_size)),
        );
        const reclaimed = scratch.reclaimRetainedWorkspace(self.alloc, target_bytes, max_candidates);
        if (reclaimed == 0) return 0;
        self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted -| reclaimed);
        return reclaimed;
    }

    pub fn attachSharedCache(self: *HBCIndex, cache: *Cache) void {
        if (self.resource_manager) |manager| {
            manager.unregisterReclaimer(self.local_reclaimer_identity);
            self.local_reclaimer_identity = 0;
        }
        if (self.shared_cache) |current| {
            if (current == cache and self.shared_cache_registered) return;
            if (self.shared_cache_registered) current.unregisterNamespacePath(self.cache_namespace, self.cache_path);
            self.shared_cache = null;
            self.shared_cache_registered = false;
        }
        self.clearNodeCache();
        self.clearQuantizedCache();
        self.clearVectorCache();
        self.clearMetadataCache();
        if (!cache.registerNamespacePath(self.cache_namespace, self.cache_path)) return;
        self.shared_cache = cache;
        self.shared_cache_registered = true;
        if (self.bind_shared_cache_resource_manager) {
            if (self.resource_manager) |manager| cache.attachResourceManager(manager);
        }
    }

    /// Test-only fault/cache-coherency control. Production cache policy is
    /// owned by ResourceManager.
    fn setCacheEnabled(self: *HBCIndex, enabled: bool) void {
        if (self.cache_enabled == enabled) return;
        self.cache_enabled = enabled;
        if (!enabled) {
            self.clearNodeCache();
            self.clearQuantizedCache();
            self.clearVectorCache();
            self.clearMetadataCache();
        }
    }

    /// Explicit policy control. Production IndexManager resolves its adaptive
    /// default from ResourceManager capacity; admission and eviction remain
    /// governed after the path is enabled.
    pub fn setRetainedVectorCacheEnabled(self: *HBCIndex, enabled: bool) void {
        if (self.retained_vector_cache_enabled == enabled) return;
        self.retained_vector_cache_enabled = enabled;
        if (!enabled) self.clearVectorCache();
    }

    pub fn setBypassExternalVectorCache(self: *HBCIndex, enabled: bool) void {
        self.bypass_external_vector_cache = enabled;
    }

    pub fn acquireRoutingScratch(self: *HBCIndex) !RoutingScratchHandle {
        lockAtomic(&self.routing_scratch_mu);
        defer self.routing_scratch_mu.unlock();
        if (self.cached_routing_scratch) |scratch| {
            self.cached_routing_scratch = null;
            return .{ .scratch = scratch, .from_cache = true };
        }
        const scratch = try RoutingScratch.init(self.alloc, self.config.dims, self.config.branching_factor);
        self.observeRoutingScratchBytes(self.routing_scratch_bytes_accounted + scratch.bytes());
        return .{
            .scratch = scratch,
            .from_cache = false,
        };
    }

    pub fn releaseRoutingScratch(self: *HBCIndex, handle: *RoutingScratchHandle) void {
        lockAtomic(&self.routing_scratch_mu);
        defer self.routing_scratch_mu.unlock();
        if (self.cached_routing_scratch == null) {
            self.cached_routing_scratch = handle.scratch;
        } else {
            var scratch = handle.scratch;
            self.observeRoutingScratchBytes(self.routing_scratch_bytes_accounted -| scratch.bytes());
            scratch.deinit(self.alloc);
        }
    }

    fn observeRoutingScratchBytes(self: *HBCIndex, next: u64) void {
        if (self.resource_manager) |manager| {
            manager.observeUsage(.dense_routing_working_set, &self.routing_scratch_bytes_accounted, next);
        } else {
            self.routing_scratch_bytes_accounted = next;
        }
    }

    pub fn observeSearchWorkspaceBytes(self: *HBCIndex, next: u64) void {
        if (self.resource_manager) |manager| {
            manager.observeUsage(.dense_search_working_set, &self.search_workspace_bytes_accounted, next);
        } else {
            self.search_workspace_bytes_accounted = next;
        }
    }

    /// Admit index-sized search growth before allocating it. The
    /// scratch mutex serializes the shared observer ledger across concurrent
    /// request handles; ordinary bounded search growth stays on the existing
    /// telemetry-only path.
    pub fn reserveSearchScratchBytes(self: *HBCIndex, handle: *ScratchHandle, target_bytes: u64) !void {
        if (target_bytes <= handle.accounted_bytes) return;
        lockAtomic(&self.scratch_mu);
        defer self.scratch_mu.unlock();

        const delta = target_bytes - handle.accounted_bytes;
        const next_total = std.math.add(u64, self.search_workspace_bytes_accounted, delta) catch
            return error.ResourceBudgetExceeded;
        if (self.resource_manager) |manager| {
            try manager.adjustUsage(.dense_search_working_set, &self.search_workspace_bytes_accounted, next_total);
        } else {
            self.search_workspace_bytes_accounted = next_total;
        }
        handle.accounted_bytes = target_bytes;
    }

    /// Reconcile a failed pre-admitted growth to the memory the allocator
    /// actually retained. A realloc or a sequence of allocations may have
    /// succeeded partially before the failure, so rolling all the way back to
    /// the old value would temporarily make live memory invisible.
    pub fn rollbackSearchScratchBytes(self: *HBCIndex, handle: *ScratchHandle, previous_bytes: u64) void {
        lockAtomic(&self.scratch_mu);
        defer self.scratch_mu.unlock();

        const live_bytes = handle.scratch.bytes();
        const reconciled_bytes = @max(previous_bytes, live_bytes);
        if (reconciled_bytes > handle.accounted_bytes) {
            self.observeSearchWorkspaceBytes(
                self.search_workspace_bytes_accounted +| (reconciled_bytes - handle.accounted_bytes),
            );
        } else if (reconciled_bytes < handle.accounted_bytes) {
            self.observeSearchWorkspaceBytes(
                self.search_workspace_bytes_accounted -| (handle.accounted_bytes - reconciled_bytes),
            );
        }
        handle.accounted_bytes = reconciled_bytes;
    }

    /// Reconcile post-request shrink under the same serialization used for
    /// pre-admission. Growth here remains telemetry-only because all
    /// index-sized growth is admitted before allocation.
    pub fn reconcileSearchScratchBytes(self: *HBCIndex, handle: *ScratchHandle, next_bytes: u64) void {
        lockAtomic(&self.scratch_mu);
        defer self.scratch_mu.unlock();
        if (next_bytes > handle.accounted_bytes) {
            self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted +| (next_bytes - handle.accounted_bytes));
        } else if (next_bytes < handle.accounted_bytes) {
            self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted -| (handle.accounted_bytes - next_bytes));
        }
        handle.accounted_bytes = next_bytes;
    }

    fn currentApplyWorkspaceBytes(self: *const HBCIndex) u64 {
        const staged_node_key_bytes = @as(u64, @intCast(self.deferred_node_keys.count())) *
            @as(u64, @intCast(@sizeOf(u128) + @sizeOf(DeferredNodeValue)));
        return self.apply_workspace_split_bytes +
            @as(u64, @intCast(self.deferred_oversized_leaves.count() * @sizeOf(u64))) +
            self.deferred_node_key_value_bytes +
            staged_node_key_bytes;
    }

    fn observeApplyWorkspaceBytes(self: *HBCIndex) void {
        const next = self.currentApplyWorkspaceBytes();
        if (self.resource_manager) |manager| {
            manager.observeUsage(.dense_apply_working_set, &self.apply_workspace_bytes_accounted, next);
        } else {
            self.apply_workspace_bytes_accounted = next;
        }
    }

    fn maybeObserveApplyWorkspaceBytes(self: *HBCIndex) void {
        const next = self.currentApplyWorkspaceBytes();
        const current = self.apply_workspace_bytes_accounted;
        const delta = if (next >= current) next - current else current - next;
        if (delta >= 1024 * 1024 or (self.deferred_node_keys.count() & 1023) == 0) {
            self.observeApplyWorkspaceBytes();
        }
    }

    pub fn addApplyWorkspaceBytes(self: *HBCIndex, bytes: u64) void {
        if (bytes == 0) return;
        self.apply_workspace_split_bytes +|= bytes;
        self.observeApplyWorkspaceBytes();
    }

    pub fn releaseApplyWorkspaceBytes(self: *HBCIndex, bytes: u64) void {
        if (bytes == 0) return;
        self.apply_workspace_split_bytes -|= bytes;
        self.observeApplyWorkspaceBytes();
    }

    fn observeBulkSplitVectorWorkspaceBytes(self: *HBCIndex) void {
        const next = self.bulk_split_vector_workspace.bytes();
        const current = self.bulk_split_vector_workspace.accounted_bytes;
        if (next > current) {
            self.apply_workspace_split_bytes +|= next - current;
        } else if (current > next) {
            self.apply_workspace_split_bytes -|= current - next;
        }
        self.bulk_split_vector_workspace.accounted_bytes = next;
        self.observeApplyWorkspaceBytes();
    }

    fn beginBulkSplitVectorWorkspace(self: *HBCIndex) void {
        self.deinitBulkSplitVectorWorkspace();
        self.bulk_split_vector_workspace.active = true;
    }

    fn endBulkSplitVectorWorkspace(self: *HBCIndex) void {
        self.bulk_split_vector_workspace.active = false;
        self.deinitBulkSplitVectorWorkspace();
    }

    fn deinitBulkSplitVectorWorkspace(self: *HBCIndex) void {
        if (self.bulk_split_vector_workspace.accounted_bytes != 0) {
            self.apply_workspace_split_bytes -|= self.bulk_split_vector_workspace.accounted_bytes;
            self.bulk_split_vector_workspace.accounted_bytes = 0;
            self.observeApplyWorkspaceBytes();
        }
        self.bulk_split_vector_workspace.deinit(self.alloc);
    }

    fn bulkSplitVectorWorkspaceBudgetBytes(self: *const HBCIndex) u64 {
        _ = self;
        return default_bulk_split_vector_workspace_budget_bytes;
    }

    fn bulkSplitVectorWorkspaceLookup(self: *HBCIndex, vector_id: u64, out: []f32) bool {
        const workspace = &self.bulk_split_vector_workspace;
        if (!workspace.active) return false;
        if (out.len != self.config.dims) return false;
        const offset = workspace.map.get(vector_id) orelse return false;
        if (offset + self.config.dims > workspace.vectors.items.len) return false;
        @memcpy(out, workspace.vectors.items[offset .. offset + self.config.dims]);
        return true;
    }

    fn bulkSplitVectorWorkspaceAdmit(self: *HBCIndex, vector_id: u64, transformed: []const f32) void {
        var workspace = &self.bulk_split_vector_workspace;
        if (!workspace.active) return;
        if (transformed.len != self.config.dims) return;
        if (workspace.map.contains(vector_id)) return;

        const vector_bytes = @as(u64, @intCast(self.config.dims)) * @sizeOf(f32);
        if (workspace.accounted_bytes + vector_bytes > self.bulkSplitVectorWorkspaceBudgetBytes()) return;

        const offset = workspace.vectors.items.len;
        workspace.vectors.appendSlice(self.alloc, transformed) catch return;
        workspace.map.put(self.alloc, vector_id, offset) catch {
            workspace.vectors.items.len = offset;
            return;
        };
        self.observeBulkSplitVectorWorkspaceBytes();
    }

    pub fn setExternalVectorLoader(self: *HBCIndex, ctx: *anyopaque, loader: ExternalVectorLoader) void {
        self.external_vector_ctx = ctx;
        self.external_vector_loader = loader;
    }

    pub fn setExternalVectorScratchLoader(self: *HBCIndex, ctx: *anyopaque, loader: ExternalVectorScratchLoader) void {
        self.external_vector_ctx = ctx;
        self.external_vector_scratch_loader = loader;
    }

    pub fn setExternalVectorBatchScratchLoader(self: *HBCIndex, ctx: *anyopaque, loader: ExternalVectorBatchScratchLoader) void {
        self.external_vector_ctx = ctx;
        self.external_vector_batch_scratch_loader = loader;
    }

    pub fn setExternalVectorBatchTransformedMatrixLoader(self: *HBCIndex, ctx: *anyopaque, loader: ExternalVectorBatchTransformedMatrixLoader) void {
        self.external_vector_ctx = ctx;
        self.external_vector_batch_transformed_matrix_loader = loader;
    }

    pub fn setExternalVectorBatchDistanceLoader(self: *HBCIndex, ctx: *anyopaque, loader: ExternalVectorBatchDistanceLoader) void {
        self.external_vector_ctx = ctx;
        self.external_vector_batch_distance_loader = loader;
    }

    pub fn hasExternalVectorLoader(self: *const HBCIndex) bool {
        return self.external_vector_ctx != null and
            (self.external_vector_loader != null or self.external_vector_scratch_loader != null or self.external_vector_batch_scratch_loader != null or self.external_vector_batch_transformed_matrix_loader != null or self.external_vector_batch_distance_loader != null);
    }

    pub fn refreshHbcCacheUsage(self: *HBCIndex) void {
        if (self.shared_cache != null) return;
        const manager = self.resource_manager orelse return;
        manager.observeUsage(.hbc_node_metadata_cache, &self.hbc_cache_bytes_accounted, self.refreshHbcCacheKindBytes());
    }

    fn hbcCacheBytes(self: *const HBCIndex) u64 {
        var total: u64 = 0;
        var node_it = self.node_cache.iterator();
        while (node_it.next()) |entry| total +|= estimateNodeCacheBytes(&entry.value_ptr.*.node);
        var quantized_it = self.quantized_cache.iterator();
        while (quantized_it.next()) |entry| total +|= estimateQuantizedCacheBytes(&entry.value_ptr.*.quantized);
        var vector_it = self.vector_cache.iterator();
        while (vector_it.next()) |entry| total +|= estimateVectorCacheBytes(entry.value_ptr.*.vector);
        var metadata_it = self.metadata_cache.iterator();
        while (metadata_it.next()) |entry| total +|= estimateMetadataCacheBytes(entry.value_ptr.*.metadata);
        return total;
    }

    fn refreshHbcCacheKindBytes(self: *HBCIndex) u64 {
        var bytes: [hbc_cache_kind_count]u64 = .{0} ** hbc_cache_kind_count;
        var node_it = self.node_cache.iterator();
        while (node_it.next()) |entry| bytes[@intFromEnum(HbcCacheKind.node)] +|= estimateNodeCacheBytes(&entry.value_ptr.*.node);
        var quantized_it = self.quantized_cache.iterator();
        while (quantized_it.next()) |entry| bytes[@intFromEnum(HbcCacheKind.quantized)] +|= estimateQuantizedCacheBytes(&entry.value_ptr.*.quantized);
        var vector_it = self.vector_cache.iterator();
        while (vector_it.next()) |entry| bytes[@intFromEnum(HbcCacheKind.vector)] +|= estimateVectorCacheBytes(entry.value_ptr.*.vector);
        var metadata_it = self.metadata_cache.iterator();
        while (metadata_it.next()) |entry| bytes[@intFromEnum(HbcCacheKind.metadata)] +|= estimateMetadataCacheBytes(entry.value_ptr.*.metadata);

        var total: u64 = 0;
        for (bytes, 0..) |used_bytes, i| {
            total +|= used_bytes;
            self.hbc_cache_kind_stats[i].used_bytes = used_bytes;
            self.hbc_cache_kind_stats[i].peak_bytes = @max(self.hbc_cache_kind_stats[i].peak_bytes, used_bytes);
        }
        return total;
    }

    fn hbcCacheEntryBytes(self: *const HBCIndex, kind: HbcCacheKind, key: u64) u64 {
        return switch (kind) {
            .node => if (self.node_cache.get(key)) |entry| estimateNodeCacheBytes(&entry.node) else 0,
            .quantized => if (self.quantized_cache.get(key)) |entry| estimateQuantizedCacheBytes(&entry.quantized) else 0,
            .vector => if (self.vector_cache.get(key)) |entry| estimateVectorCacheBytes(entry.vector) else 0,
            .metadata => if (self.metadata_cache.get(key)) |entry| estimateMetadataCacheBytes(entry.metadata) else 0,
        };
    }

    fn noteHbcCacheInsertion(self: *HBCIndex, kind: HbcCacheKind) void {
        self.hbc_cache_kind_stats[@intFromEnum(kind)].insertions += 1;
    }

    fn noteHbcCacheLookup(self: *HBCIndex, kind: HbcCacheKind, hit: bool) void {
        const counters = &self.hbc_cache_kind_stats[@intFromEnum(kind)];
        if (hit) {
            _ = @atomicRmw(u64, &counters.hits, .Add, 1, .monotonic);
        } else {
            _ = @atomicRmw(u64, &counters.misses, .Add, 1, .monotonic);
        }
    }

    fn noteHbcCacheAdmissionSkip(self: *HBCIndex, kind: HbcCacheKind) void {
        self.hbc_cache_kind_stats[@intFromEnum(kind)].admission_skips += 1;
    }

    fn noteHbcCacheEviction(self: *HBCIndex, kind: HbcCacheKind) void {
        self.hbc_cache_kind_stats[@intFromEnum(kind)].evictions += 1;
    }

    pub fn hbcCacheStats(self: *HBCIndex) HbcCacheStats {
        if (self.shared_cache) |cache| return cache.namespaceStats(self.cache_namespace);
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        const total_bytes = self.refreshHbcCacheKindBytes();
        return .{
            .total_bytes = total_bytes,
            .accounted_bytes = self.hbc_cache_bytes_accounted +| self.detached_hbc_accounting.current(),
            .pinned_bytes = self.detached_hbc_accounting.pinned_bytes.load(.monotonic),
            .node = self.hbc_cache_kind_stats[@intFromEnum(HbcCacheKind.node)],
            .quantized = self.hbc_cache_kind_stats[@intFromEnum(HbcCacheKind.quantized)],
            .vector = self.hbc_cache_kind_stats[@intFromEnum(HbcCacheKind.vector)],
            .metadata = self.hbc_cache_kind_stats[@intFromEnum(HbcCacheKind.metadata)],
        };
    }

    pub fn clearAllCaches(self: *HBCIndex) void {
        self.clearNodeCache();
        self.clearQuantizedCache();
        self.clearVectorCache();
        self.clearMetadataCache();
    }

    fn reserveHbcCacheDelta(self: *HBCIndex, admission: *HbcCacheAdmission, delta_bytes: u64) bool {
        if (delta_bytes == 0) return true;
        const manager = self.resource_manager orelse return true;
        const next = std.math.add(u64, self.hbc_cache_bytes_accounted, delta_bytes) catch return false;
        manager.adjustUsage(
            .hbc_node_metadata_cache,
            &self.hbc_cache_bytes_accounted,
            next,
        ) catch return false;
        admission.* = .{
            .index = self,
            .reserved_bytes = delta_bytes,
            .active = true,
        };
        return true;
    }

    fn prepareHbcCacheAdmission(self: *HBCIndex, kind: HbcCacheKind, key: u64, next_entry_bytes: u64) ?HbcCacheAdmission {
        var admission = HbcCacheAdmission.none(self);
        const manager = self.resource_manager orelse return admission;

        self.refreshHbcCacheUsage();
        const existing_bytes = self.hbcCacheEntryBytes(kind, key);
        if (existing_bytes != 0) {
            // Remove the cache-owner reference before reserving the
            // replacement. An unpinned value releases its physical charge;
            // a leased value transfers to detached accounting, so copy-on-
            // write cannot hide a temporary double allocation from the hard
            // envelope.
            switch (kind) {
                .node => self.invalidateLocalNodeCacheLocked(key),
                .quantized => self.invalidateLocalQuantizedCacheLocked(key),
                .vector => self.invalidateLocalVectorCacheLocked(key),
                .metadata => self.invalidateLocalMetadataCacheLocked(key),
            }
            self.refreshHbcCacheUsage();
        }

        const delta_bytes = next_entry_bytes;
        if (self.reserveHbcCacheDelta(&admission, delta_bytes)) return admission;

        const protection = HbcCacheProtection.one(kind, key);
        while (self.evictOneHbcCacheEntry(protection)) {
            manager.observeUsage(.hbc_node_metadata_cache, &self.hbc_cache_bytes_accounted, self.hbcCacheBytes());
            if (self.reserveHbcCacheDelta(&admission, delta_bytes)) return admission;
        }

        return null;
    }

    fn refreshAndEnforceHbcCacheUsage(self: *HBCIndex, protection: HbcCacheProtection) void {
        if (self.shared_cache != null) return;
        self.refreshHbcCacheUsage();
        self.enforceHbcCacheBudget(protection);
    }

    fn enforceHbcCacheBudget(self: *HBCIndex, protection: HbcCacheProtection) void {
        const manager = self.resource_manager orelse return;
        const cache_stats = manager.sliceStats(.hbc_node_metadata_cache);
        const action = switch (cache_stats.pressure) {
            .normal => return,
            .soft => cache_stats.soft_action,
            .hard => cache_stats.hard_action,
        };
        if (action != .shrink_cache) return;

        const target_bytes = if (cache_stats.soft_limit_bytes > 0) cache_stats.soft_limit_bytes else cache_stats.hard_limit_bytes;
        if (target_bytes == 0) return;

        var map_bytes = self.hbcCacheBytes();
        var current_bytes = map_bytes +| self.detached_hbc_accounting.current();
        var evicted_any = false;
        while (current_bytes > target_bytes) {
            if (!self.evictOneHbcCacheEntry(protection)) break;
            evicted_any = true;
            map_bytes = self.hbcCacheBytes();
            current_bytes = map_bytes +| self.detached_hbc_accounting.current();
        }
        if (evicted_any or map_bytes != self.hbc_cache_bytes_accounted) {
            manager.observeUsage(.hbc_node_metadata_cache, &self.hbc_cache_bytes_accounted, map_bytes);
        }
    }

    fn evictOneHbcCacheEntry(self: *HBCIndex, protection: HbcCacheProtection) bool {
        if (self.evictOneVectorCacheEntry(protection)) return true;
        if (self.evictOneMetadataCacheEntry(protection)) return true;
        if (self.evictOneQuantizedCacheEntry(protection)) return true;
        return self.evictOneNodeCacheEntry(protection);
    }

    fn releaseLocalNodeEntry(self: *HBCIndex, entry: *NodeCacheEntry) void {
        const bytes = estimateNodeCacheBytes(&entry.node);
        if (entry.refs.load(.acquire) > 1 and entry.accounting == null) {
            self.detached_hbc_accounting.observeAllocate(bytes);
            entry.accounting = &self.detached_hbc_accounting;
            entry.accounted_bytes = bytes;
            detachCacheEntry(entry.accounting, null, 0, bytes, entry.refs.load(.acquire), &entry.detached_pinned);
        }
        releaseNodeCacheEntry(self.alloc, entry);
    }

    fn releaseLocalQuantizedEntry(self: *HBCIndex, entry: *QuantizedCacheEntry) void {
        const bytes = estimateQuantizedCacheBytes(&entry.quantized);
        if (entry.refs.load(.acquire) > 1 and entry.accounting == null) {
            self.detached_hbc_accounting.observeAllocate(bytes);
            entry.accounting = &self.detached_hbc_accounting;
            entry.accounted_bytes = bytes;
            detachCacheEntry(entry.accounting, null, 0, bytes, entry.refs.load(.acquire), &entry.detached_pinned);
        }
        releaseQuantizedCacheEntry(self.alloc, entry);
    }

    fn releaseLocalVectorEntry(self: *HBCIndex, entry: *VectorCacheEntry) void {
        const bytes = estimateVectorCacheBytes(entry.vector);
        if (entry.refs.load(.acquire) > 1 and entry.accounting == null) {
            self.detached_hbc_accounting.observeAllocate(bytes);
            entry.accounting = &self.detached_hbc_accounting;
            entry.accounted_bytes = bytes;
            detachCacheEntry(entry.accounting, null, 0, bytes, entry.refs.load(.acquire), &entry.detached_pinned);
        }
        releaseVectorCacheEntry(self.alloc, entry);
    }

    fn releaseLocalMetadataEntry(self: *HBCIndex, entry: *MetadataCacheEntry) void {
        const bytes = estimateMetadataCacheBytes(entry.metadata);
        if (entry.refs.load(.acquire) > 1 and entry.accounting == null) {
            self.detached_hbc_accounting.observeAllocate(bytes);
            entry.accounting = &self.detached_hbc_accounting;
            entry.accounted_bytes = bytes;
            detachCacheEntry(entry.accounting, null, 0, bytes, entry.refs.load(.acquire), &entry.detached_pinned);
        }
        releaseMetadataCacheEntry(self.alloc, entry);
    }

    fn evictOneVectorCacheEntry(self: *HBCIndex, protection: HbcCacheProtection) bool {
        const victim = nextHbcClockVictim(self.vector_clock_keys, self.vector_clock_refs, &self.vector_clock_hand, protection, .vector) orelse return false;
        if (self.vector_cache_slots.fetchRemove(victim)) |removed_slot| {
            self.vector_clock_keys[removed_slot.value] = 0;
            self.vector_clock_refs[removed_slot.value] = false;
        }
        if (self.vector_cache.fetchRemove(victim)) |removed| {
            self.releaseLocalVectorEntry(removed.value);
            self.noteHbcCacheEviction(.vector);
            return true;
        }
        return false;
    }

    fn evictOneMetadataCacheEntry(self: *HBCIndex, protection: HbcCacheProtection) bool {
        const victim = nextHbcClockVictim(self.metadata_clock_keys, self.metadata_clock_refs, &self.metadata_clock_hand, protection, .metadata) orelse return false;
        if (self.metadata_cache_slots.fetchRemove(victim)) |removed_slot| {
            self.metadata_clock_keys[removed_slot.value] = 0;
            self.metadata_clock_refs[removed_slot.value] = false;
        }
        if (self.metadata_cache.fetchRemove(victim)) |removed| {
            self.releaseLocalMetadataEntry(removed.value);
            self.noteHbcCacheEviction(.metadata);
            return true;
        }
        return false;
    }

    fn evictOneQuantizedCacheEntry(self: *HBCIndex, protection: HbcCacheProtection) bool {
        const victim = nextHbcClockVictim(self.quantized_clock_keys, self.quantized_clock_refs, &self.quantized_clock_hand, protection, .quantized) orelse return false;
        if (self.quantized_cache_slots.fetchRemove(victim)) |removed_slot| {
            self.quantized_clock_keys[removed_slot.value] = 0;
            self.quantized_clock_refs[removed_slot.value] = false;
        }
        if (self.quantized_cache.fetchRemove(victim)) |removed| {
            self.releaseLocalQuantizedEntry(removed.value);
            self.noteHbcCacheEviction(.quantized);
            return true;
        }
        return false;
    }

    fn evictOneNodeCacheEntry(self: *HBCIndex, protection: HbcCacheProtection) bool {
        const victim = nextHbcClockVictim(self.node_clock_keys, self.node_clock_refs, &self.node_clock_hand, protection, .node) orelse return false;
        if (self.node_cache_slots.fetchRemove(victim)) |removed_slot| {
            self.node_clock_keys[removed_slot.value] = 0;
            self.node_clock_refs[removed_slot.value] = false;
        }
        if (self.node_cache.fetchRemove(victim)) |removed| {
            self.releaseLocalNodeEntry(removed.value);
            self.noteHbcCacheEviction(.node);
            return true;
        }
        return false;
    }

    pub fn close(self: *HBCIndex) void {
        self.deinitWithBackendDisposition(false);
    }

    /// Test and fault-injection teardown that discards unsynced backend state
    /// instead of performing a graceful close. Production recovery code does
    /// not call this; it exists to exercise the exact interrupted-session
    /// boundary with modeled and WAL-backed storage.
    pub fn abandonAfterCrash(self: *HBCIndex) void {
        self.deinitWithBackendDisposition(true);
    }

    fn deinitWithBackendDisposition(self: *HBCIndex, abandon_after_crash: bool) void {
        if (self.resource_manager) |manager| {
            manager.unregisterReclaimer(self.local_reclaimer_identity);
            self.local_reclaimer_identity = 0;
            manager.unregisterReclaimer(self.search_workspace_reclaimer_identity);
            self.search_workspace_reclaimer_identity = 0;
            manager.unregisterReclaimer(self.flat_centroid_reclaimer_identity);
            self.flat_centroid_reclaimer_identity = 0;
        }
        if (self.shared_cache == null) {
            self.clearNodeCache();
            self.clearQuantizedCache();
            self.clearVectorCache();
            self.clearMetadataCache();
        } else {
            self.cache_mu.lockExclusive();
            self.clearLocalNodeCacheLocked();
            self.clearLocalQuantizedCacheLocked();
            self.cache_mu.unlockExclusive();
        }
        if (self.shared_cache) |cache| {
            if (self.shared_cache_registered) cache.unregisterNamespacePath(self.cache_namespace, self.cache_path);
            self.shared_cache = null;
            self.shared_cache_registered = false;
        }
        self.alloc.free(self.node_clock_keys);
        self.alloc.free(self.node_clock_refs);
        self.alloc.free(self.quantized_clock_keys);
        self.alloc.free(self.quantized_clock_refs);
        self.alloc.free(self.vector_clock_keys);
        self.alloc.free(self.vector_clock_refs);
        self.alloc.free(self.metadata_clock_keys);
        self.alloc.free(self.metadata_clock_refs);
        if (self.hilbert) |*hilbert| hilbert.deinit();
        if (self.cached_scratch) |*scratch| {
            self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted -| scratch.bytes());
            scratch.deinit(self.alloc);
        }
        if (self.cached_routing_scratch) |*scratch| {
            self.observeRoutingScratchBytes(self.routing_scratch_bytes_accounted -| scratch.bytes());
            scratch.deinit(self.alloc);
        }
        vectorindex_spfresh_index.clearFlatCentroidDirectory(self);
        std.debug.assert(self.flat_centroid_directory_bytes_accounted == 0);
        std.debug.assert(self.flat_centroid_build_bytes_accounted == 0);
        std.debug.assert(self.flat_centroid_retained_reservation_bytes_accounted == 0);
        lockAtomic(&self.flat_centroid_build_mu);
        std.debug.assert(self.flat_centroid_build_flight == null);
        self.flat_centroid_build_mu.unlock();
        lockAtomic(&self.published_flight_mu);
        std.debug.assert(self.published_flight == null);
        const published_spare_flight = self.published_spare_flight;
        self.published_spare_flight = null;
        self.published_flight_mu.unlock();
        if (published_spare_flight) |flight| self.alloc.destroy(flight);
        self.deinitBulkSplitVectorWorkspace();
        self.deferred_oversized_leaves.clearRetainingCapacity();
        self.apply_workspace_split_bytes = 0;
        self.deferred_node_key_value_bytes = 0;
        self.observeApplyWorkspaceBytes();
        self.deferred_quantized_nodes.deinit(self.alloc);
        self.clearDeferredNodeKeys();
        self.deferred_node_keys.deinit(self.alloc);
        self.deferred_oversized_leaves.deinit(self.alloc);
        self.rot.deinit();
        self.quantizer.deinit();
        self.alloc.free(self.cache_path);
        self.store.deinit();
        if (abandon_after_crash) {
            self.env_owner.abandonAfterCrash(self.alloc);
        } else {
            self.env_owner.close(self.alloc);
        }
        self.* = undefined;
    }

    pub fn sync(self: *HBCIndex, force: bool) !void {
        try self.env_owner.sync(force);
    }

    pub fn syncReplayState(self: *HBCIndex) !void {
        try self.env_owner.syncReplayState();
    }

    fn txnLikeChild(comptime T: type) type {
        return switch (@typeInfo(T)) {
            .pointer => |ptr| ptr.child,
            else => @compileError("expected pointer to transaction-like type"),
        };
    }

    pub fn bindTxnLike(self: *HBCIndex, txn: anytype) !void {
        _ = self;
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceReadTxn,
            vectorindex_store.NamespaceWriteTxn,
            vectorindex_store.NamespaceBatch,
            PublishedWriteTxn,
            PublishedBatchTxn,
            => {},
            else => @compileError("expected vectorindex namespace transaction"),
        }
    }

    fn decodeNodeKey(key: []const u8) ?struct { id: u64, suffix: Suffix } {
        if (key.len != 12) return null;
        if (key[0] != 'n' or key[1] != ':' or key[10] != ':') return null;
        const suffix: Suffix = switch (key[11]) {
            @intFromEnum(Suffix.header) => .header,
            @intFromEnum(Suffix.centroid) => .centroid,
            @intFromEnum(Suffix.children) => .children,
            @intFromEnum(Suffix.members) => .members,
            @intFromEnum(Suffix.packed_node) => .packed_node,
            @intFromEnum(Suffix.range) => .range,
            @intFromEnum(Suffix.posting) => .posting,
            else => return null,
        };
        return .{
            .id = std.mem.readInt(u64, key[2..10], .big),
            .suffix = suffix,
        };
    }

    fn stagedNodeKeyId(key: []const u8) ?u128 {
        const decoded = decodeNodeKey(key) orelse return null;
        return (@as(u128, decoded.id) << 8) | @as(u128, @intFromEnum(decoded.suffix));
    }

    fn stagedNodeKeyParts(staged_key: u128) struct { id: u64, suffix: Suffix } {
        const suffix_byte: u8 = @intCast(staged_key & 0xff);
        const suffix: Suffix = switch (suffix_byte) {
            @intFromEnum(Suffix.header) => .header,
            @intFromEnum(Suffix.centroid) => .centroid,
            @intFromEnum(Suffix.children) => .children,
            @intFromEnum(Suffix.members) => .members,
            @intFromEnum(Suffix.packed_node) => .packed_node,
            @intFromEnum(Suffix.range) => .range,
            @intFromEnum(Suffix.posting) => .posting,
            else => unreachable,
        };
        return .{
            .id = @intCast(staged_key >> 8),
            .suffix = suffix,
        };
    }

    fn clearDeferredNodeKeys(self: *HBCIndex) void {
        {
            var it = self.deferred_node_keys.valueIterator();
            while (it.next()) |entry| entry.deinit(self.alloc);
        }
        self.deferred_node_keys.clearRetainingCapacity();
        self.deferred_node_key_value_bytes = 0;
        self.observeApplyWorkspaceBytes();
    }

    fn releaseDeferredBulkWorkspaceCapacity(self: *HBCIndex) void {
        self.endBulkSplitVectorWorkspace();
        self.deferred_quantized_nodes.deinit(self.alloc);
        self.deferred_quantized_nodes = .empty;
        self.clearDeferredNodeKeys();
        self.deferred_node_keys.deinit(self.alloc);
        self.deferred_node_keys = .empty;
        self.deferred_oversized_leaves.deinit(self.alloc);
        self.deferred_oversized_leaves = .empty;
        self.apply_workspace_split_bytes = 0;
        self.deferred_node_key_value_bytes = 0;
        self.observeApplyWorkspaceBytes();
    }

    fn stagedNodeKey(self: *HBCIndex, key: []const u8) ?*DeferredNodeValue {
        const staged_key = stagedNodeKeyId(key) orelse return null;
        return self.deferred_node_keys.getPtr(staged_key);
    }

    fn stageNodeKeyPut(self: *HBCIndex, key: []const u8, value: []const u8) !bool {
        if (!self.crossBatchPublicationActive()) return false;
        const staged_key = stagedNodeKeyId(key) orelse return false;
        const owned = try self.alloc.dupe(u8, value);
        errdefer self.alloc.free(owned);
        const result = try self.deferred_node_keys.getOrPut(self.alloc, staged_key);
        if (result.found_existing) {
            if (result.value_ptr.value) |old| self.deferred_node_key_value_bytes -|= old.len;
            result.value_ptr.deinit(self.alloc);
        }
        result.value_ptr.* = .{ .value = owned };
        self.deferred_node_key_value_bytes +|= owned.len;
        self.maybeObserveApplyWorkspaceBytes();
        return true;
    }

    fn stageNodeKeyDelete(self: *HBCIndex, key: []const u8) !bool {
        if (!self.crossBatchPublicationActive()) return false;
        const staged_key = stagedNodeKeyId(key) orelse return false;
        const result = try self.deferred_node_keys.getOrPut(self.alloc, staged_key);
        if (result.found_existing) {
            if (result.value_ptr.value) |old| self.deferred_node_key_value_bytes -|= old.len;
            result.value_ptr.deinit(self.alloc);
        }
        result.value_ptr.* = .{ .value = null };
        self.maybeObserveApplyWorkspaceBytes();
        return true;
    }

    pub fn getNamespaced(self: *HBCIndex, txn: anytype, comptime namespace: Namespace, key: []const u8) ![]const u8 {
        if (namespace == .nodes) {
            if (self.stagedNodeKey(key)) |staged| {
                return staged.value orelse error.NotFound;
            }
        }
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceReadTxn,
            vectorindex_store.NamespaceWriteTxn,
            vectorindex_store.NamespaceBatch,
            PublishedWriteTxn,
            PublishedBatchTxn,
            => return try txn.get(if (namespace == .vecs) self.vectorArtifactReadNamespace() else namespace, key),
            else => @compileError("expected vectorindex namespace transaction"),
        }
    }

    /// Prefer one decoded, governed exact-vector copy over retaining both that
    /// copy and the LSM data block that supplied it. If decoded-vector
    /// retention is disabled, preserve normal LSM block caching so the
    /// fallback path does not regress.
    pub fn vectorArtifactReadNamespace(self: *const HBCIndex) Namespace {
        if (!self.cache_enabled or !self.retained_vector_cache_enabled) return .vecs;
        if (self.bypass_external_vector_cache or self.lsmSessionBatchingActive()) return .vecs;
        if (self.config.max_cached_vectors == 0) return .vecs;
        return .vecs_transient;
    }

    fn canOwnDecodedVectorResidency(self: *const HBCIndex) bool {
        if (!self.cache_enabled or !self.retained_vector_cache_enabled) return false;
        if (self.bypass_external_vector_cache or self.lsmSessionBatchingActive()) return false;
        if (self.config.max_cached_vectors == 0) return false;
        _ = self.shared_cache orelse return false;
        return true;
    }

    /// Reserve enough governed cache headroom to make decoded-vector ownership
    /// stable for a complete request. Failure is an intentional routing choice:
    /// the caller retains primary-store blocks and suppresses decoded writes.
    pub fn acquireDecodedVectorResidency(self: *HBCIndex, expected_vectors: usize) ?DecodedVectorResidencyLease {
        if (!self.canOwnDecodedVectorResidency() or expected_vectors == 0) return null;
        const shared = self.shared_cache.?;
        const vector_bytes = estimateDecodedVectorResidencyBytes(@intCast(self.config.dims));
        const requested_bytes = @as(u64, @intCast(expected_vectors)) *| vector_bytes;
        return shared.tryReserveDecodedQuery(self.cache_namespace, requested_bytes, vector_bytes);
    }

    pub fn acquireDecodedVectorResidencyForSearch(self: *HBCIndex, req: SearchRequest) ?DecodedVectorResidencyLease {
        const expected_vectors = if (!self.config.use_quantization or self.config.rerank_policy == .never)
            std.math.cast(usize, self.published_active_count.load(.acquire)) orelse std.math.maxInt(usize)
        else blk: {
            const epsilon = req.epsilon orelse self.config.epsilon;
            const rerank_factor = req.rerank_factor orelse vectorindex_search.rerankFactor(epsilon);
            break :blk std.math.mul(usize, req.k, rerank_factor) catch std.math.maxInt(usize);
        };
        return self.acquireDecodedVectorResidency(expected_vectors);
    }

    fn getNamespacedCommitted(self: *HBCIndex, txn: anytype, comptime namespace: Namespace, key: []const u8) ![]const u8 {
        _ = self;
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceReadTxn,
            vectorindex_store.NamespaceWriteTxn,
            vectorindex_store.NamespaceBatch,
            PublishedWriteTxn,
            PublishedBatchTxn,
            => return try txn.get(namespace, key),
            else => @compileError("expected vectorindex namespace transaction"),
        }
    }

    pub fn putNamespaced(self: *HBCIndex, txn: anytype, comptime namespace: Namespace, key: []const u8, value: []const u8) !void {
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceWriteTxn,
            vectorindex_store.NamespaceBatch,
            PublishedWriteTxn,
            PublishedBatchTxn,
            => {
                if (namespace == .nodes and try self.stageNodeKeyPut(key, value)) return;
                try txn.put(namespace, key, value);
                self.noteNamespacePut(namespace, key.len, value.len, false);
            },
            vectorindex_store.NamespaceReadTxn => return error.ReadOnly,
            else => @compileError("expected vectorindex namespace transaction"),
        }
    }

    pub fn appendNamespaced(self: *HBCIndex, txn: anytype, comptime namespace: Namespace, key: []const u8, value: []const u8) !void {
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceWriteTxn, PublishedWriteTxn => {
                txn.appendPut(namespace, key, value) catch |err| switch (err) {
                    error.Unsupported => {
                        try txn.put(namespace, key, value);
                        self.noteNamespacePut(namespace, key.len, value.len, false);
                        return;
                    },
                    else => return err,
                };
                self.noteNamespacePut(namespace, key.len, value.len, true);
            },
            vectorindex_store.NamespaceBatch, PublishedBatchTxn => {
                txn.appendPut(namespace, key, value) catch |err| switch (err) {
                    error.Unsupported => {
                        try txn.put(namespace, key, value);
                        self.noteNamespacePut(namespace, key.len, value.len, false);
                        return;
                    },
                    else => return err,
                };
                self.noteNamespacePut(namespace, key.len, value.len, true);
            },
            vectorindex_store.NamespaceReadTxn => return error.ReadOnly,
            else => @compileError("expected vectorindex namespace transaction"),
        }
    }

    pub fn deleteNamespaced(self: *HBCIndex, txn: anytype, comptime namespace: Namespace, key: []const u8) !void {
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceWriteTxn,
            vectorindex_store.NamespaceBatch,
            PublishedWriteTxn,
            PublishedBatchTxn,
            => {
                if (namespace == .nodes and try self.stageNodeKeyDelete(key)) return;
                try txn.delete(namespace, key);
                self.noteNamespaceDelete(namespace, key.len);
            },
            vectorindex_store.NamespaceReadTxn => return error.ReadOnly,
            else => @compileError("expected vectorindex namespace transaction"),
        }
    }

    fn noteNamespacePut(self: *HBCIndex, namespace: Namespace, key_len: usize, value_len: usize, append: bool) void {
        const key_bytes: u64 = @intCast(key_len);
        const value_bytes: u64 = @intCast(value_len);
        switch (namespace) {
            .nodes => {
                if (append) {
                    self.write_profile.ns_nodes_append_calls += 1;
                } else {
                    self.write_profile.ns_nodes_put_calls += 1;
                }
                self.write_profile.ns_nodes_key_bytes += key_bytes;
                self.write_profile.ns_nodes_value_bytes += value_bytes;
            },
            .meta => {
                if (append) {
                    self.write_profile.ns_meta_append_calls += 1;
                } else {
                    self.write_profile.ns_meta_put_calls += 1;
                }
                self.write_profile.ns_meta_key_bytes += key_bytes;
                self.write_profile.ns_meta_value_bytes += value_bytes;
            },
            .quant => {
                if (append) {
                    self.write_profile.ns_quant_append_calls += 1;
                } else {
                    self.write_profile.ns_quant_put_calls += 1;
                }
                self.write_profile.ns_quant_key_bytes += key_bytes;
                self.write_profile.ns_quant_value_bytes += value_bytes;
            },
            .vecs, .vecs_transient => {
                if (append) {
                    self.write_profile.ns_vecs_append_calls += 1;
                } else {
                    self.write_profile.ns_vecs_put_calls += 1;
                }
                self.write_profile.ns_vecs_key_bytes += key_bytes;
                self.write_profile.ns_vecs_value_bytes += value_bytes;
            },
        }
    }

    fn noteNamespaceDelete(self: *HBCIndex, namespace: Namespace, key_len: usize) void {
        const key_bytes: u64 = @intCast(key_len);
        switch (namespace) {
            .nodes => {
                self.write_profile.ns_nodes_delete_calls += 1;
                self.write_profile.ns_nodes_key_bytes += key_bytes;
            },
            .meta => {
                self.write_profile.ns_meta_delete_calls += 1;
                self.write_profile.ns_meta_key_bytes += key_bytes;
            },
            .quant => {
                self.write_profile.ns_quant_delete_calls += 1;
                self.write_profile.ns_quant_key_bytes += key_bytes;
            },
            .vecs, .vecs_transient => {
                self.write_profile.ns_vecs_delete_calls += 1;
                self.write_profile.ns_vecs_key_bytes += key_bytes;
            },
        }
    }

    pub fn openNamespacedCursor(self: *HBCIndex, allocator: Allocator, txn: anytype, comptime namespace: Namespace) !vectorindex_store.Cursor {
        _ = self;
        _ = allocator;
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceReadTxn,
            vectorindex_store.NamespaceWriteTxn,
            PublishedWriteTxn,
            => return try txn.openCursor(namespace),
            else => @compileError("expected vectorindex namespace transaction"),
        }
    }

    pub fn beginRuntimeReadTxn(self: *HBCIndex) !vectorindex_store.NamespaceReadTxn {
        const fill_epoch = self.beginSearchCacheFill();
        var txn = try self.store.beginRead();
        txn.cache_fill_epoch = fill_epoch;
        return txn;
    }

    pub fn beginRuntimeSearchTxn(self: *HBCIndex) !vectorindex_store.NamespaceReadTxn {
        const fill_epoch = self.beginSearchCacheFill();
        var txn = try self.store.beginProbeOrRead();
        txn.cache_fill_epoch = fill_epoch;
        return txn;
    }

    pub fn beginRuntimeCompleteSearchTxn(self: *HBCIndex) !vectorindex_store.NamespaceReadTxn {
        return try self.beginRuntimeReadTxn();
    }

    pub fn beginRuntimeSearchTxnForCoverage(self: *HBCIndex, complete_snapshot: bool) !vectorindex_store.NamespaceReadTxn {
        return if (complete_snapshot)
            try self.beginRuntimeCompleteSearchTxn()
        else
            try self.beginRuntimeSearchTxn();
    }

    pub fn beginCompleteSnapshotRead(
        self: *HBCIndex,
        cancellation: ?vectorindex_search_types.CancellationToken,
    ) !void {
        try self.published_snapshot_mu.lockSharedIo(self.runtimeIo(), cancellation);
    }

    pub fn endCompleteSnapshotRead(self: *HBCIndex) void {
        self.published_snapshot_mu.unlockShared();
    }

    fn publicationMutationActive(self: *const HBCIndex) bool {
        return self.published_mutation_active.load(.acquire);
    }

    pub fn notifyCompleteSnapshotCapturedForTest(self: *HBCIndex) void {
        if (!builtin.is_test) return;
        if (test_complete_snapshot_capture_hook) |hook| {
            hook(test_complete_snapshot_capture_ctx, self);
        }
    }

    pub fn notifyBeforeDurableSnapshotCaptureForTest(self: *HBCIndex) void {
        if (!builtin.is_test) return;
        if (test_before_durable_snapshot_capture_hook) |hook| {
            hook(test_before_durable_snapshot_capture_ctx, self);
        }
    }

    pub fn beginRuntimeWriteTxn(self: *HBCIndex) !vectorindex_store.NamespaceWriteTxn {
        if (self.crossBatchPublicationActive()) self.bulk_publication_may_have_mutated = true;
        if (!self.crossBatchPublicationActive()) {
            self.deferred_quantized_nodes.clearRetainingCapacity();
            self.deferred_oversized_leaves.clearRetainingCapacity();
            self.apply_workspace_split_bytes = 0;
            self.observeApplyWorkspaceBytes();
        }
        return try self.store.beginWrite();
    }

    pub fn beginRuntimeBatchTxn(self: *HBCIndex) !vectorindex_store.NamespaceBatch {
        if (self.crossBatchPublicationActive()) self.bulk_publication_may_have_mutated = true;
        if (!self.crossBatchPublicationActive()) {
            self.deferred_quantized_nodes.clearRetainingCapacity();
            self.deferred_oversized_leaves.clearRetainingCapacity();
            self.apply_workspace_split_bytes = 0;
            self.observeApplyWorkspaceBytes();
        }
        return try self.store.beginBatch();
    }

    pub fn beginRuntimeBatchTxnOptions(self: *HBCIndex, options: BatchInsertOptions) !vectorindex_store.NamespaceBatch {
        if (self.crossBatchPublicationActive()) self.bulk_publication_may_have_mutated = true;
        if (!self.shouldDeferQuantizedRebuildToBulkFinish(options)) {
            self.deferred_quantized_nodes.clearRetainingCapacity();
        }
        if (!self.crossBatchPublicationActive()) {
            self.deferred_oversized_leaves.clearRetainingCapacity();
            self.apply_workspace_split_bytes = 0;
            self.observeApplyWorkspaceBytes();
        }
        const in_bulk_session = self.lsmSessionBatchingActive();
        // HBC mutation batches rewrite nodes, ranges, and quantized payloads
        // heavily. Keep mutable-state coalescing, but preserve bulk transaction
        // mode when the LSM profile disables direct bulk ingest so LSM defers
        // batch-exit maintenance until the session boundary. Direct-bulk LSM
        // profiles and non-LSM backends stay in default mode; otherwise stale
        // internal rewrites can become durable table bytes during large loads.
        return try self.store.beginBatchWithOptions(.{
            .mode = self.runtimeBatchMode(in_bulk_session),
            .defer_commit_flush = in_bulk_session,
        });
    }

    fn runtimeBatchMode(self: *const HBCIndex, in_bulk_session: bool) vectorindex_store.BatchMode {
        return switch (self.env_owner) {
            .lsm => |handle| hbcRuntimeBatchMode(in_bulk_session, handle.backend.options.direct_bulk_ingest),
            .lmdb => hbcRuntimeBatchMode(in_bulk_session, null),
        };
    }

    fn commitTxn(txn: anytype) !void {
        const Child = comptime txnLikeChild(@TypeOf(txn));
        switch (Child) {
            vectorindex_store.NamespaceWriteTxn, vectorindex_store.NamespaceBatch => try txn.commit(),
            else => @compileError("expected writable transaction with commit()"),
        }
    }

    fn clearLocalNodeCacheLocked(self: *HBCIndex) void {
        var pinned_it = self.pinned_node_cache.iterator();
        while (pinned_it.next()) |entry| releaseNodeCacheEntry(self.alloc, entry.value_ptr.*);
        self.pinned_node_cache.deinit(self.alloc);
        self.pinned_node_cache = .empty;

        var it = self.node_cache.iterator();
        while (it.next()) |entry| self.releaseLocalNodeEntry(entry.value_ptr.*);
        self.node_cache.deinit(self.alloc);
        self.node_cache = .empty;
        self.node_cache_slots.deinit(self.alloc);
        self.node_cache_slots = .empty;
        @memset(self.node_clock_keys, 0);
        @memset(self.node_clock_refs, false);
        self.node_clock_hand = 0;
    }

    fn clearLocalQuantizedCacheLocked(self: *HBCIndex) void {
        var pinned_it = self.pinned_quantized_cache.iterator();
        while (pinned_it.next()) |entry| releaseQuantizedCacheEntry(self.alloc, entry.value_ptr.*);
        self.pinned_quantized_cache.deinit(self.alloc);
        self.pinned_quantized_cache = .empty;

        var it = self.quantized_cache.iterator();
        while (it.next()) |entry| self.releaseLocalQuantizedEntry(entry.value_ptr.*);
        self.quantized_cache.deinit(self.alloc);
        self.quantized_cache = .empty;
        self.quantized_cache_slots.deinit(self.alloc);
        self.quantized_cache_slots = .empty;
        @memset(self.quantized_clock_keys, 0);
        @memset(self.quantized_clock_refs, false);
        self.quantized_clock_hand = 0;
    }

    fn clearLocalVectorCacheLocked(self: *HBCIndex) void {
        var it = self.vector_cache.iterator();
        while (it.next()) |entry| self.releaseLocalVectorEntry(entry.value_ptr.*);
        self.vector_cache.deinit(self.alloc);
        self.vector_cache = .empty;
        self.vector_cache_slots.deinit(self.alloc);
        self.vector_cache_slots = .empty;
        @memset(self.vector_clock_keys, 0);
        @memset(self.vector_clock_refs, false);
        self.vector_clock_hand = 0;
    }

    fn clearLocalMetadataCacheLocked(self: *HBCIndex) void {
        var it = self.metadata_cache.iterator();
        while (it.next()) |entry| self.releaseLocalMetadataEntry(entry.value_ptr.*);
        self.metadata_cache.deinit(self.alloc);
        self.metadata_cache = .empty;
        self.metadata_cache_slots.deinit(self.alloc);
        self.metadata_cache_slots = .empty;
        @memset(self.metadata_clock_keys, 0);
        @memset(self.metadata_clock_refs, false);
        self.metadata_clock_hand = 0;
    }

    fn invalidateLocalNodeCacheLocked(self: *HBCIndex, node_id: u64) void {
        if (self.pinned_node_cache.fetchRemove(node_id)) |removed| releaseNodeCacheEntry(self.alloc, removed.value);
        if (self.node_cache_slots.fetchRemove(node_id)) |removed_slot| {
            self.node_clock_keys[removed_slot.value] = 0;
            self.node_clock_refs[removed_slot.value] = false;
        }
        if (self.node_cache.fetchRemove(node_id)) |removed| self.releaseLocalNodeEntry(removed.value);
    }

    fn invalidateLocalQuantizedCacheLocked(self: *HBCIndex, node_id: u64) void {
        if (self.pinned_quantized_cache.fetchRemove(node_id)) |removed| releaseQuantizedCacheEntry(self.alloc, removed.value);
        if (self.quantized_cache_slots.fetchRemove(node_id)) |removed_slot| {
            self.quantized_clock_keys[removed_slot.value] = 0;
            self.quantized_clock_refs[removed_slot.value] = false;
        }
        if (self.quantized_cache.fetchRemove(node_id)) |removed| self.releaseLocalQuantizedEntry(removed.value);
    }

    fn invalidateLocalVectorCacheLocked(self: *HBCIndex, vector_id: u64) void {
        if (self.vector_cache_slots.fetchRemove(vector_id)) |removed_slot| {
            self.vector_clock_keys[removed_slot.value] = 0;
            self.vector_clock_refs[removed_slot.value] = false;
        }
        if (self.vector_cache.fetchRemove(vector_id)) |removed| self.releaseLocalVectorEntry(removed.value);
    }

    fn invalidateLocalMetadataCacheLocked(self: *HBCIndex, vector_id: u64) void {
        if (self.metadata_cache_slots.fetchRemove(vector_id)) |removed_slot| {
            self.metadata_clock_keys[removed_slot.value] = 0;
            self.metadata_clock_refs[removed_slot.value] = false;
        }
        if (self.metadata_cache.fetchRemove(vector_id)) |removed| self.releaseLocalMetadataEntry(removed.value);
    }

    fn ensureLocalNodeCacheCapacityLocked(self: *HBCIndex, key: u64) ?usize {
        if (self.config.max_cached_nodes == 0) return null;
        if (self.node_cache.contains(key)) return null;
        while (self.node_cache.count() >= self.config.max_cached_nodes) {
            const victim = nextHbcClockVictim(self.node_clock_keys, self.node_clock_refs, &self.node_clock_hand, .none(), .node) orelse break;
            const slot = self.node_cache_slots.get(victim).?;
            if (self.node_cache_slots.fetchRemove(victim)) |removed_slot| {
                self.node_clock_keys[removed_slot.value] = 0;
                self.node_clock_refs[removed_slot.value] = false;
            }
            if (self.node_cache.fetchRemove(victim)) |removed| self.releaseLocalNodeEntry(removed.value);
            return slot;
        }
        return null;
    }

    fn ensureLocalQuantizedCacheCapacityLocked(self: *HBCIndex, key: u64) ?usize {
        if (self.config.max_cached_nodes == 0) return null;
        if (self.quantized_cache.contains(key)) return null;
        while (self.quantized_cache.count() >= self.config.max_cached_nodes) {
            const victim = nextHbcClockVictim(self.quantized_clock_keys, self.quantized_clock_refs, &self.quantized_clock_hand, .none(), .quantized) orelse break;
            const slot = self.quantized_cache_slots.get(victim).?;
            if (self.quantized_cache_slots.fetchRemove(victim)) |removed_slot| {
                self.quantized_clock_keys[removed_slot.value] = 0;
                self.quantized_clock_refs[removed_slot.value] = false;
            }
            if (self.quantized_cache.fetchRemove(victim)) |removed| self.releaseLocalQuantizedEntry(removed.value);
            return slot;
        }
        return null;
    }

    fn ensureLocalVectorCacheCapacityLocked(self: *HBCIndex, key: u64) ?usize {
        if (self.config.max_cached_vectors == 0) return null;
        if (self.vector_cache.contains(key)) return null;
        while (self.vector_cache.count() >= self.config.max_cached_vectors) {
            const victim = nextHbcClockVictim(self.vector_clock_keys, self.vector_clock_refs, &self.vector_clock_hand, .none(), .vector) orelse break;
            const slot = self.vector_cache_slots.get(victim).?;
            self.invalidateLocalVectorCacheLocked(victim);
            return slot;
        }
        return null;
    }

    fn ensureLocalMetadataCacheCapacityLocked(self: *HBCIndex, key: u64) ?usize {
        if (self.config.max_cached_metadata == 0) return null;
        if (self.metadata_cache.contains(key)) return null;
        while (self.metadata_cache.count() >= self.config.max_cached_metadata) {
            const victim = nextHbcClockVictim(self.metadata_clock_keys, self.metadata_clock_refs, &self.metadata_clock_hand, .none(), .metadata) orelse break;
            const slot = self.metadata_cache_slots.get(victim).?;
            self.invalidateLocalMetadataCacheLocked(victim);
            return slot;
        }
        return null;
    }

    fn cacheNodeLocalLocked(self: *HBCIndex, node: Node) !void {
        var owned = node;
        errdefer owned.deinit(self.alloc);
        const reserved_slot = self.ensureLocalNodeCacheCapacityLocked(owned.id);
        if (self.node_cache_slots.fetchRemove(owned.id)) |removed_slot| {
            self.node_clock_keys[removed_slot.value] = 0;
            self.node_clock_refs[removed_slot.value] = false;
        }
        if (self.node_cache.fetchRemove(owned.id)) |removed| self.releaseLocalNodeEntry(removed.value);
        const entry = try self.alloc.create(NodeCacheEntry);
        errdefer self.alloc.destroy(entry);
        entry.* = .{ .node = owned };
        errdefer releaseNodeCacheEntry(self.alloc, entry);
        try self.node_cache.put(self.alloc, owned.id, entry);
        const slot = reserved_slot orelse claimLocalClockSlot(self.node_clock_keys, self.node_clock_hand, owned.id) orelse return error.CacheDisabled;
        self.node_clock_refs[slot] = true;
        try self.node_cache_slots.put(self.alloc, owned.id, slot);
    }

    fn cacheQuantizedLocalLocked(self: *HBCIndex, node_id: u64, qs: QuantizedSet) !void {
        var owned = qs;
        errdefer owned.deinit(self.alloc);
        const reserved_slot = self.ensureLocalQuantizedCacheCapacityLocked(node_id);
        if (self.quantized_cache_slots.fetchRemove(node_id)) |removed_slot| {
            self.quantized_clock_keys[removed_slot.value] = 0;
            self.quantized_clock_refs[removed_slot.value] = false;
        }
        if (self.quantized_cache.fetchRemove(node_id)) |removed| self.releaseLocalQuantizedEntry(removed.value);
        const entry = try self.alloc.create(QuantizedCacheEntry);
        errdefer self.alloc.destroy(entry);
        entry.* = .{ .quantized = owned };
        errdefer releaseQuantizedCacheEntry(self.alloc, entry);
        try self.quantized_cache.put(self.alloc, node_id, entry);
        const slot = reserved_slot orelse claimLocalClockSlot(self.quantized_clock_keys, self.quantized_clock_hand, node_id) orelse return error.CacheDisabled;
        self.quantized_clock_refs[slot] = true;
        try self.quantized_cache_slots.put(self.alloc, node_id, slot);
    }

    fn cachePinnedNodeLocked(self: *HBCIndex, node: *const Node, replace_existing: bool) !void {
        if (self.config.max_pinned_tree_nodes == 0) return;
        if (self.pinned_node_cache.get(node.id)) |_| {
            if (!replace_existing) return;
            if (self.pinned_node_cache.fetchRemove(node.id)) |removed| releaseNodeCacheEntry(self.alloc, removed.value);
        } else if (self.pinned_node_cache.count() >= self.config.max_pinned_tree_nodes) {
            return;
        }

        var cloned = try node.clone(self.alloc);
        var cloned_active = true;
        errdefer if (cloned_active) cloned.deinit(self.alloc);
        const entry = try self.alloc.create(NodeCacheEntry);
        entry.* = .{ .node = cloned };
        cloned_active = false;
        errdefer releaseNodeCacheEntry(self.alloc, entry);
        try self.pinned_node_cache.put(self.alloc, node.id, entry);
    }

    fn cachePinnedQuantizedOwnedLocked(self: *HBCIndex, node_id: u64, qs: QuantizedSet, replace_existing: bool) !void {
        if (self.config.max_pinned_tree_nodes == 0) {
            var owned = qs;
            owned.deinit(self.alloc);
            return;
        }
        var owned = qs;
        var owned_active = true;
        errdefer if (owned_active) owned.deinit(self.alloc);
        if (self.pinned_quantized_cache.get(node_id)) |_| {
            if (!replace_existing) {
                owned.deinit(self.alloc);
                owned_active = false;
                return;
            }
            if (self.pinned_quantized_cache.fetchRemove(node_id)) |removed| releaseQuantizedCacheEntry(self.alloc, removed.value);
        } else if (self.pinned_quantized_cache.count() >= self.config.max_pinned_tree_nodes) {
            owned.deinit(self.alloc);
            owned_active = false;
            return;
        }

        const entry = try self.alloc.create(QuantizedCacheEntry);
        entry.* = .{ .quantized = owned };
        owned_active = false;
        errdefer releaseQuantizedCacheEntry(self.alloc, entry);
        try self.pinned_quantized_cache.put(self.alloc, node_id, entry);
    }

    fn cacheVectorLocalLocked(self: *HBCIndex, vector_id: u64, vector_data: []const f32) ![]const f32 {
        if (self.vector_cache.get(vector_id)) |existing| {
            if (std.mem.eql(f32, existing.vector, vector_data)) return existing.vector;
        }
        const reserved_slot = self.ensureLocalVectorCacheCapacityLocked(vector_id);
        self.invalidateLocalVectorCacheLocked(vector_id);
        const copied = try self.alloc.dupe(f32, vector_data);
        errdefer self.alloc.free(copied);
        const entry = try self.alloc.create(VectorCacheEntry);
        errdefer self.alloc.destroy(entry);
        entry.* = .{ .vector = copied };
        errdefer releaseVectorCacheEntry(self.alloc, entry);
        try self.vector_cache.put(self.alloc, vector_id, entry);
        const slot = reserved_slot orelse claimLocalClockSlot(self.vector_clock_keys, self.vector_clock_hand, vector_id) orelse return error.CacheDisabled;
        self.vector_clock_refs[slot] = true;
        try self.vector_cache_slots.put(self.alloc, vector_id, slot);
        return entry.vector;
    }

    fn cacheMetadataLocalLocked(self: *HBCIndex, vector_id: u64, metadata: []const u8) !void {
        const reserved_slot = self.ensureLocalMetadataCacheCapacityLocked(vector_id);
        self.invalidateLocalMetadataCacheLocked(vector_id);
        const copied = try self.alloc.dupe(u8, metadata);
        errdefer self.alloc.free(copied);
        const entry = try self.alloc.create(MetadataCacheEntry);
        errdefer self.alloc.destroy(entry);
        entry.* = .{ .metadata = copied };
        errdefer releaseMetadataCacheEntry(self.alloc, entry);
        try self.metadata_cache.put(self.alloc, vector_id, entry);
        const slot = reserved_slot orelse claimLocalClockSlot(self.metadata_clock_keys, self.metadata_clock_hand, vector_id) orelse return error.CacheDisabled;
        self.metadata_clock_refs[slot] = true;
        try self.metadata_cache_slots.put(self.alloc, vector_id, slot);
    }

    fn clearNodeCache(self: *HBCIndex) void {
        if (self.shared_cache) |cache| {
            cache.clearNodeNamespace(self.cache_namespace);
            self.cache_mu.lockExclusive();
            defer self.cache_mu.unlockExclusive();
            self.clearLocalNodeCacheLocked();
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.clearLocalNodeCacheLocked();
        self.refreshHbcCacheUsage();
    }

    fn clearQuantizedCache(self: *HBCIndex) void {
        if (self.shared_cache) |cache| {
            cache.clearQuantizedNamespace(self.cache_namespace);
            self.cache_mu.lockExclusive();
            defer self.cache_mu.unlockExclusive();
            self.clearLocalQuantizedCacheLocked();
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.clearLocalQuantizedCacheLocked();
        self.refreshHbcCacheUsage();
    }

    fn clearVectorCache(self: *HBCIndex) void {
        if (self.shared_cache) |cache| {
            cache.clearVectorNamespace(self.cache_namespace);
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.clearLocalVectorCacheLocked();
        self.refreshHbcCacheUsage();
    }

    pub fn clearMetadataCache(self: *HBCIndex) void {
        if (self.shared_cache) |cache| {
            cache.clearMetadataNamespace(self.cache_namespace);
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.clearLocalMetadataCacheLocked();
        self.refreshHbcCacheUsage();
    }

    pub fn invalidateNodeCache(self: *HBCIndex, node_id: u64) void {
        if (self.shared_cache) |cache| {
            cache.invalidateNode(self.cache_namespace, node_id);
            self.cache_mu.lockExclusive();
            defer self.cache_mu.unlockExclusive();
            self.invalidateLocalNodeCacheLocked(node_id);
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.invalidateLocalNodeCacheLocked(node_id);
        self.refreshHbcCacheUsage();
    }

    pub fn invalidateQuantizedCache(self: *HBCIndex, node_id: u64) void {
        if (self.shared_cache) |cache| {
            cache.invalidateQuantized(self.cache_namespace, node_id);
            self.cache_mu.lockExclusive();
            defer self.cache_mu.unlockExclusive();
            self.invalidateLocalQuantizedCacheLocked(node_id);
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.invalidateLocalQuantizedCacheLocked(node_id);
        self.refreshHbcCacheUsage();
    }

    pub fn invalidateVectorCache(self: *HBCIndex, vector_id: u64) void {
        self.beginVectorCacheMutation(vector_id);
        if (self.shared_cache) |cache| {
            cache.invalidateVector(self.cache_namespace, vector_id);
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.invalidateLocalVectorCacheLocked(vector_id);
        self.refreshHbcCacheUsage();
    }

    fn vectorCacheFillStripe(vector_id: u64) usize {
        // Vector ids commonly arrive sequentially, so mix their high and low
        // bits before selecting a fixed stripe.
        var value = vector_id;
        value ^= value >> 33;
        value *%= 0xff51afd7ed558ccd;
        value ^= value >> 33;
        return @intCast(value & (vector_cache_fill_stripe_count - 1));
    }

    fn beginVectorCacheMutation(self: *HBCIndex, vector_id: u64) void {
        const stripe = vectorCacheFillStripe(vector_id);
        const word = stripe / 64;
        const mask = @as(u64, 1) << @intCast(stripe % 64);
        const previous = self.vector_cache_fill_dirty[word].fetchOr(mask, .acq_rel);
        if (previous & mask == 0) _ = self.vector_cache_fill_epochs[stripe].fetchAdd(1, .acq_rel);
    }

    fn finishVectorCacheMutations(self: *HBCIndex) void {
        for (&self.vector_cache_fill_dirty, 0..) |*dirty, word| {
            var bits = dirty.swap(0, .acq_rel);
            while (bits != 0) {
                const bit: usize = @intCast(@ctz(bits));
                const stripe = word * 64 + bit;
                _ = self.vector_cache_fill_epochs[stripe].fetchAdd(1, .acq_rel);
                bits &= bits - 1;
            }
        }
    }

    pub fn beginVectorCacheFill(self: *HBCIndex, vector_id: u64) ?u64 {
        const epoch = self.vector_cache_fill_epochs[vectorCacheFillStripe(vector_id)].load(.acquire);
        if (epoch & 1 != 0) return null;
        return epoch;
    }

    pub fn abortVectorCacheMutations(self: *HBCIndex) void {
        self.finishVectorCacheMutations();
    }

    pub fn invalidateMetadataCache(self: *HBCIndex, vector_id: u64) void {
        if (self.shared_cache) |cache| {
            cache.invalidateMetadata(self.cache_namespace, vector_id);
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        self.invalidateLocalMetadataCacheLocked(vector_id);
        self.refreshHbcCacheUsage();
    }

    fn shouldAdmitConcurrentVectorCache(self: *HBCIndex) bool {
        // Exact scoring does not enter the HBC traversal epoch. Always consult
        // the shared-cache doorkeeper so those misses cannot bypass pressure
        // sampling and serialize on clone/insert/evict churn.
        if (self.shared_cache) |cache| return cache.shouldAdmitConcurrentVector(self.cache_namespace);
        // Local cache entries use lock-backed borrows and remain mutation-free
        // while multiple searches overlap.
        return self.active_searches.load(.acquire) <= 1;
    }

    /// Capture the publication epoch immediately before an authoritative cache
    /// miss read. Admission later rechecks this token under the cache's write
    /// lock, which makes invalidation and stale-fill rejection one ordered
    /// operation for both local and shared caches.
    pub fn beginSearchCacheFill(self: *const HBCIndex) ?u64 {
        if (self.lsmSessionBatchingActive()) return null;
        const epoch = self.published_mutation_epoch.load(.acquire);
        if (epoch & 1 != 0) return null;
        return epoch;
    }

    fn searchCacheFillCurrent(self: *const HBCIndex, expected_epoch: u64) bool {
        return cacheFillEpochCurrent(&self.published_mutation_epoch, expected_epoch);
    }

    pub fn cacheNode(self: *HBCIndex, node: *const Node) !void {
        return try self.cacheNodeWithFillEpoch(node, null);
    }

    fn cacheNodeWithFillEpoch(self: *HBCIndex, node: *const Node, fill_epoch: ?u64) !void {
        if (!self.cache_enabled) return;
        if (fill_epoch) |expected| if (!self.searchCacheFillCurrent(expected)) return;
        {
            self.cache_mu.lockExclusive();
            defer self.cache_mu.unlockExclusive();
            if (fill_epoch) |expected| if (!self.searchCacheFillCurrent(expected)) return;
            if (self.pinned_node_cache.contains(node.id)) {
                try self.cachePinnedNodeLocked(node, true);
            }
        }
        if (self.shared_cache) |cache| {
            if (self.config.max_cached_nodes == 0) return;
            _ = if (fill_epoch) |expected|
                try cache.cacheNodeGuarded(self.cache_namespace, node, &self.published_mutation_epoch, expected)
            else
                try cache.cacheNode(self.cache_namespace, node);
            return;
        }
        const cloned = try node.clone(self.alloc);
        errdefer {
            var owned = cloned;
            owned.deinit(self.alloc);
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        if (fill_epoch) |expected| {
            if (!self.searchCacheFillCurrent(expected)) {
                var owned = cloned;
                owned.deinit(self.alloc);
                return;
            }
        }
        var admission = self.prepareHbcCacheAdmission(.node, node.id, estimateNodeCacheBytes(node)) orelse {
            self.noteHbcCacheAdmissionSkip(.node);
            var owned = cloned;
            owned.deinit(self.alloc);
            return;
        };
        errdefer {
            admission.rollback();
            self.refreshHbcCacheUsage();
        }
        try self.cacheNodeLocalLocked(cloned);
        self.noteHbcCacheInsertion(.node);
        admission.commit();
        self.refreshAndEnforceHbcCacheUsage(.one(.node, node.id));
    }

    pub fn cacheSearchNode(self: *HBCIndex, node: *const Node) !void {
        if (self.lsmSessionBatchingActive() or self.publicationMutationActive()) return;
        try self.cacheNode(node);
    }

    pub fn cacheSearchNodeIfFillCurrent(self: *HBCIndex, node: *const Node, fill_epoch: u64) !void {
        try self.cacheNodeWithFillEpoch(node, fill_epoch);
    }

    pub fn cacheQuantized(self: *HBCIndex, node_id: u64, qs: *const QuantizedSet) !void {
        if (self.lsmSessionBatchingActive() or self.publicationMutationActive()) return;
        try self.cacheQuantizedWithFillEpoch(node_id, qs, null);
    }

    pub fn cacheQuantizedIfFillCurrent(self: *HBCIndex, node_id: u64, qs: *const QuantizedSet, fill_epoch: u64) !void {
        try self.cacheQuantizedWithFillEpoch(node_id, qs, fill_epoch);
    }

    fn cacheQuantizedWithFillEpoch(self: *HBCIndex, node_id: u64, qs: *const QuantizedSet, fill_epoch: ?u64) !void {
        if (!self.cache_enabled) return;
        if (fill_epoch) |expected| if (!self.searchCacheFillCurrent(expected)) return;
        {
            self.cache_mu.lockExclusive();
            defer self.cache_mu.unlockExclusive();
            if (fill_epoch) |expected| if (!self.searchCacheFillCurrent(expected)) return;
            if (self.pinned_quantized_cache.contains(node_id)) {
                try self.cachePinnedQuantizedOwnedLocked(node_id, try qs.clone(self.alloc), true);
            }
        }
        if (self.shared_cache) |cache| {
            if (self.config.max_cached_nodes == 0) return;
            _ = if (fill_epoch) |expected|
                try cache.cacheQuantizedGuarded(self.cache_namespace, node_id, qs, &self.published_mutation_epoch, expected)
            else
                try cache.cacheQuantized(self.cache_namespace, node_id, qs);
            return;
        }
        var cloned = try qs.clone(self.alloc);
        errdefer cloned.deinit(self.alloc);
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        if (fill_epoch) |expected| {
            if (!self.searchCacheFillCurrent(expected)) {
                cloned.deinit(self.alloc);
                return;
            }
        }
        var admission = self.prepareHbcCacheAdmission(.quantized, node_id, estimateQuantizedCacheBytes(qs)) orelse {
            self.noteHbcCacheAdmissionSkip(.quantized);
            cloned.deinit(self.alloc);
            return;
        };
        errdefer {
            admission.rollback();
            self.refreshHbcCacheUsage();
        }
        try self.cacheQuantizedLocalLocked(node_id, cloned);
        self.noteHbcCacheInsertion(.quantized);
        admission.commit();
        self.refreshAndEnforceHbcCacheUsage(.one(.quantized, node_id));
    }

    pub fn cacheQuantizedOwned(self: *HBCIndex, node_id: u64, qs: QuantizedSet) !void {
        if (!self.cache_enabled) {
            var owned = qs;
            owned.deinit(self.alloc);
            return error.CacheDisabled;
        }
        if (self.shared_cache) |cache| {
            if (self.config.max_cached_nodes == 0) {
                var owned = qs;
                owned.deinit(self.alloc);
                return error.CacheDisabled;
            }
            try cache.cacheQuantizedOwned(self.cache_namespace, node_id, qs);
            return;
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        var admission = self.prepareHbcCacheAdmission(.quantized, node_id, estimateQuantizedCacheBytes(&qs)) orelse HbcCacheAdmission.none(self);
        errdefer {
            admission.rollback();
            self.refreshHbcCacheUsage();
        }
        try self.cacheQuantizedLocalLocked(node_id, qs);
        self.noteHbcCacheInsertion(.quantized);
        admission.commit();
        self.refreshAndEnforceHbcCacheUsage(.one(.quantized, node_id));
    }

    pub fn cacheVector(self: *HBCIndex, vector_id: u64, vector_data: []const f32) ![]const f32 {
        if (!self.cache_enabled) return vector_data;
        if (!self.retained_vector_cache_enabled) return vector_data;
        if (self.bypass_external_vector_cache) return vector_data;
        if (self.lsmSessionBatchingActive()) return vector_data;
        if (!self.shouldAdmitConcurrentVectorCache()) return vector_data;
        const fill_epoch = self.beginVectorCacheFill(vector_id) orelse return vector_data;
        return try self.cacheVectorRetained(vector_id, vector_data, fill_epoch, false, false, null);
    }

    /// Publish a decoded miss for a request that already reserved decoded
    /// residency. The lease replaces per-vector sampling; admission and
    /// eviction remain governed by the shared ResourceManager-backed cache.
    pub fn cacheVectorForResidencyLease(
        self: *HBCIndex,
        lease: *DecodedVectorResidencyLease,
        vector_id: u64,
        vector_data: []const f32,
    ) ![]const f32 {
        if (!self.cache_enabled or !self.retained_vector_cache_enabled) return vector_data;
        if (self.bypass_external_vector_cache or self.lsmSessionBatchingActive()) return vector_data;
        const shared = self.shared_cache orelse return vector_data;
        if (!lease.belongsTo(shared, self.cache_namespace)) return vector_data;
        const bytes = estimateVectorCacheBytes(vector_data);
        if (!lease.ensureCapacity(bytes)) return vector_data;
        const fill_epoch = self.beginVectorCacheFill(vector_id) orelse return vector_data;
        var inserted = false;
        const retained = try self.cacheVectorRetained(vector_id, vector_data, fill_epoch, false, true, &inserted);
        if (inserted) lease.consume(bytes);
        return retained;
    }

    pub fn cacheVectorIfFillCurrent(
        self: *HBCIndex,
        vector_id: u64,
        vector_data: []const f32,
        fill_epoch: u64,
    ) ![]const f32 {
        if (!self.cache_enabled or !self.retained_vector_cache_enabled) return vector_data;
        if (self.bypass_external_vector_cache or self.lsmSessionBatchingActive()) return vector_data;
        if (!self.shouldAdmitConcurrentVectorCache()) return vector_data;
        return try self.cacheVectorRetained(vector_id, vector_data, fill_epoch, false, false, null);
    }

    pub fn cacheVectorForWarmup(self: *HBCIndex, vector_id: u64, vector_data: []const f32) ![]const f32 {
        if (!self.cache_enabled) return vector_data;
        if (!self.retained_vector_cache_enabled) return vector_data;
        if (self.bypass_external_vector_cache) return vector_data;
        if (!self.shouldAdmitConcurrentVectorCache()) return vector_data;
        const fill_epoch = self.beginVectorCacheFill(vector_id) orelse return vector_data;
        return try self.cacheVectorRetained(vector_id, vector_data, fill_epoch, false, false, null);
    }

    fn cacheVectorRetained(
        self: *HBCIndex,
        vector_id: u64,
        vector_data: []const f32,
        fill_epoch: u64,
        must_cache: bool,
        precharged: bool,
        inserted_out: ?*bool,
    ) ![]const f32 {
        const stripe = vectorCacheFillStripe(vector_id);
        const epoch = &self.vector_cache_fill_epochs[stripe];
        if (self.shared_cache) |cache| {
            if (self.config.max_cached_vectors == 0) return vector_data;
            return try cache.cacheVectorGuarded(
                self.cache_namespace,
                vector_id,
                vector_data,
                epoch,
                fill_epoch,
                must_cache,
                precharged,
                inserted_out,
            );
        }
        if (precharged) return vector_data;
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        if (fill_epoch & 1 != 0 or epoch.load(.acquire) != fill_epoch) return vector_data;
        if (self.vector_cache.get(vector_id)) |existing| {
            if (std.mem.eql(f32, existing.vector, vector_data)) return vector_data;
        }
        const replaced = self.vector_cache.contains(vector_id);
        var admission = self.prepareHbcCacheAdmission(.vector, vector_id, estimateVectorCacheBytes(vector_data)) orelse {
            if (must_cache) return error.ResourceBudgetExceeded;
            self.noteHbcCacheAdmissionSkip(.vector);
            return vector_data;
        };
        errdefer {
            admission.rollback();
            self.refreshHbcCacheUsage();
        }
        _ = try self.cacheVectorLocalLocked(vector_id, vector_data);
        self.noteHbcCacheInsertion(.vector);
        if (replaced) self.hbc_cache_kind_stats[@intFromEnum(HbcCacheKind.vector)].replacements += 1;
        admission.commit();
        self.refreshAndEnforceHbcCacheUsage(.one(.vector, vector_id));
        return vector_data;
    }

    pub fn cacheMetadata(self: *HBCIndex, vector_id: u64, metadata: []const u8) ![]const u8 {
        if (!self.cache_enabled) return metadata;
        if (self.lsmSessionBatchingActive() or self.publicationMutationActive()) return metadata;
        return try self.cacheMetadataWithFillEpoch(vector_id, metadata, null);
    }

    pub fn cacheMetadataIfFillCurrent(self: *HBCIndex, vector_id: u64, metadata: []const u8, fill_epoch: u64) ![]const u8 {
        return try self.cacheMetadataWithFillEpoch(vector_id, metadata, fill_epoch);
    }

    fn cacheMetadataWithFillEpoch(self: *HBCIndex, vector_id: u64, metadata: []const u8, fill_epoch: ?u64) ![]const u8 {
        if (!self.cache_enabled) return metadata;
        if (fill_epoch) |expected| if (!self.searchCacheFillCurrent(expected)) return metadata;
        if (self.config.max_cached_metadata == 0) return metadata;
        if (self.shared_cache) |cache| {
            return if (fill_epoch) |expected|
                try cache.cacheMetadataGuarded(self.cache_namespace, vector_id, metadata, &self.published_mutation_epoch, expected)
            else
                try cache.cacheMetadata(self.cache_namespace, vector_id, metadata);
        }
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        if (fill_epoch) |expected| if (!self.searchCacheFillCurrent(expected)) return metadata;
        var admission = self.prepareHbcCacheAdmission(.metadata, vector_id, estimateMetadataCacheBytes(metadata)) orelse {
            self.noteHbcCacheAdmissionSkip(.metadata);
            return metadata;
        };
        errdefer {
            admission.rollback();
            self.refreshHbcCacheUsage();
        }
        try self.cacheMetadataLocalLocked(vector_id, metadata);
        self.noteHbcCacheInsertion(.metadata);
        admission.commit();
        self.refreshAndEnforceHbcCacheUsage(.one(.metadata, vector_id));
        return metadata;
    }

    pub fn getCachedNodeClone(self: *HBCIndex, node_id: u64) !?Node {
        if (!self.cache_enabled) return null;
        self.cache_mu.lockShared();
        if (self.pinned_node_cache.get(node_id)) |entry| {
            self.noteHbcCacheLookup(.node, true);
            const cloned = entry.node.clone(self.alloc) catch |err| {
                self.cache_mu.unlockShared();
                return err;
            };
            self.cache_mu.unlockShared();
            return cloned;
        }
        if (self.shared_cache == null) {
            if (self.node_cache.get(node_id)) |entry| {
                const cloned = entry.node.clone(self.alloc) catch |err| {
                    self.cache_mu.unlockShared();
                    return err;
                };
                self.cache_mu.unlockShared();
                return cloned;
            }
        }
        self.cache_mu.unlockShared();
        if (self.shared_cache) |cache| return try cache.cloneNode(self.cache_namespace, node_id);
        return null;
    }

    pub fn borrowCachedNode(self: *HBCIndex, node_id: u64) ?BorrowedNode {
        if (!self.cache_enabled) return null;
        self.cache_mu.lockShared();
        if (self.pinned_node_cache.get(node_id)) |entry| {
            self.noteHbcCacheLookup(.node, true);
            retainNodeCacheEntry(entry);
            self.cache_mu.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        if (self.shared_cache != null) {
            self.cache_mu.unlockShared();
            if (self.shared_cache) |cache| return cache.borrowNode(self.cache_namespace, node_id);
            return null;
        }
        if (self.node_cache.get(node_id)) |entry| {
            self.noteHbcCacheLookup(.node, true);
            retainNodeCacheEntry(entry);
            self.cache_mu.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteHbcCacheLookup(.node, false);
        self.cache_mu.unlockShared();
        return null;
    }

    pub fn borrowCachedNodeForSearch(self: *HBCIndex, node_id: u64) ?BorrowedNode {
        if (self.lsmSessionBatchingActive() or self.publicationMutationActive()) return null;
        return self.borrowCachedNode(node_id);
    }

    pub fn getCachedQuantizedClone(self: *HBCIndex, node_id: u64) !?QuantizedSet {
        if (!self.cache_enabled) return null;
        self.cache_mu.lockShared();
        if (self.pinned_quantized_cache.get(node_id)) |entry| {
            self.noteHbcCacheLookup(.quantized, true);
            const cloned = entry.quantized.clone(self.alloc) catch |err| {
                self.cache_mu.unlockShared();
                return err;
            };
            self.cache_mu.unlockShared();
            return cloned;
        }
        if (self.shared_cache == null) {
            if (self.quantized_cache.get(node_id)) |entry| {
                const cloned = entry.quantized.clone(self.alloc) catch |err| {
                    self.cache_mu.unlockShared();
                    return err;
                };
                self.cache_mu.unlockShared();
                return cloned;
            }
        }
        self.cache_mu.unlockShared();
        if (self.shared_cache) |cache| return try cache.cloneQuantized(self.cache_namespace, node_id);
        return null;
    }

    pub fn borrowCachedQuantized(self: *HBCIndex, node_id: u64) ?BorrowedQuantized {
        if (self.lsmSessionBatchingActive() or self.publicationMutationActive()) return null;
        if (!self.cache_enabled) return null;
        self.cache_mu.lockShared();
        if (self.pinned_quantized_cache.get(node_id)) |entry| {
            self.noteHbcCacheLookup(.quantized, true);
            retainQuantizedCacheEntry(entry);
            self.cache_mu.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        if (self.shared_cache != null) {
            self.cache_mu.unlockShared();
            if (self.shared_cache) |cache| return cache.borrowQuantized(self.cache_namespace, node_id);
            return null;
        }
        if (self.quantized_cache.get(node_id)) |entry| {
            self.noteHbcCacheLookup(.quantized, true);
            retainQuantizedCacheEntry(entry);
            self.cache_mu.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteHbcCacheLookup(.quantized, false);
        self.cache_mu.unlockShared();
        return null;
    }

    pub fn noteMutatedCachedQuantized(self: *HBCIndex, node_id: u64) void {
        if (!self.cache_enabled) return;
        self.refreshAndEnforceHbcCacheUsage(.one(.quantized, node_id));
    }

    pub fn borrowCachedVector(self: *HBCIndex, vector_id: u64) ?BorrowedVector {
        if (!self.cache_enabled) return null;
        if (!self.retained_vector_cache_enabled) return null;
        if (self.shared_cache) |cache| return cache.borrowVector(self.cache_namespace, vector_id);
        self.cache_mu.lockShared();
        if (self.vector_cache.get(vector_id)) |entry| {
            self.noteHbcCacheLookup(.vector, true);
            retainVectorCacheEntry(entry);
            self.cache_mu.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteHbcCacheLookup(.vector, false);
        self.cache_mu.unlockShared();
        return null;
    }

    pub fn borrowCachedMetadata(self: *HBCIndex, vector_id: u64) ?BorrowedMetadata {
        if (self.lsmSessionBatchingActive() or self.publicationMutationActive()) return null;
        if (!self.cache_enabled) return null;
        if (self.shared_cache) |cache| return cache.borrowMetadata(self.cache_namespace, vector_id);
        self.cache_mu.lockShared();
        if (self.metadata_cache.get(vector_id)) |entry| {
            self.noteHbcCacheLookup(.metadata, true);
            retainMetadataCacheEntry(entry);
            self.cache_mu.unlockShared();
            return .{ .retained = .{ .alloc = self.alloc, .entry = entry } };
        }
        self.noteHbcCacheLookup(.metadata, false);
        self.cache_mu.unlockShared();
        return null;
    }

    pub fn acquireSearchScratch(self: *HBCIndex) !ScratchHandle {
        return try vectorindex_hbc_runtime.acquireSearchScratch(self);
    }

    pub fn releaseSearchScratch(self: *HBCIndex, handle: *ScratchHandle) void {
        vectorindex_hbc_runtime.releaseSearchScratch(self, handle);
    }

    pub fn refreshSearchScratchAccounting(self: *HBCIndex, handle: *ScratchHandle) void {
        vectorindex_hbc_runtime.refreshSearchScratchAccounting(self, handle);
    }

    pub fn transformVector(self: *HBCIndex, original: []const f32, transformed: []f32) []const f32 {
        return vectorindex_hbc_runtime.transformVector(self, original, transformed);
    }

    pub fn nextNodeId(self: *HBCIndex) u64 {
        return vectorindex_hbc_runtime.nextNodeId(self);
    }

    fn flushMetadataNow(self: *HBCIndex, txn: anytype) !void {
        var buf: [IndexMetadata.encoded_size]u8 = undefined;
        try self.putNamespaced(txn, .meta, meta_key, self.metadata.encode(&buf));
    }

    pub fn projectionCheckpointMetadata(self: *const HBCIndex) ProjectionCheckpointMetadata {
        return self.metadata.projectionCheckpoint();
    }

    pub fn saveProjectionCheckpointMetadata(self: *HBCIndex, checkpoint: ProjectionCheckpointMetadata) !void {
        var txn = try self.beginRuntimeBatchTxnOptions(.{});
        var active = true;
        errdefer if (active) txn.abort();
        self.metadata.setProjectionCheckpoint(checkpoint);
        try self.flushMetadataNow(&txn);
        try txn.commit();
        active = false;
    }

    fn persistBulkPublishState(self: *HBCIndex) !void {
        // Publication intent must not itself make an otherwise empty session
        // look mutated, and it must be committed outside deferred session batch
        // mode before any cross-batch HBC mutation begins.
        var txn = try self.store.beginBatch();
        var active = true;
        errdefer if (active) txn.abort();
        try self.putNamespaced(&txn, .meta, bulk_publish_state_key, bulk_publish_state_value);
        try txn.commit();
        active = false;
        if (self.writeSessionFinishNeedsExplicitDurableSync()) try self.sync(true);
    }

    fn clearBulkPublishStateTxn(self: *HBCIndex, txn: anytype) !void {
        self.deleteNamespaced(txn, .meta, bulk_publish_state_key) catch |err| switch (err) {
            error.NotFound => {},
            else => return err,
        };
    }

    fn clearBulkPublishStateBestEffort(self: *HBCIndex) void {
        var txn = self.store.beginWrite() catch return;
        var active = true;
        defer if (active) txn.abort();
        self.clearBulkPublishStateTxn(&txn) catch return;
        txn.commit() catch return;
        active = false;
        if (self.writeSessionFinishNeedsExplicitDurableSync()) self.sync(true) catch return;
    }

    pub fn flushMetadata(self: *HBCIndex, txn: anytype) !void {
        if (!self.mustPublishMetadataPerBatch()) return;
        try self.flushMetadataNow(txn);
    }

    pub fn beginReadTxn(self: *HBCIndex) !vectorindex_store.NamespaceReadTxn {
        return try self.beginRuntimeReadTxn();
    }

    pub fn beginWriteTxn(self: *HBCIndex) !PublishedWriteTxn {
        try self.beginPublishedSearchStateRefreshIo();
        errdefer self.abortPublishedSearchStateRefresh();
        return .{
            .owner = self,
            .inner = try self.beginRuntimeWriteTxn(),
        };
    }

    pub fn beginBatchTxn(self: *HBCIndex) !PublishedBatchTxn {
        try self.beginPublishedSearchStateRefreshIo();
        errdefer self.abortPublishedSearchStateRefresh();
        return .{
            .owner = self,
            .inner = try self.beginRuntimeBatchTxn(),
        };
    }

    pub fn finishWriteTxn(self: *HBCIndex, txn: *PublishedWriteTxn) !void {
        try self.finishWriteTxnOptions(txn, .{});
    }

    pub fn finishWriteTxnOptions(self: *HBCIndex, txn: *PublishedWriteTxn, options: BatchInsertOptions) !void {
        errdefer self.abortVectorCacheMutations();
        try self.finalizeWriteTxnOptions(txn, options);
        const commit_start = nowNs();
        try txn.commit();
        self.write_profile.insert_commit_ns += elapsedSince(commit_start);
    }

    fn finalizeWriteTxnOptions(self: *HBCIndex, txn: anytype, options: BatchInsertOptions) !void {
        try vectorindex_hbc_index.finalizeWriteTxnOptions(self, txn, options, nowNs, elapsedSince);
        if (options.bulk_ingest and
            !self.shouldDeferLeafSplitToBulkFinish(options) and
            !self.shouldDeferQuantizedRebuildToBulkFinish(options))
        {
            try self.publishDeferredNodeKeysForBulkFinishTxn(txn);
        }
    }

    fn rebuildAllQuantized(self: *HBCIndex, txn: anytype) !void {
        try vectorindex_hbc_index.rebuildAllQuantized(self, txn);
    }

    fn rebuildQuantizedSubtree(self: *HBCIndex, txn: anytype, node_id: u64) !void {
        var node = try self.loadNode(txn, node_id);
        defer node.deinit(self.alloc);
        if (!node.is_leaf) {
            for (node.children) |child_id| {
                try self.rebuildQuantizedSubtree(txn, child_id);
            }
        }
        try self.refreshQuantized(txn, &node);
    }

    pub fn recordDeferredQuantizedNode(self: *HBCIndex, node_id: u64) !void {
        if (!self.config.use_quantization or node_id == 0) return;
        try self.deferred_quantized_nodes.put(self.alloc, node_id, {});
    }

    pub fn clearDeferredQuantizedNode(self: *HBCIndex, node_id: u64) void {
        _ = self.deferred_quantized_nodes.remove(node_id);
    }

    pub fn shouldDeferQuantizedRebuildToBulkFinish(self: *const HBCIndex, options: BatchInsertOptions) bool {
        return self.crossBatchPublicationActive() and options.bulk_ingest and options.defer_quantized_rebuild_to_bulk_finish;
    }

    pub fn shouldDeferLeafSplitToBulkFinish(self: *const HBCIndex, options: BatchInsertOptions) bool {
        return self.crossBatchPublicationActive() and options.bulk_ingest and options.defer_leaf_splits_to_bulk_finish;
    }

    pub fn recordDeferredOversizedLeaf(self: *HBCIndex, leaf_id: u64) !void {
        const gop = try self.deferred_oversized_leaves.getOrPut(self.alloc, leaf_id);
        if (!gop.found_existing) {
            self.deferred_oversized_leaves_peak = @max(self.deferred_oversized_leaves_peak, @as(u64, @intCast(self.deferred_oversized_leaves.count())));
            self.observeApplyWorkspaceBytes();
        }
    }

    fn publishDeferredQuantizedNodesForBulkFinishTxn(self: *HBCIndex, txn: anytype) !void {
        if (self.deferred_quantized_nodes.count() == 0) return;
        const rebuild_start = nowNs();
        try self.rebuildDeferredQuantizedNodes(txn);
        self.write_profile.refresh_quantized_ns += elapsedSince(rebuild_start);
    }

    fn publishDeferredNodeKeysForBulkFinishTxn(self: *HBCIndex, txn: anytype) !void {
        if (self.deferred_node_keys.count() == 0) return;

        var key_buf: [12]u8 = undefined;
        var it = self.deferred_node_keys.iterator();
        while (it.next()) |entry| {
            const parts = stagedNodeKeyParts(entry.key_ptr.*);
            const key = encodeNodeKey(&key_buf, parts.id, parts.suffix);
            if (entry.value_ptr.value) |value| {
                try txn.put(.nodes, key, value);
                self.noteNamespacePut(.nodes, key.len, value.len, false);
            } else {
                try txn.delete(.nodes, key);
                self.noteNamespaceDelete(.nodes, key.len);
            }
        }

        self.clearDeferredNodeKeys();
    }

    pub fn publishDeferredNodeKeysForBatchFinishTxn(self: *HBCIndex, txn: anytype, options: BatchInsertOptions) !void {
        if (!options.bulk_ingest) return;
        if (self.crossBatchPublicationActive() and self.config.centroid_directory_mode == .flat_rabitq) return;
        if (self.shouldDeferLeafSplitToBulkFinish(options)) return;
        if (self.shouldDeferQuantizedRebuildToBulkFinish(options)) return;
        try self.publishDeferredNodeKeysForBulkFinishTxn(txn);
    }

    pub fn normalizeDeferredOversizedLeavesForBatchFinishTxn(self: *HBCIndex, txn: anytype, options: BatchInsertOptions) !void {
        var split_options = options;
        split_options.defer_leaf_splits_to_batch_finish = false;
        split_options.defer_leaf_splits_to_bulk_finish = false;
        split_options.defer_quantized_rebuild = true;
        split_options.coalesce_leaf_writes = true;
        split_options.skip_vector_store = true;
        split_options.bulk_ingest = true;
        while (try self.normalizeDeferredOversizedLeavesTxn(txn, null, null, split_options, false)) {}
    }

    fn normalizeDeferredOversizedLeavesForBulkFinishTxn(self: *HBCIndex, txn: anytype, options: backend_types.BulkIngestFinishOptions) !bool {
        const split_options: BatchInsertOptions = .{
            .defer_quantized_rebuild = true,
            .coalesce_leaf_writes = true,
            .skip_vector_store = true,
            .bulk_ingest = true,
            .bulk_rebuild_leaf_min_members = options.bulk_rebuild_hbc_leaf_min_members orelse @max(
                @as(usize, @intCast(self.config.leaf_size)) * 2,
                @as(usize, @intCast(self.config.leaf_size)) + 1,
            ),
        };
        return try self.normalizeDeferredOversizedLeavesTxn(
            txn,
            options.max_deferred_hbc_leaf_splits_per_publish,
            options.max_deferred_hbc_leaf_split_members_per_publish,
            split_options,
            true,
        );
    }

    fn normalizeDeferredOversizedLeavesTxn(
        self: *HBCIndex,
        txn: anytype,
        max_splits_per_publish: ?usize,
        max_split_members_per_publish: ?usize,
        split_options: BatchInsertOptions,
        allow_kway: bool,
    ) !bool {
        if (self.deferred_oversized_leaves.count() == 0) return false;

        var steps: usize = 0;
        var split_members: usize = 0;
        const split_limit = @max(max_splits_per_publish orelse default_deferred_hbc_leaf_splits_per_publish, 1);
        const member_limit = max_split_members_per_publish orelse std.math.maxInt(usize);
        const max_steps: usize = @max(
            @as(usize, @intCast(self.metadata.active_count)) * 8,
            self.deferred_oversized_leaves.count() * 4,
        ) + 64;

        while (self.deferred_oversized_leaves.count() > 0) {
            if (steps > max_steps) return error.HBCBatchSplitLimitExceeded;
            if (steps >= split_limit) return true;

            const maybe_leaf_id: ?u64 = blk: {
                var it = self.deferred_oversized_leaves.keyIterator();
                break :blk if (it.next()) |leaf_id| leaf_id.* else null;
            };
            const leaf_id = maybe_leaf_id orelse break;
            _ = self.deferred_oversized_leaves.remove(leaf_id);
            self.observeApplyWorkspaceBytes();

            var leaf = self.loadNode(txn, leaf_id) catch |err| {
                if (isNotFound(err)) continue;
                return err;
            };
            defer leaf.deinit(self.alloc);
            if (!leaf.is_leaf or leaf.members.len <= self.config.leaf_size) continue;
            if (!(try self.deferredLeafIsStillAttached(txn, &leaf))) continue;

            if (steps > 0 and split_members + leaf.members.len > member_limit) {
                try self.deferred_oversized_leaves.put(self.alloc, leaf_id, {});
                self.observeApplyWorkspaceBytes();
                return true;
            }

            steps += 1;
            split_members += leaf.members.len;

            const bulk_rebuild_min_members = if (split_options.bulk_rebuild_leaf_min_members != 0)
                split_options.bulk_rebuild_leaf_min_members
            else if (split_options.bulk_ingest)
                @max(@as(usize, @intCast(self.config.leaf_size)) * 4, @as(usize, @intCast(self.config.leaf_size)) + 1)
            else
                0;
            const should_bulk_rebuild = bulk_rebuild_min_members != 0 and leaf.members.len >= bulk_rebuild_min_members;
            const kway_within_member_budget = max_split_members_per_publish == null or leaf.members.len <= member_limit;
            if (allow_kway and kway_within_member_budget and !should_bulk_rebuild and try self.rebuildOversizedLeafKmeansWithOptions(txn, &leaf, split_options)) {
                continue;
            }

            const right_leaf_id = self.metadata.node_count + 1;
            try self.splitLeafWithOptions(txn, &leaf, split_options);
            {
                var left = self.loadNode(txn, leaf.id) catch |err| {
                    if (isNotFound(err)) continue;
                    return err;
                };
                defer left.deinit(self.alloc);
                if (left.is_leaf and left.members.len > self.config.leaf_size) {
                    try self.deferred_oversized_leaves.put(self.alloc, left.id, {});
                }
            }
            {
                var right = self.loadNode(txn, right_leaf_id) catch |err| {
                    if (isNotFound(err)) continue;
                    return err;
                };
                defer right.deinit(self.alloc);
                if (right.is_leaf and right.members.len > self.config.leaf_size) {
                    try self.deferred_oversized_leaves.put(self.alloc, right.id, {});
                }
            }
        }
        return false;
    }

    fn deferredLeafIsStillAttached(self: *HBCIndex, txn: anytype, leaf: *const Node) !bool {
        if (!leaf.is_leaf) return false;
        if (leaf.parent == 0) return leaf.id == self.metadata.root_node;

        var parent = self.loadNode(txn, leaf.parent) catch |err| {
            if (isNotFound(err)) return false;
            return err;
        };
        defer parent.deinit(self.alloc);
        for (parent.children) |child_id| {
            if (child_id == leaf.id) return true;
        }
        return false;
    }

    pub fn rebuildDeferredQuantizedNodes(self: *HBCIndex, txn: anytype) !void {
        try self.rebuildDeferredQuantizedNodesWithOptions(txn, .{});
    }

    pub fn rebuildDeferredQuantizedNodesWithOptions(self: *HBCIndex, txn: anytype, options: BatchInsertOptions) !void {
        if (!self.config.use_quantization) {
            self.deferred_quantized_nodes.clearRetainingCapacity();
            return;
        }

        var retained = std.ArrayListUnmanaged(u64).empty;
        defer retained.deinit(self.alloc);

        {
            var it = self.deferred_quantized_nodes.keyIterator();
            while (it.next()) |node_id| {
                var node = self.loadNode(txn, node_id.*) catch |err| {
                    if (isNotFound(err)) continue;
                    return err;
                };
                defer node.deinit(self.alloc);
                if (self.deferred_oversized_leaves.contains(node_id.*) and node.is_leaf and node.members.len > self.config.leaf_size) {
                    try retained.append(self.alloc, node_id.*);
                    continue;
                }
                self.refreshQuantizedWithOptions(txn, &node, options) catch |err| {
                    if (isNotFound(err)) {
                        var key_buf: [10]u8 = undefined;
                        self.deleteNamespaced(txn, .quant, encodeQuantKey(&key_buf, node.id)) catch {};
                        self.invalidateQuantizedCache(node.id);
                        continue;
                    }
                    return err;
                };
            }
        }
        self.deferred_quantized_nodes.clearRetainingCapacity();
        for (retained.items) |node_id| {
            try self.deferred_quantized_nodes.put(self.alloc, node_id, {});
        }
    }

    // ========================================================================
    // Node I/O
    // ========================================================================

    pub fn loadNodeFromStorage(self: *HBCIndex, txn: anytype, node_id: u64) !Node {
        var key_buf: [12]u8 = undefined;

        const packed_data = try self.getNamespaced(txn, .nodes, encodeNodeKey(&key_buf, node_id, .packed_node));
        const packed_value = try vectorindex_hbc.decodePackedNodeValue(packed_data);
        if (packed_value.centroid_bytes.len % @sizeOf(f32) != 0) return error.Corrupted;
        if (packed_value.ids_bytes.len % @sizeOf(u64) != 0) return error.Corrupted;
        const max_centroid_bytes = std.math.mul(
            usize,
            @as(usize, @intCast(self.config.dims)),
            @sizeOf(f32),
        ) catch return error.Corrupted;
        if (packed_value.centroid_bytes.len > max_centroid_bytes) return error.Corrupted;
        const centroid_len = packed_value.centroid_bytes.len;
        const ids_len = packed_value.ids_bytes.len;
        const ids_offset = std.mem.alignForward(usize, centroid_len, @alignOf(u64));
        const total_len = std.math.add(usize, ids_offset, ids_len) catch return error.Corrupted;

        var backing: []align(@alignOf(u64)) u8 = if (total_len > 0)
            try self.alloc.alignedAlloc(u8, std.mem.Alignment.of(u64), total_len)
        else
            &.{};
        errdefer if (backing.len > 0) self.alloc.free(backing);

        const centroid: []f32 = if (centroid_len > 0) blk: {
            const dst: []align(@alignOf(f32)) u8 = @alignCast(backing[0..centroid_len]);
            @memcpy(dst, packed_value.centroid_bytes);
            break :blk @as([*]f32, @ptrCast(dst.ptr))[0 .. centroid_len / @sizeOf(f32)];
        } else blk: {
            break :blk &.{};
        };

        var children: []u64 = &.{};
        var members: []u64 = &.{};
        if (ids_len > 0) {
            const dst: []u8 = backing[ids_offset .. ids_offset + ids_len];
            @memcpy(dst, packed_value.ids_bytes);
            const aligned_dst: []align(@alignOf(u64)) u8 = @alignCast(dst);
            if (packed_value.header.is_leaf) {
                members = std.mem.bytesAsSlice(u64, aligned_dst);
            } else {
                children = std.mem.bytesAsSlice(u64, aligned_dst);
            }
        }

        const node = Node{
            .id = node_id,
            .is_leaf = packed_value.header.is_leaf,
            .level = packed_value.header.level,
            .parent = packed_value.header.parent,
            .centroid = centroid,
            .covering_radius = packed_value.covering_radius,
            .children = children,
            .members = members,
            .posting_state = if (packed_value.header.is_leaf) try vectorindex_posting.PostingStore.loadState(self, txn, node_id, isNotFound) else .{},
            .backing = backing,
        };
        return node;
    }

    fn loadCommittedPostingState(self: *HBCIndex, txn: anytype, posting_id: u64) !vectorindex_posting.PostingState {
        var key_buf: [12]u8 = undefined;
        const data = self.getNamespacedCommitted(txn, .nodes, encodeNodeKey(&key_buf, posting_id, .posting)) catch |err| {
            if (isNotFound(err)) return .{};
            return err;
        };
        return try vectorindex_posting.decodeState(data);
    }

    /// Decode only the fields needed to build the flat centroid directory.
    /// Leaf membership can legitimately exceed `leaf_size` between bounded
    /// bulk-finish publication windows, but the directory only needs to know
    /// whether the posting is empty. Represent that fact with one marker id so
    /// malformed or very large leaf payloads cannot bypass cold-build
    /// admission by forcing a second full membership copy.
    pub fn loadFlatCentroidDirectoryNodeFromStorage(self: *HBCIndex, txn: anytype, node_id: u64) !Node {
        var key_buf: [12]u8 = undefined;

        const packed_data = try self.getNamespacedCommitted(txn, .nodes, encodeNodeKey(&key_buf, node_id, .packed_node));
        const packed_value = try vectorindex_hbc.decodePackedNodeValue(packed_data);
        if (packed_value.centroid_bytes.len % @sizeOf(f32) != 0) return error.Corrupted;
        if (packed_value.ids_bytes.len % @sizeOf(u64) != 0) return error.Corrupted;
        const max_centroid_bytes = std.math.mul(
            usize,
            @as(usize, @intCast(self.config.dims)),
            @sizeOf(f32),
        ) catch return error.Corrupted;
        if (packed_value.centroid_bytes.len > max_centroid_bytes) return error.Corrupted;
        const stored_id_count = packed_value.ids_bytes.len / @sizeOf(u64);
        if (!packed_value.header.is_leaf and stored_id_count > self.config.branching_factor)
            return error.Corrupted;

        const centroid_len = packed_value.centroid_bytes.len;
        const decoded_ids_len: usize = if (packed_value.header.is_leaf)
            if (stored_id_count == 0) 0 else @sizeOf(u64)
        else
            packed_value.ids_bytes.len;
        const ids_offset = std.mem.alignForward(usize, centroid_len, @alignOf(u64));
        const total_len = std.math.add(usize, ids_offset, decoded_ids_len) catch return error.Corrupted;

        var backing: []align(@alignOf(u64)) u8 = if (total_len > 0)
            try self.alloc.alignedAlloc(u8, std.mem.Alignment.of(u64), total_len)
        else
            &.{};
        errdefer if (backing.len > 0) self.alloc.free(backing);

        const centroid: []f32 = if (centroid_len > 0) blk: {
            const dst: []align(@alignOf(f32)) u8 = @alignCast(backing[0..centroid_len]);
            @memcpy(dst, packed_value.centroid_bytes);
            break :blk @as([*]f32, @ptrCast(dst.ptr))[0 .. centroid_len / @sizeOf(f32)];
        } else blk: {
            break :blk &.{};
        };

        var children: []u64 = &.{};
        var members: []u64 = &.{};
        if (decoded_ids_len > 0) {
            const dst: []u8 = backing[ids_offset .. ids_offset + decoded_ids_len];
            const aligned_dst: []align(@alignOf(u64)) u8 = @alignCast(dst);
            if (packed_value.header.is_leaf) {
                std.mem.writeInt(u64, aligned_dst[0..@sizeOf(u64)], 0, .little);
                members = std.mem.bytesAsSlice(u64, aligned_dst);
            } else {
                @memcpy(dst, packed_value.ids_bytes);
                children = std.mem.bytesAsSlice(u64, aligned_dst);
            }
        }

        return .{
            .id = node_id,
            .is_leaf = packed_value.header.is_leaf,
            .level = packed_value.header.level,
            .parent = packed_value.header.parent,
            .centroid = centroid,
            .covering_radius = packed_value.covering_radius,
            .children = children,
            .members = members,
            .posting_state = .{},
            .backing = backing,
        };
    }

    pub fn loadSearchNodeFromStorage(self: *HBCIndex, txn: anytype, node_id: u64) !Node {
        var key_buf: [12]u8 = undefined;

        const packed_data = try self.getNamespacedCommitted(txn, .nodes, encodeNodeKey(&key_buf, node_id, .packed_node));
        const packed_value = try vectorindex_hbc.decodePackedNodeValue(packed_data);
        if (packed_value.centroid_bytes.len % @sizeOf(f32) != 0) return error.Corrupted;
        if (packed_value.ids_bytes.len % @sizeOf(u64) != 0) return error.Corrupted;
        const max_centroid_bytes = std.math.mul(
            usize,
            @as(usize, @intCast(self.config.dims)),
            @sizeOf(f32),
        ) catch return error.Corrupted;
        if (packed_value.centroid_bytes.len > max_centroid_bytes) return error.Corrupted;
        if (!packed_value.header.is_leaf and
            packed_value.ids_bytes.len / @sizeOf(u64) > self.config.branching_factor)
        {
            // Published internal nodes are bounded by the configured fanout.
            // Reject malformed payloads before allocating their backing store;
            // cold-directory admission budgets exactly one bounded decoded
            // node and must not be bypassable by corrupt persisted lengths.
            return error.Corrupted;
        }
        const centroid_len = packed_value.centroid_bytes.len;
        const ids_len = packed_value.ids_bytes.len;
        const ids_offset = std.mem.alignForward(usize, centroid_len, @alignOf(u64));
        const total_len = std.math.add(usize, ids_offset, ids_len) catch return error.Corrupted;

        var backing: []align(@alignOf(u64)) u8 = if (total_len > 0)
            try self.alloc.alignedAlloc(u8, std.mem.Alignment.of(u64), total_len)
        else
            &.{};
        errdefer if (backing.len > 0) self.alloc.free(backing);

        const centroid: []f32 = if (centroid_len > 0) blk: {
            const dst: []align(@alignOf(f32)) u8 = @alignCast(backing[0..centroid_len]);
            @memcpy(dst, packed_value.centroid_bytes);
            break :blk @as([*]f32, @ptrCast(dst.ptr))[0 .. centroid_len / @sizeOf(f32)];
        } else blk: {
            break :blk &.{};
        };

        var children: []u64 = &.{};
        var members: []u64 = &.{};
        if (ids_len > 0) {
            const dst: []u8 = backing[ids_offset .. ids_offset + ids_len];
            @memcpy(dst, packed_value.ids_bytes);
            const aligned_dst: []align(@alignOf(u64)) u8 = @alignCast(dst);
            if (packed_value.header.is_leaf) {
                members = std.mem.bytesAsSlice(u64, aligned_dst);
            } else {
                children = std.mem.bytesAsSlice(u64, aligned_dst);
            }
        }

        return .{
            .id = node_id,
            .is_leaf = packed_value.header.is_leaf,
            .level = packed_value.header.level,
            .parent = packed_value.header.parent,
            .centroid = centroid,
            .covering_radius = packed_value.covering_radius,
            .children = children,
            .members = members,
            .posting_state = if (packed_value.header.is_leaf) try self.loadCommittedPostingState(txn, node_id) else .{},
            .backing = backing,
        };
    }

    fn pinnedNodeCached(self: *HBCIndex, node_id: u64) bool {
        self.cache_mu.lockShared();
        defer self.cache_mu.unlockShared();
        return self.pinned_node_cache.contains(node_id);
    }

    fn pinnedQuantizedCached(self: *HBCIndex, node_id: u64) bool {
        self.cache_mu.lockShared();
        defer self.cache_mu.unlockShared();
        return self.pinned_quantized_cache.contains(node_id);
    }

    fn ensurePinnedNode(self: *HBCIndex, node: *const Node, fill_epoch: u64) !void {
        if (self.config.max_pinned_tree_nodes == 0) return;
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        // The epoch check and admission share the invalidation lock. If a
        // publisher invalidated this node before we acquired the lock, an old
        // MVCC reader cannot put the pre-publication value back. If publication
        // starts after this check, its invalidation waits for this lock and
        // removes the entry before the new generation becomes visible.
        if (!self.searchCacheFillCurrent(fill_epoch)) return;
        try self.cachePinnedNodeLocked(node, false);
    }

    fn ensurePinnedQuantized(self: *HBCIndex, txn: anytype, node: *const Node, fill_epoch: u64) !void {
        if (!self.config.use_quantization) return;
        if (self.config.max_pinned_tree_nodes == 0) return;
        const expected_count = if (node.is_leaf) node.members.len else node.children.len;
        if (expected_count == 0) return;
        if (self.pinnedQuantizedCached(node.id)) return;

        if (self.borrowCachedQuantized(node.id)) |borrowed| {
            var handle = borrowed;
            defer handle.deinit();
            var cloned = try handle.ptr().clone(self.alloc);
            self.cache_mu.lockExclusive();
            defer self.cache_mu.unlockExclusive();
            if (!self.searchCacheFillCurrent(fill_epoch)) {
                cloned.deinit(self.alloc);
                return;
            }
            try self.cachePinnedQuantizedOwnedLocked(node.id, cloned, false);
            return;
        }

        var loaded = self.loadQuantized(txn, node.id, node.parent == 0, expected_count) catch |err| {
            if (isNotFound(err) or err == error.Corrupted) return;
            return err;
        };
        self.cache_mu.lockExclusive();
        defer self.cache_mu.unlockExclusive();
        if (!self.searchCacheFillCurrent(fill_epoch)) {
            loaded.deinit(self.alloc);
            return;
        }
        try self.cachePinnedQuantizedOwnedLocked(node.id, loaded, false);
    }

    pub fn pinUpperTreeCache(self: *HBCIndex, txn: anytype) !void {
        // Pinning is a search-cache fill just like an ordinary node miss. Bind
        // every admission to the epoch captured immediately before this read
        // transaction opened; a miss-time token would permit an old MVCC
        // snapshot to repopulate the current generation after invalidation.
        const fill_epoch = txn.cache_fill_epoch orelse return;
        if (!self.searchCacheFillCurrent(fill_epoch)) return;
        if (self.publicationMutationActive()) return;
        if (!self.cache_enabled) return;
        if (self.config.max_pinned_tree_nodes == 0) return;
        if (self.metadata.root_node == 0) return;

        const PinQueueEntry = struct {
            node_id: u64,
            depth: u8,
        };

        var pending = std.ArrayListUnmanaged(PinQueueEntry).empty;
        defer pending.deinit(self.alloc);
        try pending.append(self.alloc, .{ .node_id = self.publishedRootNode(), .depth = 0 });

        var index: usize = 0;
        var visited: usize = 0;
        while (index < pending.items.len and visited < self.config.max_pinned_tree_nodes) : (index += 1) {
            if (!self.searchCacheFillCurrent(fill_epoch)) return;
            const item = pending.items[index];
            visited += 1;

            if (self.borrowCachedNode(item.node_id)) |borrowed| {
                var handle = borrowed;
                defer handle.deinit();
                const node = handle.ptr();
                if (!self.pinnedNodeCached(item.node_id)) try self.ensurePinnedNode(node, fill_epoch);
                try self.ensurePinnedQuantized(txn, node, fill_epoch);
                if (!node.is_leaf and item.depth < self.config.pinned_tree_depth) {
                    for (node.children) |child_id| {
                        if (pending.items.len >= self.config.max_pinned_tree_nodes) break;
                        try pending.append(self.alloc, .{ .node_id = child_id, .depth = item.depth + 1 });
                    }
                }
                continue;
            }

            var node = self.loadNodeFromStorage(txn, item.node_id) catch |err| {
                if (isNotFound(err)) continue;
                return err;
            };
            defer node.deinit(self.alloc);
            try self.ensurePinnedNode(&node, fill_epoch);
            try self.ensurePinnedQuantized(txn, &node, fill_epoch);
            if (!node.is_leaf and item.depth < self.config.pinned_tree_depth) {
                for (node.children) |child_id| {
                    if (pending.items.len >= self.config.max_pinned_tree_nodes) break;
                    try pending.append(self.alloc, .{ .node_id = child_id, .depth = item.depth + 1 });
                }
            }
        }
    }

    pub fn loadNode(self: *HBCIndex, txn: anytype, node_id: u64) !Node {
        return try vectorindex_hbc_index.loadNode(self, txn, node_id);
    }

    pub fn validateStoredStructure(self: *HBCIndex, alloc: Allocator) !void {
        return try self.validateStoredStructureWithCancellation(alloc, null, null);
    }

    /// Validates the published tree through one stable runtime read snapshot.
    /// Background repair preflight supplies a cooperative cancellation hook so
    /// owner shutdown does not have to wait for a large tree walk to finish.
    pub fn validateStoredStructureWithCancellation(
        self: *HBCIndex,
        alloc: Allocator,
        cancel_ctx: ?*anyopaque,
        cancel_fn: ?*const fn (*anyopaque) bool,
    ) !void {
        if (self.metadata.active_count == 0) return;
        if (self.metadata.root_node == 0 or self.metadata.node_count == 0) return error.Corrupted;

        var txn = try self.beginRuntimeReadTxn();
        defer txn.abort();

        var pending = std.ArrayListUnmanaged(u64).empty;
        defer pending.deinit(alloc);
        try pending.append(alloc, self.metadata.root_node);

        var seen = std.AutoHashMapUnmanaged(u64, void).empty;
        defer seen.deinit(alloc);

        var visited_since_cancel_check: usize = 0;
        while (pending.pop()) |node_id| {
            visited_since_cancel_check += 1;
            if (visited_since_cancel_check == 256) {
                visited_since_cancel_check = 0;
                if (cancel_ctx != null and cancel_fn != null and cancel_fn.?(cancel_ctx.?)) return error.Canceled;
            }
            if (node_id == 0) return error.Corrupted;
            const gop = try seen.getOrPut(alloc, node_id);
            if (gop.found_existing) continue;

            var node = try self.loadNodeFromStorage(&txn, node_id);
            defer node.deinit(self.alloc);

            if (node.is_leaf) {
                if (node.members.len == 0) return error.Corrupted;
                continue;
            }
            if (node.children.len == 0) return error.Corrupted;
            try pending.appendSlice(alloc, node.children);
        }
    }

    fn collectNamespaceKeys(
        self: *HBCIndex,
        alloc: Allocator,
        txn: anytype,
        comptime namespace: Namespace,
    ) !std.ArrayListUnmanaged([]u8) {
        var keys = std.ArrayListUnmanaged([]u8).empty;
        errdefer {
            for (keys.items) |key| alloc.free(key);
            keys.deinit(alloc);
        }

        var cursor = try self.openNamespacedCursor(alloc, txn, namespace);
        defer cursor.close();

        var entry = try cursor.first();
        while (entry) |kv| : (entry = try cursor.next()) {
            try keys.append(alloc, try alloc.dupe(u8, kv.key));
        }
        return keys;
    }

    fn clearNamespace(self: *HBCIndex, txn: anytype, comptime namespace: Namespace) !void {
        var keys = try self.collectNamespaceKeys(self.alloc, txn, namespace);
        defer {
            for (keys.items) |key| self.alloc.free(key);
            keys.deinit(self.alloc);
        }

        for (keys.items) |key| {
            try self.deleteNamespaced(txn, namespace, key);
        }
    }

    fn putEmptyRootNodeTxn(self: *HBCIndex, txn: anytype) !void {
        var key_buf: [12]u8 = undefined;
        var packed_buf: [vectorindex_hbc.packed_node_header_size]u8 = undefined;
        const header = NodeHeader{ .is_leaf = true, .level = 0, .parent = 0 };
        const packed_node = try vectorindex_hbc.encodePackedNodeValue(&packed_buf, header, 0, &.{}, &.{});
        try self.putNamespaced(txn, .nodes, encodeNodeKey(&key_buf, 1, .packed_node), packed_node);
    }

    fn resetStoredStructureTxn(self: *HBCIndex, txn: anytype) !void {
        try self.clearNamespace(txn, .nodes);
        try self.clearNamespace(txn, .vecs);
        try self.clearNamespace(txn, .quant);
        try self.clearNamespace(txn, .meta);

        self.clearNodeCache();
        self.clearQuantizedCache();
        self.clearVectorCache();
        self.clearMetadataCache();
        self.deferred_quantized_nodes.clearRetainingCapacity();
        self.clearDeferredNodeKeys();
        self.deferred_oversized_leaves.clearRetainingCapacity();
        self.observeApplyWorkspaceBytes();

        self.metadata.root_node = 1;
        self.metadata.node_count = 1;
        self.metadata.active_count = 0;
        try self.putEmptyRootNodeTxn(txn);
    }

    pub fn resetStoredStructure(self: *HBCIndex) !void {
        var txn = try self.beginWriteTxn();
        errdefer txn.abort();
        try self.resetStoredStructureTxn(&txn);
        try self.finishWriteTxn(&txn);
    }

    pub fn deleteNodeHeaderForTest(self: *HBCIndex, node_id: u64) !void {
        if (!builtin.is_test) return error.Unsupported;

        var txn = try self.beginWriteTxn();
        errdefer txn.abort();

        var key_buf: [12]u8 = undefined;
        try self.deleteNamespaced(&txn, .nodes, encodeNodeKey(&key_buf, node_id, .packed_node));
        try self.finishWriteTxn(&txn);
    }

    pub fn getNodeRead(self: *HBCIndex, txn: anytype, node_id: u64) !NodeRead {
        return try vectorindex_hbc_index.loadNodeReadHandle(self, txn, node_id);
    }

    pub fn getNodeReadProfiled(self: *HBCIndex, txn: anytype, node_id: u64, profile: *SearchProfile) !NodeRead {
        return try vectorindex_hbc_index.loadNodeReadHandleProfiled(self, txn, node_id, profile, nowNs, elapsedSince);
    }

    pub fn saveNode(self: *HBCIndex, txn: anytype, node: *const Node) !void {
        try vectorindex_hbc_index.saveNode(self, txn, node, nowNsI128, elapsedSinceNs);
    }

    pub fn saveNodeWithOptions(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        options: BatchInsertOptions,
    ) !void {
        try vectorindex_hbc_index.saveNodeWithOptions(self, txn, node, options, nowNsI128, elapsedSinceNs);
    }

    pub fn saveNodeWithOptionsMode(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        options: BatchInsertOptions,
        write_header: bool,
    ) !void {
        try vectorindex_hbc_index.saveNodeWithOptionsMode(self, txn, node, options, write_header, nowNsI128, elapsedSinceNs);
    }

    pub fn saveNodeBody(self: *HBCIndex, txn: anytype, node: *const Node) !void {
        try vectorindex_hbc_index.saveNodeBody(self, txn, node, nowNsI128, elapsedSinceNs);
    }

    fn saveNodeBodyWithAddedVector(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        transformed_vector: []const f32,
    ) !void {
        try vectorindex_hbc_index.saveNodeBodyWithAddedVector(
            self,
            txn,
            node,
            transformed_vector,
            nowNsI128,
            elapsedSinceNs,
        );
    }

    fn saveNodeBodyWithAddedVectorOptions(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        transformed_vector: []const f32,
        options: BatchInsertOptions,
    ) !void {
        try vectorindex_hbc_index.saveNodeBodyWithAddedVectorOptions(
            self,
            txn,
            node,
            transformed_vector,
            options,
            nowNsI128,
            elapsedSinceNs,
        );
    }

    fn saveExistingNodeBodyWithAddedVectorOptions(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        transformed_vector: []const f32,
        options: BatchInsertOptions,
    ) !void {
        try vectorindex_hbc_index.saveExistingNodeBodyWithAddedVectorOptions(
            self,
            txn,
            node,
            transformed_vector,
            options,
            nowNsI128,
            elapsedSinceNs,
        );
    }

    fn saveNodeBodyInternal(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        added_vector: ?[]const f32,
        defer_quantized_rebuild: bool,
        write_header: bool,
    ) !void {
        try vectorindex_hbc_index.saveNodeBodyInternal(
            self,
            txn,
            node,
            added_vector,
            defer_quantized_rebuild,
            write_header,
            std.time.nanoTimestamp,
            elapsedSinceNs,
        );
    }

    fn updateQuantizedWithAddedVector(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        transformed_vector: []const f32,
    ) !bool {
        return try vectorindex_hbc_index.updateQuantizedWithAddedVector(self, txn, node, transformed_vector, nowNsI128, elapsedSinceNs, nowNsI128());
    }

    pub fn deleteNode(self: *HBCIndex, txn: anytype, node_id: u64) !void {
        try vectorindex_hbc_index.deleteNode(self, txn, node_id);
    }

    pub fn updateParent(self: *HBCIndex, txn: anytype, node_id: u64, new_parent: u64) !void {
        try vectorindex_hbc_index.updateParent(self, txn, node_id, new_parent, nowNs, elapsedSince);
    }

    fn loadNodeParent(self: *HBCIndex, txn: anytype, node_id: u64) !u64 {
        return try vectorindex_hbc_index.loadNodeParent(self, txn, node_id);
    }

    // ========================================================================
    // Vector storage
    // ========================================================================

    /// Store a raw vector by ID.
    pub fn putVector(self: *HBCIndex, txn: anytype, vector_id: u64, vector_data: []const f32) !void {
        try vectorindex_hbc_index.putVector(self, txn, vector_id, vector_data);
    }

    /// Load a raw vector by ID. Caller must free the returned slice.
    pub fn getVector(self: *HBCIndex, txn: anytype, vector_id: u64) ![]f32 {
        return vectorindex_hbc_index.getVector(self, txn, vector_id) catch |err| {
            if (!isNotFound(err)) return err;
            return try self.loadExternalVector(txn, vector_id);
        };
    }

    /// Load a raw vector into caller-provided scratch storage and return the populated view.
    pub fn getVectorInto(self: *HBCIndex, txn: anytype, vector_id: u64, scratch: []f32) ![]const f32 {
        if (builtin.is_test) {
            if (test_get_vector_view_or_scratch_hook) |hook| hook(test_get_vector_view_or_scratch_ctx, self, vector_id);
        }
        return vectorindex_hbc_index.getVectorInto(self, txn, vector_id, scratch) catch |err| {
            if (!isNotFound(err)) return err;
            if (self.bypass_external_vector_cache) {
                return try self.loadExternalVectorIntoScratch(txn, vector_id, scratch);
            }
            return try self.loadExternalVectorCachedIntoScratch(txn, vector_id, scratch);
        };
    }

    pub fn getVectorIntoUncached(self: *HBCIndex, txn: anytype, vector_id: u64, scratch: []f32) ![]const f32 {
        return vectorindex_hbc_index.getVectorIntoUncached(self, txn, vector_id, scratch) catch |err| {
            if (!isNotFound(err)) return err;
            return try self.loadExternalVectorIntoScratchUncached(txn, vector_id, scratch);
        };
    }

    pub fn getVectorViewOrScratch(self: *HBCIndex, txn: anytype, vector_id: u64, scratch: []f32) ![]const f32 {
        return try self.getVectorInto(txn, vector_id, scratch);
    }

    pub fn notifyVectorViewLoadForTest(self: *HBCIndex, vector_id: u64) void {
        if (builtin.is_test) {
            if (test_get_vector_view_or_scratch_hook) |hook| hook(test_get_vector_view_or_scratch_ctx, self, vector_id);
        }
    }

    pub fn getVectorViewOrScratchWithCursor(self: *HBCIndex, cursor: *vectorindex_store.Cursor, vector_id: u64, scratch: []f32) ![]const f32 {
        if (builtin.is_test) {
            if (test_get_vector_view_or_scratch_hook) |hook| hook(test_get_vector_view_or_scratch_ctx, self, vector_id);
        }
        return try vectorindex_hbc_index.getVectorViewOrScratchWithCursor(self, cursor, vector_id, scratch);
    }

    pub fn getExternalVectorViewsSortedWithScratch(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        vector_views: [][]const f32,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        scratch: []f32,
        batch_scratch: []f32,
    ) !bool {
        return self.getExternalVectorViewsSortedWithScratchCachePolicy(
            txn,
            vector_ids,
            vector_views,
            lookup_storage,
            key_views_storage,
            values_storage,
            scratch,
            batch_scratch,
            true,
        );
    }

    pub fn getExternalVectorViewsSortedWithScratchUncached(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        vector_views: [][]const f32,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        scratch: []f32,
        batch_scratch: []f32,
    ) !bool {
        return self.getExternalVectorViewsSortedWithScratchCachePolicy(
            txn,
            vector_ids,
            vector_views,
            lookup_storage,
            key_views_storage,
            values_storage,
            scratch,
            batch_scratch,
            false,
        );
    }

    fn getExternalVectorViewsSortedWithScratchCachePolicy(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        vector_views: [][]const f32,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        scratch: []f32,
        batch_scratch: []f32,
        comptime use_cache: bool,
    ) !bool {
        const loader = self.external_vector_batch_scratch_loader orelse return false;
        const ctx = self.external_vector_ctx orelse return false;
        if (vector_views.len < vector_ids.len) return error.InvalidArgument;
        if (vector_ids.len == 0) return true;

        const metadata = try self.alloc.alloc(?[]const u8, vector_ids.len);
        defer self.alloc.free(metadata);
        if (use_cache) {
            try self.getMetadataManySortedInTxnWithScratch(
                txn,
                vector_ids,
                metadata,
                lookup_storage,
                key_views_storage,
                values_storage,
            );
        } else {
            try self.getMetadataManySortedInTxnWithScratchUncached(
                txn,
                vector_ids,
                metadata,
                lookup_storage,
                key_views_storage,
                values_storage,
            );
        }
        loader(ctx, vector_ids, metadata, vector_views[0..vector_ids.len], batch_scratch, scratch.len) catch |err| switch (err) {
            error.Unsupported => return false,
            else => return err,
        };
        return true;
    }

    fn transformExternalVectorForMatrix(index: *HBCIndex, original: []const f32, transformed: []f32) []const f32 {
        return index.transformVector(original, transformed);
    }

    pub fn loadExternalVectorsTransformedIntoMatrix(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        matrix_positions: []const usize,
        matrix: []f32,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        scratch: []f32,
    ) !bool {
        if (self.bulk_split_vector_workspace.active) {
            return try self.loadExternalVectorsTransformedIntoMatrixWithBulkSplitWorkspace(
                txn,
                vector_ids,
                matrix_positions,
                matrix,
                lookup_storage,
                key_views_storage,
                values_storage,
                scratch,
            );
        }
        return try self.loadExternalVectorsTransformedIntoMatrixUncached(
            txn,
            vector_ids,
            matrix_positions,
            matrix,
            lookup_storage,
            key_views_storage,
            values_storage,
            scratch,
        );
    }

    pub fn loadPostingVectorsTransformed(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        matrix: []f32,
    ) !void {
        try vectorindex_hbc_index.loadPostingVectorsTransformed(self, txn, vector_ids, matrix);
    }

    pub fn loadPostingVectorsTransformedWithOptions(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        matrix: []f32,
        options: anytype,
    ) !void {
        try vectorindex_hbc_index.loadPostingVectorsTransformedWithOptions(
            self,
            txn,
            vector_ids,
            matrix,
            options,
        );
    }

    fn loadExternalVectorsTransformedIntoMatrixUncached(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        matrix_positions: []const usize,
        matrix: []f32,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        scratch: []f32,
    ) !bool {
        const loader = self.external_vector_batch_transformed_matrix_loader orelse return false;
        const ctx = self.external_vector_ctx orelse return false;
        if (vector_ids.len != matrix_positions.len) return error.InvalidArgument;
        if (vector_ids.len == 0) return true;

        const metadata = try self.alloc.alloc(?[]const u8, vector_ids.len);
        defer self.alloc.free(metadata);
        try self.getMetadataManySortedInTxnWithScratchUncached(
            txn,
            vector_ids,
            metadata,
            lookup_storage,
            key_views_storage,
            values_storage,
        );
        loader(
            ctx,
            vector_ids,
            metadata,
            matrix_positions,
            matrix,
            scratch,
            self.config.dims,
            self,
            transformExternalVectorForMatrix,
        ) catch |err| switch (err) {
            error.Unsupported => return false,
            else => return err,
        };
        return true;
    }

    fn loadExternalVectorsTransformedIntoMatrixWithBulkSplitWorkspace(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        matrix_positions: []const usize,
        matrix: []f32,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        scratch: []f32,
    ) !bool {
        if (vector_ids.len != matrix_positions.len) return error.InvalidArgument;
        if (vector_ids.len == 0) return true;

        var missing_ids = try self.alloc.alloc(u64, vector_ids.len);
        defer self.alloc.free(missing_ids);
        var missing_positions = try self.alloc.alloc(usize, vector_ids.len);
        defer self.alloc.free(missing_positions);

        var missing_count: usize = 0;
        for (vector_ids, matrix_positions) |vector_id, matrix_position| {
            const offset = std.math.mul(usize, matrix_position, self.config.dims) catch return error.BufferTooSmall;
            if (offset + self.config.dims > matrix.len) return error.BufferTooSmall;
            if (self.bulkSplitVectorWorkspaceLookup(vector_id, matrix[offset .. offset + self.config.dims])) {
                continue;
            }
            missing_ids[missing_count] = vector_id;
            missing_positions[missing_count] = matrix_position;
            missing_count += 1;
        }

        if (missing_count == 0) return true;
        const loaded = try self.loadExternalVectorsTransformedIntoMatrixUncached(
            txn,
            missing_ids[0..missing_count],
            missing_positions[0..missing_count],
            matrix,
            lookup_storage[0..missing_count],
            key_views_storage[0..missing_count],
            values_storage[0..missing_count],
            scratch,
        );
        if (!loaded) return false;

        for (missing_ids[0..missing_count], missing_positions[0..missing_count]) |vector_id, matrix_position| {
            const offset = std.math.mul(usize, matrix_position, self.config.dims) catch return error.BufferTooSmall;
            if (offset + self.config.dims > matrix.len) return error.BufferTooSmall;
            self.bulkSplitVectorWorkspaceAdmit(vector_id, matrix[offset .. offset + self.config.dims]);
        }
        return true;
    }

    pub fn scoreExternalRerankVectorsSortedWithScratch(
        self: *HBCIndex,
        txn: anytype,
        ranked_items: []const ApproxSearchResult,
        rerank_positions: []const usize,
        query: []const f32,
        query_measure: f32,
        distances: []f32,
        vector_id_storage: []u64,
        metadata_storage: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        batch_scratch: []f32,
        miss_distance_storage: []f32,
        profile: ?*SearchProfile,
    ) !bool {
        return self.scoreExternalRerankVectorsSortedWithScratchCachePolicy(
            txn,
            ranked_items,
            rerank_positions,
            query,
            query_measure,
            distances,
            vector_id_storage,
            metadata_storage,
            lookup_storage,
            key_views_storage,
            values_storage,
            batch_scratch,
            miss_distance_storage,
            profile,
            true,
        );
    }

    pub fn scoreExternalRerankVectorsSortedWithScratchUncached(
        self: *HBCIndex,
        txn: anytype,
        ranked_items: []const ApproxSearchResult,
        rerank_positions: []const usize,
        query: []const f32,
        query_measure: f32,
        distances: []f32,
        vector_id_storage: []u64,
        metadata_storage: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        batch_scratch: []f32,
        miss_distance_storage: []f32,
        profile: ?*SearchProfile,
    ) !bool {
        return self.scoreExternalRerankVectorsSortedWithScratchCachePolicy(
            txn,
            ranked_items,
            rerank_positions,
            query,
            query_measure,
            distances,
            vector_id_storage,
            metadata_storage,
            lookup_storage,
            key_views_storage,
            values_storage,
            batch_scratch,
            miss_distance_storage,
            profile,
            false,
        );
    }

    fn scoreExternalRerankVectorsSortedWithScratchCachePolicy(
        self: *HBCIndex,
        txn: anytype,
        ranked_items: []const ApproxSearchResult,
        rerank_positions: []const usize,
        query: []const f32,
        query_measure: f32,
        distances: []f32,
        vector_id_storage: []u64,
        metadata_storage: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        batch_scratch: []f32,
        miss_distance_storage: []f32,
        profile: ?*SearchProfile,
        comptime use_cache: bool,
    ) !bool {
        const loader = self.external_vector_batch_distance_loader orelse return false;
        const ctx = self.external_vector_ctx orelse return false;
        if (distances.len < rerank_positions.len) return error.InvalidArgument;
        if (vector_id_storage.len < rerank_positions.len) return error.InvalidArgument;
        if (metadata_storage.len < rerank_positions.len) return error.InvalidArgument;
        if (miss_distance_storage.len < rerank_positions.len) return error.InvalidArgument;
        if (rerank_positions.len == 0) return true;

        // Probe governed decoded residency before loading vector-to-document
        // metadata. External storage only needs that metadata to construct an
        // artifact key for a true vector miss; reading it for a warm decoded
        // hit defeats the retained representation and doubles random storage
        // work at large corpus sizes.
        var miss_count: usize = 0;
        for (rerank_positions, 0..) |index, slot| {
            const vector_id = ranked_items[index].vector_id;
            distances[slot] = std.math.inf(f32);
            if (use_cache) {
                if (self.borrowCachedVector(vector_id)) |cached_handle| {
                    var handle = cached_handle;
                    defer handle.deinit();
                    const distance_start = platform_time.monotonicNs();
                    distances[slot] = vectorindex_search_runtime.exactDistanceToStoredVector(
                        self.config,
                        query,
                        query_measure,
                        handle.view(),
                    );
                    if (profile) |p| {
                        const elapsed = platform_time.monotonicNs() - distance_start;
                        p.vector_cache_hits += 1;
                        p.rerank_artifact_cache_hits += 1;
                        p.rerank_artifact_distance_ns += elapsed;
                        p.rerank_distance_ns += elapsed;
                    }
                    continue;
                }
            }
            vector_id_storage[miss_count] = vector_id;
            miss_count += 1;
        }
        if (miss_count == 0) return true;

        const vector_ids = vector_id_storage[0..miss_count];
        const metadata = metadata_storage[0..miss_count];
        if (profile) |p| p.rerank_metadata_vectors_loaded +|= @intCast(miss_count);
        const metadata_start = platform_time.monotonicNs();
        if (use_cache) {
            try self.getMetadataManySortedInTxnWithScratch(
                txn,
                vector_ids,
                metadata,
                lookup_storage,
                key_views_storage,
                values_storage,
            );
        } else {
            try self.getMetadataManySortedInTxnWithScratchUncached(
                txn,
                vector_ids,
                metadata,
                lookup_storage,
                key_views_storage,
                values_storage,
            );
        }
        if (profile) |p| p.rerank_metadata_lookup_ns += platform_time.monotonicNs() - metadata_start;
        const miss_distances = miss_distance_storage[0..miss_count];
        loader(
            ctx,
            vector_ids,
            metadata,
            query,
            query_measure,
            self.config.metric,
            miss_distances,
            batch_scratch,
            @intCast(self.config.dims),
            .{
                .artifact_keys = key_views_storage,
                .raw_values = values_storage,
            },
            profile,
        ) catch |err| switch (err) {
            error.Unsupported => return false,
            else => return err,
        };

        // The bounded batch is small (at most 128 entries) and ApproxSearch
        // results contain unique vector ids. Re-deriving each output slot here
        // avoids another request-sized positions allocation while preserving
        // the approximate-distance ordering used by the early-stop proof.
        for (vector_ids, miss_distances) |vector_id, distance| {
            for (rerank_positions, 0..) |index, slot| {
                if (ranked_items[index].vector_id != vector_id) continue;
                distances[slot] = distance;
                break;
            }
        }
        return true;
    }

    pub fn scoreExternalVectorsSortedWithScratch(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        query: []const f32,
        query_measure: f32,
        distances: []f32,
        metadata_storage: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        batch_scratch: []f32,
    ) !bool {
        return self.scoreExternalVectorsSortedWithScratchCachePolicy(
            txn,
            vector_ids,
            query,
            query_measure,
            distances,
            metadata_storage,
            lookup_storage,
            key_views_storage,
            values_storage,
            batch_scratch,
            true,
        );
    }

    pub fn scoreExternalVectorsSortedWithScratchUncached(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        query: []const f32,
        query_measure: f32,
        distances: []f32,
        metadata_storage: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        batch_scratch: []f32,
    ) !bool {
        return self.scoreExternalVectorsSortedWithScratchCachePolicy(
            txn,
            vector_ids,
            query,
            query_measure,
            distances,
            metadata_storage,
            lookup_storage,
            key_views_storage,
            values_storage,
            batch_scratch,
            false,
        );
    }

    fn scoreExternalVectorsSortedWithScratchCachePolicy(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        query: []const f32,
        query_measure: f32,
        distances: []f32,
        metadata_storage: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
        batch_scratch: []f32,
        comptime use_cache: bool,
    ) !bool {
        const loader = self.external_vector_batch_distance_loader orelse return false;
        const ctx = self.external_vector_ctx orelse return false;
        if (distances.len < vector_ids.len) return error.InvalidArgument;
        if (metadata_storage.len < vector_ids.len) return error.InvalidArgument;
        if (vector_ids.len == 0) return true;

        for (distances[0..vector_ids.len]) |*distance| distance.* = std.math.inf(f32);
        const metadata = metadata_storage[0..vector_ids.len];
        if (use_cache) {
            try self.getMetadataManySortedInTxnWithScratch(
                txn,
                vector_ids,
                metadata,
                lookup_storage,
                key_views_storage,
                values_storage,
            );
        } else {
            try self.getMetadataManySortedInTxnWithScratchUncached(
                txn,
                vector_ids,
                metadata,
                lookup_storage,
                key_views_storage,
                values_storage,
            );
        }
        loader(
            ctx,
            vector_ids,
            metadata,
            query,
            query_measure,
            self.config.metric,
            distances[0..vector_ids.len],
            batch_scratch,
            @intCast(self.config.dims),
            .{
                .artifact_keys = key_views_storage,
                .raw_values = values_storage,
            },
            null,
        ) catch |err| switch (err) {
            error.Unsupported => return false,
            else => return err,
        };
        return true;
    }

    pub fn getVectorScratch(self: *HBCIndex, txn: anytype, vector_id: u64, scratch: []f32) ![]const f32 {
        return vectorindex_hbc_index.getVectorScratch(self, txn, vector_id, scratch) catch |err| {
            if (!isNotFound(err)) return err;
            if (self.bypass_external_vector_cache and self.hasExternalVectorLoader()) {
                return try self.loadExternalVectorIntoScratch(txn, vector_id, scratch);
            }
            return try self.loadExternalVectorCachedIntoScratch(txn, vector_id, scratch);
        };
    }

    fn vectorViewFromRaw(data: []const u8, scratch: []f32) ![]const f32 {
        return try vectorindex_hbc_index.vectorViewFromRaw(data, scratch);
    }

    fn loadExternalVector(self: *HBCIndex, txn: anytype, vector_id: u64) ![]f32 {
        const loader = self.external_vector_loader orelse return error.NotFound;
        const ctx = self.external_vector_ctx orelse return error.NotFound;
        const metadata = (try self.loadMetadataRaw(txn, vector_id)) orelse return error.NotFound;
        return try loader(ctx, self.alloc, vector_id, metadata);
    }

    fn loadExternalVectorIntoScratch(self: *HBCIndex, txn: anytype, vector_id: u64, scratch: []f32) ![]const f32 {
        return try self.loadExternalVectorIntoScratchCachePolicy(txn, vector_id, scratch, true);
    }

    fn loadExternalVectorIntoScratchUncached(self: *HBCIndex, txn: anytype, vector_id: u64, scratch: []f32) ![]const f32 {
        return try self.loadExternalVectorIntoScratchCachePolicy(txn, vector_id, scratch, false);
    }

    fn loadExternalVectorIntoScratchCachePolicy(
        self: *HBCIndex,
        txn: anytype,
        vector_id: u64,
        scratch: []f32,
        comptime use_cache: bool,
    ) ![]const f32 {
        const metadata = (if (use_cache)
            try self.loadMetadataRaw(txn, vector_id)
        else
            try vectorindex_hbc_index.loadMetadataRawUncached(self, txn, vector_id, isNotFound)) orelse return error.NotFound;
        if (self.external_vector_scratch_loader) |loader| {
            const ctx = self.external_vector_ctx orelse return error.NotFound;
            return try loader(ctx, vector_id, metadata, scratch);
        }
        const loader = self.external_vector_loader orelse return error.NotFound;
        const ctx = self.external_vector_ctx orelse return error.NotFound;
        const vector = try loader(ctx, self.alloc, vector_id, metadata);
        defer self.alloc.free(vector);
        if (vector.len > scratch.len) return error.BufferTooSmall;
        @memcpy(scratch[0..vector.len], vector);
        return scratch[0..vector.len];
    }

    fn loadExternalVectorCachedIntoScratch(self: *HBCIndex, txn: anytype, vector_id: u64, scratch: []f32) ![]const f32 {
        if (self.borrowCachedVector(vector_id)) |cached_handle| {
            var handle = cached_handle;
            defer handle.deinit();
            const cached = handle.view();
            self.write_profile.external_vector_cache_hits += 1;
            if (cached.len > scratch.len) return error.BufferTooSmall;
            @memcpy(scratch[0..cached.len], cached);
            return scratch[0..cached.len];
        }
        self.write_profile.external_vector_cache_misses += 1;
        const fill_epoch = self.beginVectorCacheFill(vector_id);
        if (self.external_vector_scratch_loader != null) {
            return try self.loadExternalVectorIntoScratch(txn, vector_id, scratch);
        }
        const vector = try self.loadExternalVector(txn, vector_id);
        defer self.alloc.free(vector);
        if (vector.len > scratch.len) return error.BufferTooSmall;
        @memcpy(scratch[0..vector.len], vector);
        if (fill_epoch) |epoch| _ = try self.cacheVectorIfFillCurrent(vector_id, vector, epoch);
        return scratch[0..vector.len];
    }

    /// Store vector-to-leaf mapping.
    pub fn putVecLeaf(self: *HBCIndex, txn: anytype, vector_id: u64, leaf_id: u64) !void {
        try vectorindex_hbc_index.putVecLeaf(self, txn, vector_id, leaf_id);
    }

    /// Get which leaf a vector belongs to.
    pub fn getVecLeaf(self: *HBCIndex, txn: anytype, vector_id: u64) !u64 {
        return try vectorindex_hbc_index.getVecLeaf(self, txn, vector_id);
    }

    pub fn loadMetadataRaw(self: *HBCIndex, txn: anytype, vector_id: u64) !?[]const u8 {
        return try vectorindex_hbc_index.loadMetadataRaw(self, txn, vector_id, isNotFound);
    }

    fn putMetadata(self: *HBCIndex, txn: anytype, vector_id: u64, metadata: []const u8) !void {
        try vectorindex_hbc_index.putMetadata(self, txn, vector_id, metadata);
    }

    pub fn getMetadata(self: *HBCIndex, vector_id: u64) !?[]u8 {
        return try vectorindex_hbc_index.getMetadata(self, vector_id);
    }

    pub fn getMetadataInTxn(self: *HBCIndex, txn: anytype, vector_id: u64) !?[]const u8 {
        return try vectorindex_hbc_index.getMetadataInTxn(self, txn, vector_id, isNotFound);
    }

    pub fn getMetadataManySortedInTxn(self: *HBCIndex, txn: anytype, vector_ids: []const u64, out_metadata: []?[]const u8) !void {
        return try vectorindex_hbc_index.getMetadataManySortedInTxn(self, txn, vector_ids, out_metadata);
    }

    pub fn getMetadataManySortedInTxnUncached(self: *HBCIndex, txn: anytype, vector_ids: []const u64, out_metadata: []?[]const u8) !void {
        return try vectorindex_hbc_index.getMetadataManySortedInTxnUncached(self, txn, vector_ids, out_metadata);
    }

    pub fn getMetadataManySortedInTxnWithScratch(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        out_metadata: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
    ) !void {
        return try vectorindex_hbc_index.getMetadataManySortedInTxnWithScratch(
            self,
            txn,
            vector_ids,
            out_metadata,
            lookup_storage,
            key_views_storage,
            values_storage,
        );
    }

    pub fn getMetadataManySortedInTxnWithScratchUncached(
        self: *HBCIndex,
        txn: anytype,
        vector_ids: []const u64,
        out_metadata: []?[]const u8,
        lookup_storage: []FixedKeyLookup,
        key_views_storage: [][]const u8,
        values_storage: []?[]const u8,
    ) !void {
        return try vectorindex_hbc_index.getMetadataManySortedInTxnWithScratchUncached(
            self,
            txn,
            vector_ids,
            out_metadata,
            lookup_storage,
            key_views_storage,
            values_storage,
        );
    }

    fn loadNodeSplitRange(self: *HBCIndex, txn: anytype, node_id: u64) !?NodeSplitRange {
        return try vectorindex_hbc_index.loadNodeSplitRange(self, txn, node_id, isNotFound);
    }

    fn computeNodeSplitRange(self: *HBCIndex, txn: anytype, node: *const Node) !?NodeSplitRange {
        return try vectorindex_hbc_index.computeNodeSplitRange(self, txn, node, isNotFound);
    }

    fn saveNodeSplitRange(self: *HBCIndex, txn: anytype, node: *const Node) !void {
        try vectorindex_hbc_index.saveNodeSplitRange(self, txn, node, isNotFound);
    }

    pub fn putNodeSplitRange(self: *HBCIndex, txn: anytype, node_id: u64, range: ?*const NodeSplitRange) !void {
        try vectorindex_hbc_index.putNodeSplitRange(self, txn, node_id, range, isNotFound);
    }

    pub fn getNodeSplitRange(self: *HBCIndex, node_id: u64) !?NodeSplitRange {
        return try vectorindex_hbc_index.getNodeSplitRange(self, node_id, isNotFound);
    }

    fn classifyNodeForSplitInTxn(self: *HBCIndex, txn: anytype, node_id: u64, split_key: []const u8) !NodeSplitClass {
        return try vectorindex_hbc_index.classifyNodeForSplitInTxn(self, txn, node_id, split_key, isNotFound);
    }

    pub fn classifyNodeForSplit(self: *HBCIndex, node_id: u64, split_key: []const u8) !NodeSplitClass {
        return try vectorindex_hbc_index.classifyNodeForSplit(self, node_id, split_key, isNotFound);
    }

    pub fn splitPlanningStats(self: *HBCIndex, split_key: []const u8) !SplitPlanningStats {
        var txn = try self.beginRuntimeReadTxn();
        defer txn.abort();

        var pending = std.ArrayListUnmanaged(u64).empty;
        defer pending.deinit(self.alloc);
        try pending.append(self.alloc, self.metadata.root_node);

        var planning = SplitPlanningStats{};
        while (pending.pop()) |node_id| {
            var node = try self.loadNode(&txn, node_id);
            defer node.deinit(self.alloc);

            if (node.is_leaf) {
                planning.leaves += 1;
            } else {
                planning.internal += 1;
                try pending.appendSlice(self.alloc, node.children);
            }

            switch (try self.classifyNodeForSplitInTxn(&txn, node_id, split_key)) {
                .left_only => planning.left_only += 1,
                .right_only => planning.right_only += 1,
                .mixed => planning.mixed += 1,
                .unknown => planning.unknown += 1,
            }
        }
        return planning;
    }

    pub fn buildSplitReusePlan(self: *HBCIndex, split_key: []const u8) !SplitReusePlan {
        var txn = try self.beginRuntimeReadTxn();
        defer txn.abort();

        var pending = std.ArrayListUnmanaged(u64).empty;
        defer pending.deinit(self.alloc);
        try pending.append(self.alloc, self.metadata.root_node);

        var right_only_roots = std.ArrayListUnmanaged(u64).empty;
        errdefer right_only_roots.deinit(self.alloc);
        var mixed_leaves = std.ArrayListUnmanaged(u64).empty;
        errdefer mixed_leaves.deinit(self.alloc);

        while (pending.pop()) |node_id| {
            const class = try self.classifyNodeForSplitInTxn(&txn, node_id, split_key);
            switch (class) {
                .left_only => {},
                .right_only => try right_only_roots.append(self.alloc, node_id),
                .mixed => {
                    var node = try self.loadNode(&txn, node_id);
                    defer node.deinit(self.alloc);
                    if (node.is_leaf) {
                        try mixed_leaves.append(self.alloc, node_id);
                    } else {
                        try pending.appendSlice(self.alloc, node.children);
                    }
                },
                .unknown => {
                    var node = try self.loadNode(&txn, node_id);
                    defer node.deinit(self.alloc);
                    if (node.is_leaf) {
                        try mixed_leaves.append(self.alloc, node_id);
                    } else {
                        try pending.appendSlice(self.alloc, node.children);
                    }
                },
            }
        }

        return .{
            .right_only_roots = try right_only_roots.toOwnedSlice(self.alloc),
            .mixed_leaves = try mixed_leaves.toOwnedSlice(self.alloc),
        };
    }

    pub fn estimateSplitRebuildWork(self: *HBCIndex, split_key: []const u8) !SplitRebuildWork {
        var txn = try self.beginRuntimeReadTxn();
        defer txn.abort();

        var plan = try self.buildSplitReusePlan(split_key);
        defer plan.deinit(self.alloc);

        var work = SplitRebuildWork{
            .right_only_roots = plan.right_only_roots.len,
            .mixed_leaves = plan.mixed_leaves.len,
        };

        for (plan.right_only_roots) |node_id| {
            work.right_only_members += try self.subtreeMemberCount(&txn, node_id);
        }
        for (plan.mixed_leaves) |node_id| {
            work.mixed_right_members += try self.mixedLeafRightMemberCount(&txn, node_id, split_key);
        }
        return work;
    }

    pub fn collectSplitMembers(self: *HBCIndex, split_key: []const u8) !SplitMemberPlan {
        var txn = try self.beginRuntimeReadTxn();
        defer txn.abort();

        var plan = try self.buildSplitReusePlan(split_key);
        defer plan.deinit(self.alloc);

        var right_only_members = std.ArrayListUnmanaged(u64).empty;
        errdefer right_only_members.deinit(self.alloc);
        var mixed_right_members = std.ArrayListUnmanaged(u64).empty;
        errdefer mixed_right_members.deinit(self.alloc);

        for (plan.right_only_roots) |node_id| {
            try self.appendSubtreeMembers(&txn, node_id, &right_only_members);
        }
        for (plan.mixed_leaves) |node_id| {
            try self.appendMixedLeafRightMembers(&txn, node_id, split_key, &mixed_right_members);
        }

        return .{
            .right_only_members = try right_only_members.toOwnedSlice(self.alloc),
            .mixed_right_members = try mixed_right_members.toOwnedSlice(self.alloc),
        };
    }

    fn subtreeMemberCount(self: *HBCIndex, txn: anytype, node_id: u64) !usize {
        var node = try self.loadNode(txn, node_id);
        defer node.deinit(self.alloc);
        if (node.is_leaf) return node.members.len;

        var total: usize = 0;
        for (node.children) |child_id| {
            total += try self.subtreeMemberCount(txn, child_id);
        }
        return total;
    }

    fn mixedLeafRightMemberCount(self: *HBCIndex, txn: anytype, node_id: u64, split_key: []const u8) !usize {
        var node = try self.loadNode(txn, node_id);
        defer node.deinit(self.alloc);
        if (!node.is_leaf) return error.ExpectedLeaf;

        var count: usize = 0;
        for (node.members) |member_id| {
            const metadata = (try self.loadMetadataRaw(txn, member_id)) orelse continue;
            if (std.mem.order(u8, metadata, split_key) != .lt) count += 1;
        }
        return count;
    }

    fn appendSubtreeMembers(
        self: *HBCIndex,
        txn: anytype,
        node_id: u64,
        out: *std.ArrayListUnmanaged(u64),
    ) !void {
        var node = try self.loadNode(txn, node_id);
        defer node.deinit(self.alloc);
        if (node.is_leaf) {
            try out.appendSlice(self.alloc, node.members);
            return;
        }
        for (node.children) |child_id| {
            try self.appendSubtreeMembers(txn, child_id, out);
        }
    }

    fn appendMixedLeafRightMembers(
        self: *HBCIndex,
        txn: anytype,
        node_id: u64,
        split_key: []const u8,
        out: *std.ArrayListUnmanaged(u64),
    ) !void {
        var node = try self.loadNode(txn, node_id);
        defer node.deinit(self.alloc);
        if (!node.is_leaf) return error.ExpectedLeaf;

        for (node.members) |member_id| {
            const metadata = (try self.loadMetadataRaw(txn, member_id)) orelse continue;
            if (std.mem.order(u8, metadata, split_key) != .lt) {
                try out.append(self.alloc, member_id);
            }
        }
    }

    // ========================================================================
    // Quantized vector set I/O
    // ========================================================================

    pub fn saveQuantized(self: *HBCIndex, txn: anytype, node_id: u64, qs: *const QuantizedSet) !void {
        try vectorindex_hbc_index.saveQuantized(self, txn, node_id, qs, nowNs, elapsedSince);
    }

    pub fn putQuantizedCached(self: *HBCIndex, txn: anytype, node_id: u64, qs: *const QuantizedSet) !void {
        try vectorindex_hbc_index.putQuantizedCached(self, txn, node_id, qs, nowNs, elapsedSince);
    }

    pub fn loadQuantized(self: *HBCIndex, txn: anytype, node_id: u64, is_root: bool, expected_count: usize) !QuantizedSet {
        return try vectorindex_hbc_index.loadQuantized(self, txn, node_id, is_root, expected_count, isNotFound);
    }

    pub fn getQuantized(self: *HBCIndex, txn: anytype, node_id: u64, is_root: bool, expected_count: usize) !?QuantizedRead {
        return try vectorindex_hbc_index.getQuantized(self, txn, node_id, is_root, expected_count, isNotFound);
    }

    pub fn getQuantizedProfiled(self: *HBCIndex, txn: anytype, node_id: u64, is_root: bool, expected_count: usize, profile: *SearchProfile) !?QuantizedRead {
        return try vectorindex_hbc_index.getQuantizedProfiled(self, txn, node_id, is_root, expected_count, profile, isNotFound, nowNs, elapsedSince);
    }

    pub fn estimateQuantizedDistances(
        self: *HBCIndex,
        qs: *const QuantizedSet,
        query: []const f32,
        query_measure: f32,
        distances: []f32,
        error_bounds: []f32,
        scratch: *quantizer_mod.RaBitQuantizer.EstimateScratch,
    ) !void {
        try vectorindex_hbc_index.estimateQuantizedDistances(self, qs, query, query_measure, distances, error_bounds, scratch);
    }

    fn refreshAncestorSplitRanges(self: *HBCIndex, txn: anytype, parent_id: u64) !void {
        try vectorindex_hbc_index.refreshAncestorSplitRanges(self, txn, parent_id);
    }

    fn extendAncestorSplitRanges(
        self: *HBCIndex,
        txn: anytype,
        parent_id: u64,
        child_range: *const NodeSplitRange,
    ) !void {
        try vectorindex_hbc_index.extendAncestorSplitRanges(self, txn, parent_id, child_range);
    }

    pub fn refreshQuantized(self: *HBCIndex, txn: anytype, node: *const Node) !void {
        try vectorindex_hbc_index.refreshQuantized(self, txn, node, nowNs, elapsedSince);
    }

    pub fn refreshQuantizedWithOptions(self: *HBCIndex, txn: anytype, node: *const Node, options: BatchInsertOptions) !void {
        try vectorindex_hbc_index.refreshQuantizedWithOptions(self, txn, node, options, nowNs, elapsedSince);
    }

    // ========================================================================
    // Balanced binary k-means++ splitting
    // ========================================================================

    /// Split a set of vectors into two groups using balanced binary k-means++.
    /// Returns (left_centroid, left_ids, right_centroid, right_ids).
    pub fn splitVectorSet(
        self: *HBCIndex,
        vectors: *const vec.Set,
        ids: []const u64,
    ) !SplitResult {
        return try vectorindex_hbc_index.splitVectorSet(self, vectors, ids);
    }

    pub fn getHilbert(self: *HBCIndex) !*vec.Hilbert {
        if (self.hilbert == null) {
            self.hilbert = try vec.Hilbert.init(self.alloc, self.config.dims);
        }
        return &self.hilbert.?;
    }

    pub fn minLeafOccupancy(self: *const HBCIndex) usize {
        return vectorindex_hbc_index.minLeafOccupancy(self);
    }

    const BatchVectorContext = struct {
        items: []const BatchInsertItem,
        map: std.AutoHashMapUnmanaged(u64, usize) = .empty,
        accounted_bytes: u64 = 0,

        fn init(index: *HBCIndex, items: []const BatchInsertItem) !@This() {
            var self: @This() = .{ .items = items };
            errdefer self.deinit(index);
            try self.map.ensureTotalCapacity(index.alloc, @intCast(items.len));
            for (items, 0..) |item, item_index| {
                try self.map.put(index.alloc, item.vector_id, item_index);
            }
            self.accounted_bytes = @as(u64, @intCast(self.map.capacity())) * (@sizeOf(u64) + @sizeOf(usize));
            index.addApplyWorkspaceBytes(self.accounted_bytes);
            return self;
        }

        fn deinit(self: *@This(), index: *HBCIndex) void {
            index.releaseApplyWorkspaceBytes(self.accounted_bytes);
            self.map.deinit(index.alloc);
            self.* = undefined;
        }

        fn lookup(ptr: *const anyopaque, vector_id: u64) ?[]const f32 {
            const self: *const @This() = @ptrCast(@alignCast(ptr));
            const item_index = self.map.get(vector_id) orelse return null;
            return self.items[item_index].vector;
        }

        fn erased(self: *const @This()) vectorindex_hbc_runtime.BatchVectorLookup {
            return .{
                .ptr = self,
                .getFn = lookup,
            };
        }
    };

    fn optionsWithBatchVectors(
        options: BatchInsertOptions,
        batch_vectors: vectorindex_hbc_runtime.BatchVectorLookup,
    ) BatchInsertOptions {
        var next = options;
        if (next.batch_vectors == null) next.batch_vectors = batch_vectors;
        return next;
    }

    fn recomputeLeafCentroid(self: *HBCIndex, txn: anytype, leaf: *Node) !void {
        try vectorindex_hbc_index.recomputeLeafCentroid(self, txn, leaf);
    }

    pub fn recomputeInternalCentroid(self: *HBCIndex, txn: anytype, node: *Node) !void {
        try vectorindex_hbc_index.recomputeInternalCentroid(self, txn, node);
    }

    pub fn collapseSingleChildParents(self: *HBCIndex, txn: anytype, start_node_id: u64) !void {
        try vectorindex_hbc_index.collapseSingleChildParents(self, txn, start_node_id);
    }

    // ========================================================================
    // Insert
    // ========================================================================

    /// Insert a vector into the index.
    pub fn insert(self: *HBCIndex, vector_id: u64, vector_data: []const f32) !void {
        try vectorindex_hbc_index.insert(self, vector_id, vector_data, nowNs, elapsedSince);
    }

    pub fn insertWithMetadata(self: *HBCIndex, vector_id: u64, vector_data: []const f32, metadata_value: []const u8) !void {
        try vectorindex_hbc_index.insertWithMetadata(self, vector_id, vector_data, metadata_value, nowNs, elapsedSince);
    }

    pub fn batchInsertWithMetadata(self: *HBCIndex, items: []const BatchInsertItem) !void {
        try vectorindex_hbc_index.batchInsertWithMetadata(self, items, nowNs, elapsedSince);
    }

    pub fn batchApply(self: *HBCIndex, writes: []const BatchInsertItem, deletes: []const u64) !void {
        try vectorindex_hbc_index.batchApply(self, writes, deletes, nowNs, elapsedSince);
    }

    pub fn batchApplyOptions(
        self: *HBCIndex,
        writes: []const BatchInsertItem,
        deletes: []const u64,
        options: BatchInsertOptions,
    ) !void {
        if (writes.len == 0 or options.batch_vectors != null) {
            try vectorindex_hbc_index.batchApplyOptions(self, writes, deletes, options, nowNs, elapsedSince);
            return;
        }
        var batch_vectors = try BatchVectorContext.init(self, writes);
        defer batch_vectors.deinit(self);
        try vectorindex_hbc_index.batchApplyOptions(self, writes, deletes, optionsWithBatchVectors(options, batch_vectors.erased()), nowNs, elapsedSince);
    }

    pub fn batchInsertWithMetadataOptions(self: *HBCIndex, items: []const BatchInsertItem, options: BatchInsertOptions) !void {
        if (items.len == 0 or options.batch_vectors != null) {
            try vectorindex_hbc_index.batchInsertWithMetadataOptions(self, items, options, nowNs, elapsedSince);
            return;
        }
        var batch_vectors = try BatchVectorContext.init(self, items);
        defer batch_vectors.deinit(self);
        try vectorindex_hbc_index.batchInsertWithMetadataOptions(self, items, optionsWithBatchVectors(options, batch_vectors.erased()), nowNs, elapsedSince);
    }

    pub fn batchInsertWithMetadataTxn(self: *HBCIndex, txn: anytype, items: []const BatchInsertItem) !void {
        try vectorindex_hbc_index.batchInsertWithMetadataTxn(self, txn, items);
    }

    pub fn bulkBuildWithMetadata(self: *HBCIndex, items: []const BatchInsertItem) !void {
        try vectorindex_hbc_index.bulkBuildWithMetadata(self, items, nowNs, elapsedSince);
    }

    pub fn bulkBuildWithMetadataOptions(self: *HBCIndex, items: []const BatchInsertItem, options: BulkBuildOptions) !void {
        try vectorindex_hbc_index.bulkBuildWithMetadataOptions(self, items, options, nowNs, elapsedSince);
    }

    pub fn bulkBuildWithMetadataTxn(self: *HBCIndex, txn: anytype, items: []const BatchInsertItem) !void {
        try vectorindex_hbc_index.bulkBuildWithMetadataTxn(self, txn, items);
    }

    pub fn bulkBuildPreparedInputsTxn(self: *HBCIndex, txn: anytype, inputs: []const PreparedBulkBuildInput) !void {
        try vectorindex_hbc_index.bulkBuildPreparedInputsTxn(self, txn, inputs);
    }

    pub fn bulkBuildPreparedInputsTxnOptions(
        self: *HBCIndex,
        txn: anytype,
        inputs: []const PreparedBulkBuildInput,
        options: BulkBuildOptions,
    ) !void {
        try vectorindex_hbc_index.bulkBuildPreparedInputsTxnOptions(self, txn, inputs, options, nowNs, elapsedSince);
    }

    pub fn bulkBuildWithMetadataTxnOptions(
        self: *HBCIndex,
        txn: anytype,
        items: []const BatchInsertItem,
        options: BulkBuildOptions,
    ) !void {
        try vectorindex_hbc_index.bulkBuildWithMetadataTxnOptions(self, txn, items, options, nowNs, elapsedSince);
    }

    pub fn buildBulkRecursiveFromInputs(
        self: *HBCIndex,
        txn: anytype,
        inputs: []const PreparedBulkBuildInput,
    ) !BuiltBulkNode {
        return try vectorindex_hbc_index.buildBulkRecursiveFromInputs(self, txn, inputs);
    }

    pub fn batchInsertWithMetadataTxnOptions(
        self: *HBCIndex,
        txn: anytype,
        items: []const BatchInsertItem,
        options: BatchInsertOptions,
    ) !void {
        try vectorindex_hbc_index.batchInsertWithMetadataTxnOptions(self, txn, items, options);
    }

    pub fn prepareEmptyBulkBuild(self: *HBCIndex, txn: anytype, items: []const BatchInsertItem) !void {
        if (self.metadata.active_count != 0) return error.IndexNotEmpty;

        var seen = std.AutoHashMapUnmanaged(u64, void).empty;
        defer seen.deinit(self.alloc);
        try seen.ensureTotalCapacity(self.alloc, @intCast(items.len));
        for (items) |item| {
            if (seen.contains(item.vector_id)) return error.DuplicateVectorId;
            seen.putAssumeCapacity(item.vector_id, {});
        }

        if (self.metadata.root_node != 0) {
            self.deleteNode(txn, self.metadata.root_node) catch |err| {
                if (isNotFound(err)) {} else return err;
            };
        }
        self.clearNodeCache();
        self.clearQuantizedCache();
        self.clearVectorCache();
        self.clearMetadataCache();
        self.metadata.root_node = 0;
        self.metadata.node_count = 0;
        self.metadata.active_count = 0;
    }

    pub fn prepareEmptyPreparedBulkBuild(self: *HBCIndex, txn: anytype, inputs: []const PreparedBulkBuildInput) !void {
        if (self.metadata.active_count != 0) return error.IndexNotEmpty;

        var seen = std.AutoHashMapUnmanaged(u64, void).empty;
        defer seen.deinit(self.alloc);
        try seen.ensureTotalCapacity(self.alloc, @intCast(inputs.len));
        for (inputs) |input| {
            if (seen.contains(input.vector_id)) return error.DuplicateVectorId;
            seen.putAssumeCapacity(input.vector_id, {});
        }

        if (self.metadata.root_node != 0) {
            self.deleteNode(txn, self.metadata.root_node) catch |err| {
                if (isNotFound(err)) {} else return err;
            };
        }
        self.clearNodeCache();
        self.clearQuantizedCache();
        self.clearVectorCache();
        self.clearMetadataCache();
        self.metadata.root_node = 0;
        self.metadata.node_count = 0;
        self.metadata.active_count = 0;
    }

    pub fn buildBulkHilbertSeeded(
        self: *HBCIndex,
        txn: anytype,
        inputs: []const PreparedBulkBuildInput,
    ) !BuiltBulkNode {
        return try vectorindex_hbc_index.buildBulkHilbertSeeded(self, txn, inputs);
    }

    pub fn buildBulkDocKeySeeded(
        self: *HBCIndex,
        txn: anytype,
        inputs: []const PreparedBulkBuildInput,
    ) !BuiltBulkNode {
        return try vectorindex_hbc_index.buildBulkDocKeySeeded(self, txn, inputs);
    }

    pub fn buildBulkKmeansFromInputs(
        self: *HBCIndex,
        txn: anytype,
        inputs: []const PreparedBulkBuildInput,
    ) !BuiltBulkNode {
        return try vectorindex_hbc_index.buildBulkKmeansFromInputs(self, txn, inputs);
    }

    pub fn ingestMembersFrom(self: *HBCIndex, src: *HBCIndex, member_ids: []const u64, batch_size: usize) !void {
        try vectorindex_hbc_transfer.ingestMembersFrom(self, src, member_ids, batch_size);
    }

    pub fn bulkBuildMembersFrom(self: *HBCIndex, src: *HBCIndex, member_ids: []const u64) !void {
        try vectorindex_hbc_transfer.bulkBuildMembersFrom(self, src, member_ids);
    }

    pub fn streamSplitMembers(
        self: *HBCIndex,
        split_key: []const u8,
        batch_size: usize,
        ctx: anytype,
        comptime consume: fn (@TypeOf(ctx), []const BatchInsertItem) anyerror!void,
    ) !usize {
        return try vectorindex_hbc_transfer.streamSplitMembers(self, split_key, batch_size, ctx, consume);
    }

    fn insertWithMetadataTxn(
        self: *HBCIndex,
        txn: anytype,
        vector_id: u64,
        vector_data: []const f32,
        metadata_value: []const u8,
        transformed_vector: []f32,
    ) !void {
        try vectorindex_hbc_index.insertWithMetadataTxn(
            self,
            txn,
            vector_id,
            vector_data,
            metadata_value,
            transformed_vector,
            nowNs,
            elapsedSince,
        );
    }

    pub fn insertWithMetadataTxnOptions(
        self: *HBCIndex,
        txn: anytype,
        vector_id: u64,
        vector_data: []const f32,
        pretransformed_vector: ?[]const f32,
        metadata_value: []const u8,
        transformed_vector: []f32,
        options: BatchInsertOptions,
    ) !void {
        try vectorindex_hbc_index.insertWithMetadataTxnOptions(
            self,
            txn,
            vector_id,
            vector_data,
            pretransformed_vector,
            metadata_value,
            transformed_vector,
            options,
            nowNs,
            elapsedSince,
        );
    }

    fn removeFromLeaf(self: *HBCIndex, txn: anytype, leaf_id: u64, vector_id: u64) !void {
        try vectorindex_hbc_index.removeFromLeaf(self, txn, leaf_id, vector_id);
    }

    /// Find the best leaf for a vector by traversing from root.
    fn findLeaf(self: *HBCIndex, txn: anytype, node_id: u64, query: []const f32) !u64 {
        return self.findLeafWithOptions(txn, node_id, query, true);
    }

    pub fn findLeafWithOptions(
        self: *HBCIndex,
        txn: anytype,
        node_id: u64,
        query: []const f32,
        allow_quantized: bool,
    ) !u64 {
        var handle = try self.acquireRoutingScratch();
        defer self.releaseRoutingScratch(&handle);
        return try self.findLeafWithOptionsScratch(txn, node_id, query, allow_quantized, &handle.scratch);
    }

    pub fn findLeafWithOptionsScratch(
        self: *HBCIndex,
        txn: anytype,
        node_id: u64,
        query: []const f32,
        allow_quantized: bool,
        scratch: *RoutingScratch,
    ) !u64 {
        try self.bindTxnLike(txn);
        var node = try self.loadNode(txn, node_id);
        defer node.deinit(self.alloc);
        if (node.is_leaf) return node_id;

        const n_children = node.children.len;
        try scratch.ensureCapacity(self.alloc, n_children);
        @memcpy(scratch.child_ids[0..n_children], node.children);
        const child_ids = scratch.child_ids[0..n_children];

        const query_measure: f32 = switch (self.config.metric) {
            .l2_squared => vec.dot(query, query),
            .cosine => vec.norm(query),
            .inner_product => 0,
        };
        var best_child: u64 = 0;
        var best_dist: f32 = std.math.inf(f32);

        if (allow_quantized and self.config.use_quantization) quantized_route: {
            var borrowed_quantized: ?BorrowedQuantized = self.borrowCachedQuantized(node_id);
            defer if (borrowed_quantized) |*borrowed| borrowed.deinit();

            var owned_quantized: ?QuantizedSet = null;
            defer if (owned_quantized) |*owned| owned.deinit(self.alloc);

            const quantized: *const QuantizedSet = if (borrowed_quantized) |*borrowed|
                borrowed.ptr()
            else blk: {
                owned_quantized = self.loadQuantized(txn, node_id, node.parent == 0, n_children) catch |err| {
                    if (isNotFound(err) or err == error.Corrupted) break :quantized_route;
                    return err;
                };
                break :blk &owned_quantized.?;
            };

            self.estimateQuantizedDistances(
                quantized,
                query,
                query_measure,
                scratch.distances[0..n_children],
                scratch.error_bounds[0..n_children],
                &scratch.estimate,
            ) catch {
                self.invalidateQuantizedCache(node_id);
                break :quantized_route;
            };
            for (child_ids, 0..) |child_id, i| {
                const dist = scratch.distances[i];
                if (dist < best_dist) {
                    best_dist = dist;
                    best_child = child_id;
                }
            }
            if (best_child != 0) {
                return self.findLeafWithOptionsScratch(txn, best_child, query, allow_quantized, scratch);
            }
        }

        for (child_ids) |child_id| {
            var child = self.loadNode(txn, child_id) catch continue;
            defer child.deinit(self.alloc);
            const dist = vec.distanceToQuery(query, query_measure, child.centroid, self.config.metric);
            if (dist < best_dist) {
                best_dist = dist;
                best_child = child_id;
            }
        }

        if (best_child == 0) {
            self.invalidateNodeCache(node_id);
            var fresh_node = self.loadNodeFromStorage(txn, node_id) catch |err| {
                if (isNotFound(err)) return error.Corrupted;
                return err;
            };
            defer fresh_node.deinit(self.alloc);
            if (fresh_node.is_leaf) return node_id;
            if (fresh_node.children.len == 0) return error.Corrupted;

            try scratch.ensureCapacity(self.alloc, fresh_node.children.len);
            @memcpy(scratch.child_ids[0..fresh_node.children.len], fresh_node.children);
            const fresh_child_ids = scratch.child_ids[0..fresh_node.children.len];

            for (fresh_child_ids) |child_id| {
                var child = self.loadNodeFromStorage(txn, child_id) catch |err| {
                    if (isNotFound(err)) continue;
                    return err;
                };
                defer child.deinit(self.alloc);
                const dist = vec.distanceToQuery(query, query_measure, child.centroid, self.config.metric);
                if (dist < best_dist) {
                    best_dist = dist;
                    best_child = child_id;
                }
            }
            if (best_child == 0) return error.Corrupted;
        }
        return self.findLeafWithOptionsScratch(txn, best_child, query, false, scratch);
    }

    pub fn collectCompetitiveInsertCandidatesScratch(
        _: *HBCIndex,
        child_ids: []const u64,
        distances: []const f32,
        error_bounds: []const f32,
        scratch: []vectorindex_types.PriorityItem,
    ) ![]vectorindex_types.PriorityItem {
        var competitive_len: usize = 0;

        outer: for (child_ids, 0..) |child_id, i| {
            const candidate: vectorindex_types.PriorityItem = .{
                .id = child_id,
                .distance = distances[i],
                .error_bound = error_bounds[i],
            };
            if (competitive_len == 0) {
                scratch[0] = candidate;
                competitive_len = 1;
                continue;
            }

            while (true) {
                const worst_idx = worstCompetitiveIndex(scratch[0..competitive_len]);
                const worst = scratch[worst_idx];
                if (!candidate.definitelyCloser(worst)) break;
                competitive_len -= 1;
                if (worst_idx != competitive_len) scratch[worst_idx] = scratch[competitive_len];
                if (competitive_len == 0) {
                    scratch[0] = candidate;
                    competitive_len = 1;
                    continue :outer;
                }
            }

            const worst_idx = worstCompetitiveIndex(scratch[0..competitive_len]);
            if (candidate.maybeCloser(scratch[worst_idx])) {
                scratch[competitive_len] = candidate;
                competitive_len += 1;
            }
        }

        return scratch[0..competitive_len];
    }

    pub fn collectCompetitiveInsertCandidates(
        self: *HBCIndex,
        child_ids: []const u64,
        distances: []const f32,
        error_bounds: []const f32,
    ) ![]vectorindex_types.PriorityItem {
        const scratch = try self.alloc.alloc(vectorindex_types.PriorityItem, child_ids.len);
        errdefer self.alloc.free(scratch);
        const competitive = try self.collectCompetitiveInsertCandidatesScratch(child_ids, distances, error_bounds, scratch);
        return self.alloc.realloc(scratch, competitive.len);
    }

    fn worstCompetitiveIndex(items: []const vectorindex_types.PriorityItem) usize {
        var worst_idx: usize = 0;
        for (items[1..], 1..) |item, idx| {
            if (item.distance > items[worst_idx].distance) worst_idx = idx;
        }
        return worst_idx;
    }

    // ========================================================================
    // Split operations
    // ========================================================================

    /// Split a leaf using balanced binary k-means++.
    pub fn splitLeaf(self: *HBCIndex, txn: anytype, leaf: *const Node) !void {
        try vectorindex_hbc_index.splitLeaf(self, txn, leaf);
    }

    pub fn splitLeafWithOptions(
        self: *HBCIndex,
        txn: anytype,
        leaf: *const Node,
        options: BatchInsertOptions,
    ) !void {
        try vectorindex_hbc_index.splitLeafWithOptions(self, txn, leaf, options, nowNsI128, elapsedSinceNs);
    }

    pub fn rebuildOversizedLeafKmeansWithOptions(
        self: *HBCIndex,
        txn: anytype,
        leaf: *const Node,
        options: BatchInsertOptions,
    ) !bool {
        return try vectorindex_hbc_index.rebuildOversizedLeafKmeansWithOptions(self, txn, leaf, options, nowNsI128, elapsedSinceNs);
    }

    pub fn maybeBuildKeyLocalLeafSplit(
        self: *HBCIndex,
        txn: anytype,
        member_ids: []const u64,
        vectors: *const vec.Set,
        current: *const SplitResult,
    ) !?SplitResult {
        return try vectorindex_hbc_index.maybeBuildKeyLocalLeafSplit(self, txn, member_ids, vectors, current);
    }

    /// Split an internal node using balanced binary k-means++ on child centroids.
    fn splitInternal(self: *HBCIndex, txn: anytype, node: *const Node) !void {
        try vectorindex_hbc_index.splitInternal(self, txn, node);
    }

    pub fn splitInternalWithOptions(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        options: BatchInsertOptions,
    ) !void {
        try vectorindex_hbc_index.splitInternalWithOptions(self, txn, node, options, nowNsI128, elapsedSinceNs);
    }

    // ========================================================================
    // Search
    // ========================================================================

    /// Search for the k nearest vectors. Returns results sorted by distance.
    pub fn search(self: *HBCIndex, query: []const f32, k: usize) !SearchResults {
        return try vectorindex_hbc_index.search(self, query, k, nowNs, elapsedSince);
    }

    pub fn searchWithRequest(self: *HBCIndex, req: SearchRequest) !SearchResults {
        return try vectorindex_hbc_index.searchWithRequest(self, req, nowNs, elapsedSince);
    }

    pub fn searchProfiled(self: *HBCIndex, query: []const f32, k: usize) !ProfiledSearchResults {
        return try vectorindex_hbc_index.searchProfiled(self, query, k, nowNs, elapsedSince);
    }

    pub fn searchProfiledRequest(self: *HBCIndex, req: SearchRequest) !ProfiledSearchResults {
        return try vectorindex_hbc_index.searchProfiledRequest(self, req, nowNs, elapsedSince);
    }

    fn routeRateEwma(previous: u64, sample: u64) u64 {
        if (sample == 0) return previous;
        if (previous == 0) return sample;
        return previous - previous / 8 + sample / 8;
    }

    fn routeRateEwmaObserved(previous: u64, sample: u64, observations: u64) u64 {
        if (observations == 0) return sample;
        return previous - previous / 8 + sample / 8;
    }

    fn perUnit(total: u64, count: u64) u64 {
        if (total == 0 or count == 0) return 0;
        return @max(total / count, 1);
    }

    pub fn denseRouteCostSnapshot(self: *HBCIndex) DenseRouteCostSnapshot {
        while (!self.dense_route_cost_mu.tryLock()) std.atomic.spinLoopHint();
        defer self.dense_route_cost_mu.unlock();
        return self.dense_route_cost;
    }

    pub fn noteDenseRouteChoice(self: *HBCIndex, route: DenseRoute) void {
        while (!self.dense_route_cost_mu.tryLock()) std.atomic.spinLoopHint();
        defer self.dense_route_cost_mu.unlock();
        self.dense_route_cost.last_route = route;
    }

    pub fn observeExactDenseRouteCost(self: *HBCIndex, observation: ExactRouteCostObservation) void {
        while (!self.dense_route_cost_mu.tryLock()) std.atomic.spinLoopHint();
        defer self.dense_route_cost_mu.unlock();
        const state = &self.dense_route_cost;
        state.filter_scan_ns_per_candidate = routeRateEwma(
            state.filter_scan_ns_per_candidate,
            perUnit(observation.metadata_ns, observation.candidates),
        );
        state.artifact_read_decode_ns_per_vector = routeRateEwma(
            state.artifact_read_decode_ns_per_vector,
            perUnit(
                observation.artifact_read_decode_ns,
                observation.artifact_vectors_loaded,
            ),
        );
        state.exact_distance_ns_per_vector = routeRateEwma(
            state.exact_distance_ns_per_vector,
            perUnit(observation.distance_ns, observation.candidates),
        );
        const cache_total = observation.artifact_cache_hits +| observation.artifact_vectors_loaded;
        if (cache_total > 0) {
            const hit_permille = @min(@as(u64, 1000), observation.artifact_cache_hits *| 1000 / cache_total);
            state.rerank_cache_hit_permille = routeRateEwmaObserved(
                state.rerank_cache_hit_permille,
                hit_permille,
                state.rerank_cache_observations,
            );
            state.rerank_cache_observations +|= 1;
        }
        state.exact_observations +|= 1;
        state.last_route = .exact;
    }

    fn observeHbcDenseRouteCost(self: *HBCIndex, profile: *const SearchProfile) void {
        while (!self.dense_route_cost_mu.tryLock()) std.atomic.spinLoopHint();
        defer self.dense_route_cost_mu.unlock();
        const state = &self.dense_route_cost;
        state.filter_scan_ns_per_candidate = routeRateEwma(
            state.filter_scan_ns_per_candidate,
            perUnit(profile.filter_metadata_batch_ns, profile.filter_candidates),
        );
        const quantized_ns = profile.leaf_score_ns -| profile.filter_metadata_batch_ns;
        state.quantized_score_ns_per_candidate = routeRateEwma(
            state.quantized_score_ns_per_candidate,
            perUnit(quantized_ns, profile.approx_vectors_scored),
        );
        state.artifact_read_decode_ns_per_vector = routeRateEwma(
            state.artifact_read_decode_ns_per_vector,
            perUnit(
                profile.rerank_artifact_read_ns +| profile.rerank_artifact_decode_ns,
                profile.rerank_artifact_vectors_loaded,
            ),
        );
        state.exact_distance_ns_per_vector = routeRateEwma(
            state.exact_distance_ns_per_vector,
            // External-vector scoring reports its distance subphase in both
            // the generic rerank total and the artifact-specific breakdown.
            // Prefer the specific measurement when present so one interval is
            // never charged twice into the adaptive route model.
            perUnit(
                if (profile.rerank_artifact_distance_ns > 0)
                    profile.rerank_artifact_distance_ns
                else
                    profile.rerank_distance_ns,
                profile.reranked_vectors,
            ),
        );
        const cache_total = profile.rerank_artifact_cache_hits +| profile.rerank_artifact_vectors_loaded;
        if (cache_total > 0) {
            const hit_permille = @min(@as(u64, 1000), profile.rerank_artifact_cache_hits *| 1000 / cache_total);
            state.rerank_cache_hit_permille = routeRateEwmaObserved(
                state.rerank_cache_hit_permille,
                hit_permille,
                state.rerank_cache_observations,
            );
            state.rerank_cache_observations +|= 1;
        }
        state.hbc_observations +|= 1;
        state.last_route = .hbc;
    }

    pub fn observeSearchCacheBenefit(self: *HBCIndex, profile: *const SearchProfile) void {
        self.observeHbcDenseRouteCost(profile);
        const manager = self.resource_manager orelse return;
        if (!manager.beginHbcCacheBenefitSample()) return;
        const cache_stats = self.hbcCacheStats();
        const node_hits = profile.nodes_visited -| profile.node_cache_misses;
        const quantized_hits = profile.approx_leaves_scored -| profile.quantized_cache_misses;
        manager.observeHbcCacheBenefitSampled(.{
            .{
                .hits = node_hits,
                .misses = profile.node_cache_misses,
                .miss_service_ns = profile.node_cache_miss_ns,
                .resident_bytes = cache_stats.node.used_bytes,
            },
            .{
                .hits = quantized_hits,
                .misses = profile.quantized_cache_misses,
                .miss_service_ns = profile.quantized_cache_miss_ns,
                .resident_bytes = cache_stats.quantized.used_bytes,
            },
            .{
                .hits = profile.vector_cache_hits,
                .misses = profile.vector_cache_misses,
                .miss_service_ns = profile.rerank_artifact_read_ns +| profile.rerank_artifact_decode_ns,
                .resident_bytes = cache_stats.vector.used_bytes,
            },
            .{
                .hits = profile.metadata_cache_hits,
                .misses = profile.metadata_cache_misses,
                .miss_service_ns = profile.metadata_cache_miss_ns,
                .resident_bytes = cache_stats.metadata.used_bytes,
            },
        });
    }

    /// Add children of a node to the candidate queue.
    fn addChildCandidates(
        self: *HBCIndex,
        txn: anytype,
        node: *const Node,
        query: []const f32,
        query_measure: f32,
        candidates: *std.PriorityQueue(PriorityItem, void, candidateLessThan),
        scratch: *SearchScratch,
        profile: *SearchProfile,
    ) !void {
        try vectorindex_hbc_index.addChildCandidates(self, txn, node, query, query_measure, candidates, scratch, profile, nowNs, elapsedSince);
    }

    /// Score all members of a leaf against the query using exact distances.
    fn scoreLeafMembers(
        self: *HBCIndex,
        txn: anytype,
        leaf: *const Node,
        approx_query: []const f32,
        approx_query_measure: f32,
        exact_query: []const f32,
        exact_query_measure: f32,
        req: SearchRequest,
        filter_state: *const RequestFilterState,
        results: *ApproxSearchResults,
        scratch: *SearchScratch,
        profile: *SearchProfile,
    ) !void {
        try vectorindex_hbc_index.scoreLeafMembers(
            self,
            txn,
            leaf,
            approx_query,
            approx_query_measure,
            exact_query,
            exact_query_measure,
            req,
            filter_state,
            results,
            scratch,
            profile,
            nowNs,
            elapsedSince,
        );
    }

    fn rerankResults(
        self: *HBCIndex,
        txn: anytype,
        approx_results: *const ApproxSearchResults,
        query: []const f32,
        query_measure: f32,
        req: SearchRequest,
        filter_state: *const RequestFilterState,
        scratch: *SearchScratch,
        profile: *SearchProfile,
    ) !SearchResults {
        return try vectorindex_hbc_index.rerankResults(self, txn, approx_results, query, query_measure, req, filter_state, scratch, profile, nowNs, elapsedSince);
    }

    fn populateMetadata(self: *HBCIndex, txn: anytype, results: *SearchResults) !void {
        try vectorindex_hbc_index.populateMetadata(self, txn, results);
    }

    fn memberMatchesRequest(
        self: *HBCIndex,
        txn: anytype,
        vector_id: u64,
        distance: f32,
        error_bound: f32,
        req: SearchRequest,
        filter_state: *const RequestFilterState,
        approximate: bool,
    ) !bool {
        return try vectorindex_hbc_index.memberMatchesRequest(self, txn, vector_id, distance, error_bound, req, filter_state, approximate);
    }

    // ========================================================================
    // Delete
    // ========================================================================

    /// Delete a vector from the index by ID.
    pub fn delete(self: *HBCIndex, vector_id: u64) !void {
        try vectorindex_hbc_index.delete(self, vector_id);
    }

    pub fn batchDelete(self: *HBCIndex, vector_ids: []const u64) !void {
        try vectorindex_hbc_index.batchDelete(self, vector_ids);
    }

    fn deleteTxn(self: *HBCIndex, txn: anytype, vector_id: u64) !void {
        try vectorindex_hbc_index.deleteTxn(self, txn, vector_id);
    }

    // ========================================================================
    // Tree link consistency
    // ========================================================================

    pub fn noteTreeLinkInconsistency(self: *HBCIndex) void {
        self.link_repair_pending.store(true, .release);
    }

    pub fn noteIncompletePublishedSnapshot(self: *HBCIndex) void {
        self.noteIncompletePublishedSnapshotForGeneration(self.publishedGeneration());
    }

    pub fn noteIncompletePublishedSnapshotForGeneration(self: *HBCIndex, generation: u64) void {
        self.link_repair_pending.store(true, .release);
        // Concurrent searches may finish out of order. Never let a delayed
        // failure from an older snapshot overwrite a newer generation's
        // repair signal.
        var pending = self.generation_repair_pending_generation.load(.acquire);
        while (pending == std.math.maxInt(u64) or generation > pending) {
            pending = self.generation_repair_pending_generation.cmpxchgWeak(
                pending,
                generation,
                .acq_rel,
                .acquire,
            ) orelse break;
        }
    }

    pub fn generationRepairPending(self: *const HBCIndex) bool {
        const generation = self.publishedGeneration();
        return (generation & 1) == 0 and
            self.generation_repair_pending_generation.load(.acquire) == generation;
    }

    pub fn treeLinkRepairPending(self: *const HBCIndex) bool {
        return self.link_repair_pending.load(.acquire);
    }

    /// Read-only structural invariant check (see hbc_index.verifyTreeLinks).
    pub fn verifyTreeLinks(self: *HBCIndex) !vectorindex_hbc_index.TreeLinkReport {
        var txn = try self.beginReadTxn();
        defer txn.abort();
        return try vectorindex_hbc_index.verifyTreeLinks(self, &txn);
    }

    /// Bounded repair sweep (see hbc_index.repairTreeLinks). Clears the
    /// pending flag once a sweep completes within budget.
    pub fn repairTreeLinks(self: *HBCIndex, max_nodes: usize) !vectorindex_hbc_index.TreeLinkRepairReport {
        const report = try vectorindex_hbc_index.repairLinks(self, max_nodes);
        if (report.completed) self.link_repair_pending.store(false, .release);
        return report;
    }

    /// Runs a repair sweep only when a write path flagged an inconsistency.
    pub fn maybeRepairTreeLinks(self: *HBCIndex, max_nodes: usize) !?vectorindex_hbc_index.TreeLinkRepairReport {
        if (!self.treeLinkRepairPending()) return null;
        return try self.repairTreeLinks(max_nodes);
    }

    // ========================================================================
    // Stats
    // ========================================================================

    pub fn stats(self: *const HBCIndex) IndexStats {
        return .{
            .dims = self.metadata.dims,
            .active_count = self.publishedActiveCount(),
            .node_count = self.publishedNodeCount(),
            .root_node = self.publishedRootNode(),
            .branching_factor = self.metadata.branching_factor,
            .leaf_size = self.metadata.leaf_size,
        };
    }

    pub fn debugLeafForVector(self: *HBCIndex, vector_id: u64) !?u64 {
        return try vectorindex_hbc_debug.debugLeafForVector(self, vector_id, isNotFound);
    }

    pub fn debugLeafMembers(self: *HBCIndex, alloc: Allocator, leaf_id: u64) ![]u64 {
        return try vectorindex_hbc_debug.debugLeafMembers(self, alloc, leaf_id);
    }

    pub fn debugScanLeafForVector(self: *HBCIndex, vector_id: u64) !?u64 {
        return try vectorindex_hbc_debug.debugScanLeafForVector(self, vector_id);
    }

    pub fn debugDumpNodes(self: *HBCIndex, alloc: Allocator) ![]HBCDebugNode {
        return try vectorindex_hbc_debug.debugDumpNodes(self, alloc);
    }

    pub fn debugScoreLeaf(self: *HBCIndex, alloc: Allocator, leaf_id: u64, query: []const f32) ![]DebugLeafScore {
        return try vectorindex_hbc_debug.debugScoreLeaf(self, alloc, leaf_id, query);
    }

    pub fn debugScoreLeafFreshQuantized(self: *HBCIndex, alloc: Allocator, leaf_id: u64, query: []const f32) ![]DebugLeafScore {
        return try vectorindex_hbc_debug.debugScoreLeafFreshQuantized(self, alloc, leaf_id, query);
    }

    pub fn debugLeafCentroidL2Error(self: *HBCIndex, alloc: Allocator, leaf_id: u64) !f32 {
        return try vectorindex_hbc_debug.debugLeafCentroidL2Error(self, alloc, leaf_id);
    }

    pub fn debugLeafCentroid(self: *HBCIndex, alloc: Allocator, leaf_id: u64) ![]f32 {
        return try vectorindex_hbc_debug.debugLeafCentroid(self, alloc, leaf_id);
    }

    pub fn debugRootChildDistances(self: *HBCIndex, alloc: Allocator, query: []const f32) ![]DebugNodeDistance {
        return try vectorindex_hbc_debug.debugRootChildDistances(self, alloc, query);
    }

    pub fn debugFindLeafForQuery(self: *HBCIndex, query: []const f32, allow_quantized: bool) !u64 {
        return try vectorindex_hbc_debug.debugFindLeafForQuery(self, query, allow_quantized);
    }

    pub fn debugChildDistances(self: *HBCIndex, alloc: Allocator, node_id: u64, query: []const f32) ![]DebugNodeDistance {
        return try vectorindex_hbc_debug.debugChildDistances(self, alloc, node_id, query);
    }

    pub fn resetWriteProfile(self: *HBCIndex) void {
        self.write_profile = .{};
    }

    pub fn getWriteProfile(self: *const HBCIndex) WriteProfile {
        return self.write_profile;
    }

    pub fn repairDirtyPostings(self: *HBCIndex) !PostingMaintenanceResult {
        return try self.repairDirtyPostingsWithOptions(.{});
    }

    pub fn repairDirtyPostingsWithOptions(self: *HBCIndex, options: PostingMaintenanceOptions) !PostingMaintenanceResult {
        var txn = try self.beginWriteTxn();
        errdefer txn.abort();
        const result = try vectorindex_hbc_index.repairDirtyPostingsTxnWithOptions(self, &txn, options);
        // This is the maintenance operation itself. Running the generic
        // write finalizer here would apply the separately configured auto
        // maintenance budget after the caller's explicit bound.
        const commit_start = nowNs();
        try txn.commit();
        self.write_profile.insert_commit_ns += elapsedSince(commit_start);
        return result;
    }

    pub fn postingBacklogStats(self: *HBCIndex) !PostingBacklogStats {
        var txn = try self.beginReadTxn();
        defer txn.abort();
        return try vectorindex_hbc_index.postingBacklogStatsTxn(self, &txn);
    }

    pub fn writePostingBacklogStats(self: *HBCIndex, writer: *std.Io.Writer) !void {
        const backlog = try self.postingBacklogStats();
        try backlog.write(writer);
    }
};

// ============================================================================
// Search results (bounded max-heap)
// ============================================================================

pub const SearchResult = vectorindex_search_results.SearchResult;
pub const ApproxSearchResult = vectorindex_search_results.ApproxSearchResult;
pub const SearchRequest = vectorindex_search_types.SearchRequest;
pub const CancellationToken = vectorindex_search_types.CancellationToken;
pub const SearchProfile = vectorindex_search_types.SearchProfile;

pub const WriteProfile = vectorindex_hbc_runtime.WriteProfile;
pub const BatchInsertItem = vectorindex_hbc_runtime.BatchInsertItem;
pub const FixedKeyLookup = vectorindex_search_runtime.RerankLookup;
pub const BatchInsertOptions = vectorindex_hbc_runtime.BatchInsertOptions;
pub const VectorId = vectorindex_posting.VectorId;
pub const PostingId = vectorindex_posting.PostingId;
pub const PostingView = vectorindex_posting.PostingView;
pub const PostingState = vectorindex_posting.PostingState;
pub const PostingMaintenanceOptions = vectorindex_posting.PostingMaintenanceOptions;
pub const PostingMaintenanceResult = vectorindex_posting.PostingMaintenanceResult;
pub const PostingBacklogStats = vectorindex_posting.PostingBacklogStats;
pub const PostingStore = vectorindex_posting.PostingStore;
pub const AssignmentMap = vectorindex_posting.AssignmentMap;
pub const CentroidDirectory = vectorindex_posting.CentroidDirectory;

pub const BulkBuildOptions = vectorindex_bulk_build.BulkBuildOptions;
pub const PreparedBulkBuildInput = vectorindex_bulk_build.PreparedBulkBuildInput;

const BuiltBulkNode = vectorindex_hbc_index.BuiltBulkNode;

pub const ProfiledSearchResults = vectorindex_search_types.ProfiledSearchResults;
const RequestFilterState = vectorindex_search_types.RequestFilterState;

const SearchScratch = vectorindex_search_runtime.SearchScratch;

const ScratchHandle = vectorindex_hbc_runtime.ScratchHandle;

fn expectCachedMetadata(index: *HBCIndex, vector_id: u64, expected: []const u8) !void {
    var borrowed = index.borrowCachedMetadata(vector_id) orelse return error.TestUnexpectedResult;
    defer borrowed.deinit();
    try std.testing.expectEqualStrings(expected, borrowed.view());
}

fn expectMetadataNotCached(index: *HBCIndex, vector_id: u64) !void {
    try std.testing.expect(index.borrowCachedMetadata(vector_id) == null);
}

fn expectVectorCached(index: *HBCIndex, vector_id: u64) !void {
    var borrowed = index.borrowCachedVector(vector_id) orelse return error.TestUnexpectedResult;
    defer borrowed.deinit();
    try std.testing.expect(borrowed.view().len > 0);
}

fn expectVectorNotCached(index: *HBCIndex, vector_id: u64) !void {
    try std.testing.expect(index.borrowCachedVector(vector_id) == null);
}

fn expectSharedVectorCached(cache: *Cache, namespace: u64, vector_id: u64, expected: []const f32) !void {
    var borrowed = cache.borrowVector(namespace, vector_id) orelse return error.TestUnexpectedResult;
    defer borrowed.deinit();
    try std.testing.expectEqualSlices(f32, expected, borrowed.view());
}

fn expectSharedVectorNotCached(cache: *Cache, namespace: u64, vector_id: u64) !void {
    try std.testing.expect(cache.borrowVector(namespace, vector_id) == null);
}

pub const SearchResults = vectorindex_search_results.SearchResults;
const ApproxSearchResults = vectorindex_search_results.ApproxSearchResults;

pub const DebugLeafScore = vectorindex_search_types.DebugLeafScore;
pub const DebugNodeDistance = vectorindex_search_types.DebugNodeDistance;
pub const IndexStats = vectorindex_search_types.IndexStats;
pub const HBCDebugNode = vectorindex_search_types.HBCDebugNode;

// ============================================================================
// Tests
// ============================================================================

test "create and open index" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    {
        var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
        defer idx.close();

        const s = idx.stats();
        try std.testing.expectEqual(@as(u32, 4), s.dims);
        try std.testing.expectEqual(@as(u64, 0), s.active_count);
        try std.testing.expectEqual(@as(u64, 1), s.node_count);
    }

    // Reopen and verify persistence
    {
        var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
        defer idx.close();
        try std.testing.expectEqual(@as(u32, 4), idx.stats().dims);
    }
}

test "hbc randomized insert delete churn preserves tree link invariants" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    // Small fanout forces frequent splits, sibling merges, and single-child
    // collapses — the maintenance operations that rewrite parent/child links.
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = 4,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0x5eed_11ab);
    const random = prng.random();

    var live = std.ArrayListUnmanaged(u64).empty;
    defer live.deinit(alloc);
    var next_id: u64 = 1;

    var op: usize = 0;
    while (op < 1200) : (op += 1) {
        const do_insert = live.items.len < 8 or random.intRangeLessThan(u8, 0, 100) < 60;
        if (do_insert) {
            var v: [4]f32 = undefined;
            for (&v) |*x| x.* = random.float(f32) * 2.0 - 1.0;
            try idx.insert(next_id, &v);
            try live.append(alloc, next_id);
            next_id += 1;
        } else if (random.boolean() or live.items.len < 4) {
            const pick = random.intRangeLessThan(usize, 0, live.items.len);
            const vid = live.swapRemove(pick);
            try idx.delete(vid);
        } else {
            var batch: [6]u64 = undefined;
            const want = @min(live.items.len, random.intRangeLessThan(usize, 2, 7));
            var i: usize = 0;
            while (i < want) : (i += 1) {
                const pick = random.intRangeLessThan(usize, 0, live.items.len);
                batch[i] = live.swapRemove(pick);
            }
            try idx.batchDelete(batch[0..want]);
        }

        if (op % 50 == 49) {
            const report = try idx.verifyTreeLinks();
            if (!report.consistent()) {
                std.debug.print("tree links inconsistent after op {d}: {any}\n", .{ op, report });
                return error.TestUnexpectedResult;
            }
            try std.testing.expect(!idx.treeLinkRepairPending());
        }
    }

    const final_report = try idx.verifyTreeLinks();
    if (!final_report.consistent()) {
        std.debug.print("tree links inconsistent at end: {any}\n", .{final_report});
        return error.TestUnexpectedResult;
    }
    try std.testing.expectEqual(@as(u64, @intCast(live.items.len)), idx.stats().active_count);
}

test "hbc repairTreeLinks clears dangling references and restores consistency" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = 4,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0xfee1_600d);
    const random = prng.random();
    var id: u64 = 1;
    while (id <= 60) : (id += 1) {
        var v: [4]f32 = undefined;
        for (&v) |*x| x.* = random.float(f32) * 2.0 - 1.0;
        try idx.insert(id, &v);
    }

    // Corrupt the tree the way the field incident did: a leaf node vanishes
    // while its parent still lists it and vec→leaf entries still point at it.
    const victim_leaf = (try idx.debugLeafForVector(7)) orelse return error.TestUnexpectedResult;
    const victim_members = try idx.debugLeafMembers(alloc, victim_leaf);
    defer alloc.free(victim_members);
    try std.testing.expect(victim_members.len > 0);
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        try idx.deleteNode(&txn, victim_leaf);
        try txn.commit();
    }

    const broken = try idx.verifyTreeLinks();
    try std.testing.expect(!broken.consistent());
    try std.testing.expect(broken.dangling_children >= 1);

    // Search still serves while the tree is inconsistent (tolerant descent).
    {
        const query = [_]f32{ 0.1, 0.2, 0.3, 0.4 };
        var results = try idx.search(&query, 5);
        defer results.deinit();
        try std.testing.expect(results.getHits().len > 0);

        // Full effort is a coverage contract, so it must distinguish a
        // damaged published topology from a valid but low-recall result.
        try std.testing.expectError(
            error.IncompletePublishedSnapshot,
            idx.searchWithRequest(.{
                .query = &query,
                .k = 5,
                .search_effort = 1,
                .load_metadata = false,
            }),
        );
    }

    // Deleting a vector whose leaf is gone cleans up instead of erroring,
    // and flags the index for repair.
    try idx.delete(victim_members[0]);
    try std.testing.expect(idx.treeLinkRepairPending());

    const repair = try idx.repairTreeLinks(10_000);
    try std.testing.expect(repair.completed);
    try std.testing.expect(repair.dangling_children_removed >= 1);
    try std.testing.expect(!idx.treeLinkRepairPending());

    const healed = try idx.verifyTreeLinks();
    if (!healed.consistent()) {
        std.debug.print("tree links inconsistent after repair: {any}\n", .{healed});
        return error.TestUnexpectedResult;
    }

    // The index stays fully usable after repair.
    const probe = [_]f32{ 0.5, -0.25, 0.75, -0.5 };
    try idx.insert(1000, &probe);
    {
        var results = try idx.search(&probe, 3);
        defer results.deinit();
        const hits = results.getHits();
        try std.testing.expect(hits.len > 0);
        try std.testing.expectEqual(@as(u64, 1000), hits[0].vector_id);
    }
}

test "flat rabitq complete snapshot rejects a directory built with dangling nodes" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = 4,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0xf1a7_d1a0);
    const random = prng.random();
    var id: u64 = 1;
    while (id <= 60) : (id += 1) {
        var vector: [4]f32 = undefined;
        for (&vector) |*value| value.* = random.float(f32) * 2.0 - 1.0;
        try idx.insert(id, &vector);
    }

    const victim_leaf = (try idx.debugLeafForVector(7)) orelse return error.TestUnexpectedResult;
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        try idx.deleteNode(&txn, victim_leaf);
        try txn.commit();
    }

    const query = [_]f32{ 0.1, 0.2, 0.3, 0.4 };
    var partial = try idx.searchWithRequest(.{
        .query = &query,
        .k = 5,
        .load_metadata = false,
    });
    defer partial.deinit();
    try std.testing.expect(partial.getHits().len > 0);

    try std.testing.expectError(
        error.IncompletePublishedSnapshot,
        idx.searchWithRequest(.{
            .query = &query,
            .k = 5,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );
}

test "flat rabitq complete snapshot rejects a cyclic directory topology" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = 4,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0xc1c1_e001);
    const random = prng.random();
    var id: u64 = 1;
    while (id <= 60) : (id += 1) {
        var vector: [4]f32 = undefined;
        for (&vector) |*value| value.* = random.float(f32) * 2.0 - 1.0;
        try idx.insert(id, &vector);
    }

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(!root.is_leaf);
        try root.ensureUnbacked(alloc);
        // Keep persisted fanout within its configured bound while replacing
        // one edge with a cycle. Oversized payload rejection is covered
        // separately; this case exercises enqueue-time cycle detection while
        // preserving a useful best-effort partial frontier.
        try std.testing.expect(root.children.len > 1);
        root.children[root.children.len - 1] = root.id;
        // Bypass derived split-range maintenance so the test can persist the
        // malformed edge and exercise read-side cycle hardening directly.
        try idx.saveNodeBody(&txn, &root);
        try txn.commit();
    }

    // The flat-directory builder must terminate, preserve best-effort
    // availability, and reject the invalid topology for complete coverage.
    var partial = try idx.searchWithRequest(.{
        .query = &.{ 0.1, 0.2, 0.3, 0.4 },
        .k = 5,
        .load_metadata = false,
    });
    defer partial.deinit();
    try std.testing.expect(partial.getHits().len > 0);

    try std.testing.expectError(
        error.IncompletePublishedSnapshot,
        idx.searchWithRequest(.{
            .query = &.{ 0.1, 0.2, 0.3, 0.4 },
            .k = 5,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );
}

test "search node loading rejects oversized published internal fanout" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const branching_factor = 4;
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = branching_factor,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0x0a51_2ed0);
    const random = prng.random();
    var id: u64 = 1;
    while (id <= 60) : (id += 1) {
        var vector: [4]f32 = undefined;
        for (&vector) |*value| value.* = random.float(f32) * 2.0 - 1.0;
        try idx.insert(id, &vector);
    }

    const root_id = idx.metadata.root_node;
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var root = try idx.loadNode(&txn, root_id);
        defer root.deinit(alloc);
        try std.testing.expect(!root.is_leaf);
        try std.testing.expect(root.children.len > 0);
        try root.ensureUnbacked(alloc);

        const existing_child = root.children[0];
        const oversized = try alloc.alloc(u64, branching_factor + 1);
        @memset(oversized, existing_child);
        alloc.free(root.children);
        root.children = oversized;

        // Persist a malformed body directly so the read path is responsible
        // for rejecting its untrusted length before allocating decode space.
        try idx.saveNodeBody(&txn, &root);
        try txn.commit();
    }

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    try std.testing.expectError(error.Corrupted, idx.loadSearchNodeFromStorage(&txn, root_id));
    try std.testing.expectError(error.Corrupted, idx.loadFlatCentroidDirectoryNodeFromStorage(&txn, root_id));
}

test "flat directory node loading bounds oversized leaf payloads and centroids" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const dims = 4;
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = dims,
        .leaf_size = 4,
        .branching_factor = 4,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
    });
    defer idx.close();
    try idx.insert(1, &.{ 0.1, 0.2, 0.3, 0.4 });

    const root_id = idx.metadata.root_node;
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var root = try idx.loadNode(&txn, root_id);
        defer root.deinit(alloc);
        try std.testing.expect(root.is_leaf);
        try root.ensureUnbacked(alloc);
        const oversized_members = try alloc.alloc(u64, 1024);
        @memset(oversized_members, 1);
        alloc.free(root.members);
        root.members = oversized_members;
        try idx.saveNodeBody(&txn, &root);
        try txn.commit();
    }

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var directory_node = try idx.loadFlatCentroidDirectoryNodeFromStorage(&txn, root_id);
        defer directory_node.deinit(alloc);
        try std.testing.expectEqual(@as(usize, 1), directory_node.members.len);
        try std.testing.expect(directory_node.backing.len <= dims * @sizeOf(f32) + @sizeOf(u64));
    }

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var root = try idx.loadNode(&txn, root_id);
        defer root.deinit(alloc);
        try root.ensureUnbacked(alloc);
        const oversized_centroid = try alloc.alloc(f32, dims + 1);
        @memset(oversized_centroid, 0);
        alloc.free(root.centroid);
        root.centroid = oversized_centroid;
        try idx.saveNodeBody(&txn, &root);
        try txn.commit();
    }

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    try std.testing.expectError(error.Corrupted, idx.loadSearchNodeFromStorage(&txn, root_id));
    try std.testing.expectError(error.Corrupted, idx.loadFlatCentroidDirectoryNodeFromStorage(&txn, root_id));
}

test "complete snapshot rejects orphaned reachable coverage and schedules generation repair" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = 4,
        .use_quantization = false,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0x0bad_c0de);
    const random = prng.random();
    var id: u64 = 1;
    while (id <= 60) : (id += 1) {
        var vector: [4]f32 = undefined;
        for (&vector) |*value| value.* = random.float(f32) * 2.0 - 1.0;
        try idx.insert(id, &vector);
    }

    const victim_leaf = (try idx.debugLeafForVector(7)) orelse return error.TestUnexpectedResult;
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var leaf = try idx.loadNode(&txn, victim_leaf);
        defer leaf.deinit(alloc);
        try std.testing.expect(leaf.parent != 0);
        var parent = try idx.loadNode(&txn, leaf.parent);
        defer parent.deinit(alloc);
        try parent.ensureUnbacked(alloc);
        const children = try alloc.alloc(u64, parent.children.len - 1);
        var write_index: usize = 0;
        for (parent.children) |child_id| {
            if (child_id == victim_leaf) continue;
            children[write_index] = child_id;
            write_index += 1;
        }
        try std.testing.expectEqual(children.len, write_index);
        alloc.free(parent.children);
        parent.children = children;
        try idx.saveNode(&txn, &parent);
        try txn.commit();
    }

    try std.testing.expect(!idx.generationRepairPending());
    try std.testing.expectError(
        error.IncompletePublishedSnapshot,
        idx.searchWithRequest(.{
            .query = &.{ 0.1, 0.2, 0.3, 0.4 },
            .k = 5,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );
    try std.testing.expect(idx.generationRepairPending());
    try std.testing.expect(idx.treeLinkRepairPending());
}

test "small quantized complete snapshot validates authoritative leaf assignments" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 4,
        .use_quantization = true,
        .rerank_policy = .always,
    });
    defer idx.close();

    for (0..128) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, 0 });
    }
    const near_leaf = (try idx.debugLeafForVector(1)) orelse return error.TestUnexpectedResult;
    const far_leaf = (try idx.debugLeafForVector(128)) orelse return error.TestUnexpectedResult;
    try std.testing.expect(near_leaf != far_leaf);

    // Preserve the reachable, unique member set and all quantized payloads,
    // but corrupt the authoritative assignment for a far-away vector that will
    // not enter the k=1 rerank window. Count+uniqueness validation alone accepts
    // this generation even though its membership publication is inconsistent.
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        try idx.putVecLeaf(&txn, 128, near_leaf);
        try txn.commit();
    }

    try std.testing.expectError(
        error.IncompletePublishedSnapshot,
        idx.searchWithRequest(.{
            .query = &.{ 0, 0 },
            .k = 1,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );
    try std.testing.expect(idx.generationRepairPending());
}

test "incomplete snapshot repair marker is scoped to its publication generation" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 4,
        .use_quantization = false,
    });
    defer idx.close();

    const old_generation = idx.publishedGeneration();
    idx.noteIncompletePublishedSnapshotForGeneration(old_generation);
    try std.testing.expect(idx.generationRepairPending());

    idx.refreshPublishedSearchState();
    try std.testing.expect(!idx.generationRepairPending());

    // A delayed search completion from the old snapshot cannot poison the
    // newer serving generation or overwrite a newer repair observation.
    idx.noteIncompletePublishedSnapshotForGeneration(old_generation);
    try std.testing.expect(!idx.generationRepairPending());
    idx.noteIncompletePublishedSnapshotForGeneration(idx.publishedGeneration());
    try std.testing.expect(idx.generationRepairPending());
    idx.noteIncompletePublishedSnapshotForGeneration(old_generation);
    try std.testing.expect(idx.generationRepairPending());
}

test "complete coverage validation claim caches success and retries failure" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();

    const generation = idx.publishedGeneration();
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));
    idx.finishCompleteCoverageValidation(generation, true);
    try std.testing.expect(!try idx.beginCompleteCoverageValidation(generation, null));

    const next_generation = generation + 2;
    try std.testing.expect(try idx.beginCompleteCoverageValidation(next_generation, null));
    idx.finishCompleteCoverageValidation(next_generation, false);
    try std.testing.expect(try idx.beginCompleteCoverageValidation(next_generation, null));
    idx.finishCompleteCoverageValidation(next_generation, true);
    try std.testing.expect(!try idx.beginCompleteCoverageValidation(next_generation, null));
}

fn waitForCompleteCoverageWaiter(index: *HBCIndex, generation: u64, io: std.Io) !void {
    for (0..5_000) |_| {
        lockAtomic(&index.complete_coverage_state_mu);
        var current = index.complete_coverage_flight;
        const joined = while (current) |flight| : (current = flight.next) {
            if (flight.generation == generation and flight.refs > 1) break true;
        } else false;
        index.complete_coverage_state_mu.unlock();
        if (joined) return;
        try io.sleep(std.Io.Duration.fromMilliseconds(1), .awake);
    }
    return error.TestUnexpectedResult;
}

test "complete coverage validation waiter honors cancellation without canceling owner" {
    if (builtin.single_threaded or builtin.os.tag == .freestanding) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    idx.setIo(io);

    const generation = idx.publishedGeneration();
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));
    var owner_active = true;
    defer if (owner_active) idx.finishCompleteCoverageValidation(generation, false);

    var cancelled = std.atomic.Value(bool).init(false);
    const Waiter = struct {
        fn run(index: *HBCIndex, signal: *const std.atomic.Value(bool), expected_generation: u64) !bool {
            return try index.beginCompleteCoverageValidation(
                expected_generation,
                vectorindex_search_types.CancellationToken.fromAtomic(signal),
            );
        }
    };
    var waiter = std.Io.async(io, Waiter.run, .{ &idx, &cancelled, generation });
    try waitForCompleteCoverageWaiter(&idx, generation, io);
    cancelled.store(true, .release);
    try std.testing.expectError(error.Cancelled, waiter.await(io));

    // The canceled waiter only releases its reference. The elected producer
    // still owns the flight and can publish a successful validation.
    idx.finishCompleteCoverageValidation(generation, true);
    owner_active = false;
    try std.testing.expect(!try idx.beginCompleteCoverageValidation(generation, null));
}

test "complete coverage flight preserves older success after newer validation" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    const older = idx.publishedGeneration();
    const newer = older +| 2;

    try std.testing.expect(try idx.beginCompleteCoverageValidation(older, null));
    lockAtomic(&idx.complete_coverage_state_mu);
    const older_flight = idx.complete_coverage_flight orelse return error.TestUnexpectedResult;
    older_flight.refs += 1;
    idx.complete_coverage_state_mu.unlock();

    idx.finishCompleteCoverageValidation(older, true);
    try std.testing.expect(try idx.beginCompleteCoverageValidation(newer, null));
    idx.finishCompleteCoverageValidation(newer, true);
    try std.testing.expect(idx.completeCoverageAlreadyValidated(newer));

    // A waiter retained the generation-keyed flight before the newer
    // validation advanced the one-entry fast cache. It must consume the old
    // successful outcome instead of electing another O(N) validator.
    try std.testing.expect(try idx.waitForCompleteCoverageFlight(older_flight, null));
}

test "complete coverage flight shares a deterministic producer failure" {
    if (builtin.single_threaded or builtin.os.tag == .freestanding) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    idx.setIo(io);

    const generation = idx.publishedGeneration();
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));
    var owner_active = true;
    defer if (owner_active) idx.finishCompleteCoverageValidation(generation, false);

    const Waiter = struct {
        fn run(index: *HBCIndex, expected_generation: u64) !bool {
            return try index.beginCompleteCoverageValidation(expected_generation, null);
        }
    };
    var waiter = std.Io.async(io, Waiter.run, .{ &idx, generation });
    try waitForCompleteCoverageWaiter(&idx, generation, io);
    idx.failCompleteCoverageValidation(generation, error.IncompletePublishedSnapshot);
    owner_active = false;
    try std.testing.expectError(error.IncompletePublishedSnapshot, waiter.await(io));

    // The terminal result is scoped to callers that joined this flight. A
    // later request may retry after repair or another external state change.
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));
    idx.finishCompleteCoverageValidation(generation, false);
}

test "complete coverage flight retries an owner-local cancellation" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    const generation = idx.publishedGeneration();
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));

    lockAtomic(&idx.complete_coverage_state_mu);
    const flight = idx.complete_coverage_flight orelse return error.TestUnexpectedResult;
    flight.refs += 1;
    idx.complete_coverage_state_mu.unlock();

    idx.failCompleteCoverageValidation(generation, error.Cancelled);
    try std.testing.expect(!try idx.waitForCompleteCoverageFlight(flight, null));
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));
    idx.finishCompleteCoverageValidation(generation, false);
}

test "complete coverage flight does not broadcast a query-scoped failure" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    const generation = idx.publishedGeneration();
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));

    lockAtomic(&idx.complete_coverage_state_mu);
    const flight = idx.complete_coverage_flight orelse return error.TestUnexpectedResult;
    flight.refs += 1;
    idx.complete_coverage_state_mu.unlock();

    idx.failCompleteCoverageValidation(generation, error.ExternalVectorUnavailable);
    try std.testing.expect(!try idx.waitForCompleteCoverageFlight(flight, null));
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));
    idx.finishCompleteCoverageValidation(generation, false);
}

test "complete coverage flight propagates backend runtime cancellation" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    const generation = idx.publishedGeneration();
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));

    lockAtomic(&idx.complete_coverage_state_mu);
    const flight = idx.complete_coverage_flight orelse return error.TestUnexpectedResult;
    flight.refs += 1;
    idx.complete_coverage_state_mu.unlock();

    idx.failCompleteCoverageValidation(generation, error.Canceled);
    try std.testing.expectError(error.Canceled, idx.waitForCompleteCoverageFlight(flight, null));
    try std.testing.expect(try idx.beginCompleteCoverageValidation(generation, null));
    idx.finishCompleteCoverageValidation(generation, false);
}

fn waitForFlatCentroidBuildWaiter(index: *HBCIndex, generation: u64, io: std.Io) !void {
    for (0..5_000) |_| {
        lockAtomic(&index.flat_centroid_build_mu);
        var current = index.flat_centroid_build_flight;
        const joined = while (current) |flight| : (current = flight.next) {
            if (flight.generation == generation and flight.refs > 1) break true;
        } else false;
        index.flat_centroid_build_mu.unlock();
        if (joined) return;
        try io.sleep(std.Io.Duration.fromMilliseconds(1), .awake);
    }
    return error.TestUnexpectedResult;
}

test "flat centroid build single flight waits on backend runtime" {
    if (builtin.single_threaded or builtin.os.tag == .freestanding) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    idx.setIo(io);

    const generation = idx.publishedGeneration();
    switch (try idx.beginFlatCentroidDirectoryBuild(generation, null)) {
        .owner => {},
        else => return error.TestUnexpectedResult,
    }
    var owner_active = true;
    defer if (owner_active) idx.finishFlatCentroidDirectoryBuild(generation, .retry);

    const Waiter = struct {
        fn run(index: *HBCIndex, expected_generation: u64) !vectorindex_spfresh_index.FlatCentroidBuildClaim {
            return try index.beginFlatCentroidDirectoryBuild(expected_generation, null);
        }
    };
    var waiter = std.Io.async(io, Waiter.run, .{ &idx, generation });
    try waitForFlatCentroidBuildWaiter(&idx, generation, io);
    idx.finishFlatCentroidDirectoryBuild(generation, .retry);
    owner_active = false;
    switch (try waiter.await(io)) {
        .retry => {},
        else => return error.TestUnexpectedResult,
    }

    switch (try idx.beginFlatCentroidDirectoryBuild(generation, null)) {
        .owner => {},
        else => return error.TestUnexpectedResult,
    }
    idx.finishFlatCentroidDirectoryBuild(generation, .retry);
}

test "flat centroid build flight shares a completed stale generation result" {
    if (builtin.single_threaded or builtin.os.tag == .freestanding) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    idx.setIo(io);

    const generation = idx.publishedGeneration();
    switch (try idx.beginFlatCentroidDirectoryBuild(generation, null)) {
        .owner => {},
        else => return error.TestUnexpectedResult,
    }
    var owner_active = true;
    defer if (owner_active) idx.finishFlatCentroidDirectoryBuild(generation, .retry);

    const Waiter = struct {
        fn run(index: *HBCIndex, expected_generation: u64) !vectorindex_spfresh_index.FlatCentroidBuildClaim {
            return try index.beginFlatCentroidDirectoryBuild(expected_generation, null);
        }
    };
    var waiter = std.Io.async(io, Waiter.run, .{ &idx, generation });
    try waitForFlatCentroidBuildWaiter(&idx, generation, io);

    const directory = try alloc.create(vectorindex_spfresh_index.FlatCentroidDirectory);
    directory.* = .{ .publish_generation_snapshot = generation };
    var directory_owned = true;
    defer if (directory_owned) directory.release(alloc);
    idx.finishFlatCentroidDirectoryBuild(generation, .{ .ready = directory });
    owner_active = false;

    switch (try waiter.await(io)) {
        .ready => |shared| {
            try std.testing.expectEqual(directory, shared);
            shared.release(alloc);
        },
        else => return error.TestUnexpectedResult,
    }
    directory.release(alloc);
    directory_owned = false;
}

test "flat centroid build flight shares a deterministic producer failure" {
    if (builtin.single_threaded or builtin.os.tag == .freestanding) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    idx.setIo(io);

    const generation = idx.publishedGeneration();
    switch (try idx.beginFlatCentroidDirectoryBuild(generation, null)) {
        .owner => {},
        else => return error.TestUnexpectedResult,
    }
    var owner_active = true;
    defer if (owner_active) idx.finishFlatCentroidDirectoryBuild(generation, .retry);

    const Waiter = struct {
        fn run(index: *HBCIndex, expected_generation: u64) !vectorindex_spfresh_index.FlatCentroidBuildClaim {
            return try index.beginFlatCentroidDirectoryBuild(expected_generation, null);
        }
    };
    var waiter = std.Io.async(io, Waiter.run, .{ &idx, generation });
    try waitForFlatCentroidBuildWaiter(&idx, generation, io);
    idx.finishFlatCentroidDirectoryBuild(generation, .{ .failed = error.ResourceBudgetExceeded });
    owner_active = false;
    try std.testing.expectError(error.ResourceBudgetExceeded, waiter.await(io));

    switch (try idx.beginFlatCentroidDirectoryBuild(generation, null)) {
        .owner => {},
        else => return error.TestUnexpectedResult,
    }
    idx.finishFlatCentroidDirectoryBuild(generation, .retry);
}

test "stale flat directory build preserves the current generation cache" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = 4,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
    });
    defer idx.close();
    var id: u64 = 1;
    while (id <= 60) : (id += 1) {
        const value: f32 = @floatFromInt(id);
        try idx.insert(id, &.{ value, value / 2, value / 3, value / 4 });
    }

    const older_snapshot: vectorindex_spfresh_index.PublishedSnapshot = .{
        .root_node = idx.publishedRootNode(),
        .node_count = idx.publishedNodeCount(),
        .publish_generation = idx.publishedGeneration(),
    };
    var older_txn = try idx.beginReadTxn();
    defer older_txn.abort();

    idx.refreshPublishedSearchState();
    const current_generation = idx.publishedGeneration();
    try std.testing.expect(current_generation > older_snapshot.publish_generation);
    var current_results = try idx.searchWithRequest(.{
        .query = &.{ 1, 1, 1, 1 },
        .k = 5,
        .load_metadata = false,
    });
    current_results.deinit();

    lockAtomic(&idx.flat_centroid_mu);
    const current_directory = idx.flat_centroid_directory orelse {
        idx.flat_centroid_mu.unlock();
        return error.TestUnexpectedResult;
    };
    const cached_generation = current_directory.publish_generation_snapshot;
    idx.flat_centroid_mu.unlock();
    try std.testing.expectEqual(current_generation, cached_generation);

    var scratch_handle = try idx.acquireSearchScratch();
    defer {
        idx.refreshSearchScratchAccounting(&scratch_handle);
        idx.releaseSearchScratch(&scratch_handle);
    }
    var profile: SearchProfile = .{};
    const probes = try vectorindex_spfresh_index.selectFlatRabitqPostingsAlloc(
        &idx,
        &older_txn,
        &.{ 1, 1, 1, 1 },
        &scratch_handle,
        &profile,
        .complete_snapshot,
        older_snapshot,
        null,
        nowNs,
        elapsedSince,
    );
    try std.testing.expect(probes.len > 0);

    lockAtomic(&idx.flat_centroid_mu);
    defer idx.flat_centroid_mu.unlock();
    try std.testing.expectEqual(current_directory, idx.flat_centroid_directory.?);
    try std.testing.expectEqual(current_generation, idx.flat_centroid_directory.?.publish_generation_snapshot);
}

test "coverage and flat build flights do not block a newer generation" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    const older = idx.publishedGeneration();
    const newer = older +| 2;

    try std.testing.expect(try idx.beginCompleteCoverageValidation(older, null));
    try std.testing.expect(try idx.beginCompleteCoverageValidation(newer, null));
    idx.finishCompleteCoverageValidation(newer, true);
    idx.finishCompleteCoverageValidation(older, true);
    try std.testing.expect(idx.completeCoverageAlreadyValidated(newer));

    switch (try idx.beginFlatCentroidDirectoryBuild(older, null)) {
        .owner => {},
        else => return error.TestUnexpectedResult,
    }
    switch (try idx.beginFlatCentroidDirectoryBuild(newer, null)) {
        .owner => {},
        else => return error.TestUnexpectedResult,
    }
    idx.finishFlatCentroidDirectoryBuild(older, .retry);
    idx.finishFlatCentroidDirectoryBuild(newer, .retry);
}

test "search publication wait uses runtime wakeups and honors cancellation" {
    if (builtin.single_threaded or builtin.os.tag == .freestanding) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    idx.setIo(io);

    const Searcher = struct {
        fn run(index: *HBCIndex, cancellation: ?vectorindex_search_types.CancellationToken) !usize {
            var results = try index.searchWithRequest(.{
                .query = &.{ 0, 0 },
                .k = 1,
                .cancellation = cancellation,
            });
            defer results.deinit();
            return results.getHits().len;
        }
    };
    const FenceReader = struct {
        fn run(index: *HBCIndex, signal: *const std.atomic.Value(bool)) !void {
            try index.beginCompleteSnapshotRead(vectorindex_search_types.CancellationToken.fromAtomic(signal));
            index.endCompleteSnapshotRead();
        }
    };

    idx.beginPublishedSearchStateRefresh();
    var publication_active = true;
    defer if (publication_active) idx.abortPublishedSearchStateRefresh();

    // The writer fence is acquired before generation becomes odd. A
    // pessimistic complete-search retry must also wait cooperatively and honor
    // cancellation during this preparation window.
    var fence_cancelled = std.atomic.Value(bool).init(false);
    var fence_reader = std.Io.async(io, FenceReader.run, .{ &idx, &fence_cancelled });
    try io.sleep(std.Io.Duration.fromMilliseconds(10), .awake);
    fence_cancelled.store(true, .release);
    try std.testing.expectError(error.Cancelled, fence_reader.await(io));

    try idx.markPublishedSearchStateCommitting();
    const first_odd_generation = idx.publishedGeneration();
    try std.testing.expect((first_odd_generation & 1) != 0);

    var cancelled = std.atomic.Value(bool).init(false);
    var cancelled_search = std.Io.async(io, Searcher.run, .{
        &idx,
        vectorindex_search_types.CancellationToken.fromAtomic(&cancelled),
    });
    try io.sleep(std.Io.Duration.fromMilliseconds(10), .awake);
    cancelled.store(true, .release);
    try std.testing.expectError(error.Cancelled, cancelled_search.await(io));

    // Cancellation releases only the reader reference. The publisher still
    // owns the flight and can complete the aborted generation normally.
    idx.abortPublishedSearchStateRefresh();
    publication_active = false;
    try std.testing.expect((idx.publishedGeneration() & 1) == 0);
    try std.testing.expect(idx.published_flight == null);
    try std.testing.expect(idx.published_spare_flight != null);

    // Reuse the same flight for a second generation and prove that a normal
    // waiter wakes immediately when publication becomes stable.
    idx.beginPublishedSearchStateRefresh();
    try idx.markPublishedSearchStateCommitting();
    publication_active = true;
    const second_odd_generation = idx.publishedGeneration();
    try std.testing.expect(second_odd_generation > first_odd_generation);
    var waiting_search = std.Io.async(io, Searcher.run, .{ &idx, null });
    try io.sleep(std.Io.Duration.fromMilliseconds(10), .awake);
    idx.finishPublishedSearchStateRefresh();
    publication_active = false;
    try std.testing.expectEqual(@as(usize, 0), try waiting_search.await(io));
    try std.testing.expect((idx.publishedGeneration() & 1) == 0);
    try std.testing.expect(idx.published_flight == null);
    try std.testing.expect(idx.published_spare_flight != null);

    // A waiter may retain the old flight after its generation is stable. The
    // next publisher must safely allocate and initialize an overflow flight;
    // releasing either generation later must preserve exactly one spare.
    idx.beginPublishedSearchStateRefresh();
    try idx.markPublishedSearchStateCommitting();
    publication_active = true;
    lockAtomic(&idx.published_flight_mu);
    const retained_flight = idx.published_flight.?;
    retained_flight.refs += 1;
    idx.published_flight_mu.unlock();
    idx.finishPublishedSearchStateRefresh();
    publication_active = false;
    try std.testing.expect(idx.published_spare_flight == null);

    idx.beginPublishedSearchStateRefresh();
    try idx.markPublishedSearchStateCommitting();
    publication_active = true;
    const overflow_flight = idx.published_flight.?;
    try std.testing.expect(overflow_flight != retained_flight);
    try std.testing.expect(!overflow_flight.ready.isSet());
    try std.testing.expectEqual(@as(usize, 1), overflow_flight.refs);
    idx.abortPublishedSearchStateRefresh();
    publication_active = false;
    idx.releasePublishedSearchStateFlightRef(retained_flight);
    try std.testing.expect(idx.published_flight == null);
    try std.testing.expect(idx.published_spare_flight != null);
}

test "complete snapshot retry releases publication fence after durable txn capture" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 64,
        .branching_factor = 4,
        .use_quantization = false,
    });
    defer idx.close();
    try idx.insert(1, &.{ 0, 0 });
    try idx.insert(2, &.{ 1, 0 });
    try idx.insert(3, &.{ 0, 1 });
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();

    const HookContext = struct {
        io: std.Io,
        captures: usize = 0,
        optimistic_capture_unfenced: bool = false,
        retry_capture_released_fence: bool = false,
        writer_failed: std.atomic.Value(bool) = std.atomic.Value(bool).init(false),

        fn publishConcurrentInsert(index: *HBCIndex, vector_id: u64, failed: *std.atomic.Value(bool)) void {
            index.insert(vector_id, &.{ 1, 1 }) catch failed.store(true, .release);
        }

        fn onCapture(raw_ctx: ?*anyopaque, index: *HBCIndex) void {
            const ctx: *@This() = @ptrCast(@alignCast(raw_ctx.?));
            ctx.captures += 1;
            if (ctx.captures == 1) {
                if (!index.published_snapshot_mu.tryLockExclusive()) return;
                index.published_snapshot_mu.unlockExclusive();
                ctx.optimistic_capture_unfenced = true;

                // Publish a real concurrent mutation after capture. The
                // optimistic attempt must discard its result and retry from a
                // durable MVCC transaction.
                var writer = std.Io.async(ctx.io, publishConcurrentInsert, .{ index, 4, &ctx.writer_failed });
                writer.await(ctx.io);
                return;
            }
            // The retry hook runs immediately after transaction capture. The
            // shared fence must already be released, and a second publisher
            // must complete while the search continues on the older snapshot.
            if (!index.published_snapshot_mu.tryLockExclusive()) return;
            index.published_snapshot_mu.unlockExclusive();
            ctx.retry_capture_released_fence = true;
            var writer = std.Io.async(ctx.io, publishConcurrentInsert, .{ index, 5, &ctx.writer_failed });
            writer.await(ctx.io);
        }
    };
    var hook_ctx = HookContext{ .io = io };
    test_complete_snapshot_capture_ctx = &hook_ctx;
    test_complete_snapshot_capture_hook = HookContext.onCapture;
    defer {
        test_complete_snapshot_capture_ctx = null;
        test_complete_snapshot_capture_hook = null;
    }

    var results = try idx.searchWithRequest(.{
        .query = &.{ 0, 0 },
        .k = 4,
        .search_effort = 1,
        .load_metadata = false,
    });
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 2), hook_ctx.captures);
    try std.testing.expect(hook_ctx.optimistic_capture_unfenced);
    try std.testing.expect(hook_ctx.retry_capture_released_fence);
    try std.testing.expect(!hook_ctx.writer_failed.load(.acquire));
    try std.testing.expectEqual(@as(u64, 5), idx.stats().active_count);
    try std.testing.expectEqual(@as(usize, 4), results.getHits().len);
    for (results.getHits()) |hit| try std.testing.expect(hit.vector_id != 5);
    try std.testing.expect(!idx.generationRepairPending());
}

test "durable snapshot captures a publisher immediately before its fence" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 64,
        .branching_factor = 4,
        .use_quantization = false,
    });
    defer idx.close();
    try idx.insert(1, &.{ 0, 0 });
    try idx.insert(2, &.{ 1, 0 });
    try idx.insert(3, &.{ 0, 1 });
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();

    const HookContext = struct {
        io: std.Io,
        captures: usize = 0,
        before_durable_captures: usize = 0,
        writer_failed: std.atomic.Value(bool) = .init(false),

        fn publishConcurrentInsert(index: *HBCIndex, vector_id: u64, failed: *std.atomic.Value(bool)) void {
            index.insert(vector_id, &.{ 1, 1 }) catch failed.store(true, .release);
        }

        fn publishAndWait(self: *@This(), index: *HBCIndex, vector_id: u64) void {
            var writer = std.Io.async(self.io, publishConcurrentInsert, .{ index, vector_id, &self.writer_failed });
            writer.await(self.io);
        }

        fn onCapture(raw_ctx: ?*anyopaque, index: *HBCIndex) void {
            const self: *@This() = @ptrCast(@alignCast(raw_ctx.?));
            self.captures += 1;
            if (self.captures == 1) self.publishAndWait(index, 4);
        }

        fn beforeDurableCapture(raw_ctx: ?*anyopaque, index: *HBCIndex) void {
            const self: *@This() = @ptrCast(@alignCast(raw_ctx.?));
            self.before_durable_captures += 1;
            self.publishAndWait(index, 5);
        }
    };
    var hook_ctx = HookContext{ .io = io_impl.io() };
    test_complete_snapshot_capture_ctx = &hook_ctx;
    test_complete_snapshot_capture_hook = HookContext.onCapture;
    test_before_durable_snapshot_capture_ctx = &hook_ctx;
    test_before_durable_snapshot_capture_hook = HookContext.beforeDurableCapture;
    defer {
        test_complete_snapshot_capture_ctx = null;
        test_complete_snapshot_capture_hook = null;
        test_before_durable_snapshot_capture_ctx = null;
        test_before_durable_snapshot_capture_hook = null;
    }

    var results = try idx.searchWithRequest(.{
        .query = &.{ 0, 0 },
        .k = 5,
        .search_effort = 1,
        .load_metadata = false,
    });
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 2), hook_ctx.captures);
    try std.testing.expectEqual(@as(usize, 1), hook_ctx.before_durable_captures);
    try std.testing.expect(!hook_ctx.writer_failed.load(.acquire));
    try std.testing.expectEqual(@as(u64, 5), idx.stats().active_count);
    try std.testing.expectEqual(@as(usize, 5), results.getHits().len);
}

test "durable incomplete snapshot terminates when publication advances during traversal" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 64,
        .branching_factor = 4,
        .use_quantization = false,
    });
    defer idx.close();
    try idx.insert(1, &.{ 0, 0 });
    try idx.insert(2, &.{ 1, 0 });
    try idx.insert(3, &.{ 0, 1 });
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var key_buf: [10]u8 = undefined;
        try idx.deleteNamespaced(&txn, .vecs, encodeVecKey(&key_buf, 1));
        try txn.commit();
    }
    idx.invalidateVectorCache(1);

    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    const HookContext = struct {
        io: std.Io,
        captures: usize = 0,
        writer_failed: std.atomic.Value(bool) = .init(false),

        fn publishConcurrentInsert(index: *HBCIndex, vector_id: u64, failed: *std.atomic.Value(bool)) void {
            index.insert(vector_id, &.{ 1, 1 }) catch failed.store(true, .release);
        }

        fn onCapture(raw_ctx: ?*anyopaque, index: *HBCIndex) void {
            const ctx: *@This() = @ptrCast(@alignCast(raw_ctx.?));
            ctx.captures += 1;
            // Keep both the optimistic and durable corrupt snapshots stale.
            // A durable failure must still terminate; retrying it can livelock
            // indefinitely when publishers remain active.
            if (ctx.captures > 2) return;
            var writer = std.Io.async(
                ctx.io,
                publishConcurrentInsert,
                .{ index, @as(u64, 3) + ctx.captures, &ctx.writer_failed },
            );
            writer.await(ctx.io);
        }
    };
    var hook_ctx = HookContext{ .io = io };
    test_complete_snapshot_capture_ctx = &hook_ctx;
    test_complete_snapshot_capture_hook = HookContext.onCapture;
    defer {
        test_complete_snapshot_capture_ctx = null;
        test_complete_snapshot_capture_hook = null;
    }

    try std.testing.expectError(
        error.IncompletePublishedSnapshot,
        idx.searchWithRequest(.{
            .query = &.{ 0, 0 },
            .k = 3,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );
    try std.testing.expectEqual(@as(usize, 2), hook_ctx.captures);
    try std.testing.expect(!hook_ctx.writer_failed.load(.acquire));
    try std.testing.expectEqual(@as(u64, 5), idx.stats().active_count);
    try std.testing.expect(!idx.generationRepairPending());
}

test "aborted published transaction cannot leak staged topology through caches" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 64,
        .branching_factor = 4,
        .use_quantization = false,
    });
    defer idx.close();
    try idx.insertWithMetadata(1, &.{ 0, 0 }, "committed:1");
    try idx.insertWithMetadata(2, &.{ 1, 0 }, "committed:2");
    try idx.insertWithMetadata(3, &.{ 0, 1 }, "committed:3");
    const warmed_metadata = (try idx.getMetadata(1)) orelse return error.TestUnexpectedResult;
    defer alloc.free(warmed_metadata);
    var warmed = idx.borrowCachedMetadata(1) orelse return error.TestUnexpectedResult;
    warmed.deinit();

    const epoch_before = idx.publishedMutationEpoch();
    {
        var txn = try idx.beginWriteTxn();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try root.ensureUnbacked(alloc);
        const staged_members = try alloc.dupe(u64, root.members[0..1]);
        alloc.free(root.members);
        root.members = staged_members;
        try idx.saveNode(&txn, &root);
        try idx.putMetadata(&txn, 1, "staged:1");
        try std.testing.expect(idx.borrowCachedMetadata(1) == null);
        idx.metadata.active_count = 1;
        txn.abort();
    }

    const epoch_after = idx.publishedMutationEpoch();
    try std.testing.expectEqual(epoch_before + 2, epoch_after);
    try std.testing.expectEqual(@as(u64, 0), epoch_after & 1);
    try std.testing.expectEqual(@as(u64, 3), idx.stats().active_count);
    try std.testing.expect(idx.borrowCachedMetadata(1) == null);

    var results = try idx.searchWithRequest(.{
        .query = &.{ 0, 0 },
        .k = 3,
        .search_effort = 1,
        .load_metadata = true,
    });
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 3), results.getHits().len);
    var found_committed_metadata = false;
    for (results.getHits()) |hit| {
        if (hit.vector_id != 1) continue;
        const metadata = hit.metadata orelse return error.TestUnexpectedResult;
        try std.testing.expectEqualStrings("committed:1", metadata);
        found_committed_metadata = true;
    }
    try std.testing.expect(found_committed_metadata);
    try std.testing.expect(!idx.generationRepairPending());
}

test "root leaf complete snapshot rejects a missing referenced vector" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 64,
        .branching_factor = 4,
        .use_quantization = false,
    });
    defer idx.close();

    try idx.insert(1, &.{ 0, 0 });
    try idx.insert(2, &.{ 1, 0 });
    try idx.insert(3, &.{ 0, 1 });

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var key_buf: [10]u8 = undefined;
        try idx.deleteNamespaced(&txn, .vecs, encodeVecKey(&key_buf, 1));
        try txn.commit();
    }
    idx.invalidateVectorCache(1);

    var partial = try idx.searchWithRequest(.{
        .query = &.{ 0, 0 },
        .k = 3,
        .load_metadata = false,
    });
    defer partial.deinit();
    try std.testing.expectEqual(@as(usize, 2), partial.getHits().len);

    try std.testing.expectError(
        error.IncompletePublishedSnapshot,
        idx.searchWithRequest(.{
            .query = &.{ 0, 0 },
            .k = 3,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );
}

test "hbc duplicate child links are dropped by unlink and repair" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 4,
        .branching_factor = 4,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0xd00b_1e5);
    const random = prng.random();
    var id: u64 = 1;
    while (id <= 40) : (id += 1) {
        var v: [4]f32 = undefined;
        for (&v) |*x| x.* = random.float(f32) * 2.0 - 1.0;
        try idx.insert(id, &v);
    }

    const victim_leaf = (try idx.debugLeafForVector(3)) orelse return error.TestUnexpectedResult;
    const victim_members = try idx.debugLeafMembers(alloc, victim_leaf);
    defer alloc.free(victim_members);
    try std.testing.expect(victim_members.len > 0);

    const corrupt = struct {
        fn duplicateChildLink(index: *HBCIndex, leaf_id: u64) !void {
            var txn = try index.beginWriteTxn();
            errdefer txn.abort();
            var leaf = try index.loadNode(&txn, leaf_id);
            defer leaf.deinit(index.alloc);
            const parent_id = leaf.parent;
            try std.testing.expect(parent_id != 0);
            var parent = try index.loadNode(&txn, parent_id);
            defer parent.deinit(index.alloc);
            try parent.ensureUnbacked(index.alloc);
            const dup = try index.alloc.alloc(u64, parent.children.len + 1);
            @memcpy(dup[0..parent.children.len], parent.children);
            dup[parent.children.len] = leaf_id;
            index.alloc.free(parent.children);
            parent.children = dup;
            try index.saveNode(&txn, &parent);
            try txn.commit();
        }
    }.duplicateChildLink;

    // Repair path: the sweep must drop the duplicate occurrence.
    try corrupt(&idx, victim_leaf);
    {
        const broken = try idx.verifyTreeLinks();
        try std.testing.expect(!broken.consistent());
    }
    try std.testing.expectError(
        error.IncompletePublishedSnapshot,
        idx.searchWithRequest(.{
            .query = &.{ 0.1, 0.2, 0.3, 0.4 },
            .k = 5,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );
    const repair = try idx.repairTreeLinks(10_000);
    try std.testing.expect(repair.completed);
    try std.testing.expect(repair.duplicate_children_removed >= 1);
    {
        const healed = try idx.verifyTreeLinks();
        try std.testing.expect(healed.consistent());
    }

    // Unlink path: emptying the leaf drives removeChildLink against the
    // duplicated reference, which must drop BOTH occurrences — an
    // underfilled rebuild here used to persist an uninitialized child id.
    try corrupt(&idx, victim_leaf);
    try idx.batchDelete(victim_members);
    {
        const after_delete = try idx.verifyTreeLinks();
        if (!after_delete.consistent()) {
            std.debug.print("tree links inconsistent after duplicate unlink: {any}\n", .{after_delete});
            return error.TestUnexpectedResult;
        }
    }
}

test "default random ortho transform matches go hbc" {
    const alloc = std.testing.allocator;
    const input = [_]f32{ 1.0, 2.0, 3.0, 4.0 };

    {
        var tp: TestPath = .{};
        const path = tp.init();
        defer tp.cleanup();

        var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
        defer idx.close();

        var transformed: [4]f32 = undefined;
        _ = idx.transformVector(&input, &transformed);
        try std.testing.expectEqual(vec.RotAlgorithm.none, idx.rot.algo);
        try std.testing.expectEqualSlices(f32, &input, &transformed);
    }

    {
        var tp: TestPath = .{};
        const path = tp.init();
        defer tp.cleanup();

        var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .use_random_ortho_trans = true });
        defer idx.close();

        var transformed: [4]f32 = undefined;
        _ = idx.transformVector(&input, &transformed);
        try std.testing.expectEqual(vec.RotAlgorithm.givens, idx.rot.algo);
        try std.testing.expect(!std.mem.eql(f32, &input, &transformed));
    }
}

test "hbc shared cache namespaces entries" {
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();

    const ns_a = hbcCacheNamespace("/tmp/hbc-a");
    const ns_b = hbcCacheNamespace("/tmp/hbc-b");
    const vec_a = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const vec_b = [_]f32{ 5.0, 6.0, 7.0, 8.0 };

    _ = try cache.cacheVector(ns_a, 7, &vec_a);
    _ = try cache.cacheVector(ns_b, 7, &vec_b);

    try expectSharedVectorCached(&cache, ns_a, 7, &vec_a);
    try expectSharedVectorCached(&cache, ns_b, 7, &vec_b);

    cache.invalidateNamespace(ns_a);
    try expectSharedVectorNotCached(&cache, ns_a, 7);
    try expectSharedVectorCached(&cache, ns_b, 7, &vec_b);
}

test "hbc shared cache rejects node quantized and metadata fills from an older publication" {
    const alloc = std.testing.allocator;
    var cache = Cache.init(alloc);
    defer cache.deinit();
    const namespace = hbcCacheNamespace("/tmp/hbc-publication-fill-guard");
    var epoch = std.atomic.Value(u64).init(0);

    var current_centroid = [_]f32{ 9, 9 };
    const current_node = Node{
        .id = 7,
        .is_leaf = true,
        .level = 0,
        .parent = 9,
        .centroid = &current_centroid,
        .children = &.{},
        .members = &.{},
    };
    var stale_centroid = [_]f32{ 1, 1 };
    const stale_node = Node{
        .id = 7,
        .is_leaf = true,
        .level = 0,
        .parent = 1,
        .centroid = &stale_centroid,
        .children = &.{},
        .members = &.{},
    };
    var current_vectors = [_]f32{ 9, 9 };
    const current_quantized: QuantizedSet = .{ .nonquant = .{ .vectors = .{
        .dims = 2,
        .count = 1,
        .data = &current_vectors,
    } } };
    var stale_vectors = [_]f32{ 1, 1 };
    const stale_quantized: QuantizedSet = .{ .nonquant = .{ .vectors = .{
        .dims = 2,
        .count = 1,
        .data = &stale_vectors,
    } } };

    try std.testing.expect(try cache.cacheNode(namespace, &current_node));
    try std.testing.expect(try cache.cacheQuantized(namespace, 7, &current_quantized));
    _ = try cache.cacheMetadata(namespace, 7, "current");

    // Generation 0 was captured before the writer's publication. Generation 2
    // is now live; a delayed MVCC reader must not replace any current entry.
    epoch.store(2, .release);
    try std.testing.expect(!try cache.cacheNodeGuarded(namespace, &stale_node, &epoch, 0));
    try std.testing.expect(!try cache.cacheQuantizedGuarded(namespace, 7, &stale_quantized, &epoch, 0));
    _ = try cache.cacheMetadataGuarded(namespace, 7, "stale", &epoch, 0);

    var node = cache.borrowNode(namespace, 7).?;
    defer node.deinit();
    try std.testing.expectEqual(@as(u64, 9), node.ptr().parent);
    var quantized = cache.borrowQuantized(namespace, 7).?;
    defer quantized.deinit();
    switch (quantized.ptr().*) {
        .nonquant => |set| try std.testing.expectEqualSlices(f32, &current_vectors, set.vectors.data),
        .rabit => return error.TestUnexpectedResult,
    }
    var metadata = cache.borrowMetadata(namespace, 7).?;
    defer metadata.deinit();
    try std.testing.expectEqualStrings("current", metadata.view());
}

test "hbc shared cache evicts across namespaces under one resource budget" {
    const vector_bytes = estimateVectorCacheBytes(&.{ 1.0, 2.0, 3.0, 4.0 });
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes,
        .hard_limit_bytes = vector_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);

    const ns_a = hbcCacheNamespace("/tmp/hbc-a");
    const ns_b = hbcCacheNamespace("/tmp/hbc-b");
    _ = try cache.cacheVector(ns_a, 1, &.{ 1.0, 2.0, 3.0, 4.0 });
    _ = try cache.cacheVector(ns_b, 1, &.{ 5.0, 6.0, 7.0, 8.0 });

    try expectSharedVectorNotCached(&cache, ns_a, 1);
    try expectSharedVectorCached(&cache, ns_b, 1, &.{ 5.0, 6.0, 7.0, 8.0 });
    try std.testing.expectEqual(@as(u64, 0), cache.namespaceStats(ns_a).vector.used_bytes);
    try std.testing.expectEqual(vector_bytes, cache.namespaceStats(ns_b).vector.used_bytes);
    try std.testing.expectEqual(vector_bytes, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
}

test "hbc shared cache CLOCK refreshes recency on borrowed vector hits" {
    const vector_bytes = estimateVectorCacheBytes(&.{ 1.0, 2.0, 3.0, 4.0 });
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = 2 * vector_bytes,
        .hard_limit_bytes = 2 * vector_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(std.testing.allocator);
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    const namespace = hbcCacheNamespace("/tmp/hbc-clock-hit");

    _ = try cache.cacheVector(namespace, 1, &.{ 1.0, 2.0, 3.0, 4.0 });
    _ = try cache.cacheVector(namespace, 2, &.{ 5.0, 6.0, 7.0, 8.0 });

    // Complete one CLOCK pass so both insertion reference bits are cold, then
    // refresh vector 1 through the same retained-borrow path used by rerank.
    cache.mutex.lockExclusive();
    _ = Cache.nextVictim(&cache.vector_clock, &cache.vector_hand, .{ .namespace = 0, .id = 0 });
    cache.mutex.unlockExclusive();
    var hot = cache.borrowVector(namespace, 1).?;
    hot.deinit();
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(namespace).vector.hits);

    cache.attachResourceManager(&resource_manager);

    _ = try cache.cacheVector(namespace, 3, &.{ 9.0, 10.0, 11.0, 12.0 });
    try expectSharedVectorCached(&cache, namespace, 1, &.{ 1.0, 2.0, 3.0, 4.0 });
    try expectSharedVectorNotCached(&cache, namespace, 2);
    try expectSharedVectorCached(&cache, namespace, 3, &.{ 9.0, 10.0, 11.0, 12.0 });
}

test "hbc shared vector replacement cannot return an older external value" {
    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    defer resource_manager.deinit(std.testing.allocator);
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    const namespace = hbcCacheNamespace("/tmp/hbc-vector-replacement");

    _ = try cache.cacheVector(namespace, 7, &.{ 1.0, 2.0, 3.0, 4.0 });
    _ = try cache.cacheVector(namespace, 7, &.{ 9.0, 8.0, 7.0, 6.0 });
    var retained = cache.borrowVector(namespace, 7).?;
    defer retained.deinit();
    try std.testing.expectEqualSlices(f32, &.{ 9.0, 8.0, 7.0, 6.0 }, retained.view());
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(namespace).vector.replacements);
}

test "hbc shared vector leases remain coherent during invalidate and replacement" {
    const Reader = struct {
        fn run(
            cache: *Cache,
            namespace: u64,
            ready: *std.atomic.Value(u32),
            start: *std.atomic.Value(bool),
            stop: *std.atomic.Value(bool),
            borrows: *std.atomic.Value(u64),
            failed: *std.atomic.Value(bool),
        ) void {
            const value_a = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
            const value_b = [_]f32{ 9.0, 8.0, 7.0, 6.0 };
            _ = ready.fetchAdd(1, .release);
            while (!start.load(.acquire)) std.atomic.spinLoopHint();
            while (!stop.load(.acquire)) {
                if (cache.borrowVector(namespace, 7)) |lease_value| {
                    var lease = lease_value;
                    _ = borrows.fetchAdd(1, .monotonic);
                    const view = lease.view();
                    if (!std.mem.eql(f32, view, &value_a) and !std.mem.eql(f32, view, &value_b)) {
                        failed.store(true, .release);
                    }
                    lease.deinit();
                }
            }
        }
    };

    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    const namespace = hbcCacheNamespace("/tmp/hbc-vector-lease-replacement-stress");
    const value_a = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const value_b = [_]f32{ 9.0, 8.0, 7.0, 6.0 };
    _ = try cache.cacheVector(namespace, 7, &value_a);

    var ready = std.atomic.Value(u32).init(0);
    var start = std.atomic.Value(bool).init(false);
    var stop = std.atomic.Value(bool).init(false);
    var borrows = std.atomic.Value(u64).init(0);
    var failed = std.atomic.Value(bool).init(false);
    var readers: [8]std.Thread = undefined;
    for (&readers) |*reader| {
        reader.* = try std.Thread.spawn(.{}, Reader.run, .{
            &cache,
            namespace,
            &ready,
            &start,
            &stop,
            &borrows,
            &failed,
        });
    }
    while (ready.load(.acquire) != readers.len) std.atomic.spinLoopHint();
    start.store(true, .release);
    while (borrows.load(.acquire) == 0) std.atomic.spinLoopHint();

    for (0..512) |iteration| {
        if (iteration % 4 == 0) cache.invalidateVector(namespace, 7);
        const value = if (iteration & 1 == 0) &value_a else &value_b;
        _ = cache.cacheVector(namespace, 7, value) catch {
            failed.store(true, .release);
            break;
        };
    }

    stop.store(true, .release);
    for (&readers) |*reader| reader.join();
    try std.testing.expect(!failed.load(.acquire));
    try std.testing.expect(borrows.load(.acquire) > 0);
}

test "hbc vector fill captured before a committed mutation cannot repopulate stale data" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var cache = Cache.init(alloc);
    defer cache.deinit();
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    defer idx.close();
    idx.attachSharedCache(&cache);

    const old_vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const new_vector = [_]f32{ 9.0, 8.0, 7.0, 6.0 };
    _ = try idx.cacheVector(7, &old_vector);
    const stale_fill_epoch = idx.beginVectorCacheFill(7).?;

    idx.invalidateVectorCache(7);
    idx.abortVectorCacheMutations(); // models commit/abort publication completion
    _ = try idx.cacheVector(7, &new_vector);
    _ = try idx.cacheVectorIfFillCurrent(7, &old_vector, stale_fill_epoch);

    var retained = idx.borrowCachedVector(7).?;
    defer retained.deinit();
    try std.testing.expectEqualSlices(f32, &new_vector, retained.view());
}

test "hbc shared detached leases remain physically accounted until release" {
    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    defer resource_manager.deinit(std.testing.allocator);
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    const namespace = hbcCacheNamespace("/tmp/hbc-pinned-accounting");
    const other_namespace = hbcCacheNamespace("/tmp/hbc-pinned-accounting-other");
    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const bytes = estimateVectorCacheBytes(&vector);

    _ = try cache.cacheVector(namespace, 1, &vector);
    var retained = cache.borrowVector(namespace, 1).?;
    cache.invalidateVector(namespace, 1);
    try std.testing.expectEqual(bytes, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(bytes, cache.namespaceStats(namespace).pinned_bytes);
    try std.testing.expectEqual(bytes, cache.namespaceStats(namespace).accounted_bytes);
    try std.testing.expectEqual(@as(u64, 0), cache.namespaceStats(other_namespace).pinned_bytes);
    retained.deinit();
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(@as(u64, 0), cache.namespaceStats(namespace).pinned_bytes);
}

test "hbc retained node and quantized handles survive threaded eviction" {
    const alloc = std.testing.allocator;
    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    const namespace = hbcCacheNamespace("/tmp/hbc-retained-routing-eviction");

    var centroid = [_]f32{ 1.0, 2.0 };
    var children = [_]u64{7};
    const node = Node{
        .id = 3,
        .is_leaf = false,
        .level = 1,
        .parent = 0,
        .centroid = &centroid,
        .children = &children,
        .members = &.{},
    };
    var vectors = [_]f32{ 3.0, 4.0 };
    const quantized: QuantizedSet = .{ .nonquant = .{ .vectors = .{
        .dims = 2,
        .count = 1,
        .data = &vectors,
    } } };
    try std.testing.expect(try cache.cacheNode(namespace, &node));
    try std.testing.expect(try cache.cacheQuantized(namespace, node.id, &quantized));

    var node_lease = cache.borrowNode(namespace, node.id).?;
    var quantized_lease = cache.borrowQuantized(namespace, node.id).?;
    const Evict = struct {
        fn run(target: *Cache, ns: u64, node_id: u64) void {
            target.invalidateNode(ns, node_id);
            target.invalidateQuantized(ns, node_id);
        }
    };
    const evictor = try std.Thread.spawn(.{}, Evict.run, .{ &cache, namespace, node.id });
    evictor.join();

    try std.testing.expectEqual(@as(u64, 3), node_lease.ptr().id);
    try std.testing.expectEqualSlices(f32, &centroid, node_lease.ptr().centroid);
    switch (quantized_lease.ptr().*) {
        .nonquant => |set| try std.testing.expectEqualSlices(f32, &vectors, set.vectors.data),
        .rabit => return error.TestUnexpectedResult,
    }
    try std.testing.expect(cache.namespaceStats(namespace).pinned_bytes > 0);

    node_lease.deinit();
    quantized_lease.deinit();
    try std.testing.expectEqual(@as(u64, 0), cache.namespaceStats(namespace).pinned_bytes);
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
}

test "hbc standalone detached leases remain physically accounted until release" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    defer resource_manager.deinit(std.testing.allocator);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const bytes = estimateVectorCacheBytes(&vector);

    _ = try idx.cacheVector(1, &vector);
    var retained = idx.borrowCachedVector(1).?;
    idx.invalidateVectorCache(1);
    try std.testing.expectEqual(bytes, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(bytes, idx.hbcCacheStats().pinned_bytes);
    retained.deinit();
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().pinned_bytes);
}

test "hbc standalone cache yields to foreground aggregate admission" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const bytes = estimateVectorCacheBytes(&vector);
    var resource_manager = resource_manager_mod.ResourceManager.init(.{
        .memory_budget = .{ .soft_limit_bytes = bytes, .hard_limit_bytes = bytes + 1 },
    });
    defer resource_manager.deinit(std.testing.allocator);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);

    _ = try idx.cacheVector(1, &vector);
    var foreground = try resource_manager.reserve(.dense_apply_working_set, 2);
    defer foreground.release();
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
    try std.testing.expectEqual(@as(u64, 2), resource_manager.snapshot().memory.used_bytes);
}

test "hbc concurrent vector admission samples at a full steady target" {
    const vector_bytes = estimateVectorCacheBytes(&.{ 1.0, 2.0, 3.0, 4.0 });
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes,
        .hard_limit_bytes = vector_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(std.testing.allocator);
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    const namespace = hbcCacheNamespace("/tmp/hbc-full-admission");
    _ = try cache.cacheVector(namespace, 1, &.{ 1.0, 2.0, 3.0, 4.0 });

    var admitted: usize = 0;
    for (0..8) |_| if (cache.shouldAdmitConcurrentVector(namespace)) {
        admitted += 1;
    };
    try std.testing.expectEqual(@as(usize, 1), admitted);
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(namespace).vector.sampled_admissions);
}

test "hbc decoded residency lease reserves a complete query and bypasses mid-query sampling" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const first = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const second = [_]f32{ 5.0, 6.0, 7.0, 8.0 };
    const vector_bytes = estimateVectorCacheBytes(&first);
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes * 2,
        .hard_limit_bytes = vector_bytes * 2,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    var abandoned = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
    try std.testing.expectEqual(@as(u64, 1), cache.decoded_query_active_leases.load(.acquire));
    try std.testing.expect(abandoned.ensureCapacity(vector_bytes));
    abandoned.deinit();
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_active_leases.load(.acquire));
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_reserved_bytes.load(.acquire));
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);

    var lease = idx.acquireDecodedVectorResidency(2) orelse return error.TestUnexpectedResult;
    defer lease.deinit();
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_reserved_bytes.load(.acquire));
    try std.testing.expect(lease.ensureCapacity(vector_bytes * 2));
    try std.testing.expectEqual(vector_bytes * 2, cache.decoded_query_reserved_bytes.load(.acquire));

    // The precharge is authoritative ResourceManager usage. An unrelated
    // optional insertion cannot consume the promised bytes before publication.
    _ = try cache.cacheMetadata(idx.cache_namespace, 99, "not admitted");
    try std.testing.expectEqual(@as(u64, 0), cache.namespaceStats(idx.cache_namespace).metadata.used_bytes);
    try std.testing.expectEqual(vector_bytes * 2, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);

    _ = try idx.cacheVectorForResidencyLease(&lease, 1, &first);
    _ = try idx.cacheVectorForResidencyLease(&lease, 2, &second);

    try expectVectorCached(&idx, 1);
    try expectVectorCached(&idx, 2);
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_reserved_bytes.load(.acquire));
    try std.testing.expectEqual(vector_bytes * 2, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
}

test "hbc sequential cold-start leases fill eagerly and sample replacement at capacity" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const vector_bytes = estimateVectorCacheBytes(&vector);
    const fill_count = 4;
    const target_bytes = vector_bytes * fill_count;
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = target_bytes,
        .hard_limit_bytes = target_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = vector.len, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    // A serial calibration pass owns at most one lease at a time and should
    // populate decoded residency eagerly until it reaches the byte target.
    for (0..fill_count) |i| {
        var lease = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
        try std.testing.expectEqual(@as(u64, 1), cache.decoded_query_active_leases.load(.acquire));
        try std.testing.expect(lease.ensureCapacity(vector_bytes));
        _ = try idx.cacheVectorForResidencyLease(&lease, @intCast(i + 1), &vector);
        lease.deinit();
        try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_active_leases.load(.acquire));
    }
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_entitled_bytes.load(.acquire));
    try std.testing.expectEqual(target_bytes, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(@as(u64, 0), cache.namespaceStats(idx.cache_namespace).vector.sampled_admissions);

    // At steady saturation, one ticket receives one bounded replacement
    // window. Other overlapping requests cannot all retain stale full-query
    // entitlements and rotate the resident set behind it.
    var replacement = idx.acquireDecodedVectorResidency(fill_count) orelse return error.TestUnexpectedResult;
    defer replacement.deinit();
    try std.testing.expectEqual(vector_bytes, replacement.capacity_bytes);
    try std.testing.expectEqual(vector_bytes, cache.decoded_query_replacement_entitled_bytes.load(.acquire));
    for (0..7) |_| try std.testing.expect(idx.acquireDecodedVectorResidency(fill_count) == null);
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(idx.cache_namespace).vector.sampled_admissions);

    try std.testing.expect(replacement.ensureCapacity(vector_bytes));
    _ = try idx.cacheVectorForResidencyLease(&replacement, 100, &vector);
    try expectVectorCached(&idx, 100);
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_entitled_bytes.load(.acquire));
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_replacement_entitled_bytes.load(.acquire));
    try std.testing.expectEqual(target_bytes, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
}

test "hbc overlapping cold-start leases activate the admission doorkeeper" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const vector_bytes = estimateVectorCacheBytes(&vector);
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes * 16,
        .hard_limit_bytes = vector_bytes * 16,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = vector.len, .max_cached_vectors = 32 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    var first = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
    defer first.deinit();
    try std.testing.expectEqual(@as(u64, 1), cache.decoded_query_active_leases.load(.acquire));

    // Ticket zero admits one bounded overlapping fill. The following seven
    // requests retain LSM residency instead of joining the publication wave.
    var sampled = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
    defer sampled.deinit();
    for (0..7) |_| try std.testing.expect(idx.acquireDecodedVectorResidency(1) == null);
    try std.testing.expectEqual(@as(u64, 2), cache.decoded_query_active_leases.load(.acquire));
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(idx.cache_namespace).vector.sampled_admissions);

    sampled.deinit();
    first.deinit();
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_active_leases.load(.acquire));
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_entitled_bytes.load(.acquire));

    // Once overlap ends, normal-pressure admission is eager again regardless
    // of the doorkeeper ticket's current phase.
    var serial = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
    serial.deinit();
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(idx.cache_namespace).vector.sampled_admissions);
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
}

test "hbc concurrent cold-start lease acquisition remains bounded" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const worker_count = 8;
    const vector_bytes = estimateVectorCacheBytes(&.{ 1.0, 2.0, 3.0, 4.0 });
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes * 32,
        .hard_limit_bytes = vector_bytes * 32,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 64 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    var first = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
    defer first.deinit();

    const Worker = struct {
        fn run(index: *HBCIndex, start: *std.atomic.Value(bool), admitted: *std.atomic.Value(u32)) void {
            while (!start.load(.acquire)) std.atomic.spinLoopHint();
            if (index.acquireDecodedVectorResidency(1)) |lease_value| {
                var lease = lease_value;
                _ = admitted.fetchAdd(1, .monotonic);
                lease.deinit();
            }
        }
    };

    var start = std.atomic.Value(bool).init(false);
    var admitted = std.atomic.Value(u32).init(0);
    var workers: [worker_count]std.Thread = undefined;
    for (&workers) |*worker| worker.* = try std.Thread.spawn(.{}, Worker.run, .{ &idx, &start, &admitted });
    start.store(true, .release);
    for (&workers) |*worker| worker.join();

    try std.testing.expectEqual(@as(u32, 1), admitted.load(.acquire));
    try std.testing.expectEqual(@as(u64, 1), cache.decoded_query_active_leases.load(.acquire));
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(idx.cache_namespace).vector.sampled_admissions);

    first.deinit();
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_active_leases.load(.acquire));
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_entitled_bytes.load(.acquire));
}

test "hbc sampled decoded residency evolves a full resident set within its byte target" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const first = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const second = [_]f32{ 5.0, 6.0, 7.0, 8.0 };
    const replacement = [_]f32{ 9.0, 10.0, 11.0, 12.0 };
    const vector_bytes = estimateVectorCacheBytes(&first);
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes * 2,
        .hard_limit_bytes = vector_bytes * 2,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    _ = try idx.cacheVector(1, &first);
    _ = try idx.cacheVector(2, &second);
    try std.testing.expectEqual(vector_bytes * 2, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);

    // The first saturation ticket is the sampled request. It precharges one
    // bounded replacement, evicts before the read, and transfers that charge
    // to the new retained vector without exceeding the hard target.
    var lease = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
    defer lease.deinit();
    try std.testing.expect(lease.ensureCapacity(vector_bytes));
    _ = try idx.cacheVectorForResidencyLease(&lease, 3, &replacement);

    try expectVectorCached(&idx, 3);
    try std.testing.expectEqual(vector_bytes * 2, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_reserved_bytes.load(.acquire));
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(idx.cache_namespace).vector.sampled_admissions);
}

test "hbc decoded residency fails closed when pinned entries prevent precharge" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const vector_bytes = estimateVectorCacheBytes(&vector);
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes,
        .hard_limit_bytes = vector_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    _ = try idx.cacheVector(1, &vector);
    var pinned = idx.borrowCachedVector(1) orelse return error.TestUnexpectedResult;
    defer pinned.deinit();
    var lease = idx.acquireDecodedVectorResidency(1) orelse return error.TestUnexpectedResult;
    defer lease.deinit();

    try std.testing.expect(!lease.ensureCapacity(vector_bytes));
    try std.testing.expectEqual(@as(u64, 0), cache.decoded_query_reserved_bytes.load(.acquire));
    try std.testing.expectEqual(vector_bytes, resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expect(resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes <= vector_bytes);
}

test "hbc route observation counts external distance timing once" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    defer idx.close();

    var profile: SearchProfile = .{
        .reranked_vectors = 2,
        .rerank_artifact_distance_ns = 100,
        .rerank_distance_ns = 100,
        .rerank_artifact_cache_hits = 2,
    };
    idx.observeSearchCacheBenefit(&profile);
    try std.testing.expectEqual(@as(u64, 50), idx.denseRouteCostSnapshot().exact_distance_ns_per_vector);
    try std.testing.expectEqual(@as(u64, 1000), idx.denseRouteCostSnapshot().rerank_cache_hit_permille);

    // A valid all-miss sample must pull the estimate down. Treating zero as
    // "no sample" leaves the router permanently optimistic after warmup.
    profile = .{
        .reranked_vectors = 2,
        .rerank_artifact_read_ns = 200,
        .rerank_artifact_vectors_loaded = 2,
        .rerank_artifact_distance_ns = 100,
    };
    idx.observeSearchCacheBenefit(&profile);
    const snapshot = idx.denseRouteCostSnapshot();
    try std.testing.expectEqual(@as(u64, 875), snapshot.rerank_cache_hit_permille);
    try std.testing.expectEqual(@as(u64, 100), snapshot.artifact_read_decode_ns_per_vector);
}

test "hbc exact-route vector admission samples outside the search epoch" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();
    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const vector_bytes = estimateVectorCacheBytes(&vector);
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes,
        .hard_limit_bytes = vector_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 16 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    // active_searches intentionally remains zero, matching the exact scorer.
    _ = try idx.cacheVector(1, &vector);
    for (2..10) |vector_id| _ = try idx.cacheVector(vector_id, &vector);

    const stats = cache.namespaceStats(idx.cache_namespace).vector;
    try std.testing.expectEqual(@as(u64, 1), stats.sampled_admissions);
    try std.testing.expectEqual(@as(u64, 2), stats.insertions);
    try std.testing.expect(stats.used_bytes <= vector_bytes);
}

test "hbc shared cache reclaims exact vectors before protected routing nodes" {
    var centroid = [_]f32{ 0.0, 1.0 };
    var children = [_]u64{7};
    const node_one = Node{
        .id = 1,
        .is_leaf = false,
        .level = 1,
        .parent = 0,
        .centroid = centroid[0..],
        .children = children[0..],
        .members = &.{},
    };
    const node_two = Node{
        .id = 2,
        .is_leaf = false,
        .level = 1,
        .parent = 0,
        .centroid = centroid[0..],
        .children = children[0..],
        .members = &.{},
    };
    const vector = [_]f32{1.0} ** 64;
    const node_bytes = estimateNodeCacheBytes(&node_one);
    const vector_bytes = estimateVectorCacheBytes(&vector);
    try std.testing.expect(vector_bytes >= node_bytes);

    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = node_bytes + vector_bytes,
        .hard_limit_bytes = node_bytes + vector_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(std.testing.allocator);
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);
    const namespace = hbcCacheNamespace("/tmp/hbc-class-reclaim");

    try std.testing.expect(try cache.cacheNode(namespace, &node_one));
    _ = try cache.cacheVector(namespace, 9, &vector);
    try std.testing.expect(try cache.cacheNode(namespace, &node_two));

    try expectSharedVectorNotCached(&cache, namespace, 9);
    var first = cache.borrowNode(namespace, 1).?;
    defer first.deinit();
    var second = cache.borrowNode(namespace, 2).?;
    defer second.deinit();
}

test "hbc shared cache reclaims an over-quota namespace for a borrowing peer" {
    const vector = [_]f32{1.0} ** 64;
    const entry_bytes = estimateVectorCacheBytes(&vector);
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = entry_bytes * 4,
        .hard_limit_bytes = entry_bytes * 4,
    };
    var manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer manager.deinit(std.testing.allocator);
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    cache.attachResourceManager(&manager);
    const first = hbcCacheNamespace("/tmp/hbc-fair-first");
    const second = hbcCacheNamespace("/tmp/hbc-fair-second");
    try std.testing.expect(cache.registerNamespacePath(first, "/tmp/hbc-fair-first"));
    defer cache.unregisterNamespacePath(first, "/tmp/hbc-fair-first");
    try std.testing.expect(cache.registerNamespacePath(second, "/tmp/hbc-fair-second"));
    defer cache.unregisterNamespacePath(second, "/tmp/hbc-fair-second");

    for (0..3) |id| _ = try cache.cacheVector(first, id + 1, &vector);
    _ = try cache.cacheVector(second, 101, &vector);
    _ = try cache.cacheVector(second, 102, &vector);

    try std.testing.expect(cache.namespaceStats(first).vector.used_bytes <= entry_bytes * 2);
    try std.testing.expectEqual(entry_bytes * 2, cache.namespaceStats(second).vector.used_bytes);
}

test "hbc shared vector cache warms during concurrent search" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    defer resource_manager.deinit(std.testing.allocator);
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);
    idx.active_searches.store(2, .release);
    defer idx.active_searches.store(0, .release);

    const input = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const retained = try idx.cacheVector(1, &input);
    try std.testing.expectEqual(@intFromPtr(input[0..].ptr), @intFromPtr(retained.ptr));
    var borrowed = idx.borrowCachedVector(1).?;
    defer borrowed.deinit();
    try std.testing.expect(@intFromPtr(input[0..].ptr) != @intFromPtr(borrowed.view().ptr));
    try std.testing.expectEqualSlices(f32, &input, borrowed.view());
    try std.testing.expect(idx.hbcCacheStats().vector.used_bytes > 0);
}

test "hbc external rerank loads metadata only for decoded vector misses" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .max_cached_vectors = 8,
    });
    defer idx.close();
    try idx.insertWithMetadata(1, &.{ 1, 0 }, "doc:cached");
    try idx.insertWithMetadata(2, &.{ 0, 1 }, "doc:miss");
    idx.setRetainedVectorCacheEnabled(true);
    _ = try idx.cacheVector(1, &.{ 1, 0 });

    const Loader = struct {
        calls: usize = 0,
        ids: [2]u64 = .{ 0, 0 },
        count: usize = 0,

        fn score(
            context: *anyopaque,
            vector_ids: []const u64,
            metadata: []const ?[]const u8,
            _: []const f32,
            _: f32,
            _: vec.DistanceMetric,
            distances: []f32,
            _: []f32,
            _: usize,
            _: HBCIndex.ExternalVectorBatchDistanceScratch,
            _: ?*SearchProfile,
        ) !void {
            const self: *@This() = @ptrCast(@alignCast(context));
            self.calls += 1;
            try std.testing.expectEqual(@as(usize, 1), vector_ids.len);
            try std.testing.expectEqual(@as(u64, 2), vector_ids[0]);
            try std.testing.expectEqualStrings("doc:miss", metadata[0] orelse return error.TestUnexpectedResult);
            self.ids[self.count] = vector_ids[0];
            self.count += 1;
            distances[0] = 7;
        }
    };
    var loader = Loader{};
    idx.setExternalVectorBatchDistanceLoader(&loader, Loader.score);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    const ranked = [_]ApproxSearchResult{
        .{ .vector_id = 1, .distance = 0.1 },
        .{ .vector_id = 2, .distance = 0.2 },
    };
    var distances: [2]f32 = undefined;
    var vector_ids: [2]u64 = undefined;
    var metadata: [2]?[]const u8 = undefined;
    var lookups: [2]FixedKeyLookup = undefined;
    var key_views: [2][]const u8 = undefined;
    var values: [2]?[]const u8 = undefined;
    var batch_scratch: [4]f32 = undefined;
    var miss_distances: [2]f32 = undefined;
    var profile: SearchProfile = .{};
    try std.testing.expect(try idx.scoreExternalRerankVectorsSortedWithScratch(
        &txn,
        &ranked,
        &.{ 0, 1 },
        &.{ 1, 0 },
        1,
        &distances,
        &vector_ids,
        &metadata,
        &lookups,
        &key_views,
        &values,
        &batch_scratch,
        &miss_distances,
        &profile,
    ));
    try std.testing.expectEqual(@as(usize, 1), loader.calls);
    try std.testing.expectEqual(@as(usize, 1), loader.count);
    try std.testing.expectEqual(@as(f32, 0), distances[0]);
    try std.testing.expectEqual(@as(f32, 7), distances[1]);
    try std.testing.expectEqual(@as(u64, 1), profile.vector_cache_hits);
    try std.testing.expectEqual(@as(u64, 1), profile.rerank_artifact_cache_hits);
    try std.testing.expectEqual(@as(u64, 1), profile.rerank_metadata_vectors_loaded);
}

test "hbc uncached external rerank does not publish snapshot metadata" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .max_cached_metadata = 8,
    });
    defer idx.close();
    try idx.insertWithMetadata(1, &.{ 1, 0 }, "snapshot:old");
    idx.invalidateMetadataCache(1);

    const Loader = struct {
        fn score(
            _: *anyopaque,
            vector_ids: []const u64,
            metadata: []const ?[]const u8,
            _: []const f32,
            _: f32,
            _: vec.DistanceMetric,
            distances: []f32,
            _: []f32,
            _: usize,
            _: HBCIndex.ExternalVectorBatchDistanceScratch,
            _: ?*SearchProfile,
        ) !void {
            try std.testing.expectEqualSlices(u64, &.{1}, vector_ids);
            try std.testing.expectEqualStrings("snapshot:old", metadata[0] orelse return error.TestUnexpectedResult);
            distances[0] = 1;
        }
    };
    var loader_context: u8 = 0;
    idx.setExternalVectorBatchDistanceLoader(&loader_context, Loader.score);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    const ranked = [_]ApproxSearchResult{.{ .vector_id = 1, .distance = 0.1 }};
    var distances: [1]f32 = undefined;
    var vector_ids: [1]u64 = undefined;
    var metadata: [1]?[]const u8 = undefined;
    var lookups: [1]FixedKeyLookup = undefined;
    var key_views: [1][]const u8 = undefined;
    var values: [1]?[]const u8 = undefined;
    var batch_scratch: [2]f32 = undefined;
    var miss_distances: [1]f32 = undefined;
    try std.testing.expect(try idx.scoreExternalRerankVectorsSortedWithScratchUncached(
        &txn,
        &ranked,
        &.{0},
        &.{ 1, 0 },
        1,
        &distances,
        &vector_ids,
        &metadata,
        &lookups,
        &key_views,
        &values,
        &batch_scratch,
        &miss_distances,
        null,
    ));
    try std.testing.expectEqual(@as(f32, 1), distances[0]);
    try std.testing.expect(idx.borrowCachedMetadata(1) == null);
}

test "hbc old snapshot metadata cannot poison the current cache generation" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .max_cached_metadata = 8,
    });
    defer idx.close();
    try idx.insertWithMetadata(1, &.{ 1, 0 }, "snapshot:old");
    idx.invalidateMetadataCache(1);

    var old_txn = try idx.beginReadTxn();
    defer old_txn.abort();
    try std.testing.expect(old_txn.cache_fill_epoch != null);
    const ids = [_]u64{1};
    var metadata: [1]?[]const u8 = undefined;
    var lookups: [1]FixedKeyLookup = undefined;
    var key_views: [1][]const u8 = undefined;
    var values: [1]?[]const u8 = undefined;
    try idx.getMetadataManySortedInTxnWithScratchUncached(
        &old_txn,
        &ids,
        &metadata,
        &lookups,
        &key_views,
        &values,
    );
    try std.testing.expectEqualStrings("snapshot:old", metadata[0] orelse return error.TestUnexpectedResult);
    try expectMetadataNotCached(&idx, 1);

    {
        var write_txn = try idx.beginWriteTxn();
        errdefer write_txn.abort();
        try idx.putMetadata(&write_txn, 1, "snapshot:new");
        try write_txn.commit();
    }
    try expectMetadataNotCached(&idx, 1);

    try idx.getMetadataManySortedInTxnWithScratch(
        &old_txn,
        &ids,
        &metadata,
        &lookups,
        &key_views,
        &values,
    );
    try std.testing.expectEqualStrings("snapshot:old", metadata[0] orelse return error.TestUnexpectedResult);
    try expectMetadataNotCached(&idx, 1);

    var current_txn = try idx.beginReadTxn();
    defer current_txn.abort();
    try idx.getMetadataManySortedInTxnWithScratch(
        &current_txn,
        &ids,
        &metadata,
        &lookups,
        &key_views,
        &values,
    );
    try std.testing.expectEqualStrings("snapshot:new", metadata[0] orelse return error.TestUnexpectedResult);
    try expectCachedMetadata(&idx, 1, "snapshot:new");
}

test "hbc old snapshot cannot repopulate pinned upper tree after publication" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .leaf_size = 64,
        .branching_factor = 4,
        .use_quantization = true,
        .max_pinned_tree_nodes = 8,
        .pinned_tree_depth = 1,
    });
    defer idx.close();
    try idx.insert(1, &.{ 0, 0 });
    try idx.insert(2, &.{ 1, 0 });
    idx.clearNodeCache();
    idx.clearQuantizedCache();

    // Establish an old MVCC snapshot and materialize its root before the
    // writer invalidates the corresponding pinned-cache keys.
    var old_txn = try idx.beginRuntimeSearchTxn();
    defer old_txn.abort();
    const old_fill_epoch = old_txn.cache_fill_epoch orelse return error.TestUnexpectedResult;
    const root_id = idx.publishedRootNode();
    var old_root = try idx.loadNodeFromStorage(&old_txn, root_id);
    defer old_root.deinit(alloc);
    try std.testing.expectEqual(@as(usize, 2), old_root.members.len);

    // Publish a new root payload after the old read. Delayed node and
    // quantized admissions from the old transaction must both fail closed,
    // including the upper-tree walk itself.
    try idx.insert(3, &.{ 0, 1 });
    try std.testing.expect(idx.publishedMutationEpoch() != old_fill_epoch);
    try idx.ensurePinnedNode(&old_root, old_fill_epoch);
    try idx.ensurePinnedQuantized(&old_txn, &old_root, old_fill_epoch);
    try idx.pinUpperTreeCache(&old_txn);

    {
        idx.cache_mu.lockShared();
        defer idx.cache_mu.unlockShared();
        try std.testing.expect(!idx.pinned_node_cache.contains(root_id));
        try std.testing.expect(!idx.pinned_quantized_cache.contains(root_id));
    }

    // A transaction bound to the live publication can still warm the pinned
    // caches, proving that stale rejection does not disable the fast path.
    var current_txn = try idx.beginRuntimeSearchTxn();
    defer current_txn.abort();
    try idx.pinUpperTreeCache(&current_txn);

    {
        idx.cache_mu.lockShared();
        defer idx.cache_mu.unlockShared();
        const pinned_node = idx.pinned_node_cache.get(root_id) orelse return error.TestUnexpectedResult;
        try std.testing.expectEqual(@as(usize, 3), pinned_node.node.members.len);
        const pinned_quantized = idx.pinned_quantized_cache.get(root_id) orelse return error.TestUnexpectedResult;
        try std.testing.expectEqual(@as(usize, 3), pinned_quantized.quantized.getCount());
    }
}

test "hbc shared vector publication coalesces concurrent duplicate fills" {
    const Worker = struct {
        fn run(cache: *Cache, namespace: u64, start: *std.atomic.Value(bool), failed: *std.atomic.Value(bool)) void {
            while (!start.load(.acquire)) std.atomic.spinLoopHint();
            _ = cache.cacheVector(namespace, 42, &.{ 1.0, 2.0, 3.0, 4.0 }) catch {
                failed.store(true, .release);
            };
        }
    };

    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();
    const namespace = hbcCacheNamespace("/tmp/hbc-vector-single-flight");
    var start = std.atomic.Value(bool).init(false);
    var failed = std.atomic.Value(bool).init(false);
    var threads: [16]std.Thread = undefined;
    for (&threads) |*thread| {
        thread.* = try std.Thread.spawn(.{}, Worker.run, .{ &cache, namespace, &start, &failed });
    }
    start.store(true, .release);
    for (&threads) |*thread| thread.join();

    try std.testing.expect(!failed.load(.acquire));
    const stats = cache.namespaceStats(namespace).vector;
    try std.testing.expectEqual(@as(u64, 1), stats.insertions);
    try std.testing.expectEqual(@as(u64, 0), stats.replacements);
    var borrowed = cache.borrowVector(namespace, 42).?;
    defer borrowed.deinit();
    try std.testing.expectEqualSlices(f32, &.{ 1.0, 2.0, 3.0, 4.0 }, borrowed.view());
}

test "hbc shared vector lookup stats preserve compulsory and cross-stripe misses" {
    var cache = Cache.init(std.testing.allocator);
    defer cache.deinit();

    const registered_path = "/tmp/hbc-vector-lookup-stats";
    const registered_namespace = hbcCacheNamespace(registered_path);
    try std.testing.expect(cache.registerNamespacePath(registered_namespace, registered_path));
    try std.testing.expect(cache.borrowVector(registered_namespace, 42) == null);
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(registered_namespace).vector.misses);

    const direct_namespace = hbcCacheNamespace("/tmp/hbc-vector-lookup-stats-direct");
    const cached_id: u64 = 1;
    _ = try cache.cacheVector(direct_namespace, cached_id, &.{ 1.0, 2.0, 3.0, 4.0 });
    const cached_stripe = CacheRwLock.vectorReadStripe(direct_namespace, cached_id);
    var missing_id: u64 = cached_id + 1;
    while (CacheRwLock.vectorReadStripe(direct_namespace, missing_id) == cached_stripe) missing_id += 1;
    try std.testing.expect(cache.borrowVector(direct_namespace, missing_id) == null);
    try std.testing.expectEqual(@as(u64, 1), cache.namespaceStats(direct_namespace).vector.misses);
}

test "hbc shared cache lock reports striped reader wait" {
    const Writer = struct {
        fn run(
            lock: *CacheRwLock,
            acquired: *std.atomic.Value(bool),
            release: *std.atomic.Value(bool),
        ) void {
            lock.lockExclusive();
            acquired.store(true, .release);
            while (!release.load(.acquire)) std.atomic.spinLoopHint();
            lock.unlockExclusive();
        }
    };

    var lock: CacheRwLock = .{};
    const read_stripe = lock.lockVectorShared(1, 1);
    var writer_acquired = std.atomic.Value(bool).init(false);
    var release_writer = std.atomic.Value(bool).init(false);
    var writer = try std.Thread.spawn(.{}, Writer.run, .{ &lock, &writer_acquired, &release_writer });
    while (!lock.vector_fence_pending.load(.acquire)) std.atomic.spinLoopHint();
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    try io_impl.io().sleep(std.Io.Duration.fromMilliseconds(10), .awake);
    lock.unlockVectorShared(read_stripe);
    while (!writer_acquired.load(.acquire)) std.atomic.spinLoopHint();
    try std.testing.expect(lock.vector_fence_pending.load(.acquire));
    release_writer.store(true, .release);
    writer.join();

    try std.testing.expect(writer_acquired.load(.acquire));
    try std.testing.expect(!lock.vector_fence_pending.load(.acquire));
    const stats = lock.snapshot();
    try std.testing.expectEqual(@as(u64, 1), stats.exclusive_lock_calls);
    try std.testing.expectEqual(@as(u64, 1), stats.exclusive_contended_calls);
    try std.testing.expect(stats.exclusive_wait_ns > 0);
    try std.testing.expect(stats.exclusive_max_wait_ns > 0);
}

test "hbc shared cache queued writer cannot be bypassed by nonblocking reclaim" {
    const Writer = struct {
        fn run(lock: *CacheRwLock, acquired: *std.atomic.Value(bool)) void {
            lock.lockExclusive();
            acquired.store(true, .release);
            lock.unlockExclusive();
        }
    };

    var lock: CacheRwLock = .{};
    lockAtomic(&lock.writer_gate);
    var writer_acquired = std.atomic.Value(bool).init(false);
    var writer = try std.Thread.spawn(.{}, Writer.run, .{ &lock, &writer_acquired });
    while (lock.writers_waiting.load(.acquire) == 0) std.atomic.spinLoopHint();

    try std.testing.expect(!lock.tryLockExclusive());
    try std.testing.expect(!writer_acquired.load(.acquire));

    lock.writer_gate.unlock();
    writer.join();
    try std.testing.expect(writer_acquired.load(.acquire));

    try std.testing.expect(lock.tryLockExclusive());
    try std.testing.expect(lock.vector_fence_pending.load(.acquire));
    lock.unlockExclusive();
    try std.testing.expect(!lock.vector_fence_pending.load(.acquire));
}

test "hbc shared cache writer progresses under continuous striped reads" {
    const Reader = struct {
        fn run(
            lock: *CacheRwLock,
            namespace: u64,
            vector_id: u64,
            ready: *std.atomic.Value(u32),
            start: *std.atomic.Value(bool),
            stop: *std.atomic.Value(bool),
            reads: *std.atomic.Value(u64),
        ) void {
            _ = ready.fetchAdd(1, .release);
            while (!start.load(.acquire)) std.atomic.spinLoopHint();
            while (!stop.load(.acquire)) {
                const stripe = lock.lockVectorShared(namespace, vector_id);
                _ = reads.fetchAdd(1, .monotonic);
                std.atomic.spinLoopHint();
                lock.unlockVectorShared(stripe);
            }
        }
    };
    const Writer = struct {
        fn run(lock: *CacheRwLock, acquired: *std.atomic.Value(bool)) void {
            lock.lockExclusive();
            acquired.store(true, .release);
            lock.unlockExclusive();
        }
    };

    var lock: CacheRwLock = .{};
    var ready = std.atomic.Value(u32).init(0);
    var start = std.atomic.Value(bool).init(false);
    var stop = std.atomic.Value(bool).init(false);
    var reads = std.atomic.Value(u64).init(0);
    var writer_acquired = std.atomic.Value(bool).init(false);
    var readers: [8]std.Thread = undefined;
    for (&readers, 0..) |*reader, index| {
        reader.* = try std.Thread.spawn(.{}, Reader.run, .{
            &lock,
            @as(u64, @intCast(index + 1)),
            @as(u64, @intCast(index * 17 + 1)),
            &ready,
            &start,
            &stop,
            &reads,
        });
    }
    while (ready.load(.acquire) != readers.len) std.atomic.spinLoopHint();
    start.store(true, .release);
    while (reads.load(.acquire) < readers.len) std.atomic.spinLoopHint();

    var writer = try std.Thread.spawn(.{}, Writer.run, .{ &lock, &writer_acquired });
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    var attempts: usize = 0;
    while (!writer_acquired.load(.acquire) and attempts < 1_000) : (attempts += 1) {
        try io_impl.io().sleep(std.Io.Duration.fromMilliseconds(1), .awake);
    }
    const progressed_under_load = writer_acquired.load(.acquire);
    stop.store(true, .release);
    for (&readers) |*reader| reader.join();
    writer.join();

    try std.testing.expect(progressed_under_load);
}

test "hbc stable cache namespace canonicalizes equivalent path spellings" {
    const alloc = std.testing.allocator;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();

    const root_rel = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/hbc-cache-namespace", .{tmp.sub_path});
    defer alloc.free(root_rel);

    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const cwd = try std.process.currentPathAlloc(io_impl.io(), alloc);
    defer alloc.free(cwd);
    const root = try std.fs.path.resolve(alloc, &.{ cwd, root_rel });
    defer alloc.free(root);
    try std.Io.Dir.cwd().createDirPath(io_impl.io(), root);

    const absolute = try std.Io.Dir.realPathFileAbsoluteAlloc(io_impl.io(), root, alloc);
    defer alloc.free(absolute);
    const alt = try std.fmt.allocPrint(alloc, "{s}/../{s}", .{ absolute, "hbc-cache-namespace" });
    defer alloc.free(alt);

    try std.testing.expectEqual(hbcCacheNamespaceStable(alloc, absolute), hbcCacheNamespaceStable(alloc, alt));
}

test "hbc index cache disable clears shared namespace and stops accounting growth" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);
    idx.setRetainedVectorCacheEnabled(true);

    _ = try idx.cacheVector(1, &.{ 1.0, 2.0, 3.0, 4.0 });
    try std.testing.expect(cache.namespaceStats(idx.cache_namespace).total_bytes > 0);
    try std.testing.expect(resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes > 0);

    idx.setCacheEnabled(false);
    try expectVectorNotCached(&idx, 1);
    try std.testing.expectEqual(@as(u64, 0), cache.namespaceStats(idx.cache_namespace).total_bytes);
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);

    const bypass = try idx.cacheVector(2, &.{ 5.0, 6.0, 7.0, 8.0 });
    try std.testing.expectEqualSlices(f32, &.{ 5.0, 6.0, 7.0, 8.0 }, bypass);
    try expectVectorNotCached(&idx, 2);
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
}

test "hbc retained vector cache is bypassed during external vector replay" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.setRetainedVectorCacheEnabled(true);

    idx.setBypassExternalVectorCache(true);
    defer idx.setBypassExternalVectorCache(false);

    const input = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const returned = try idx.cacheVector(1, &input);
    try std.testing.expectEqual(@intFromPtr(input[0..].ptr), @intFromPtr(returned.ptr));
    try expectVectorNotCached(&idx, 1);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
}

test "hbc retained vector cache can be disabled independently of metadata cache" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);

    idx.setRetainedVectorCacheEnabled(false);

    const input = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const returned = try idx.cacheVector(1, &input);
    try std.testing.expectEqual(@intFromPtr(input[0..].ptr), @intFromPtr(returned.ptr));
    try expectVectorNotCached(&idx, 1);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);

    _ = try idx.cacheMetadata(1, "doc:1");
    try expectCachedMetadata(&idx, 1, "doc:1");
    try std.testing.expect(idx.hbcCacheStats().metadata.used_bytes > 0);
    try std.testing.expect(resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes > 0);
}

test "hbc metadata cache remains active when vector cache capacity is zero" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .max_cached_vectors = 0,
        .max_cached_metadata = 8,
    });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);
    idx.setRetainedVectorCacheEnabled(false);

    const vector = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const returned_vector = try idx.cacheVector(1, &vector);
    try std.testing.expectEqual(@intFromPtr(vector[0..].ptr), @intFromPtr(returned_vector.ptr));
    try expectVectorNotCached(&idx, 1);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);

    _ = try idx.cacheMetadata(1, "doc:1");
    try expectCachedMetadata(&idx, 1, "doc:1");
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
    try std.testing.expect(idx.hbcCacheStats().metadata.used_bytes > 0);
    try std.testing.expect(resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes > 0);
}

test "hbc metadata cache is retained and resource managed during concurrent search" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var cache = Cache.init(alloc);
    defer cache.deinit();
    cache.attachResourceManager(&resource_manager);

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.attachSharedCache(&cache);
    idx.setRetainedVectorCacheEnabled(false);

    idx.active_searches.store(2, .release);
    defer idx.active_searches.store(0, .release);

    const input = "doc:concurrent";
    const returned = try idx.cacheMetadata(11, input);
    try std.testing.expectEqual(@intFromPtr(input.ptr), @intFromPtr(returned.ptr));
    try expectCachedMetadata(&idx, 11, input);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
    try std.testing.expect(idx.hbcCacheStats().metadata.used_bytes > 0);
    try std.testing.expect(resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes > 0);
}

test "hbc retained vector cache defaults on for search performance" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();

    const input = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const returned = try idx.cacheVector(1, &input);
    try std.testing.expectEqual(@intFromPtr(input[0..].ptr), @intFromPtr(returned.ptr));
    var borrowed = idx.borrowCachedVector(1).?;
    defer borrowed.deinit();
    try std.testing.expect(@intFromPtr(input[0..].ptr) != @intFromPtr(borrowed.view().ptr));
    try std.testing.expectEqualSlices(f32, &input, borrowed.view());
    try std.testing.expect(idx.hbcCacheStats().vector.used_bytes > 0);

    _ = try idx.cacheMetadata(1, "doc:1");
    try expectCachedMetadata(&idx, 1, "doc:1");
    try std.testing.expect(idx.hbcCacheStats().metadata.used_bytes > 0);
}

test "hbc vector artifact reads avoid duplicate LSM block residency only with retained vectors" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();

    try std.testing.expectEqual(vectorindex_store.Namespace.vecs_transient, idx.vectorArtifactReadNamespace());
    const transient = HBCIndex.runtimeNamespace(.vecs_transient);
    try std.testing.expectEqualStrings("hbc_vecs", transient.name.?);
    try std.testing.expect(!transient.retainDataBlocks());
    const retained = HBCIndex.runtimeNamespace(.vecs);
    try std.testing.expectEqualStrings("hbc_vecs", retained.name.?);
    try std.testing.expect(retained.retainDataBlocks());

    idx.setRetainedVectorCacheEnabled(false);
    try std.testing.expectEqual(vectorindex_store.Namespace.vecs, idx.vectorArtifactReadNamespace());
    idx.setRetainedVectorCacheEnabled(true);
    idx.setBypassExternalVectorCache(true);
    try std.testing.expectEqual(vectorindex_store.Namespace.vecs, idx.vectorArtifactReadNamespace());
}

test "hbc index close does not clear shared namespace bytes" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var cache = Cache.init(alloc);
    defer cache.deinit();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachSharedCache(&cache);
    idx.setRetainedVectorCacheEnabled(true);

    _ = try idx.cacheVector(1, &.{ 1.0, 2.0, 3.0, 4.0 });
    const namespace = idx.cache_namespace;
    try std.testing.expect(cache.namespaceStats(namespace).total_bytes > 0);

    var second = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    second.attachSharedCache(&cache);
    second.close();

    try std.testing.expect(cache.namespaceStats(namespace).total_bytes > 0);
    try std.testing.expect(cache.namespaceStats(namespace).vector.used_bytes > 0);
}

test "hbc shared cache releases unused namespace path registrations" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var cache = Cache.init(alloc);
    defer cache.deinit();

    var first = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    var first_open = true;
    defer if (first_open) first.close();
    first.attachSharedCache(&cache);
    const namespace = first.cache_namespace;
    try std.testing.expectEqual(@as(usize, 1), cache.namespace_paths.get(namespace).?.active_owners);

    var second = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    var second_open = true;
    defer if (second_open) second.close();
    second.attachSharedCache(&cache);
    try std.testing.expectEqual(@as(usize, 2), cache.namespace_paths.get(namespace).?.active_owners);

    first.close();
    first_open = false;
    try std.testing.expectEqual(@as(usize, 1), cache.namespace_paths.get(namespace).?.active_owners);

    second.close();
    second_open = false;
    try std.testing.expect(!cache.namespace_paths.contains(namespace));
    try std.testing.expect(!cache.namespace_stats.contains(namespace));
}

test "hbc shared cache bounds namespace state across path churn" {
    const alloc = std.testing.allocator;
    var cache = Cache.init(alloc);
    defer cache.deinit();

    var path_buf: [128]u8 = undefined;
    for (0..512) |i| {
        const path = try std.fmt.bufPrint(&path_buf, "/tmp/antfly-hbc-namespace-churn/{d}", .{i});
        const namespace = hbcCacheNamespace(path);
        try std.testing.expect(cache.registerNamespacePath(namespace, path));
        cache.unregisterNamespacePath(namespace, path);
    }

    try std.testing.expectEqual(@as(usize, 0), cache.namespace_paths.count());
    try std.testing.expectEqual(@as(usize, 0), cache.namespace_stats.count());
    for (&cache.vector_lookup_stats) |*lookup_stats| {
        try std.testing.expectEqual(@as(usize, 0), lookup_stats.count());
    }
}

test "hbc cache reports byte usage to resource manager" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = 1,
        .hard_limit_bytes = 256,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.setRetainedVectorCacheEnabled(true);

    _ = try idx.cacheVector(1, &.{ 1.0, 2.0, 3.0, 4.0 });
    _ = try idx.cacheMetadata(1, "doc:1");
    var stats = resource_manager.snapshot();
    try std.testing.expect(stats.slices[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)].used_bytes > 0);
    try std.testing.expect(stats.slices[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)].soft_limit_events > 0);
    try std.testing.expectEqual(@as(u64, 0), stats.slices[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)].hard_limit_rejections);

    idx.invalidateVectorCache(1);
    idx.invalidateMetadataCache(1);
    stats = resource_manager.snapshot();
    try std.testing.expectEqual(@as(u64, 0), stats.slices[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)].used_bytes);
}

test "hbc resource manager reattachment is idempotent and transfers local cache usage" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var first_manager = resource_manager_mod.ResourceManager.init(.{});
    defer first_manager.deinit(std.testing.allocator);
    var second_manager = resource_manager_mod.ResourceManager.init(.{});
    defer second_manager.deinit(std.testing.allocator);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    defer idx.close();

    _ = try idx.cacheVector(1, &.{ 1.0, 2.0, 3.0, 4.0 });
    idx.attachResourceManager(&first_manager);
    const bytes = first_manager.sliceStats(.hbc_node_metadata_cache).used_bytes;
    try std.testing.expect(bytes > 0);

    idx.attachResourceManager(&first_manager);
    try std.testing.expectEqual(bytes, first_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(@as(u64, 0), first_manager.snapshot().memory.accounting_errors);

    idx.attachResourceManager(&second_manager);
    try std.testing.expectEqual(@as(u64, 0), first_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(bytes, second_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);
    try std.testing.expectEqual(@as(u64, 0), second_manager.snapshot().memory.accounting_errors);
}

test "hbc opportunistic vector cache skips instead of overcommitting resource budget" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = 1,
        .hard_limit_bytes = 2,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.setRetainedVectorCacheEnabled(true);

    const input = [_]f32{ 1.0, 2.0, 3.0, 4.0 };
    const returned = try idx.cacheVector(1, &input);
    try std.testing.expectEqual(@intFromPtr(input[0..].ptr), @intFromPtr(returned.ptr));

    const stats = resource_manager.snapshot();
    try std.testing.expectEqual(@as(u64, 0), stats.slices[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)].used_bytes);
    try std.testing.expect(stats.slices[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)].hard_limit_rejections > 0);
    try std.testing.expectEqual(@as(u64, 1), idx.hbcCacheStats().vector.admission_skips);
}

test "hbc routing scratch reports bytes to resource manager" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    idx.attachResourceManager(&resource_manager);

    try idx.batchInsertWithMetadata(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 1.0, 1.0 }, .metadata = "doc:2" },
    });

    var txn = try idx.beginReadTxn();
    _ = try idx.findLeafWithOptions(&txn, idx.metadata.root_node, &.{ 0.5, 0.5 }, true);
    txn.abort();

    try std.testing.expect(resource_manager.sliceStats(.dense_routing_working_set).used_bytes > 0);

    idx.close();
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.dense_routing_working_set).used_bytes);
}

test "hbc search scratch reports bytes to resource manager" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    idx.attachResourceManager(&resource_manager);

    try idx.batchInsertWithMetadata(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 1.0, 1.0 }, .metadata = "doc:2" },
    });

    var results = try idx.searchWithRequest(.{
        .query = &[_]f32{ 0.1, 0.1 },
        .k = 1,
    });
    defer results.deinit();

    try std.testing.expect(idx.search_workspace_bytes_accounted > 0);
    try std.testing.expect(resource_manager.sliceStats(.dense_search_working_set).used_bytes > 0);

    idx.close();
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.dense_search_working_set).used_bytes);
}

test "failed search scratch reservation keeps partially grown buffers accounted" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    defer resource_manager.deinit(alloc);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
    var idx_open = true;
    defer if (idx_open) idx.close();
    idx.attachResourceManager(&resource_manager);

    var handle = try idx.acquireSearchScratch();
    const previous = handle.accounted_bytes;
    try idx.reserveSearchScratchBytes(&handle, previous + 1024 * 1024);
    try handle.scratch.ensureFlatProbeCapacity(alloc, 32, true);
    const live = handle.scratch.bytes();
    try std.testing.expect(live > previous);

    idx.rollbackSearchScratchBytes(&handle, previous);
    try std.testing.expectEqual(live, handle.accounted_bytes);
    try std.testing.expectEqual(live, resource_manager.sliceStats(.dense_search_working_set).used_bytes);

    idx.releaseSearchScratch(&handle);
    idx.close();
    idx_open = false;
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.dense_search_working_set).used_bytes);
}

test "cold flat centroid build preadmits transient and retained memory" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const dims: usize = 8;
    const block_size: usize = 16;
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = dims,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 16,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = block_size,
    });
    var idx_open = true;
    defer if (idx_open) idx.close();
    var vector: [dims]f32 = undefined;
    for (0..48) |i| {
        for (&vector, 0..) |*value, dim| value.* = @floatFromInt(i + dim);
        try idx.insert(@intCast(i + 1), &vector);
    }
    idx.clearNodeCache();
    idx.clearQuantizedCache();
    idx.clearVectorCache();
    idx.clearMetadataCache();

    const projection = try vectorindex_spfresh_index.projectedFlatCentroidDirectoryBuildBytes(
        idx.publishedNodeCount(),
        dims,
        block_size,
        @max(idx.config.leaf_size, idx.config.branching_factor),
    );
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .hard_limit_bytes = projection.retained_bytes - 1,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer {
        idx.close();
        idx_open = false;
        resource_manager.deinit(alloc);
    }
    idx.attachResourceManager(&resource_manager);
    @memset(&vector, 0);

    try std.testing.expectError(error.ResourceBudgetExceeded, idx.searchWithRequest(.{
        .query = &vector,
        .k = 3,
        .load_metadata = false,
    }));
    try std.testing.expect(idx.flat_centroid_directory == null);
    try std.testing.expectEqual(@as(u64, 0), idx.flat_centroid_build_bytes_accounted);
    try std.testing.expectEqual(@as(u64, 0), idx.flat_centroid_retained_reservation_bytes_accounted);
    try std.testing.expect(resource_manager.sliceStats(.hbc_node_metadata_cache).hard_limit_rejections > 0);
}

test "flat centroid directory stays accounted until its final reference" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    defer resource_manager.deinit(alloc);
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 2,
        .branching_factor = 2,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 8,
    });
    var idx_open = true;
    defer if (idx_open) idx.close();
    for (0..32) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, value, value, value });
    }
    idx.attachResourceManager(&resource_manager);
    var results = try idx.searchWithRequest(.{
        .query = &.{ 0, 0, 0, 0 },
        .k = 3,
        .load_metadata = false,
    });
    results.deinit();

    const directory_bytes = idx.flat_centroid_directory_bytes_accounted;
    try std.testing.expect(directory_bytes > 0);
    const retained_directory = idx.flat_centroid_directory orelse return error.TestUnexpectedResult;
    retained_directory.retain();
    const before = resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes;
    vectorindex_spfresh_index.clearFlatCentroidDirectory(&idx);
    try std.testing.expectEqual(directory_bytes, idx.flat_centroid_directory_bytes_accounted);
    retained_directory.release(alloc);
    try std.testing.expectEqual(@as(u64, 0), idx.flat_centroid_directory_bytes_accounted);
    try std.testing.expectEqual(
        directory_bytes,
        before - resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes,
    );

    idx.close();
    idx_open = false;
}

test "flat centroid reservation handoff does not double count retained bytes" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const directory_bytes: u64 = @sizeOf(vectorindex_spfresh_index.FlatCentroidDirectory);
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .hard_limit_bytes = directory_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer resource_manager.deinit(alloc);
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    var idx_open = true;
    defer if (idx_open) idx.close();
    idx.attachResourceManager(&resource_manager);

    const reservation: vectorindex_spfresh_index.FlatCentroidBuildReservation = .{
        .retained_bytes = directory_bytes,
    };
    _ = try idx.reserveFlatCentroidDirectoryBuildBytes(reservation);
    const directory = try alloc.create(vectorindex_spfresh_index.FlatCentroidDirectory);
    directory.* = .{};
    try idx.accountFlatCentroidDirectory(directory, reservation);

    const stats = resource_manager.sliceStats(.hbc_node_metadata_cache);
    try std.testing.expectEqual(directory_bytes, stats.used_bytes);
    try std.testing.expectEqual(directory_bytes, stats.peak_bytes);
    try std.testing.expectEqual(@as(u64, 0), stats.hard_limit_rejections);
    directory.release(alloc);

    idx.close();
    idx_open = false;
}

test "exhaustive search workspace is admitted before growth and released after rejection" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 16,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
        .flat_centroid_probe_count = 0,
    });
    var idx_open = true;
    defer if (idx_open) idx.close();
    for (0..64) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, value / 8 });
    }

    // Materialize only the bounded baseline before setting a hard limit. The
    // first exhaustive request must pre-admit its index-sized coverage and
    // flat-frontier buffers instead of allocating and reporting afterward.
    var scratch_handle = try idx.acquireSearchScratch();
    idx.releaseSearchScratch(&scratch_handle);
    const baseline_bytes = idx.search_workspace_bytes_accounted;
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.dense_search_working_set)] = .{
        .soft_limit_bytes = baseline_bytes + 1,
        .hard_limit_bytes = baseline_bytes + 1,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer {
        idx.close();
        idx_open = false;
        resource_manager.deinit(alloc);
    }
    idx.attachResourceManager(&resource_manager);

    try std.testing.expectError(
        error.ResourceBudgetExceeded,
        idx.searchWithRequest(.{
            .query = &.{ 0, 0 },
            .k = 3,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );

    const stats = resource_manager.sliceStats(.dense_search_working_set);
    try std.testing.expectEqual(idx.search_workspace_bytes_accounted, stats.used_bytes);
    try std.testing.expect(stats.used_bytes <= baseline_bytes);
    try std.testing.expect(stats.hard_limit_rejections > 0);
    const cached = &(idx.cached_scratch orelse return error.TestUnexpectedResult);
    try std.testing.expectEqual(@as(usize, 0), cached.flat_probes.len);
    try std.testing.expectEqual(@as(usize, 0), cached.flat_probe_merge.len);
    try std.testing.expectEqual(@as(usize, 0), cached.coverage_members.len);
    try std.testing.expectEqual(@as(usize, 0), cached.coverage_visited_words.len);
    try std.testing.expectEqual(@as(usize, 0), cached.lookups.len);
    try std.testing.expectEqual(@as(usize, 0), cached.key_views.len);
    try std.testing.expectEqual(@as(usize, 0), cached.values.len);
}

test "flat block scoring workspace is included in exhaustive pre-admission" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const dims: usize = 2;
    const leaf_size: usize = 2;
    const branching_factor: usize = 2;
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = dims,
        .leaf_size = leaf_size,
        .branching_factor = branching_factor,
        .search_width = 32,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 32,
        .flat_centroid_probe_count = 0,
    });
    var idx_open = true;
    defer if (idx_open) idx.close();
    for (0..128) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, value / 8 });
    }

    // Materialize the immutable directory, then return the cached request
    // scratch to its bounded shape so this test controls every later growth.
    var warm = try idx.searchWithRequest(.{
        .query = &.{ 0, 0 },
        .k = 3,
        .search_effort = 0.5,
        .load_metadata = false,
    });
    warm.deinit();
    const directory = idx.flat_centroid_directory orelse return error.TestUnexpectedResult;
    var max_block_count: usize = 0;
    for (directory.blocks) |block| max_block_count = @max(max_block_count, block.posting_ids.len);
    try std.testing.expect(max_block_count > leaf_size);

    const cached = &(idx.cached_scratch orelse return error.TestUnexpectedResult);
    const before_trim = cached.bytes();
    const trimmed = cached.reclaimRetainedWorkspace(alloc, std.math.maxInt(u64), leaf_size);
    idx.search_workspace_bytes_accounted -|= trimmed;
    try std.testing.expect(cached.bytes() < before_trim);

    // Model the exact state at flat selection: complete-coverage buffers have
    // already been admitted and allocated, but neither the frontier nor the
    // directory block-scoring workspace has grown yet.
    var model = try vectorindex_search_runtime.SearchScratch.init(
        alloc,
        dims,
        branching_factor,
        leaf_size,
    );
    defer model.deinit(alloc);
    _ = model.reclaimRetainedWorkspace(alloc, std.math.maxInt(u64), leaf_size);
    try model.ensureCoverageMemberCapacity(alloc, 8_192);
    try model.ensureLookupCapacity(alloc, 8_192);
    try model.resetCoverageVisited(alloc, idx.metadata.node_count);
    const frontier_only = try model.projectedBytesWithFlatProbeCapacity(directory.posting_count, false, 0);
    const complete_flat = try model.projectedBytesWithFlatProbeCapacity(
        directory.posting_count,
        false,
        max_block_count,
    );
    try std.testing.expect(complete_flat > frontier_only);

    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.dense_search_working_set)] = .{
        .soft_limit_bytes = frontier_only,
        .hard_limit_bytes = frontier_only,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer {
        idx.close();
        idx_open = false;
        resource_manager.deinit(alloc);
    }
    idx.attachResourceManager(&resource_manager);

    try std.testing.expectError(
        error.ResourceBudgetExceeded,
        idx.searchWithRequest(.{
            .query = &.{ 0, 0 },
            .k = 3,
            .search_effort = 1,
            .load_metadata = false,
        }),
    );

    const stats = resource_manager.sliceStats(.dense_search_working_set);
    try std.testing.expect(stats.peak_bytes <= frontier_only);
    try std.testing.expect(stats.hard_limit_rejections > 0);
    const rejected = &(idx.cached_scratch orelse return error.TestUnexpectedResult);
    try std.testing.expect(rejected.vector_batch.len <= dims * leaf_size);
    try std.testing.expect(rejected.positions.len <= leaf_size);
    try std.testing.expectEqual(@as(usize, 0), rejected.flat_probes.len);
}

test "resource pressure reclaims retained flat search scratch" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const dims: usize = 16;
    const leaf_size: usize = 2;
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = dims,
        .leaf_size = leaf_size,
        .branching_factor = 2,
        .search_width = 32,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 64,
        .flat_centroid_probe_count = 0,
    });
    var idx_open = true;
    defer if (idx_open) idx.close();
    var vector: [dims]f32 = undefined;
    for (0..192) |i| {
        for (&vector, 0..) |*value, dim| value.* = @floatFromInt(i + dim);
        try idx.insert(@intCast(i + 1), &vector);
    }
    @memset(&vector, 0);
    var warm = try idx.searchWithRequest(.{
        .query = &vector,
        .k = 3,
        .search_effort = 0.5,
        .load_metadata = false,
    });
    warm.deinit();

    var baseline = try vectorindex_search_runtime.SearchScratch.init(alloc, dims, 2, leaf_size);
    defer baseline.deinit(alloc);
    const baseline_bytes = baseline.bytes();
    const retained_before = idx.search_workspace_bytes_accounted;
    try std.testing.expect(retained_before > baseline_bytes);

    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.dense_search_working_set)] = .{
        .hard_limit_bytes = baseline_bytes,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets });
    defer {
        idx.close();
        idx_open = false;
        resource_manager.deinit(alloc);
    }
    idx.attachResourceManager(&resource_manager);

    const reclaimed = resource_manager.reclaimForAllocation(.dense_search_working_set, 1);
    try std.testing.expect(reclaimed > 0);
    try std.testing.expect(idx.search_workspace_bytes_accounted < retained_before);
    try std.testing.expect(resource_manager.sliceStats(.dense_search_working_set).used_bytes <= baseline_bytes);
    const cached = &(idx.cached_scratch orelse return error.TestUnexpectedResult);
    try std.testing.expectEqual(@as(usize, 0), cached.flat_probes.len);
    try std.testing.expectEqual(@as(usize, dims), cached.transformed_query.len);
    try std.testing.expectEqual(@as(usize, leaf_size), cached.member_ids.len);
}

test "hbc leaf split matrix reports dense apply workspace bytes" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);

    try idx.batchInsertWithMetadata(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 1.0, 1.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 2.0, 2.0 }, .metadata = "doc:3" },
    });

    const stats = resource_manager.sliceStats(.dense_apply_working_set);
    try std.testing.expect(stats.peak_bytes >= 3 * 2 * @sizeOf(f32));
    try std.testing.expectEqual(@as(u64, 0), stats.used_bytes);
}

test "hbc cache shrinks to resource budget under pressure" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const vector_bytes = estimateVectorCacheBytes(&.{ 1.0, 2.0, 3.0, 4.0 });
    var budgets = resource_manager_mod.Options.defaultBudgets();
    budgets[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_limit_bytes = vector_bytes,
        .hard_limit_bytes = vector_bytes,
    };
    var policies = resource_manager_mod.Options.defaultPolicies();
    policies[@intFromEnum(resource_manager_mod.Slice.hbc_node_metadata_cache)] = .{
        .soft_action = .shrink_cache,
        .hard_action = .shrink_cache,
    };
    var resource_manager = resource_manager_mod.ResourceManager.init(.{ .budgets = budgets, .policies = policies });

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .max_cached_vectors = 8 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.setRetainedVectorCacheEnabled(true);

    _ = try idx.cacheVector(1, &.{ 1.0, 2.0, 3.0, 4.0 });
    _ = try idx.cacheVector(2, &.{ 5.0, 6.0, 7.0, 8.0 });
    _ = try idx.cacheVector(3, &.{ 9.0, 10.0, 11.0, 12.0 });

    const stats = resource_manager.sliceStats(.hbc_node_metadata_cache);
    try std.testing.expect(stats.used_bytes <= stats.soft_limit_bytes);
    try expectVectorNotCached(&idx, 1);
    try expectVectorCached(&idx, 3);

    const cache_stats = idx.hbcCacheStats();
    try std.testing.expectEqual(stats.used_bytes, cache_stats.total_bytes);
    try std.testing.expectEqual(@as(u64, 3), cache_stats.vector.insertions);
    try std.testing.expect(cache_stats.vector.evictions > 0);
    try std.testing.expect(cache_stats.vector.used_bytes <= stats.soft_limit_bytes);
}

test "reopen rejects dimension mismatch" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    {
        var idx = try HBCIndex.open(alloc, path, .{ .dims = 4 });
        defer idx.close();
    }

    try std.testing.expectError(error.DimensionMismatch, HBCIndex.open(alloc, path, .{ .dims = 3 }));
}

test "reopen rejects metric mismatch" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    {
        var idx = try HBCIndex.open(alloc, path, .{ .dims = 3, .metric = .l2_squared });
        defer idx.close();
    }

    try std.testing.expectError(error.DistanceMetricMismatch, HBCIndex.open(alloc, path, .{ .dims = 3, .metric = .cosine }));
}

test "insert and search" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 4, .leaf_size = 10 });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 1.0, 0.0, 0.0, 0.0 });
    try idx.insert(2, &[_]f32{ 0.0, 1.0, 0.0, 0.0 });
    try idx.insert(3, &[_]f32{ 0.0, 0.0, 1.0, 0.0 });
    try idx.insert(4, &[_]f32{ 1.0, 1.0, 0.0, 0.0 });
    try idx.insert(5, &[_]f32{ 0.0, 0.0, 0.0, 1.0 });

    try std.testing.expectEqual(@as(u64, 5), idx.stats().active_count);

    // Search near [1, 0, 0, 0] — should return vector 1 as closest
    var results = try idx.search(&[_]f32{ 1.0, 0.1, 0.0, 0.0 }, 3);
    defer results.deinit();

    const hits = results.getHits();
    try std.testing.expect(hits.len > 0);
    // First result should be vector 1 (distance ~0.01)
    try std.testing.expectEqual(@as(u64, 1), hits[0].vector_id);
}

test "progressive filtered l2 traversal preserves exact top k without bound stops" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .leaf_size = 4,
        .branching_factor = 2,
        .search_width = 32,
        .use_quantization = true,
        .rerank_policy = .boundary,
    });
    defer idx.close();

    for (0..64) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, 0 });
    }
    var filter_ids: [22]u64 = undefined;
    for (&filter_ids, 0..) |*id, i| id.* = @intCast(i * 3 + 1);

    var profiled = try idx.searchProfiledRequest(.{
        .query = &.{ 0, 0 },
        .k = 3,
        .search_width = 32,
        .filter_ids = &filter_ids,
    });
    defer profiled.results.deinit();
    const hits = profiled.results.getHits();
    try std.testing.expectEqual(@as(usize, 3), hits.len);
    try std.testing.expectEqual(@as(u64, 1), hits[0].vector_id);
    try std.testing.expectEqual(@as(u64, 4), hits[1].vector_id);
    try std.testing.expectEqual(@as(u64, 7), hits[2].vector_id);
    try std.testing.expect(profiled.profile.traversal_bound_resolutions > 0);
    try std.testing.expect(profiled.profile.traversal_waves > 0);
    try std.testing.expectEqual(@as(u64, 0), profiled.profile.traversal_bound_stops);
    try std.testing.expect(profiled.profile.leaves_explored < 32);
}

test "flat rabitq centroid directory searches leaf postings" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 4,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 2,
        .flat_centroid_probe_count = 2,
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:a" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.2, 0.0 }, .metadata = "doc:b" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:y" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.2, 10.0 }, .metadata = "doc:z" },
    };
    try idx.bulkBuildWithMetadata(&items);

    var profiled = try idx.searchProfiledRequest(.{
        .query = &[_]f32{ 10.0, 10.0 },
        .k = 2,
        .search_width = 4,
        .load_metadata = false,
    });
    defer profiled.results.deinit();

    const hits = profiled.results.getHits();
    try std.testing.expect(hits.len > 0);
    try std.testing.expectEqual(@as(u64, 3), hits[0].vector_id);
    try std.testing.expect(profiled.profile.approx_nodes_expanded > 0);
    try std.testing.expect(profiled.profile.leaves_explored > 0);
}

test "flat rabitq filtered traversal advances past its initial probe wave safely" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .leaf_size = 4,
        .branching_factor = 2,
        .search_width = 32,
        .use_quantization = true,
        .rerank_policy = .boundary,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
        .flat_centroid_probe_count = 2,
    });
    defer idx.close();

    for (0..64) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, 0 });
    }
    var filter_ids: [22]u64 = undefined;
    for (&filter_ids, 0..) |*id, i| id.* = @intCast(i * 3 + 1);

    var profiled = try idx.searchProfiledRequest(.{
        .query = &.{ 0, 0 },
        .k = 3,
        .filter_ids = &filter_ids,
    });
    defer profiled.results.deinit();
    const hits = profiled.results.getHits();
    try std.testing.expectEqual(@as(usize, 3), hits.len);
    try std.testing.expectEqual(@as(u64, 1), hits[0].vector_id);
    try std.testing.expectEqual(@as(u64, 4), hits[1].vector_id);
    try std.testing.expectEqual(@as(u64, 7), hits[2].vector_id);
    try std.testing.expect(profiled.profile.traversal_initial_wave_leaves == 2);
    try std.testing.expect(profiled.profile.traversal_waves > 1);
    try std.testing.expect(profiled.profile.leaves_explored > 2);
    try std.testing.expectEqual(@as(u64, 0), profiled.profile.traversal_bound_stops);
}

test "flat rabitq full effort exhausts an underfilled published directory" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const leaf_size: u32 = 4;
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .leaf_size = leaf_size,
        .branching_factor = 2,
        .search_width = 16,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
        .flat_centroid_probe_count = 0,
    });
    defer idx.close();

    for (0..64) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, value / 8 });
    }

    const stats = idx.stats();
    const estimated_leaves: u32 = @intCast((stats.active_count + leaf_size - 1) / leaf_size);
    var profiled = try idx.searchProfiledRequest(.{
        .query = &.{ 0, 0 },
        .k = 3,
        .search_effort = 1,
        // Model the DB-layer estimate that previously became a false ceiling.
        .search_width = estimated_leaves,
        .load_metadata = false,
    });
    defer profiled.results.deinit();

    try std.testing.expect(profiled.profile.leaves_explored > estimated_leaves);
    try std.testing.expectEqual(stats.active_count, profiled.profile.approx_vectors_scored);
    try std.testing.expectEqual(@as(u64, 0), profiled.profile.traversal_bound_stops);
    try std.testing.expectEqual(@as(u64, 0), profiled.profile.traversal_frontier_remaining);
    try std.testing.expectEqual(vectorindex_search_results.CandidateCoverage.exhausted, profiled.results.candidate_coverage);

    const published_directory = idx.flat_centroid_directory orelse return error.TestUnexpectedResult;
    var repeated = try idx.searchProfiledRequest(.{
        .query = &.{ 1, 1 },
        .k = 3,
        .search_effort = 1,
        .search_width = estimated_leaves,
        .load_metadata = false,
    });
    defer repeated.results.deinit();
    try std.testing.expectEqual(published_directory, idx.flat_centroid_directory.?);
    try std.testing.expectEqual(stats.active_count, repeated.profile.approx_vectors_scored);
}

test "tree full effort exhausts underfilled leaves beyond estimated width" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const leaf_size: u32 = 4;
    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .l2_squared,
        .leaf_size = leaf_size,
        .branching_factor = 2,
        .search_width = 16,
        .use_quantization = true,
    });
    defer idx.close();

    for (0..64) |i| {
        const value: f32 = @floatFromInt(i);
        try idx.insert(@intCast(i + 1), &.{ value, value / 8 });
    }

    const stats = idx.stats();
    const estimated_leaves: u32 = @intCast((stats.active_count + leaf_size - 1) / leaf_size);
    var profiled = try idx.searchProfiledRequest(.{
        .query = &.{ 0, 0 },
        .k = 3,
        .search_effort = 1,
        .search_width = estimated_leaves,
        .load_metadata = false,
    });
    defer profiled.results.deinit();

    try std.testing.expect(profiled.profile.leaves_explored > estimated_leaves);
    try std.testing.expectEqual(stats.active_count, profiled.profile.approx_vectors_scored);
    try std.testing.expectEqual(@as(u64, 0), profiled.profile.traversal_bound_stops);
    try std.testing.expectEqual(@as(u64, 0), profiled.profile.traversal_frontier_remaining);
    try std.testing.expectEqual(vectorindex_search_results.CandidateCoverage.exhausted, profiled.results.candidate_coverage);
}

test "searchProfiled records phase timings and counters" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 3,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    for (0..16) |i| {
        const base = @as(f32, @floatFromInt(i));
        try idx.insert(@intCast(i + 1), &[_]f32{ base, base + 0.1, base + 0.2, base + 0.3 });
    }

    var profiled = try idx.searchProfiled(&[_]f32{ 0.0, 0.1, 0.2, 0.3 }, 5);
    defer profiled.results.deinit();

    try std.testing.expect(profiled.profile.total_ns > 0);
    try std.testing.expect(profiled.profile.root_load_ns <= profiled.profile.total_ns);
    try std.testing.expect(profiled.profile.nodes_visited > 0);
    try std.testing.expect(profiled.profile.leaf_score_ns > 0);
}

test "reopened lsm hbc loads quantized payloads on cold cache miss" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    {
        var idx = try HBCIndex.open(alloc, path, .{
            .dims = 4,
            .leaf_size = 3,
            .branching_factor = 2,
            .search_width = 8,
            .use_quantization = true,
            .storage_backend = .lsm,
        });
        defer idx.close();

        for (0..24) |i| {
            const group: f32 = @floatFromInt(i / 6);
            const offset: f32 = @floatFromInt(i % 6);
            const vector = [_]f32{ group, offset * 0.1, group + 0.25, offset * 0.2 };
            try idx.insertWithMetadata(@intCast(i + 1), &vector, "doc");
        }
    }

    {
        var reopened = try HBCIndex.open(alloc, path, .{
            .dims = 4,
            .leaf_size = 3,
            .branching_factor = 2,
            .search_width = 8,
            .use_quantization = true,
            .storage_backend = .lsm,
        });
        defer reopened.close();

        var profiled = try reopened.searchProfiled(&[_]f32{ 1.0, 0.2, 1.25, 0.4 }, 5);
        defer profiled.results.deinit();

        try std.testing.expect(profiled.results.getHits().len > 0);
        try std.testing.expect(profiled.profile.quantized_cache_misses > 0);
        try std.testing.expect(profiled.profile.approx_vectors_scored > 0);
    }
}

test "searchWithRequest returns empty when published root is missing" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 1.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.0, 1.0 }, "doc:2");

    idx.published_root_node.store(std.math.maxInt(u64), .release);
    idx.published_active_count.store(2, .release);

    var results = try idx.searchWithRequest(.{
        .query = &[_]f32{ 1.0, 0.0 },
        .k = 2,
    });
    defer results.deinit();

    try std.testing.expectEqual(@as(usize, 0), results.getHits().len);
}

test "searchWithRequest tolerates concurrent readers with runtime caches enabled" {
    const alloc = std.heap.c_allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(alloc, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .max_cached_nodes = 2,
        .max_cached_vectors = 8,
    });
    defer idx.close();

    for (0..256) |i| {
        const x = @as(f32, @floatFromInt(i % 16));
        const y = @as(f32, @floatFromInt(i / 16));
        var metadata_buf: [32]u8 = undefined;
        const metadata = try std.fmt.bufPrint(&metadata_buf, "doc:{d}", .{i});
        try idx.insertWithMetadata(@intCast(i + 1), &[_]f32{ x, y }, metadata);
    }

    const Worker = struct {
        idx: *HBCIndex,
        failed: *std.atomic.Value(u8),

        fn run(self: *@This(), worker_index: usize) void {
            var iter: usize = 0;
            while (iter < 1000 and self.failed.load(.monotonic) == 0) : (iter += 1) {
                const query_id = (iter + worker_index * 29) % 256;
                const x = @as(f32, @floatFromInt(query_id % 16));
                const y = @as(f32, @floatFromInt(query_id / 16));
                var results = self.idx.searchWithRequest(.{
                    .query = &[_]f32{ x, y },
                    .k = 4,
                }) catch {
                    self.failed.store(1, .monotonic);
                    return;
                };
                defer results.deinit();
                if (results.items.items.len == 0) {
                    self.failed.store(1, .monotonic);
                    return;
                }
                if (results.items.items[0].metadata == null) {
                    self.failed.store(1, .monotonic);
                    return;
                }
            }
        }
    };

    var failed = std.atomic.Value(u8).init(0);
    var workers = [_]Worker{.{ .idx = &idx, .failed = &failed }} ** 8;
    var threads: [workers.len]std.Thread = undefined;
    for (&threads, &workers, 0..) |*thread, *worker, worker_index| {
        thread.* = try std.Thread.spawn(.{}, Worker.run, .{ worker, worker_index });
    }
    for (threads) |thread| thread.join();
    try std.testing.expectEqual(@as(u8, 0), failed.load(.monotonic));
}

test "searchWithRequest applies filter prefix and distance bounds" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();
    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .epsilon = 2,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 1.0, 0.0 }, "keep:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.9, 0.1 }, "drop:2");
    try idx.insertWithMetadata(3, &[_]f32{ 0.8, 0.2 }, "keep:3");
    try idx.insert(4, &[_]f32{ 0.7, 0.3 });

    var results = try idx.searchWithRequest(.{
        .query = &[_]f32{ 1.0, 0.0 },
        .k = 10,
        .filter_prefix = "keep:",
        .distance_under = 0.1,
    });
    defer results.deinit();

    try std.testing.expectEqual(@as(usize, 2), results.items.items.len);
    try std.testing.expect(results.items.items[0].distance < 0.1);
    try std.testing.expect(results.items.items[1].distance < 0.1);
    try std.testing.expect(results.items.items[0].metadata != null);
    try std.testing.expect(results.items.items[1].metadata != null);
    try std.testing.expect(std.mem.startsWith(u8, results.items.items[0].metadata.?, "keep:"));
    try std.testing.expect(std.mem.startsWith(u8, results.items.items[1].metadata.?, "keep:"));

    var profiled = try idx.searchProfiledRequest(.{
        .query = &[_]f32{ 1.0, 0.0 },
        .k = 10,
        .filter_prefix = "keep:",
    });
    defer profiled.results.deinit();
    try std.testing.expect(profiled.profile.filter_candidates >= 3);
    try std.testing.expect(profiled.profile.filter_rejected >= 1);
    try std.testing.expect(profiled.profile.filter_metadata_batches > 0);

    // Missing metadata is a valid non-match. Exhaustive coverage validates
    // vectors/topology independently and must not quarantine the generation.
    var exhaustive = try idx.searchWithRequest(.{
        .query = &[_]f32{ 1.0, 0.0 },
        .k = 10,
        .search_effort = 1,
        .filter_prefix = "keep:",
    });
    defer exhaustive.deinit();
    try std.testing.expectEqual(@as(usize, 2), exhaustive.items.items.len);
    try std.testing.expect(!idx.generationRepairPending());

    var over_results = try idx.searchWithRequest(.{
        .query = &[_]f32{ 1.0, 0.0 },
        .k = 10,
        .distance_over = 0.01,
    });
    defer over_results.deinit();
    for (over_results.items.items) |item| {
        try std.testing.expect(item.distance > 0.01);
    }
}

test "searchWithRequest can skip metadata population" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();
    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .epsilon = 2,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 1.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.9, 0.1 }, "doc:2");

    var results = try idx.searchWithRequest(.{
        .query = &[_]f32{ 1.0, 0.0 },
        .k = 2,
        .load_metadata = false,
    });
    defer results.deinit();

    try std.testing.expectEqual(@as(usize, 2), results.items.items.len);
    for (results.items.items) |item| {
        try std.testing.expect(item.metadata == null);
    }
}

test "searchWithRequest applies filter and exclude ids" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();
    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .epsilon = 2,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.01, 0.0 }, "doc:2");
    try idx.insertWithMetadata(3, &[_]f32{ 0.02, 0.0 }, "doc:3");

    var included = try idx.searchWithRequest(.{
        .query = &[_]f32{ 0.0, 0.0 },
        .k = 3,
        .filter_ids = &[_]u64{ 2, 3 },
    });
    defer included.deinit();
    try std.testing.expectEqual(@as(usize, 2), included.items.items.len);
    try std.testing.expectEqual(@as(u64, 2), included.items.items[0].vector_id);
    try std.testing.expectEqual(@as(u64, 3), included.items.items[1].vector_id);

    var excluded = try idx.searchWithRequest(.{
        .query = &[_]f32{ 0.0, 0.0 },
        .k = 3,
        .exclude_ids = &[_]u64{1},
    });
    defer excluded.deinit();
    try std.testing.expectEqual(@as(usize, 2), excluded.items.items.len);
    try std.testing.expectEqual(@as(u64, 2), excluded.items.items[0].vector_id);
    try std.testing.expectEqual(@as(u64, 3), excluded.items.items[1].vector_id);
}

test "searchProfiled respects rerank_policy never" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 4,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
        .rerank_policy = .never,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0, 0.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0, 0.0, 0.0 }, "doc:2");
    try idx.insertWithMetadata(3, &[_]f32{ 0.2, 0.0, 0.0, 0.0 }, "doc:3");

    var profiled = try idx.searchProfiledRequest(.{
        .query = &[_]f32{ 0.0, 0.0, 0.0, 0.0 },
        .k = 2,
    });
    defer profiled.results.deinit();

    try std.testing.expectEqual(@as(u64, 0), profiled.profile.reranked_vectors);
    try std.testing.expectEqual(@as(usize, 2), profiled.results.getHits().len);
    for (profiled.results.items.items) |item| {
        try std.testing.expect(item.metadata != null);
    }
}

test "root quantized set is persisted as nonquantized" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 8,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 1.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.0, 1.0 }, "doc:2");

    var txn = try idx.beginReadTxn();
    defer txn.abort();

    var quantized = (try idx.getQuantized(&txn, idx.metadata.root_node, true, 2)) orelse return error.TestUnexpectedResult;
    defer quantized.deinit(std.testing.allocator);
    switch (quantized.ptr().*) {
        .nonquant => |set| {
            try std.testing.expectEqual(@as(usize, 2), set.getCount());
            try std.testing.expectEqual(@as(i64, 2), set.vectors.dims);
        },
        else => return error.TestUnexpectedResult,
    }
}

test "loadQuantized rejects malformed non-root quantized count" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(alloc, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:2");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:3");
    try idx.insertWithMetadata(4, &[_]f32{ 10.1, 10.0 }, "doc:4");

    var leaf_id: u64 = 0;
    var expected_count: usize = 0;
    var bad_quantized: QuantizedSet = undefined;
    {
        var read_txn = try idx.beginReadTxn();
        defer read_txn.abort();
        var root = try idx.loadNode(&read_txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(!root.is_leaf);

        leaf_id = root.children[0];
        var leaf = try idx.loadNode(&read_txn, leaf_id);
        defer leaf.deinit(alloc);
        expected_count = leaf.members.len;

        bad_quantized = try idx.loadQuantized(&read_txn, leaf_id, false, expected_count);
    }
    defer bad_quantized.deinit(alloc);

    switch (bad_quantized) {
        .rabit => |*set| {
            try std.testing.expect(set.code_counts.len > 0);
            const shorter = try alloc.dupe(u32, set.code_counts[0 .. set.code_counts.len - 1]);
            alloc.free(set.code_counts);
            set.code_counts = shorter;
        },
        else => return error.TestUnexpectedResult,
    }

    var write_txn = try idx.beginWriteTxn();
    errdefer write_txn.abort();

    const encoded = switch (bad_quantized) {
        .rabit => |*set| try set.encode(alloc),
        .nonquant => |*set| try set.encode(alloc),
    };
    defer alloc.free(encoded);

    var key_buf: [10]u8 = undefined;
    try idx.putNamespaced(&write_txn, .quant, encodeQuantKey(&key_buf, leaf_id), encoded);
    try write_txn.commit();

    var validate_txn = try idx.beginReadTxn();
    defer validate_txn.abort();
    try std.testing.expectError(error.Corrupted, idx.loadQuantized(&validate_txn, leaf_id, false, expected_count));
}

test "batch insert options can defer quantized rebuild until finish" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.1, 10.0 }, .metadata = "doc:4" },
    };

    var txn = try idx.beginWriteTxn();
    var txn_active = true;
    errdefer if (txn_active) txn.abort();
    try idx.batchInsertWithMetadataTxnOptions(&txn, &items, .{
        .defer_quantized_rebuild = true,
        .centroid_only_routing = true,
    });
    try idx.finishWriteTxnOptions(&txn, .{
        .defer_quantized_rebuild = true,
        .centroid_only_routing = true,
    });
    txn_active = false;

    var results = try idx.search(&[_]f32{ 10.0, 10.0 }, 2);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 2), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 3), results.getHits()[0].vector_id);

    var read_txn = try idx.beginReadTxn();
    defer read_txn.abort();
    var root = try idx.loadNode(&read_txn, idx.metadata.root_node);
    defer root.deinit(std.testing.allocator);
    var quantized = (try idx.getQuantized(&read_txn, idx.metadata.root_node, root.parent == 0, root.children.len)) orelse return error.TestUnexpectedResult;
    defer quantized.deinit(std.testing.allocator);
    switch (quantized.ptr().*) {
        .nonquant, .rabit => {},
    }
}

test "deferred quantized rebuild refreshes touched nodes only" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(alloc, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    for (0..24) |i| {
        const vector_id: u64 = @intCast(i + 1);
        const x: f32 = @floatFromInt(i % 6);
        const y: f32 = @floatFromInt(i / 6);
        const vector = [_]f32{ x, y };
        var metadata_buf: [32]u8 = undefined;
        const metadata = try std.fmt.bufPrint(&metadata_buf, "doc:{d}", .{vector_id});
        try idx.insertWithMetadata(vector_id, &vector, metadata);
    }

    const node_count = idx.stats().node_count;
    try std.testing.expect(node_count > 6);

    idx.resetWriteProfile();
    const update = [_]BatchInsertItem{
        .{ .vector_id = 5, .vector = &[_]f32{ 0.25, 0.75 }, .metadata = "doc:5-updated" },
    };
    try idx.batchInsertWithMetadataOptions(&update, .{
        .defer_quantized_rebuild = true,
        .centroid_only_routing = true,
    });

    const profile = idx.getWriteProfile();
    try std.testing.expect(profile.ns_quant_put_calls > 0);
    try std.testing.expect(profile.ns_quant_put_calls < node_count);
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_quantized_nodes.count());

    var results = try idx.search(&[_]f32{ 0.25, 0.75 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 5), results.getHits()[0].vector_id);
}

test "deferred quantized append updates leaf without queued full rebuild" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    const base_vectors = [_][2]f32{
        .{ 0.0, 0.0 },
        .{ 0.1, 0.0 },
        .{ 0.2, 0.0 },
        .{ 0.3, 0.0 },
        .{ 8.0, 8.0 },
        .{ 8.1, 8.0 },
        .{ 8.2, 8.0 },
        .{ 8.3, 8.0 },
        .{ 16.0, 16.0 },
    };
    var base_items: [base_vectors.len]BatchInsertItem = undefined;
    for (&base_items, 0..) |*item, i| {
        item.* = .{
            .vector_id = @intCast(i + 1),
            .vector = &base_vectors[i],
            .metadata = "doc",
        };
    }

    var idx = try HBCIndex.open(alloc, tmp_path, .{
        .dims = 2,
        .leaf_size = 8,
        .branching_factor = 4,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    try idx.bulkBuildWithMetadata(&base_items);
    try std.testing.expect(idx.stats().node_count > 1);

    const append_vector = [_]f32{ 0.05, 0.0 };
    {
        var read_txn = try idx.beginReadTxn();
        defer read_txn.abort();
        const leaf_id = try idx.findLeafWithOptions(&read_txn, idx.metadata.root_node, &append_vector, true);
        var leaf = try idx.loadNode(&read_txn, leaf_id);
        defer leaf.deinit(alloc);
        try std.testing.expect(leaf.parent != 0);
        try std.testing.expect(leaf.members.len < idx.config.leaf_size);
    }

    const append_items = [_]BatchInsertItem{
        .{ .vector_id = 10, .vector = &append_vector, .metadata = "doc:10" },
    };
    idx.resetWriteProfile();
    var txn = try idx.beginWriteTxn();
    var txn_active = true;
    errdefer if (txn_active) txn.abort();
    try idx.batchInsertWithMetadataTxnOptions(&txn, &append_items, .{
        .assume_absent_ids = true,
        .defer_quantized_rebuild = true,
        .allow_quantized_routing = true,
    });

    try std.testing.expectEqual(@as(u32, 0), idx.deferred_quantized_nodes.count());
    const profile = idx.getWriteProfile();
    try std.testing.expect(profile.ns_quant_put_calls > 0);
    try txn.commit();
    txn_active = false;

    var results = try idx.search(&append_vector, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 10), results.getHits()[0].vector_id);
}

test "bulk ingest session publishes deferred quantized nodes once at finish" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, tmp_path, .{
        .dims = 2,
        .leaf_size = 3,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 8,
        },
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 0.2, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &[_]f32{ 10.1, 10.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &[_]f32{ 10.2, 10.0 }, .metadata = "doc:6" },
    };
    const options: BatchInsertOptions = .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .defer_quantized_rebuild_to_bulk_finish = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    try idx.batchInsertWithMetadataOptions(items[0..3], options);
    try std.testing.expect(idx.deferred_quantized_nodes.count() > 0);
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().ns_quant_put_calls);

    try idx.batchInsertWithMetadataOptions(items[3..], options);
    try std.testing.expect(idx.deferred_quantized_nodes.count() > 0);
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().ns_quant_put_calls);

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_quantized_nodes.count());
    try std.testing.expect(idx.getWriteProfile().ns_quant_put_calls > 0);

    var results = try idx.search(&[_]f32{ 10.1, 10.0 }, 2);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 2), results.getHits().len);
}

test "streaming replay session publishes reopenable batches without incomplete marker" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    const config: HBCConfig = .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
        .storage_backend = .lsm,
    };
    var idx = try HBCIndex.openWithLsmOptions(alloc, tmp_path, config, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 8,
        },
    });

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.1, 10.0 }, .metadata = "doc:4" },
    };

    try idx.beginStreamingReplaySession();
    try std.testing.expectEqual(WriteSessionKind.streaming_replay, idx.write_session_kind.?);
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .defer_quantized_rebuild_to_bulk_finish = true,
        .defer_leaf_splits_to_bulk_finish = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
    });

    try std.testing.expectEqual(@as(u32, 0), idx.deferred_quantized_nodes.count());
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_node_keys.count());
    try std.testing.expectEqual(@as(u64, items.len), idx.publishedActiveCount());

    // A long-lived streaming scope may contain several independently durable
    // mutation batches. Exercise overwrite, delete, and insert before the
    // process interruption; replaying any suffix remains idempotent.
    try idx.batchDelete(&.{2});
    try idx.batchInsertWithMetadataOptions(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 20.0, 20.0 }, .metadata = "doc:1-updated" },
        .{ .vector_id = 5, .vector = &[_]f32{ 20.1, 20.0 }, .metadata = "doc:5" },
    }, .{
        .assume_absent_ids = false,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .defer_quantized_rebuild_to_bulk_finish = true,
        .defer_leaf_splits_to_bulk_finish = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
    });
    try std.testing.expectEqual(@as(u64, 4), idx.publishedActiveCount());

    var read = try idx.beginReadTxn();
    try std.testing.expectError(error.NotFound, read.get(.meta, bulk_publish_state_key));
    read.abort();

    // Do not run either session finish or abort. The crash teardown discards
    // unsynced process state, so reopen proves that the committed batch and its
    // metadata are independently recoverable and carry no publication marker.
    idx.abandonAfterCrash();

    var reopened = try HBCIndex.openWithLsmOptions(alloc, tmp_path, config, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 8,
        },
    });
    try std.testing.expectEqual(@as(u64, 4), reopened.stats().active_count);
    var results = try reopened.search(&[_]f32{ 20.0, 20.0 }, 2);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 2), results.getHits().len);

    // Replay the committed suffix: identical insert, cross-leaf overwrite,
    // absent delete, and delete-then-insert. A second interruption must still
    // reopen with exact cardinality and final values.
    try reopened.beginStreamingReplaySession();
    try reopened.batchInsertWithMetadataOptions(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 20.0, 20.0 }, .metadata = "doc:1-updated" },
        .{ .vector_id = 3, .vector = &[_]f32{ -20.0, -20.0 }, .metadata = "doc:3-moved" },
    }, .{
        .assume_absent_ids = false,
        .coalesce_leaf_writes = true,
        .bulk_ingest = true,
    });
    try reopened.batchDelete(&.{ 99, 5 });
    try reopened.batchInsertWithMetadataOptions(&.{
        .{ .vector_id = 5, .vector = &[_]f32{ 20.1, 20.0 }, .metadata = "doc:5" },
    }, .{
        .assume_absent_ids = false,
        .coalesce_leaf_writes = true,
        .bulk_ingest = true,
    });
    try std.testing.expectEqual(@as(u64, 4), reopened.publishedActiveCount());
    reopened.abandonAfterCrash();

    var final = try HBCIndex.openWithLsmOptions(alloc, tmp_path, config, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 8,
        },
    });
    defer final.close();
    try std.testing.expectEqual(@as(u64, 4), final.stats().active_count);
    const moved_metadata = (try final.getMetadata(3)) orelse return error.TestUnexpectedResult;
    defer alloc.free(moved_metadata);
    try std.testing.expectEqualStrings("doc:3-moved", moved_metadata);
    var moved_results = try final.search(&[_]f32{ -20.0, -20.0 }, 1);
    defer moved_results.deinit();
    try std.testing.expectEqual(@as(u64, 3), moved_results.getHits()[0].vector_id);
}

test "streaming replay finish establishes durability for relaxed backend" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();
    const config: HBCConfig = .{
        .dims = 2,
        .storage_backend = .lsm,
    };
    const options: hbc_backend.LsmOptions = .{
        .backend_options = .{ .backend = .{ .durability = .none } },
    };
    var idx = try HBCIndex.openWithLsmOptions(alloc, tmp_path, config, options);
    try idx.beginStreamingReplaySession();
    try idx.batchInsertWithMetadataOptions(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 1, 0 }, .metadata = "doc:1" },
    }, .{
        .assume_absent_ids = true,
        .bulk_ingest = true,
    });
    // The finish performs an explicit forced sync before a higher layer may
    // advance its durable applied-sequence watermark.
    try idx.finishStreamingReplaySessionWithOptions(.{});
    idx.abandonAfterCrash();

    var reopened = try HBCIndex.openWithLsmOptions(alloc, tmp_path, config, options);
    defer reopened.close();
    try std.testing.expectEqual(@as(u64, 1), reopened.stats().active_count);
    var read = try reopened.beginReadTxn();
    defer read.abort();
    try std.testing.expectError(error.NotFound, read.get(.meta, bulk_publish_state_key));
}

test "streaming replay finish establishes explicit durability for lmdb no_sync" {
    if (!supports_lmdb) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();
    const config: HBCConfig = .{
        .dims = 2,
        .storage_backend = .lmdb,
        .no_sync = true,
        .no_meta_sync = true,
    };
    var idx = try HBCIndex.open(alloc, tmp_path, config);
    try idx.beginStreamingReplaySession();
    try idx.batchInsertWithMetadataOptions(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 1, 0 }, .metadata = "doc:1" },
    }, .{
        .assume_absent_ids = true,
        .bulk_ingest = true,
    });
    // The logical LMDB commit runs with no_sync, so the streaming finish must
    // perform the explicit forced sync before a caller can publish its applied
    // sequence.
    try idx.finishStreamingReplaySessionWithOptions(.{});
    idx.close();

    var reopened = try HBCIndex.open(alloc, tmp_path, config);
    defer reopened.close();
    try std.testing.expectEqual(@as(u64, 1), reopened.stats().active_count);
    const metadata = (try reopened.getMetadata(1)) orelse return error.TestUnexpectedResult;
    defer alloc.free(metadata);
    try std.testing.expectEqualStrings("doc:1", metadata);
}

test "interrupted bulk publication remains quarantined" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    const config: HBCConfig = .{
        .dims = 2,
        .storage_backend = .lsm,
    };
    var idx = try HBCIndex.open(alloc, tmp_path, config);
    try idx.beginBulkIngestSession();
    try idx.batchInsertWithMetadataOptions(&[_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 1.0, 0.0 }, .metadata = "doc:1" },
    }, .{
        .assume_absent_ids = true,
        .bulk_ingest = true,
    });
    idx.abandonAfterCrash();

    try std.testing.expectError(
        error.IncompleteBulkPublish,
        HBCIndex.open(alloc, tmp_path, config),
    );
}

test "bulk publication revalidates admission before every publish window" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    const config: HBCConfig = .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
        .prefer_key_local_leaf_splits = true,
    };
    var idx = try HBCIndex.open(alloc, tmp_path, config);
    var abandoned = false;
    defer if (!abandoned) idx.close();
    try idx.batchInsertWithMetadata(&.{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 100.0, 0.0 }, .metadata = "doc:3" },
    });
    try idx.beginBulkIngestSession();
    try idx.batchInsertWithMetadataOptions(&.{
        .{ .vector_id = 4, .vector = &.{ 100.1, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 100.2, 0.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 100.3, 0.0 }, .metadata = "doc:6" },
        .{ .vector_id = 7, .vector = &.{ 100.4, 0.0 }, .metadata = "doc:7" },
        .{ .vector_id = 8, .vector = &.{ 100.5, 0.0 }, .metadata = "doc:8" },
        .{ .vector_id = 9, .vector = &.{ 100.6, 0.0 }, .metadata = "doc:9" },
        .{ .vector_id = 10, .vector = &.{ 100.7, 0.0 }, .metadata = "doc:10" },
    }, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .defer_leaf_splits_to_bulk_finish = true,
        .bulk_ingest = true,
    });

    const Admission = struct {
        calls: usize = 0,

        fn check(ptr: *anyopaque) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.calls += 1;
            if (self.calls == 2) return error.TestCapacityUnavailable;
        }
    };
    var admission = Admission{};
    try std.testing.expectError(error.TestCapacityUnavailable, idx.finishBulkIngestSessionWithOptions(.{
        .compact = false,
        .max_deferred_hbc_leaf_splits_per_publish = 1,
        .bulk_rebuild_hbc_leaf_min_members = 999,
        .admission_ctx = &admission,
        .admission_fn = Admission.check,
    }));
    try std.testing.expectEqual(@as(usize, 2), admission.calls);

    // The first bounded window was durable. A denied second window therefore
    // leaves the publication marker intact and the partial generation fenced.
    idx.abandonAfterCrash();
    abandoned = true;
    try std.testing.expectError(error.IncompleteBulkPublish, HBCIndex.open(alloc, tmp_path, config));
}

test "bulk publication abort clears only an unmodified session" {
    const alloc = std.testing.allocator;
    var empty_path: TestPath = .{};
    const empty_tmp_path = empty_path.init();
    defer empty_path.cleanup();

    const config: HBCConfig = .{
        .dims = 2,
        .storage_backend = .lsm,
    };
    var empty = try HBCIndex.open(alloc, empty_tmp_path, config);
    try empty.beginBulkIngestSession();
    empty.abortBulkIngestSession();
    empty.close();
    var empty_reopened = try HBCIndex.open(alloc, empty_tmp_path, config);
    empty_reopened.close();

    var dirty_path: TestPath = .{};
    const dirty_tmp_path = dirty_path.init();
    defer dirty_path.cleanup();

    var dirty = try HBCIndex.open(alloc, dirty_tmp_path, config);
    try dirty.beginBulkIngestSession();
    try dirty.batchInsertWithMetadataOptions(&[_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 1.0, 0.0 }, .metadata = "doc:1" },
    }, .{
        .assume_absent_ids = true,
        .bulk_ingest = true,
    });
    dirty.abortBulkIngestSession();
    dirty.close();
    try std.testing.expectError(
        error.IncompleteBulkPublish,
        HBCIndex.open(alloc, dirty_tmp_path, config),
    );
}

test "write sessions reject mixed nested publication semantics" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(alloc, tmp_path, .{ .dims = 2 });
    defer idx.close();

    try idx.beginStreamingReplaySession();
    defer idx.abortStreamingReplaySession();
    try std.testing.expectError(error.MixedWriteSessionKinds, idx.beginBulkIngestSession());
    try std.testing.expectEqual(@as(usize, 1), idx.write_session_depth);
    try std.testing.expectEqual(WriteSessionKind.streaming_replay, idx.write_session_kind.?);
}

test "bulk ingest keeps published search state stale until finish" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .max_cached_vectors = 16,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 1.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.0, 1.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 0.9, 0.1 }, .metadata = "doc:3" },
    };
    const options: BatchInsertOptions = .{
        .assume_absent_ids = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    defer if (session_open) idx.abortBulkIngestSession();

    try idx.batchInsertWithMetadataOptions(&items, options);
    try std.testing.expectEqual(@as(u64, 0), idx.publishedActiveCount());
    try std.testing.expectEqual(@as(u64, 1), idx.publishedNodeCount());

    var pending = try idx.search(&[_]f32{ 1.0, 0.0 }, 2);
    defer pending.deinit();
    try std.testing.expectEqual(@as(usize, 0), pending.getHits().len);

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;

    try std.testing.expectEqual(@as(u64, 3), idx.publishedActiveCount());
    try std.testing.expect(idx.publishedNodeCount() >= 1);

    var published = try idx.search(&[_]f32{ 1.0, 0.0 }, 2);
    defer published.deinit();
    try std.testing.expect(published.getHits().len > 0);
}

test "flat centroid search ignores staged bulk ingest nodes until publish" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 8,
        .branching_factor = 2,
        .search_width = 4,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
        .flat_centroid_probe_count = 4,
        .max_cached_nodes = 16,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    const initial = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 1.0, 0.0 }, .metadata = "doc:2" },
    };
    try idx.bulkBuildWithMetadata(&initial);

    var warm = try idx.search(&[_]f32{ 0.0, 0.0 }, 1);
    defer warm.deinit();
    try std.testing.expectEqual(@as(u64, 1), warm.getHits()[0].vector_id);

    const staged = [_]BatchInsertItem{
        .{ .vector_id = 99, .vector = &[_]f32{ 100.0, 100.0 }, .metadata = "doc:99" },
    };
    const options: BatchInsertOptions = .{
        .assume_absent_ids = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    defer if (session_open) idx.abortBulkIngestSession();

    try idx.batchInsertWithMetadataOptions(&staged, options);
    try std.testing.expectEqual(@as(u64, 2), idx.publishedActiveCount());

    var pending = try idx.search(&[_]f32{ 100.0, 100.0 }, 2);
    defer pending.deinit();
    for (pending.getHits()) |hit| {
        try std.testing.expect(hit.vector_id != 99);
    }

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;

    var published = try idx.search(&[_]f32{ 100.0, 100.0 }, 1);
    defer published.deinit();
    try std.testing.expectEqual(@as(usize, 1), published.getHits().len);
    try std.testing.expectEqual(@as(u64, 99), published.getHits()[0].vector_id);
}

test "bulk build creates searchable index and persists metadata" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:a" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.1, 0.0 }, .metadata = "doc:b" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:y" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.1, 10.0 }, .metadata = "doc:z" },
    };
    try idx.bulkBuildWithMetadata(&items);

    const stats = idx.stats();
    try std.testing.expectEqual(@as(u64, 4), stats.active_count);
    try std.testing.expect(stats.node_count > 0);

    var results = try idx.search(&[_]f32{ 10.0, 10.0 }, 2);
    defer results.deinit();
    try std.testing.expectEqual(@as(u64, 3), results.getHits()[0].vector_id);
    try std.testing.expect(results.getHits()[0].metadata != null);
    try std.testing.expectEqualStrings("doc:y", results.getHits()[0].metadata.?);

    const reopened = try idx.getMetadata(4);
    defer if (reopened) |owned| std.testing.allocator.free(owned);
    try std.testing.expect(reopened != null);
    try std.testing.expectEqualStrings("doc:z", reopened.?);
}

test "bulk build refreshes quantized payload after internal reparenting" {
    const alloc = std.testing.allocator;
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    const dims = 384;
    const count = 32;
    var vectors = try alloc.alloc(f32, count * dims);
    defer alloc.free(vectors);
    var items = try alloc.alloc(BatchInsertItem, count);
    defer alloc.free(items);

    for (0..count) |i| {
        const vector = vectors[i * dims ..][0..dims];
        for (vector, 0..) |*value, d| {
            value.* = @as(f32, @floatFromInt(((i + 1) * 17 + (d + 3) * 11) % 97)) / 97.0;
        }
        items[i] = .{
            .vector_id = @intCast(i + 1),
            .vector = vector,
            .metadata = "doc",
        };
    }

    var idx = try HBCIndex.open(alloc, tmp_path, .{
        .dims = dims,
        .leaf_size = 4,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    try idx.bulkBuildWithMetadata(items);

    const query = vectors[13 * dims ..][0..dims];
    var results = try idx.search(query, 5);
    defer results.deinit();
    try std.testing.expect(results.getHits().len > 0);
}

test "hilbert-seeded bulk build creates searchable index" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 4,
        .search_width = 8,
        .use_quantization = true,
        .bulk_build_algo = .hilbert_seeded,
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:a" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.1, 0.0 }, .metadata = "doc:b" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:y" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.1, 10.0 }, .metadata = "doc:z" },
    };
    try idx.bulkBuildWithMetadata(&items);

    var results = try idx.search(&[_]f32{ 10.0, 10.0 }, 2);
    defer results.deinit();
    try std.testing.expectEqual(@as(u64, 3), results.getHits()[0].vector_id);
    try std.testing.expect(results.getHits()[0].metadata != null);
    try std.testing.expectEqualStrings("doc:y", results.getHits()[0].metadata.?);
}

test "doc-key-seeded bulk build creates searchable index" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 4,
        .search_width = 8,
        .use_quantization = true,
        .bulk_build_algo = .doc_key_seeded,
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:a" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.1, 0.0 }, .metadata = "doc:b" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:y" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.1, 10.0 }, .metadata = "doc:z" },
    };
    try idx.bulkBuildWithMetadata(&items);

    var results = try idx.search(&[_]f32{ 10.0, 10.0 }, 2);
    defer results.deinit();
    try std.testing.expectEqual(@as(u64, 3), results.getHits()[0].vector_id);
    try std.testing.expect(results.getHits()[0].metadata != null);
    try std.testing.expectEqualStrings("doc:y", results.getHits()[0].metadata.?);
}

test "kmeans bulk build creates searchable index" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 4,
        .search_width = 8,
        .use_quantization = true,
        .bulk_build_algo = .kmeans,
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:a" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.1, 0.0 }, .metadata = "doc:b" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.0, 10.0 }, .metadata = "doc:y" },
        .{ .vector_id = 4, .vector = &[_]f32{ 10.1, 10.0 }, .metadata = "doc:z" },
        .{ .vector_id = 5, .vector = &[_]f32{ 20.0, 0.0 }, .metadata = "doc:m" },
        .{ .vector_id = 6, .vector = &[_]f32{ 20.1, 0.0 }, .metadata = "doc:n" },
    };
    try idx.bulkBuildWithMetadata(&items);

    const stats = idx.stats();
    try std.testing.expectEqual(@as(u64, items.len), stats.active_count);
    try std.testing.expect(stats.node_count >= 4);

    var results = try idx.search(&[_]f32{ 10.0, 10.0 }, 2);
    defer results.deinit();
    try std.testing.expectEqual(@as(u64, 3), results.getHits()[0].vector_id);
    try std.testing.expect(results.getHits()[0].metadata != null);
    try std.testing.expectEqualStrings("doc:y", results.getHits()[0].metadata.?);
}

test "reinsert existing vector id does not grow active count and updates search" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .epsilon = 2,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:2");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:3");
    try std.testing.expectEqual(@as(u64, 3), idx.stats().active_count);

    try idx.insertWithMetadata(1, &[_]f32{ 10.1, 10.0 }, "doc:1-updated");
    try std.testing.expectEqual(@as(u64, 3), idx.stats().active_count);

    var near_old = try idx.searchWithRequest(.{
        .query = &[_]f32{ 0.0, 0.0 },
        .k = 3,
    });
    defer near_old.deinit();
    try std.testing.expectEqual(@as(u64, 2), near_old.items.items[0].vector_id);

    var near_new = try idx.searchWithRequest(.{
        .query = &[_]f32{ 10.0, 10.0 },
        .k = 3,
    });
    defer near_new.deinit();
    try std.testing.expectEqual(@as(u64, 3), near_new.items.items[0].vector_id);
    try std.testing.expectEqual(@as(u64, 1), near_new.items.items[1].vector_id);
    try std.testing.expect(near_new.items.items[1].metadata != null);
    try std.testing.expectEqualStrings("doc:1-updated", near_new.items.items[1].metadata.?);
}

test "reinsert existing vector id after reopen on lsm backend updates search" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    {
        var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
            .dims = 2,
            .leaf_size = 2,
            .branching_factor = 2,
            .search_width = 8,
            .epsilon = 2,
            .storage_backend = .lsm,
        });
        defer idx.close();

        try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:1");
        try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:2");
        try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:3");
        try std.testing.expectEqual(@as(u64, 3), idx.stats().active_count);
    }

    {
        var reopened = try HBCIndex.open(std.testing.allocator, tmp_path, .{
            .dims = 2,
            .leaf_size = 2,
            .branching_factor = 2,
            .search_width = 8,
            .epsilon = 2,
            .storage_backend = .lsm,
        });
        defer reopened.close();

        try reopened.insertWithMetadata(1, &[_]f32{ 10.1, 10.0 }, "doc:1-updated");
        try std.testing.expectEqual(@as(u64, 3), reopened.stats().active_count);

        var near_old = try reopened.searchWithRequest(.{
            .query = &[_]f32{ 0.0, 0.0 },
            .k = 3,
        });
        defer near_old.deinit();
        try std.testing.expectEqual(@as(u64, 2), near_old.items.items[0].vector_id);

        var near_new = try reopened.searchWithRequest(.{
            .query = &[_]f32{ 10.0, 10.0 },
            .k = 3,
        });
        defer near_new.deinit();
        try std.testing.expectEqual(@as(u64, 3), near_new.items.items[0].vector_id);
        try std.testing.expectEqual(@as(u64, 1), near_new.items.items[1].vector_id);
        try std.testing.expect(near_new.items.items[1].metadata != null);
        try std.testing.expectEqualStrings("doc:1-updated", near_new.items.items[1].metadata.?);
    }
}

test "kmeans split produces balanced clusters" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2, .leaf_size = 5 });
    defer idx.close();

    // Insert two distinct clusters
    // Cluster A near (0, 0)
    try idx.insert(1, &[_]f32{ 0.0, 0.0 });
    try idx.insert(2, &[_]f32{ 0.1, 0.1 });
    try idx.insert(3, &[_]f32{ 0.2, 0.0 });
    // Cluster B near (10, 10)
    try idx.insert(4, &[_]f32{ 10.0, 10.0 });
    try idx.insert(5, &[_]f32{ 10.1, 10.1 });
    try idx.insert(6, &[_]f32{ 10.2, 10.0 });

    // Should have split — root is internal node now
    const s = idx.stats();
    try std.testing.expectEqual(@as(u64, 6), s.active_count);
    try std.testing.expect(s.node_count > 1);

    // Search in cluster A should find cluster A vectors first
    var results = try idx.search(&[_]f32{ 0.0, 0.0 }, 3);
    defer results.deinit();
    const hits = results.getHits();
    try std.testing.expectEqual(@as(usize, 3), hits.len);
    // All three closest should be from cluster A (IDs 1-3)
    for (hits) |hit| {
        try std.testing.expect(hit.vector_id <= 3);
    }
}

test "inner product split keeps mean centroids" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .inner_product,
        .leaf_size = 8,
    });
    defer idx.close();

    const ids = [_]u64{ 1, 2, 3, 4 };
    const raw = [_]f32{
        2.0,  0.0,
        4.0,  0.0,
        -2.0, 0.0,
        -4.0, 0.0,
    };
    const vector_set = vec.Set{
        .dims = 2,
        .count = ids.len,
        .data = @constCast(raw[0..]),
    };

    const split = try idx.splitVectorSet(&vector_set, &ids);
    defer alloc.free(split.c1);
    defer alloc.free(split.g1);
    defer alloc.free(split.c2);
    defer alloc.free(split.g2);

    try std.testing.expectApproxEqAbs(@as(f32, 3.0), vec.norm(split.c1), 0.001);
    try std.testing.expectApproxEqAbs(@as(f32, 3.0), vec.norm(split.c2), 0.001);
}

test "cosine split rejects non unit vectors" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .cosine,
        .leaf_size = 8,
    });
    defer idx.close();

    const ids = [_]u64{ 1, 2 };
    const raw = [_]f32{
        2.0, 0.0,
        0.0, 1.0,
    };
    const vector_set = vec.Set{
        .dims = 2,
        .count = ids.len,
        .data = @constCast(raw[0..]),
    };

    try std.testing.expectError(error.NonUnitVector, idx.splitVectorSet(&vector_set, &ids));
}

test "cosine leaf centroid stays unit through insert update delete" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .cosine,
        .leaf_size = 8,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 1.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.0, 1.0 }, "doc:2");

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(root.is_leaf);
        try std.testing.expectApproxEqAbs(@as(f32, 1.0), vec.norm(root.centroid), 0.001);
    }

    try idx.insertWithMetadata(1, &[_]f32{ 0.70710677, 0.70710677 }, "doc:1-updated");
    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(root.is_leaf);
        try std.testing.expectApproxEqAbs(@as(f32, 1.0), vec.norm(root.centroid), 0.001);
    }

    try idx.delete(2);
    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(root.is_leaf);
        try std.testing.expectApproxEqAbs(@as(f32, 1.0), vec.norm(root.centroid), 0.001);
    }
}

test "cosine split root centroid stays unit" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .metric = .cosine,
        .leaf_size = 2,
        .branching_factor = 2,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 1.0, 0.0 }, "doc:1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.9805807, 0.19611613 }, "doc:2");
    try idx.insertWithMetadata(3, &[_]f32{ -1.0, 0.0 }, "doc:3");
    try idx.insertWithMetadata(4, &[_]f32{ -0.9805807, 0.19611613 }, "doc:4");

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    var root = try idx.loadNode(&txn, idx.metadata.root_node);
    defer root.deinit(alloc);
    try std.testing.expect(!root.is_leaf);
    try std.testing.expectApproxEqAbs(@as(f32, 1.0), vec.norm(root.centroid), 0.001);

    for (root.children) |child_id| {
        var child = try idx.loadNode(&txn, child_id);
        defer child.deinit(alloc);
        try std.testing.expectApproxEqAbs(@as(f32, 1.0), vec.norm(child.centroid), 0.001);
    }
}

test "collectCompetitiveInsertCandidates retains maybe closer children" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
    });
    defer idx.close();

    const child_ids = [_]u64{ 11, 22, 33 };
    const distances = [_]f32{ 1.00, 1.06, 1.40 };
    const error_bounds = [_]f32{ 0.02, 0.08, 0.01 };

    const competitive = try idx.collectCompetitiveInsertCandidates(&child_ids, &distances, &error_bounds);
    defer std.testing.allocator.free(competitive);

    try std.testing.expectEqual(@as(usize, 2), competitive.len);
    try std.testing.expectEqual(@as(u64, 11), competitive[0].id);
    try std.testing.expectEqual(@as(u64, 22), competitive[1].id);
}

test "hilbert split produces balanced clusters" {
    var path: TestPath = .{};
    const tmp_path = path.init();
    defer path.cleanup();

    var idx = try HBCIndex.open(std.testing.allocator, tmp_path, .{
        .dims = 2,
        .leaf_size = 5,
        .split_algo = .hilbert,
    });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 0.0, 0.0 });
    try idx.insert(2, &[_]f32{ 0.1, 0.1 });
    try idx.insert(3, &[_]f32{ 0.2, 0.0 });
    try idx.insert(4, &[_]f32{ 10.0, 10.0 });
    try idx.insert(5, &[_]f32{ 10.1, 10.1 });
    try idx.insert(6, &[_]f32{ 10.2, 10.0 });

    const s = idx.stats();
    try std.testing.expectEqual(@as(u64, 6), s.active_count);
    try std.testing.expect(s.node_count > 1);

    var results = try idx.search(&[_]f32{ 0.0, 0.0 }, 3);
    defer results.deinit();
    const hits = results.getHits();
    try std.testing.expectEqual(@as(usize, 3), hits.len);
    for (hits) |hit| {
        try std.testing.expect(hit.vector_id <= 3);
    }
}

test "delete removes vector" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2, .leaf_size = 10 });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 1.0, 0.0 });
    try idx.insert(2, &[_]f32{ 0.0, 1.0 });
    try idx.insert(3, &[_]f32{ 1.0, 1.0 });

    try std.testing.expectEqual(@as(u64, 3), idx.stats().active_count);

    try idx.delete(2);
    try std.testing.expectEqual(@as(u64, 2), idx.stats().active_count);

    // Search should not return deleted vector
    var results = try idx.search(&[_]f32{ 0.0, 1.0 }, 3);
    defer results.deinit();
    for (results.getHits()) |hit| {
        try std.testing.expect(hit.vector_id != 2);
    }
}

test "batchApply supports mixed writes and deletes" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2, .leaf_size = 10 });
    defer idx.close();

    try idx.batchInsertWithMetadata(&.{
        .{ .vector_id = 1, .vector = &[_]f32{ 1.0, 0.0 }, .metadata = "doc-1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 0.0, 1.0 }, .metadata = "doc-2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 1.0, 1.0 }, .metadata = "doc-3" },
    });

    try idx.batchApply(&.{
        .{ .vector_id = 4, .vector = &[_]f32{ 0.2, 0.9 }, .metadata = "doc-4" },
        .{ .vector_id = 3, .vector = &[_]f32{ 0.9, 0.9 }, .metadata = "doc-3b" },
    }, &.{2});

    try std.testing.expectEqual(@as(u64, 3), idx.stats().active_count);

    var results = try idx.search(&[_]f32{ 0.0, 1.0 }, 4);
    defer results.deinit();
    var saw_two = false;
    var saw_three = false;
    var saw_four = false;
    for (results.getHits()) |hit| {
        if (hit.vector_id == 2) saw_two = true;
        if (hit.vector_id == 3) saw_three = true;
        if (hit.vector_id == 4) saw_four = true;
    }
    try std.testing.expect(!saw_two);
    try std.testing.expect(saw_three);
    try std.testing.expect(saw_four);
}

test "search returns metadata for hits" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2, .leaf_size = 4, .use_quantization = true });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 1.0, 0.0 }, "doc-1");
    try idx.insertWithMetadata(2, &[_]f32{ 0.0, 1.0 }, "doc-2");

    var results = try idx.search(&[_]f32{ 1.0, 0.0 }, 2);
    defer results.deinit();

    const hits = results.getHits();
    try std.testing.expectEqual(@as(usize, 2), hits.len);
    try std.testing.expect(hits[0].metadata != null);
    try std.testing.expectEqualStrings("doc-1", hits[0].metadata.?);
}

test "delete repairs underfull leaf" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2, .leaf_size = 5, .branching_factor = 2, .search_width = 8 });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 0.0, 0.0 });
    try idx.insert(2, &[_]f32{ 0.1, 0.0 });
    try idx.insert(3, &[_]f32{ 0.2, 0.0 });
    try idx.insert(4, &[_]f32{ 10.0, 10.0 });
    try idx.insert(5, &[_]f32{ 10.1, 10.0 });
    try idx.insert(6, &[_]f32{ 10.2, 10.0 });

    try idx.delete(1);
    try idx.delete(2);

    var results = try idx.search(&[_]f32{ 0.2, 0.0 }, 4);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 4), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 3), results.getHits()[0].vector_id);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    var root = try idx.loadNode(&txn, idx.metadata.root_node);
    defer root.deinit(alloc);
    if (root.is_leaf) {
        try std.testing.expect(root.members.len > 0);
    } else {
        try std.testing.expect(root.children.len > 0);
    }
}

test "node roundtrip" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 3 });
    defer idx.close();

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();

        const centroid = try alloc.dupe(f32, &[_]f32{ 1.0, 2.0, 3.0 });
        defer alloc.free(centroid);
        const members = try alloc.dupe(u64, &[_]u64{ 10, 20, 30 });
        defer alloc.free(members);

        const node = Node{
            .id = 42,
            .is_leaf = true,
            .level = 2,
            .parent = 7,
            .centroid = centroid,
            .children = &.{},
            .members = members,
        };
        try idx.saveNode(&txn, &node);
        try txn.commit();
    }

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();

        var loaded = try idx.loadNode(&txn, 42);
        defer loaded.deinit(alloc);

        try std.testing.expectEqual(@as(u64, 42), loaded.id);
        try std.testing.expect(loaded.is_leaf);
        try std.testing.expectEqual(@as(u16, 2), loaded.level);
        try std.testing.expectEqual(@as(u64, 7), loaded.parent);
        try std.testing.expectEqualSlices(f32, &[_]f32{ 1.0, 2.0, 3.0 }, loaded.centroid);
        try std.testing.expectEqualSlices(u64, &[_]u64{ 10, 20, 30 }, loaded.members);
    }
}

test "node split ranges classify left right and mixed subtrees" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:a");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:b");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:y");
    try idx.insertWithMetadata(4, &[_]f32{ 10.1, 10.0 }, "doc:z");

    const stats = idx.stats();
    try std.testing.expect(stats.node_count > 1);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    var root = try idx.loadNode(&txn, idx.metadata.root_node);
    defer root.deinit(alloc);

    const root_range = (try idx.getNodeSplitRange(idx.metadata.root_node)) orelse return error.TestUnexpectedResult;
    defer {
        var owned = root_range;
        owned.deinit(alloc);
    }
    try std.testing.expectEqualStrings("doc:a", root_range.min_key);
    try std.testing.expectEqualStrings("doc:z", root_range.max_key);
    try std.testing.expectEqual(NodeSplitClass.mixed, try idx.classifyNodeForSplit(idx.metadata.root_node, "doc:m"));

    var saw_left = false;
    var saw_right = false;
    for (root.children) |child_id| {
        const class = try idx.classifyNodeForSplit(child_id, "doc:m");
        switch (class) {
            .left_only => saw_left = true,
            .right_only => saw_right = true,
            else => {},
        }
    }
    try std.testing.expect(saw_left);
    try std.testing.expect(saw_right);
}

test "coalesced batch insert extends ancestor split ranges for small leaf groups" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = false,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:m");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:n");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:o");
    try idx.insertWithMetadata(4, &[_]f32{ 10.1, 10.0 }, "doc:p");
    try idx.insertWithMetadata(5, &[_]f32{ 10.2, 10.0 }, "doc:q");
    try std.testing.expect(idx.stats().node_count > 1);

    idx.resetWriteProfile();
    const items = [_]BatchInsertItem{
        .{ .vector_id = 6, .vector = &[_]f32{ 0.05, 0.0 }, .metadata = "doc:a" },
        .{ .vector_id = 7, .vector = &[_]f32{ 0.06, 0.0 }, .metadata = "doc:b" },
        .{ .vector_id = 8, .vector = &[_]f32{ 10.15, 10.0 }, .metadata = "doc:z" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
    });

    try std.testing.expectEqual(@as(u64, 8), idx.stats().active_count);
    const root_range = (try idx.getNodeSplitRange(idx.metadata.root_node)) orelse return error.TestUnexpectedResult;
    defer {
        var owned = root_range;
        owned.deinit(alloc);
    }
    try std.testing.expectEqualStrings("doc:a", root_range.min_key);
    try std.testing.expectEqualStrings("doc:z", root_range.max_key);

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_calls);
    try std.testing.expect(profile.batch_route_internal_nodes > 0);
    try std.testing.expect(profile.batch_route_leaf_groups >= 2);
    try std.testing.expectEqual(@as(u64, items.len), profile.batch_route_items);
}

test "coalesced batch insert keeps routed child nodes stable across cache eviction" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const dims = 4;
    const base_count = 96;
    const insert_count = 16;
    var vectors = try alloc.alloc(f32, (base_count + insert_count) * dims);
    defer alloc.free(vectors);
    var base_items = try alloc.alloc(BatchInsertItem, base_count);
    defer alloc.free(base_items);
    var insert_items = try alloc.alloc(BatchInsertItem, insert_count);
    defer alloc.free(insert_items);

    for (0..base_count + insert_count) |i| {
        const vector = vectors[i * dims ..][0..dims];
        for (vector, 0..) |*value, d| {
            value.* = @as(f32, @floatFromInt(((i + 7) * 31 + (d + 11) * 13) % 257)) / 257.0;
        }
        if (i < base_count) {
            base_items[i] = .{
                .vector_id = @intCast(i + 1),
                .vector = vector,
                .metadata = "doc",
            };
        } else {
            insert_items[i - base_count] = .{
                .vector_id = @intCast(i + 1),
                .vector = vector,
                .metadata = "doc",
            };
        }
    }

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = dims,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 16,
        .use_quantization = false,
        .max_cached_nodes = 2,
        .max_cached_vectors = 2,
    });
    defer idx.close();

    try idx.bulkBuildWithMetadata(base_items);
    try std.testing.expect(idx.stats().node_count > 8);

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(insert_items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .allow_quantized_routing = false,
    });

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, base_count + insert_count), idx.stats().active_count);
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_calls);
    try std.testing.expect(profile.batch_route_internal_nodes > 0);
    try std.testing.expectEqual(@as(u64, insert_items.len), profile.batch_route_items);

    var results = try idx.search(insert_items[0].vector, 1);
    defer results.deinit();
    try std.testing.expect(results.getHits().len > 0);
}

test "mixed delete write batch routes covered replacements as absent" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = false,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:a");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:b");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:c");
    try idx.insertWithMetadata(4, &[_]f32{ 10.1, 10.0 }, "doc:d");
    try idx.insertWithMetadata(5, &[_]f32{ 20.0, 20.0 }, "doc:e");
    try std.testing.expect(idx.stats().node_count > 1);

    idx.resetWriteProfile();
    const writes = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.2, 0.0 }, .metadata = "doc:a2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 10.2, 10.0 }, .metadata = "doc:c2" },
        .{ .vector_id = 5, .vector = &[_]f32{ 20.2, 20.0 }, .metadata = "doc:e2" },
    };
    const deletes = [_]u64{ 1, 3, 5 };
    try idx.batchApplyOptions(&writes, &deletes, .{
        .assume_absent_ids = false,
        .coalesce_leaf_writes = true,
        .allow_quantized_routing = false,
    });

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 5), idx.stats().active_count);
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_calls);
    try std.testing.expect(profile.batch_route_internal_nodes > 0);
    try std.testing.expectEqual(@as(u64, writes.len), profile.batch_route_items);

    var results = try idx.search(&[_]f32{ 20.2, 20.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 5), results.getHits()[0].vector_id);
}

test "coalesced batch insert routes writes with quantized child scores" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const dims = 16;
    const base_count = 32;
    const insert_count = 8;
    var vectors = try alloc.alloc(f32, (base_count + insert_count) * dims);
    defer alloc.free(vectors);
    var base_items = try alloc.alloc(BatchInsertItem, base_count);
    defer alloc.free(base_items);
    var insert_items = try alloc.alloc(BatchInsertItem, insert_count);
    defer alloc.free(insert_items);

    for (0..base_count + insert_count) |i| {
        const vector = vectors[i * dims ..][0..dims];
        for (vector, 0..) |*value, d| {
            value.* = @as(f32, @floatFromInt(((i + 1) * 17 + (d + 3) * 11) % 97)) / 97.0;
        }
        if (i < base_count) {
            base_items[i] = .{
                .vector_id = @intCast(i + 1),
                .vector = vector,
                .metadata = "doc",
            };
        } else {
            insert_items[i - base_count] = .{
                .vector_id = @intCast(i + 1),
                .vector = vector,
                .metadata = "doc",
            };
        }
    }

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = dims,
        .leaf_size = 4,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = true,
    });
    defer idx.close();

    try idx.bulkBuildWithMetadata(base_items);
    try std.testing.expect(idx.stats().node_count > 1);

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(insert_items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .allow_quantized_routing = true,
    });

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_calls);
    try std.testing.expect(profile.batch_route_internal_nodes > 0);
    try std.testing.expect(profile.batch_route_quantized_nodes > 0);
    try std.testing.expectEqual(@as(u64, insert_items.len), profile.batch_route_items);
}

test "coalesced batch insert splits one-overflow leaf group once" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = false,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:a");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:b");
    try idx.insertWithMetadata(3, &[_]f32{ 0.2, 0.0 }, "doc:c");

    idx.resetWriteProfile();
    const items = [_]BatchInsertItem{
        .{ .vector_id = 4, .vector = &[_]f32{ 0.3, 0.0 }, .metadata = "doc:d" },
        .{ .vector_id = 5, .vector = &[_]f32{ 0.4, 0.0 }, .metadata = "doc:e" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
    });

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 5), idx.stats().active_count);
    try std.testing.expect(idx.stats().node_count > 1);
    try std.testing.expectEqual(@as(u64, 1), profile.split_leaf_calls);
    try std.testing.expectEqual(@as(u64, 1), profile.grouped_leaf_groups);
    try std.testing.expectEqual(@as(u64, 2), profile.grouped_items);
    try std.testing.expectEqual(@as(u64, 0), profile.grouped_fallback_items);
    try std.testing.expectEqual(@as(u64, 1), profile.grouped_split_candidates);
    try std.testing.expectEqual(@as(u64, 1), profile.grouped_recursive_splits);
    try std.testing.expectEqual(@as(u64, 2), profile.grouped_split_scan_iterations);
    try std.testing.expectEqual(@as(u64, 2), profile.grouped_split_queue_peak_total);
    try std.testing.expectEqual(@as(u64, 5), profile.split_leaf_input_members_total);
    try std.testing.expectEqual(@as(u64, 1), profile.split_leaf_input_overflow_members_total);
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_calls);
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_leaf_groups);
    try std.testing.expectEqual(@as(u64, items.len), profile.batch_route_items);

    var results = try idx.search(&[_]f32{ 0.4, 0.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 5), results.getHits()[0].vector_id);
}

test "coalesced batch insert recursively splits bounded overflow leaf group" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:a");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:b");
    try idx.insertWithMetadata(3, &[_]f32{ 0.2, 0.0 }, "doc:c");

    idx.resetWriteProfile();
    const items = [_]BatchInsertItem{
        .{ .vector_id = 4, .vector = &[_]f32{ 0.3, 0.0 }, .metadata = "doc:d" },
        .{ .vector_id = 5, .vector = &[_]f32{ 0.4, 0.0 }, .metadata = "doc:e" },
        .{ .vector_id = 6, .vector = &[_]f32{ 0.5, 0.0 }, .metadata = "doc:f" },
        .{ .vector_id = 7, .vector = &[_]f32{ 0.6, 0.0 }, .metadata = "doc:g" },
        .{ .vector_id = 8, .vector = &[_]f32{ 0.7, 0.0 }, .metadata = "doc:h" },
        .{ .vector_id = 9, .vector = &[_]f32{ 0.8, 0.0 }, .metadata = "doc:i" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
    });

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 9), idx.stats().active_count);
    try std.testing.expect(profile.split_leaf_calls >= 2);
    try std.testing.expectEqual(@as(u64, 1), profile.grouped_leaf_groups);
    try std.testing.expectEqual(@as(u64, 6), profile.grouped_items);
    try std.testing.expectEqual(@as(u64, 0), profile.grouped_fallback_items);
    try std.testing.expectEqual(@as(u64, 1), profile.grouped_split_candidates);
    try std.testing.expect(profile.grouped_recursive_splits >= 2);
    try std.testing.expect(profile.grouped_split_scan_iterations >= profile.grouped_recursive_splits);
    try std.testing.expect(profile.grouped_split_queue_peak_total >= 2);
    try std.testing.expect(profile.split_leaf_input_members_total > 0);
    try std.testing.expect(profile.split_leaf_input_overflow_members_total > 0);
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_calls);
    try std.testing.expectEqual(@as(u64, 1), profile.batch_route_leaf_groups);
    try std.testing.expectEqual(@as(u64, items.len), profile.batch_route_items);

    var results = try idx.search(&[_]f32{ 0.8, 0.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 9), results.getHits()[0].vector_id);
}

test "split planning stats count reusable dense subtrees" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:a");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:b");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:y");
    try idx.insertWithMetadata(4, &[_]f32{ 10.1, 10.0 }, "doc:z");

    const stats = try idx.splitPlanningStats("doc:m");
    try std.testing.expect(stats.leaves > 0);
    try std.testing.expect(stats.internal > 0);
    try std.testing.expect(stats.left_only > 0);
    try std.testing.expect(stats.right_only > 0);
    try std.testing.expect(stats.mixed > 0);
    try std.testing.expectEqual(@as(usize, 0), stats.unknown);
}

test "split reuse plan finds right-only subtree roots" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:a");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:b");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:y");
    try idx.insertWithMetadata(4, &[_]f32{ 10.1, 10.0 }, "doc:z");

    var plan = try idx.buildSplitReusePlan("doc:m");
    defer plan.deinit(alloc);

    try std.testing.expect(plan.right_only_roots.len > 0);
    for (plan.right_only_roots) |node_id| {
        try std.testing.expectEqual(NodeSplitClass.right_only, try idx.classifyNodeForSplit(node_id, "doc:m"));
    }
    for (plan.mixed_leaves) |node_id| {
        try std.testing.expectEqual(NodeSplitClass.mixed, try idx.classifyNodeForSplit(node_id, "doc:m"));
    }
}

test "split rebuild work counts reusable and mixed-right members" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
    });
    defer idx.close();

    try idx.insertWithMetadata(1, &[_]f32{ 0.0, 0.0 }, "doc:a");
    try idx.insertWithMetadata(2, &[_]f32{ 0.1, 0.0 }, "doc:b");
    try idx.insertWithMetadata(3, &[_]f32{ 10.0, 10.0 }, "doc:y");
    try idx.insertWithMetadata(4, &[_]f32{ 10.1, 10.0 }, "doc:z");

    const work = try idx.estimateSplitRebuildWork("doc:m");
    try std.testing.expect(work.mixed_leaves > 0 or work.right_only_roots > 0);
    try std.testing.expect(work.totalRightMembers() > 0);
}

test "search returns results from both halves after split" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 3,
        .search_width = 32,
    });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 0.0, 0.0 });
    try idx.insert(2, &[_]f32{ 0.1, 0.1 });
    try idx.insert(3, &[_]f32{ 0.2, 0.2 });
    try idx.insert(4, &[_]f32{ 10.0, 10.0 });
    try idx.insert(5, &[_]f32{ 10.1, 10.1 });

    var results = try idx.search(&[_]f32{ 0.0, 0.0 }, 5);
    defer results.deinit();

    try std.testing.expectEqual(@as(usize, 5), results.getHits().len);
}

test "vector to leaf mapping stays in sync across repeated splits" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 3,
        .branching_factor = 2,
        .search_width = 32,
    });
    defer idx.close();

    for (0..18) |i| {
        const x: f32 = @floatFromInt(i / 3);
        const y: f32 = @floatFromInt(i % 3);
        try idx.insert(@intCast(i + 1), &[_]f32{ x, y });
    }

    try std.testing.expect(idx.stats().node_count > 1);

    for (0..18) |i| {
        const vector_id: u64 = @intCast(i + 1);
        const mapped_leaf = try idx.debugLeafForVector(vector_id);
        const scanned_leaf = try idx.debugScanLeafForVector(vector_id);
        try std.testing.expect(mapped_leaf != null);
        try std.testing.expectEqual(scanned_leaf, mapped_leaf);

        const members = try idx.debugLeafMembers(alloc, mapped_leaf.?);
        defer alloc.free(members);

        var found = false;
        for (members) |member_id| {
            if (member_id == vector_id) {
                found = true;
                break;
            }
        }
        try std.testing.expect(found);
    }
}

test "insert tolerates loaded leaf with empty stored centroid" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();

        const members = try alloc.dupe(u64, &[_]u64{1});
        defer alloc.free(members);

        const root = Node{
            .id = idx.metadata.root_node,
            .is_leaf = true,
            .level = 0,
            .parent = 0,
            .centroid = &.{},
            .children = &.{},
            .members = members,
        };
        try idx.saveNode(&txn, &root);
        try idx.putVector(&txn, 1, &[_]f32{ 1.0, 0.0 });
        try idx.putVecLeaf(&txn, 1, idx.metadata.root_node);
        idx.metadata.active_count = 1;
        try txn.commit();
    }

    try idx.insert(2, &[_]f32{ 0.0, 1.0 });

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    try std.testing.expectEqual(@as(u64, 2), idx.stats().active_count);
    const leaf1 = (try idx.debugLeafForVector(1)) orelse return error.TestUnexpectedResult;
    const leaf2 = (try idx.debugLeafForVector(2)) orelse return error.TestUnexpectedResult;
    const members1 = try idx.debugLeafMembers(alloc, leaf1);
    defer alloc.free(members1);
    const members2 = if (leaf2 == leaf1) members1 else try idx.debugLeafMembers(alloc, leaf2);
    defer if (leaf2 != leaf1) alloc.free(members2);

    var found1 = false;
    for (members1) |member_id| {
        if (member_id == 1) {
            found1 = true;
            break;
        }
    }
    try std.testing.expect(found1);

    var found2 = false;
    for (members2) |member_id| {
        if (member_id == 2) {
            found2 = true;
            break;
        }
    }
    try std.testing.expect(found2);
}

test "flat centroid search falls back when published leaves have no centroids" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = true,
        .centroid_directory_mode = .flat_rabitq,
        .flat_centroid_block_size = 4,
        .flat_centroid_probe_count = 4,
    });
    defer idx.close();

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();

        const members = try alloc.dupe(u64, &[_]u64{1});
        defer alloc.free(members);

        const root = Node{
            .id = idx.metadata.root_node,
            .is_leaf = true,
            .level = 0,
            .parent = 0,
            .centroid = &.{},
            .children = &.{},
            .members = members,
        };
        try idx.saveNode(&txn, &root);
        try idx.putVector(&txn, 1, &[_]f32{ 1.0, 0.0 });
        try idx.putVecLeaf(&txn, 1, idx.metadata.root_node);
        idx.metadata.active_count = 1;
        try txn.commit();
    }
    idx.refreshPublishedSearchState();

    var results = try idx.search(&[_]f32{ 1.0, 0.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 1), results.getHits()[0].vector_id);
}

test "insert tolerates stale vec_leaf mapping without dropping the member" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
    });
    defer idx.close();

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();

        const members = try alloc.dupe(u64, &[_]u64{41});
        defer alloc.free(members);

        const root = Node{
            .id = idx.metadata.root_node,
            .is_leaf = true,
            .level = 0,
            .parent = 0,
            .centroid = try alloc.dupe(f32, &[_]f32{ 1.0, 2.0 }),
            .children = &.{},
            .members = members,
        };
        defer alloc.free(root.centroid);
        try idx.saveNode(&txn, &root);
        try idx.putVector(&txn, 41, &[_]f32{ 1.0, 2.0 });
        try idx.putMetadata(&txn, 41, "doc:41");
        try idx.putVecLeaf(&txn, 41, idx.metadata.root_node);
        try idx.putVecLeaf(&txn, 42, idx.metadata.root_node);
        idx.metadata.active_count = 1;
        try txn.commit();
    }

    try idx.insertWithMetadata(42, &[_]f32{ 2.0, 3.0 }, "doc:42");

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    try std.testing.expectEqual(@as(u64, idx.metadata.root_node), try idx.getVecLeaf(&txn, 42));
    const members = try idx.debugLeafMembers(alloc, idx.metadata.root_node);
    defer alloc.free(members);
    try std.testing.expectEqual(@as(usize, 2), members.len);
    try std.testing.expect(members[0] == 41 or members[1] == 41);
    try std.testing.expect(members[0] == 42 or members[1] == 42);
}

test "posting maintenance repairs dirty leaf centroid and state" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
    });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 1.0, 0.0 });
    try idx.insert(2, &[_]f32{ 3.0, 0.0 });

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try root.ensureUnbacked(alloc);
        @memset(root.centroid, 0);
        root.posting_state.noteMembersChanged(root.members.len);

        var key_buf: [12]u8 = undefined;
        try idx.putNamespaced(&txn, .nodes, encodeNodeKey(&key_buf, root.id, .centroid), std.mem.sliceAsBytes(root.centroid));
        try PostingStore.saveState(&idx, &txn, root.id, root.posting_state);
        try idx.finishWriteTxn(&txn);
        idx.invalidateNodeCache(root.id);
    }

    idx.resetWriteProfile();
    const result = try idx.repairDirtyPostings();
    try std.testing.expectEqual(@as(u64, 1), result.dirty_postings);
    try std.testing.expectEqual(@as(u64, 1), result.repaired_postings);
    try std.testing.expectEqual(@as(u64, 1), result.centroid_refreshed);
    try std.testing.expectEqual(@as(u64, 1), result.payload_refreshed);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    var root = try idx.loadNode(&txn, idx.metadata.root_node);
    defer root.deinit(alloc);
    try std.testing.expectApproxEqAbs(@as(f32, 2.0), root.centroid[0], 0.0001);
    try std.testing.expectApproxEqAbs(@as(f32, 0.0), root.centroid[1], 0.0001);
    try std.testing.expect(!root.posting_state.dirty);
    try std.testing.expect(!root.posting_state.centroid_dirty);
    try std.testing.expect(!root.posting_state.payload_dirty);

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 1), profile.posting_maintenance_repaired_postings);
    try std.testing.expectEqual(@as(u64, 1), profile.posting_maintenance_centroid_refreshed);
}

test "lazy posting maintenance defers foreground centroid refresh" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
        .lazy_posting_maintenance = true,
    });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 1.0, 0.0 });
    idx.resetWriteProfile();
    try idx.insert(2, &[_]f32{ 3.0, 0.0 });

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expectApproxEqAbs(@as(f32, 1.0), root.centroid[0], 0.0001);
        try std.testing.expect(root.posting_state.dirty);
        try std.testing.expect(root.posting_state.centroid_dirty);
        try std.testing.expect(!root.posting_state.payload_dirty);
    }

    const profile = idx.getWriteProfile();
    try std.testing.expect(profile.posting_lazy_centroid_deferrals > 0);

    const result = try idx.repairDirtyPostings();
    try std.testing.expectEqual(@as(u64, 1), result.centroid_refreshed);

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expectApproxEqAbs(@as(f32, 2.0), root.centroid[0], 0.0001);
        try std.testing.expect(!root.posting_state.dirty);
        try std.testing.expect(!root.posting_state.centroid_dirty);
    }
}

test "posting backlog stats report lazy dirty leaves with std Io writer" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
        .lazy_posting_maintenance = true,
    });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 1.0, 0.0 });
    try idx.insert(2, &[_]f32{ 3.0, 0.0 });

    const stats = try idx.postingBacklogStats();
    try std.testing.expect(stats.needsRepair());
    try std.testing.expectEqual(@as(u64, 1), stats.dirty_postings);
    try std.testing.expectEqual(@as(u64, 1), stats.centroid_dirty_postings);
    try std.testing.expectEqual(@as(u64, 0), stats.payload_dirty_postings);
    try std.testing.expectEqual(@as(u64, 1), stats.max_centroid_version_lag);

    var out: std.Io.Writer.Allocating = .init(alloc);
    defer out.deinit();
    try idx.writePostingBacklogStats(&out.writer);
    const rendered = out.writer.buffered();
    try std.testing.expect(std.mem.indexOf(u8, rendered, "dirty_postings=1") != null);
    try std.testing.expect(std.mem.indexOf(u8, rendered, "centroid_dirty_postings=1") != null);
}

test "dirty quantized posting payloads are scored exactly" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 10,
        .use_quantization = true,
        .rerank_policy = .never,
    });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 1.0, 0.0 });
    try idx.insert(2, &[_]f32{ 3.0, 0.0 });

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        root.posting_state.noteMembersChanged(root.members.len);
        root.posting_state.centroid_dirty = false;
        root.posting_state.centroid_version = root.posting_state.mutation_version;
        root.posting_state.dirty = root.posting_state.payload_dirty;
        try PostingStore.saveState(&idx, &txn, root.id, root.posting_state);
        try idx.finishWriteTxn(&txn);
        idx.invalidateNodeCache(root.id);
    }

    var profiled = try idx.searchProfiled(&[_]f32{ 1.0, 0.0 }, 2);
    defer profiled.results.deinit();

    try std.testing.expectEqual(@as(usize, 2), profiled.results.getHits().len);
    try std.testing.expectEqual(@as(u64, 0), profiled.profile.approx_vectors_scored);
    try std.testing.expectEqual(@as(u64, 2), profiled.profile.exact_vectors_scored);
}

test "auto posting maintenance repairs bounded lazy backlog before commit" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
        .lazy_posting_maintenance = true,
        .auto_posting_maintenance_max_postings = 1,
    });
    defer idx.close();

    try idx.insert(1, &[_]f32{ 1.0, 0.0 });
    idx.resetWriteProfile();
    try idx.insert(2, &[_]f32{ 3.0, 0.0 });

    const stats = try idx.postingBacklogStats();
    try std.testing.expect(!stats.needsRepair());
    try std.testing.expectEqual(@as(u64, 0), stats.dirty_postings);

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expectApproxEqAbs(@as(f32, 2.0), root.centroid[0], 0.0001);
        try std.testing.expect(!root.posting_state.dirty);
        try std.testing.expect(!root.posting_state.centroid_dirty);
    }

    const profile = idx.getWriteProfile();
    try std.testing.expect(profile.posting_lazy_centroid_deferrals > 0);
    try std.testing.expect(profile.posting_maintenance_repaired_postings > 0);
}

test "manual posting repair honors explicit bound when auto repair is configured" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = false,
    });
    defer idx.close();

    for (0..8) |i| {
        const vector_id: u64 = @intCast(i + 1);
        const x: f32 = if (i < 4) @floatFromInt(i) else @floatFromInt(i + 20);
        try idx.insert(vector_id, &[_]f32{ x, 0.0 });
    }

    var dirtied: u64 = 0;
    var dirtied_leaf_ids: [2]u64 = .{ 0, 0 };
    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var node_id: u64 = 1;
        while (node_id <= idx.metadata.node_count and dirtied < 2) : (node_id += 1) {
            var node = idx.loadNode(&txn, node_id) catch continue;
            defer node.deinit(alloc);
            if (!node.is_leaf or node.members.len == 0) continue;
            node.posting_state.noteMembersChanged(node.members.len);
            try PostingStore.saveState(&idx, &txn, node.id, node.posting_state);
            dirtied_leaf_ids[@intCast(dirtied)] = node.id;
            dirtied += 1;
        }
        try idx.finishWriteTxn(&txn);
    }
    try std.testing.expectEqual(@as(u64, 2), dirtied);
    for (dirtied_leaf_ids) |leaf_id| idx.invalidateNodeCache(leaf_id);

    const before = try idx.postingBacklogStats();
    try std.testing.expectEqual(@as(u64, 2), before.dirty_postings);

    idx.config.auto_posting_maintenance_max_postings = 100;
    idx.resetWriteProfile();
    const repaired = try idx.repairDirtyPostingsWithOptions(.{ .max_postings = 1 });
    try std.testing.expectEqual(@as(u64, 1), repaired.repaired_postings);

    const after = try idx.postingBacklogStats();
    try std.testing.expectEqual(before.dirty_postings - 1, after.dirty_postings);
    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 1), profile.posting_maintenance_repaired_postings);
}

test "posting dirty state survives reopen and bounded repair makes progress" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    {
        var idx = try HBCIndex.open(alloc, path, .{
            .dims = 2,
            .use_quantization = false,
            .lazy_posting_maintenance = true,
        });
        defer idx.close();

        try idx.insert(1, &[_]f32{ 1.0, 0.0 });
        try idx.insert(2, &[_]f32{ 3.0, 0.0 });
        const stats = try idx.postingBacklogStats();
        try std.testing.expectEqual(@as(u64, 1), stats.dirty_postings);
    }

    {
        var idx = try HBCIndex.open(alloc, path, .{
            .dims = 2,
            .use_quantization = false,
            .lazy_posting_maintenance = true,
        });
        defer idx.close();

        const reopened_stats = try idx.postingBacklogStats();
        try std.testing.expectEqual(@as(u64, 1), reopened_stats.dirty_postings);

        const repaired = try idx.repairDirtyPostingsWithOptions(.{ .max_postings = 1 });
        try std.testing.expectEqual(@as(u64, 1), repaired.repaired_postings);
        const clean_stats = try idx.postingBacklogStats();
        try std.testing.expectEqual(@as(u64, 0), clean_stats.dirty_postings);
    }
}

test "lazy posting maintenance keeps assignment map and members consistent through dynamic writes" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 3,
        .branching_factor = 2,
        .search_width = 16,
        .use_quantization = true,
        .lazy_posting_maintenance = true,
        .auto_posting_maintenance_max_postings = 2,
    });
    defer idx.close();

    var present = std.AutoHashMap(u64, void).init(alloc);
    defer present.deinit();

    for (0..24) |i| {
        const vector_id: u64 = @intCast(i + 1);
        const cluster: f32 = if (i % 2 == 0) 0.0 else 100.0;
        const x: f32 = cluster + @as(f32, @floatFromInt(i / 2));
        const y: f32 = @floatFromInt(i % 3);
        try idx.insert(vector_id, &[_]f32{ x, y });
        try present.put(vector_id, {});
    }

    for (&[_]u64{ 2, 5, 9, 12, 17 }) |vector_id| {
        try idx.delete(vector_id);
        _ = present.remove(vector_id);
    }

    for (&[_]u64{ 3, 7, 21 }) |vector_id| {
        try idx.insert(vector_id, &[_]f32{ @as(f32, @floatFromInt(vector_id)) + 0.5, 7.0 });
        try present.put(vector_id, {});
    }

    var it = present.keyIterator();
    while (it.next()) |vector_id_ptr| {
        const vector_id = vector_id_ptr.*;
        const mapped_leaf = try idx.debugLeafForVector(vector_id);
        const scanned_leaf = try idx.debugScanLeafForVector(vector_id);
        try std.testing.expect(mapped_leaf != null);
        try std.testing.expectEqual(scanned_leaf, mapped_leaf);

        const members = try idx.debugLeafMembers(alloc, mapped_leaf.?);
        defer alloc.free(members);
        var found = false;
        for (members) |member_id| {
            if (member_id == vector_id) {
                found = true;
                break;
            }
        }
        try std.testing.expect(found);
    }

    var results = try idx.search(&[_]f32{ 1.0, 0.0 }, 5);
    defer results.deinit();
    try std.testing.expect(results.getHits().len > 0);
}

test "posting maintenance can split and merge postings as bounded layout work" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 100,
        .branching_factor = 4,
        .search_width = 8,
        .use_quantization = false,
        .lazy_posting_maintenance = true,
    });
    defer idx.close();

    for (0..8) |i| {
        const vector_id: u64 = @intCast(i + 1);
        const x: f32 = if (i < 4) @floatFromInt(i) else 100.0 + @as(f32, @floatFromInt(i));
        try idx.insert(vector_id, &[_]f32{ x, 0.0 });
    }

    idx.config.leaf_size = 4;
    const split_result = try idx.repairDirtyPostingsWithOptions(.{
        .max_postings = 8,
        .rebalance_layout = true,
        .max_layout_changes = 1,
        .max_boundary_reassignments = 8,
    });
    try std.testing.expectEqual(@as(u64, 1), split_result.split_postings);
    try std.testing.expect(idx.stats().node_count >= 3);

    for (1..9) |vector_id_usize| {
        const vector_id: u64 = @intCast(vector_id_usize);
        try std.testing.expectEqual(try idx.debugScanLeafForVector(vector_id), try idx.debugLeafForVector(vector_id));
    }

    idx.config.leaf_size = 100;
    const merge_result = try idx.repairDirtyPostingsWithOptions(.{
        .max_postings = 8,
        .rebalance_layout = true,
        .max_layout_changes = 1,
    });
    try std.testing.expectEqual(@as(u64, 1), merge_result.merged_postings);

    for (1..9) |vector_id_usize| {
        const vector_id: u64 = @intCast(vector_id_usize);
        try std.testing.expectEqual(try idx.debugScanLeafForVector(vector_id), try idx.debugLeafForVector(vector_id));
    }

    var results = try idx.search(&[_]f32{ 102.0, 0.0 }, 4);
    defer results.deinit();
    try std.testing.expect(results.getHits().len > 0);
}

test "posting maintenance merge error releases transferred node slices once" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const LoaderCtx = struct {
        fail: bool = false,

        fn load(ctx_ptr: *anyopaque, loader_alloc: Allocator, vector_id: u64, _: []const u8) ![]f32 {
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr));
            if (ctx.fail) return error.NotFound;
            const vector = try loader_alloc.alloc(f32, 2);
            vector[0] = @floatFromInt(vector_id);
            vector[1] = @floatFromInt(vector_id % 3);
            return vector;
        }
    };

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 100,
        .branching_factor = 4,
        .search_width = 8,
        .use_quantization = false,
        .lazy_posting_maintenance = true,
    });
    defer idx.close();

    var loader_ctx = LoaderCtx{};
    idx.setExternalVectorLoader(&loader_ctx, LoaderCtx.load);
    idx.setBypassExternalVectorCache(true);
    defer idx.setBypassExternalVectorCache(false);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 1.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 2.0, 1.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 3.0, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 100.0, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 101.0, 1.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 102.0, 0.0 }, .metadata = "doc:6" },
        .{ .vector_id = 7, .vector = &.{ 103.0, 1.0 }, .metadata = "doc:7" },
        .{ .vector_id = 8, .vector = &.{ 104.0, 0.0 }, .metadata = "doc:8" },
    };
    try idx.bulkBuildWithMetadataOptions(&items, .{ .skip_vector_store = true });

    idx.config.leaf_size = 4;
    const split_result = try idx.repairDirtyPostingsWithOptions(.{
        .max_postings = 8,
        .rebalance_layout = true,
        .max_layout_changes = 1,
    });
    try std.testing.expectEqual(@as(u64, 1), split_result.split_postings);

    // Force the first centroid read after members have moved into the sibling
    // to fail. The transaction must abort without the allocation errdefer and
    // sibling destructor both freeing the transferred slice.
    loader_ctx.fail = true;
    idx.clearVectorCache();
    idx.config.leaf_size = 100;
    try std.testing.expectError(error.NotFound, idx.repairDirtyPostingsWithOptions(.{
        .max_postings = 8,
        .rebalance_layout = true,
        .max_layout_changes = 1,
    }));
}

test "bulk replay recomputes same-leaf existing members once per leaf" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const LoaderCtx = struct {
        load_calls: usize = 0,

        fn load(ctx_ptr: *anyopaque, loader_alloc: Allocator, vector_id: u64, _: []const u8) ![]f32 {
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr));
            ctx.load_calls += 1;
            const vector: []const f32 = switch (vector_id) {
                1 => &[_]f32{ 0.0, 0.0 },
                2 => &[_]f32{ 1.0, 0.0 },
                else => return error.NotFound,
            };
            return try loader_alloc.dupe(f32, vector);
        }
    };

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 8,
        .branching_factor = 2,
        .use_quantization = false,
    });
    defer idx.close();

    var loader_ctx = LoaderCtx{};
    idx.setExternalVectorLoader(&loader_ctx, LoaderCtx.load);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 1.0, 0.0 }, .metadata = "doc:2" },
    };
    const options: BatchInsertOptions = .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
    };

    try idx.beginBulkIngestSession();
    var initial_session_open = true;
    errdefer if (initial_session_open) idx.abortBulkIngestSession();
    try idx.batchInsertWithMetadataOptions(&items, options);
    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    initial_session_open = false;

    loader_ctx.load_calls = 0;
    idx.resetWriteProfile();

    try idx.beginBulkIngestSession();
    var replay_session_open = true;
    errdefer if (replay_session_open) idx.abortBulkIngestSession();
    try idx.batchApplyOptions(&items, &.{}, .{
        .assume_absent_ids = false,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = true,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
    });
    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    replay_session_open = false;

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 2), profile.insert_calls);
    try std.testing.expectEqual(@as(u64, 1), profile.save_node_calls);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    const members = try idx.debugLeafMembers(alloc, idx.metadata.root_node);
    defer alloc.free(members);
    try std.testing.expectEqual(@as(usize, 2), members.len);
}

test "deferred quantized rebuild uses current batch vectors before external loader" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const LoaderCtx = struct {
        load_calls: usize = 0,

        fn load(ctx_ptr: *anyopaque, loader_alloc: Allocator, vector_id: u64, _: []const u8) ![]f32 {
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr));
            ctx.load_calls += 1;
            const vector: []const f32 = switch (vector_id) {
                1 => &[_]f32{ 0.0, 0.0 },
                2 => &[_]f32{ 1.0, 0.0 },
                3 => &[_]f32{ 0.0, 1.0 },
                else => return error.NotFound,
            };
            return try loader_alloc.dupe(f32, vector);
        }
    };

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 8,
        .branching_factor = 2,
        .use_quantization = true,
    });
    defer idx.close();

    var loader_ctx = LoaderCtx{};
    idx.setExternalVectorLoader(&loader_ctx, LoaderCtx.load);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &[_]f32{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &[_]f32{ 1.0, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &[_]f32{ 0.0, 1.0 }, .metadata = "doc:3" },
    };

    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = true,
        .bulk_ingest = true,
    });

    try std.testing.expectEqual(@as(usize, 0), loader_ctx.load_calls);
    const profile = idx.getWriteProfile();
    try std.testing.expect(profile.refresh_quantized_ns > 0);
    try std.testing.expect(profile.quantized_vector_load_ns > 0);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    var quantized = (try idx.getQuantized(&txn, idx.metadata.root_node, true, items.len)) orelse return error.TestUnexpectedResult;
    quantized.deinit(std.testing.allocator);
}

test "vector storage roundtrip" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 3 });
    defer idx.close();

    // Insert stores vector
    try idx.insert(42, &[_]f32{ 1.0, 2.0, 3.0 });

    // Read it back
    var txn = try idx.beginReadTxn();
    defer txn.abort();
    const v = try idx.getVector(&txn, 42);
    defer alloc.free(v);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 1.0, 2.0, 3.0 }, v);
}

test "getVectorScratch caches external vector loads" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const LoaderCtx = struct {
        load_calls: usize = 0,

        fn load(ctx_ptr: *anyopaque, loader_alloc: Allocator, vector_id: u64, _: []const u8) ![]f32 {
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr));
            ctx.load_calls += 1;
            const vector: []const f32 = switch (vector_id) {
                7 => &[_]f32{ 7.0, 8.0, 9.0 },
                else => return error.NotFound,
            };
            return try loader_alloc.dupe(f32, vector);
        }
    };

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 3,
        .max_cached_vectors = 8,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    var loader_ctx = LoaderCtx{};
    idx.setExternalVectorLoader(&loader_ctx, LoaderCtx.load);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 7, .vector = &[_]f32{ 7.0, 8.0, 9.0 }, .metadata = "doc:7" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .skip_vector_store = true,
    });
    idx.setCacheEnabled(false);
    idx.setCacheEnabled(true);

    var txn = try idx.beginReadTxn();
    defer txn.abort();

    var scratch_a: [3]f32 = undefined;
    var scratch_b: [3]f32 = undefined;

    const first = try idx.getVectorScratch(&txn, 7, &scratch_a);
    const second = try idx.getVectorScratch(&txn, 7, &scratch_b);

    try std.testing.expectEqual(@as(usize, 1), loader_ctx.load_calls);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 7.0, 8.0, 9.0 }, first);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 7.0, 8.0, 9.0 }, second);
}

test "getVectorScratch can bypass external vector cache during replay sessions" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const LoaderCtx = struct {
        load_calls: usize = 0,

        fn load(ctx_ptr: *anyopaque, loader_alloc: Allocator, vector_id: u64, _: []const u8) ![]f32 {
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr));
            ctx.load_calls += 1;
            const vector: []const f32 = switch (vector_id) {
                7 => &[_]f32{ 7.0, 8.0, 9.0 },
                else => return error.NotFound,
            };
            return try loader_alloc.dupe(f32, vector);
        }
    };

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 3,
        .max_cached_vectors = 8,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    var loader_ctx = LoaderCtx{};
    idx.setExternalVectorLoader(&loader_ctx, LoaderCtx.load);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 7, .vector = &[_]f32{ 7.0, 8.0, 9.0 }, .metadata = "doc:7" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .skip_vector_store = true,
    });
    idx.setCacheEnabled(false);
    idx.setCacheEnabled(true);
    idx.setBypassExternalVectorCache(true);
    defer idx.setBypassExternalVectorCache(false);

    var txn = try idx.beginReadTxn();
    defer txn.abort();

    var scratch_a: [3]f32 = undefined;
    var scratch_b: [3]f32 = undefined;

    const first = try idx.getVectorScratch(&txn, 7, &scratch_a);
    const second = try idx.getVectorScratch(&txn, 7, &scratch_b);

    try std.testing.expectEqual(@as(usize, 2), loader_ctx.load_calls);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 7.0, 8.0, 9.0 }, first);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 7.0, 8.0, 9.0 }, second);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
}

test "skip vector store writes do not seed retained vector cache when bypassed" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 3,
        .max_cached_vectors = 8,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    _ = try idx.cacheVector(7, &[_]f32{ 1.0, 1.0, 1.0 });
    try std.testing.expect(idx.hbcCacheStats().vector.used_bytes > 0);

    idx.setBypassExternalVectorCache(true);
    defer idx.setBypassExternalVectorCache(false);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 7, .vector = &[_]f32{ 7.0, 8.0, 9.0 }, .metadata = "doc:7" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .skip_vector_store = true,
    });

    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
    try expectVectorNotCached(&idx, 7);
}

test "getVectorInto skips external vector cache population during concurrent search" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const LoaderCtx = struct {
        load_calls: usize = 0,

        fn load(ctx_ptr: *anyopaque, loader_alloc: Allocator, vector_id: u64, _: []const u8) ![]f32 {
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr));
            ctx.load_calls += 1;
            const vector: []const f32 = switch (vector_id) {
                7 => &[_]f32{ 7.0, 8.0, 9.0 },
                else => return error.NotFound,
            };
            return try loader_alloc.dupe(f32, vector);
        }
    };

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 3,
        .max_cached_vectors = 8,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    var loader_ctx = LoaderCtx{};
    idx.setExternalVectorLoader(&loader_ctx, LoaderCtx.load);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 7, .vector = &[_]f32{ 7.0, 8.0, 9.0 }, .metadata = "doc:7" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .skip_vector_store = true,
    });
    idx.setCacheEnabled(false);
    idx.setCacheEnabled(true);
    idx.active_searches.store(2, .release);
    defer idx.active_searches.store(0, .release);

    var txn = try idx.beginReadTxn();
    defer txn.abort();

    var scratch_a: [3]f32 = undefined;
    var scratch_b: [3]f32 = undefined;

    const first = try idx.getVectorInto(&txn, 7, &scratch_a);
    const second = try idx.getVectorInto(&txn, 7, &scratch_b);

    try std.testing.expectEqual(@as(usize, 2), loader_ctx.load_calls);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 7.0, 8.0, 9.0 }, first);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 7.0, 8.0, 9.0 }, second);
    try std.testing.expect(first.ptr == scratch_a[0..].ptr);
    try std.testing.expect(second.ptr == scratch_b[0..].ptr);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
}

test "updating an existing vector in the same leaf uses centroid delta instead of full recompute" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 3 });
    defer idx.close();

    try idx.insertWithMetadata(7, &[_]f32{ 1.0, 2.0, 3.0 }, "doc:7");
    const before = idx.getWriteProfile();

    try idx.insertWithMetadata(7, &[_]f32{ 3.0, 2.0, 1.0 }, "doc:7");
    const after = idx.getWriteProfile();

    try std.testing.expectEqual(before.centroid_recompute_calls, after.centroid_recompute_calls);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    const stored = try idx.getVector(&txn, 7);
    defer alloc.free(stored);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 3.0, 2.0, 1.0 }, stored);
}

test "reinserting the same vector without metadata is a no-op" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 3 });
    defer idx.close();

    try idx.insert(7, &[_]f32{ 1.0, 2.0, 3.0 });
    idx.resetWriteProfile();

    try idx.insert(7, &[_]f32{ 1.0, 2.0, 3.0 });
    const after = idx.getWriteProfile();

    try std.testing.expectEqual(@as(u64, 1), after.noop_existing_skips);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    const stored = try idx.getVector(&txn, 7);
    defer alloc.free(stored);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 1.0, 2.0, 3.0 }, stored);
}

test "bulk ingest existing vector update can stay on existing leaf without reroute" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 3 });
    defer idx.close();

    try idx.insertWithMetadata(7, &[_]f32{ 1.0, 2.0, 3.0 }, "doc:7");
    idx.resetWriteProfile();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 7, .vector = &[_]f32{ 3.0, 2.0, 1.0 }, .metadata = "doc:7" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .bulk_ingest = true,
        .centroid_only_routing = true,
        .skip_vector_store = false,
    });

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u64, 0), profile.insert_find_leaf_ns);
    try std.testing.expectEqual(@as(u64, 1), idx.stats().active_count);

    var txn = try idx.beginReadTxn();
    defer txn.abort();
    const stored = try idx.getVector(&txn, 7);
    defer alloc.free(stored);
    try std.testing.expectEqualSlices(f32, &[_]f32{ 3.0, 2.0, 1.0 }, stored);
    try std.testing.expectEqual(@as(u64, idx.metadata.root_node), try idx.getVecLeaf(&txn, 7));
}

test "hbc namespace adapters expose multi-partition txn operations" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 3 });
    defer idx.close();

    const probe_meta_key = "stats:probe";

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        var key_buf: [10]u8 = undefined;
        try txn.put(.meta, probe_meta_key, "ok");
        try txn.put(.vecs, encodeVecMetaKey(&key_buf, 42), "meta42");
        try txn.commit();
    }

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        try std.testing.expectEqualStrings("ok", try txn.get(.meta, probe_meta_key));

        var key_buf: [10]u8 = undefined;
        try std.testing.expectEqualStrings("meta42", try txn.get(.vecs, encodeVecMetaKey(&key_buf, 42)));
    }

    {
        var batch = try idx.beginBatchTxn();
        errdefer batch.abort();
        var key_buf: [10]u8 = undefined;
        try batch.put(.vecs, encodeVecMetaKey(&key_buf, 77), "meta77");
        try std.testing.expectEqualStrings("meta77", try batch.get(.vecs, encodeVecMetaKey(&key_buf, 77)));
        try batch.commit();
    }
}

test "hbc backend runtime erases namespace store handles" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 3 });
    defer idx.close();

    var runtime = try idx.runtimeNamespaceStore(std.testing.allocator);
    defer runtime.deinit();

    {
        var txn = try runtime.beginWrite();
        try txn.put(.meta, "stats:runtime", "ok");
        try txn.put(.vecs, "custom:vec", "v");
        try txn.commit();
    }

    {
        var txn = try runtime.beginRead();
        defer txn.abort();
        try std.testing.expectEqualStrings("ok", try txn.get(.meta, "stats:runtime"));
        try std.testing.expectEqualStrings("v", try txn.get(.vecs, "custom:vec"));
    }
}

test "hbc core persistence helpers work through erased namespace txns" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
    });
    defer idx.close();

    var runtime = try idx.runtimeNamespaceStore(std.testing.allocator);
    defer runtime.deinit();

    {
        var txn = try runtime.beginWrite();
        errdefer txn.abort();

        var loaded_root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer loaded_root.deinit(alloc);

        var root = try loaded_root.clone(alloc);
        defer root.deinit(alloc);

        if (root.centroid.len > 0) alloc.free(root.centroid);
        if (root.children.len > 0) alloc.free(root.children);
        if (root.members.len > 0) alloc.free(root.members);
        root.centroid = try alloc.dupe(f32, &.{ 1.0, 2.0 });
        root.children = &.{};
        root.members = try alloc.dupe(u64, &.{42});

        try idx.putVector(&txn, 42, &.{ 1.0, 2.0 });
        try idx.putMetadata(&txn, 42, "doc:1");
        try idx.putVecLeaf(&txn, 42, root.id);
        try idx.saveNode(&txn, &root);
        try txn.commit();
    }

    {
        var txn = try runtime.beginRead();
        defer txn.abort();

        const metadata = (try idx.getMetadataInTxn(&txn, 42)) orelse return error.TestUnexpectedResult;
        try std.testing.expectEqualStrings("doc:1", metadata);
        try std.testing.expectEqual(@as(u64, 1), try idx.getVecLeaf(&txn, 42));

        const vector = try idx.getVector(&txn, 42);
        defer alloc.free(vector);
        try std.testing.expectEqualSlices(f32, &.{ 1.0, 2.0 }, vector);

        var root = try idx.loadNode(&txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(root.is_leaf);
        try std.testing.expectEqual(@as(usize, 1), root.members.len);
        try std.testing.expectEqual(@as(u64, 42), root.members[0]);

        const maybe_range = try idx.loadNodeSplitRange(&txn, idx.metadata.root_node);
        try std.testing.expect(maybe_range != null);
        var range = maybe_range.?;
        defer range.deinit(alloc);
        try std.testing.expectEqualStrings("doc:1", range.min_key);
        try std.testing.expectEqualStrings("doc:1", range.max_key);
    }
}

test "hbc runtime namespace store works for lsm backend" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
        .storage_backend = .lsm,
    });
    defer idx.close();

    var runtime = try idx.runtimeNamespaceStore(std.testing.allocator);
    defer runtime.deinit();

    {
        var txn = try runtime.beginWrite();
        errdefer txn.abort();

        try idx.putVector(&txn, 7, &.{ 1.0, 3.0 });
        try idx.putMetadata(&txn, 7, "doc:lsm");
        try txn.commit();
    }

    {
        var txn = try runtime.beginRead();
        defer txn.abort();

        const metadata = (try idx.getMetadataInTxn(&txn, 7)) orelse return error.TestUnexpectedResult;
        try std.testing.expectEqualStrings("doc:lsm", metadata);

        const vector = try idx.getVector(&txn, 7);
        defer alloc.free(vector);
        try std.testing.expectEqualSlices(f32, &.{ 1.0, 3.0 }, vector);
    }
}

test "hbc getMetadataManySortedInTxn batches ordered metadata lookups and caches results" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .use_quantization = false,
        .storage_backend = .lsm,
    });
    defer idx.close();

    {
        var txn = try idx.beginWriteTxn();
        errdefer txn.abort();
        try idx.putMetadata(&txn, 2, "doc:2");
        try idx.putMetadata(&txn, 7, "doc:7");
        try txn.commit();
    }

    var read_txn = try idx.beginRuntimeReadTxn();
    defer read_txn.abort();

    const ids = [_]u64{ 7, 5, 2 };
    var out: [ids.len]?[]const u8 = undefined;
    try idx.getMetadataManySortedInTxn(&read_txn, &ids, &out);

    try std.testing.expectEqualStrings("doc:7", out[0].?);
    try std.testing.expectEqual(@as(?[]const u8, null), out[1]);
    try std.testing.expectEqualStrings("doc:2", out[2].?);
    try expectCachedMetadata(&idx, 7, "doc:7");
    try expectCachedMetadata(&idx, 2, "doc:2");
}

test "hbc bulk ingest skips retained metadata cache population" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    const input = "doc:1";
    const returned = try idx.cacheMetadata(1, input);
    try std.testing.expectEqual(@intFromPtr(input.ptr), @intFromPtr(returned.ptr));
    try expectMetadataNotCached(&idx, 1);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().metadata.used_bytes);

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;
}

test "hbc bulk ingest skips retained vector cache population" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    var idx = try HBCIndex.open(alloc, path, .{ .dims = 2 });
    defer idx.close();
    idx.attachResourceManager(&resource_manager);
    idx.setRetainedVectorCacheEnabled(true);

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    const input = [_]f32{ 1.0, 2.0 };
    const returned = try idx.cacheVector(1, &input);
    try std.testing.expectEqual(@intFromPtr(input[0..].ptr), @intFromPtr(returned.ptr));
    try expectVectorNotCached(&idx, 1);
    try std.testing.expectEqual(@as(u64, 0), idx.hbcCacheStats().vector.used_bytes);
    try std.testing.expectEqual(@as(u64, 0), resource_manager.sliceStats(.hbc_node_metadata_cache).used_bytes);

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;
}

test "hbc bulk ingest mutation batches defer manifest without direct sorted ingest" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    const before = idx.snapshotLsmWriteStats() orelse return error.TestUnexpectedResult;
    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 1.0, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.0, 1.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 1.0, 1.0 }, .metadata = "doc:4" },
    };
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .bulk_ingest = true,
        .skip_vector_store = true,
    });

    const during = idx.snapshotLsmWriteStats() orelse return error.TestUnexpectedResult;
    try std.testing.expectEqual(before.manifest_writes, during.manifest_writes);
    try std.testing.expectEqual(before.sorted_ingest_runs, during.sorted_ingest_runs);

    {
        var read_txn = try idx.beginReadTxn();
        defer read_txn.abort();
        idx.clearNodeCache();
        var staged_root = try idx.loadNode(&read_txn, idx.metadata.root_node);
        defer staged_root.deinit(alloc);
        try std.testing.expectEqual(@as(usize, items.len), staged_root.members.len);
    }

    const staged_range = (try idx.getNodeSplitRange(idx.metadata.root_node)) orelse return error.TestUnexpectedResult;
    var staged_range_owned = staged_range;
    defer staged_range_owned.deinit(alloc);
    try std.testing.expectEqualStrings("doc:1", staged_range_owned.min_key);
    try std.testing.expectEqualStrings("doc:4", staged_range_owned.max_key);

    {
        var cold = try HBCIndex.openWithLsmOptions(alloc, path, .{
            .dims = 2,
            .leaf_size = 4,
            .branching_factor = 8,
            .search_width = 8,
            .use_quantization = false,
            .storage_backend = .lsm,
        }, .{
            .backend_options = .{
                .backend = .{
                    .read_only = true,
                    .create_if_missing = false,
                },
                .flush_threshold = 1,
                .bulk_ingest_flush_threshold_multiplier = 4,
            },
        });
        defer cold.close();

        try std.testing.expectEqual(@as(u64, 0), cold.stats().active_count);
        var cold_results = try cold.search(&[_]f32{ 0.5, 0.0 }, 1);
        defer cold_results.deinit();
        try std.testing.expectEqual(@as(usize, 0), cold_results.getHits().len);
    }

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;

    const after = idx.snapshotLsmWriteStats() orelse return error.TestUnexpectedResult;
    try std.testing.expect(after.manifest_writes > before.manifest_writes);
    try std.testing.expectEqual(before.sorted_ingest_runs, after.sorted_ingest_runs);
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_node_keys.count());

    {
        var cold = try HBCIndex.openWithLsmOptions(alloc, path, .{
            .dims = 2,
            .leaf_size = 4,
            .branching_factor = 8,
            .search_width = 8,
            .use_quantization = false,
            .storage_backend = .lsm,
        }, .{
            .backend_options = .{
                .backend = .{
                    .read_only = true,
                    .create_if_missing = false,
                },
                .flush_threshold = 1,
                .bulk_ingest_flush_threshold_multiplier = 4,
            },
        });
        defer cold.close();

        try std.testing.expectEqual(@as(u64, items.len), cold.stats().active_count);
        var cold_txn = try cold.beginReadTxn();
        defer cold_txn.abort();
        var cold_root = try cold.loadNode(&cold_txn, cold.stats().root_node);
        defer cold_root.deinit(alloc);
        try std.testing.expectEqual(@as(usize, items.len), cold_root.members.len);
    }
}

test "hbc reset stored structure preserves an empty query root" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{});
    var idx_open = true;
    defer if (idx_open) idx.close();

    try idx.resetStoredStructure();
    try std.testing.expectEqual(@as(u64, 1), idx.stats().root_node);
    try std.testing.expectEqual(@as(u64, 1), idx.stats().node_count);
    try std.testing.expectEqual(@as(u64, 0), idx.stats().active_count);

    {
        var txn = try idx.beginReadTxn();
        defer txn.abort();
        var root = try idx.loadNode(&txn, idx.stats().root_node);
        defer root.deinit(alloc);
        try std.testing.expect(root.is_leaf);
        try std.testing.expectEqual(@as(usize, 0), root.members.len);
    }

    idx.close();
    idx_open = false;

    var reopened = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{});
    defer reopened.close();

    try std.testing.expectEqual(@as(u64, 1), reopened.stats().root_node);
    var txn = try reopened.beginReadTxn();
    defer txn.abort();
    var root = try reopened.loadNode(&txn, reopened.stats().root_node);
    defer root.deinit(alloc);
    try std.testing.expect(root.is_leaf);
    try std.testing.expectEqual(@as(usize, 0), root.members.len);
}

test "bulk ingest defers oversized root leaf until finish" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.2, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 0.3, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 0.4, 0.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 0.5, 0.0 }, .metadata = "doc:6" },
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
    });

    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().split_leaf_calls);
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());

    {
        var read_txn = try idx.beginReadTxn();
        defer read_txn.abort();
        idx.clearNodeCache();
        var staged_root = try idx.loadNode(&read_txn, idx.metadata.root_node);
        defer staged_root.deinit(alloc);
        try std.testing.expect(staged_root.is_leaf);
        try std.testing.expect(staged_root.members.len > idx.config.leaf_size);
    }

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;

    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u64, 1), idx.getWriteProfile().split_leaf_calls);
    try std.testing.expect(idx.stats().node_count > 1);

    var results = try idx.search(&[_]f32{ 0.5, 0.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 6), results.getHits()[0].vector_id);
}

test "bulk ingest uses kway kmeans for large deferred oversized leaf" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 16,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
        .kmeans_backend = .cpu,
        .kmeans_update_strategy = .segmented,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    var seed_items: [5]BatchInsertItem = undefined;
    var seed_vectors: [5][2]f32 = undefined;
    var seed_metadata: [5][8]u8 = undefined;
    for (&seed_items, 0..) |*item, i| {
        seed_vectors[i] = if (i < 4)
            .{ @as(f32, @floatFromInt(i)) * 0.01, 0.0 }
        else
            .{ 100.0, 0.0 };
        const key = try std.fmt.bufPrint(&seed_metadata[i], "doc:{d:0>4}", .{i});
        item.* = .{
            .vector_id = @intCast(i + 1),
            .vector = &seed_vectors[i],
            .metadata = key,
        };
    }
    try idx.batchInsertWithMetadata(&seed_items);
    {
        var read_txn = try idx.beginReadTxn();
        defer read_txn.abort();
        var root = try idx.loadNode(&read_txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(!root.is_leaf);
    }

    var items: [9]BatchInsertItem = undefined;
    var vectors: [9][2]f32 = undefined;
    var metadata: [9][8]u8 = undefined;
    for (&items, 0..) |*item, i| {
        vectors[i] = .{ 100.0 + @as(f32, @floatFromInt(i + 1)) * 0.01, 0.0 };
        const key = try std.fmt.bufPrint(&metadata[i], "doc:{d:0>4}", .{i + seed_items.len});
        item.* = .{
            .vector_id = @intCast(i + seed_items.len + 1),
            .vector = &vectors[i],
            .metadata = key,
        };
    }

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
    });
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().split_leaf_calls);
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());

    try idx.finishBulkIngestSessionWithOptions(.{
        .compact = false,
        .bulk_rebuild_hbc_leaf_min_members = 999,
    });
    session_open = false;

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u64, 1), profile.split_leaf_calls);
    try std.testing.expect(profile.kmeans_assignment_calls > 0);
    try std.testing.expect(idx.stats().node_count > 2);

    var results = try idx.search(&[_]f32{ 100.09, 0.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
}

test "bulk ingest does not persist deferred oversized leaf quantized payloads" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = true,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    const seed = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.2, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 0.3, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 100.0, 0.0 }, .metadata = "doc:5" },
    };
    try idx.batchInsertWithMetadata(&seed);
    {
        var read_txn = try idx.beginReadTxn();
        defer read_txn.abort();
        var root = try idx.loadNode(&read_txn, idx.metadata.root_node);
        defer root.deinit(alloc);
        try std.testing.expect(!root.is_leaf);
    }

    const first = [_]BatchInsertItem{
        .{ .vector_id = 6, .vector = &.{ 100.1, 0.0 }, .metadata = "doc:6" },
        .{ .vector_id = 7, .vector = &.{ 100.2, 0.0 }, .metadata = "doc:7" },
        .{ .vector_id = 8, .vector = &.{ 100.3, 0.0 }, .metadata = "doc:8" },
        .{ .vector_id = 9, .vector = &.{ 100.4, 0.0 }, .metadata = "doc:9" },
    };
    const second = [_]BatchInsertItem{
        .{ .vector_id = 10, .vector = &.{ 100.5, 0.0 }, .metadata = "doc:10" },
    };

    const options: BatchInsertOptions = .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&first, options);
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_quantized_nodes.count());
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().ns_quant_put_calls);
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().ns_quant_value_bytes);

    try idx.batchInsertWithMetadataOptions(&second, options);
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_quantized_nodes.count());
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().ns_quant_put_calls);
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().ns_quant_value_bytes);

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;

    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_quantized_nodes.count());
    try std.testing.expect(idx.getWriteProfile().ns_quant_put_calls > 0);

    var results = try idx.search(&[_]f32{ 100.5, 0.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 10), results.getHits()[0].vector_id);
}

test "bulk split workspace reuses transformed external vectors and reports apply memory" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    const LoaderCtx = struct {
        calls: usize = 0,
        loaded: usize = 0,

        fn load(
            ctx_ptr: *anyopaque,
            vector_ids: []const u64,
            metadata: []const ?[]const u8,
            matrix_positions: []const usize,
            matrix: []f32,
            scratch: []f32,
            dims: usize,
            index: *HBCIndex,
            transform: HBCIndex.ExternalVectorTransformFn,
        ) !void {
            _ = metadata;
            _ = scratch;
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr));
            ctx.calls += 1;
            ctx.loaded += vector_ids.len;
            for (vector_ids, matrix_positions) |vector_id, matrix_position| {
                const offset = matrix_position * dims;
                var original = [_]f32{
                    @as(f32, @floatFromInt(vector_id)),
                    @as(f32, @floatFromInt(vector_id + 100)),
                };
                _ = transform(index, original[0..], matrix[offset .. offset + dims]);
            }
        }
    };

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{});
    defer idx.close();

    var resource_manager = resource_manager_mod.ResourceManager.init(.{});
    idx.attachResourceManager(&resource_manager);

    var loader_ctx = LoaderCtx{};
    idx.setExternalVectorBatchTransformedMatrixLoader(&loader_ctx, LoaderCtx.load);

    var txn = try idx.beginWriteTxn();
    defer txn.abort();
    for ([_]u64{ 1, 2, 3 }) |vector_id| {
        var metadata_buf: [16]u8 = undefined;
        const metadata = try std.fmt.bufPrint(&metadata_buf, "doc:{d}", .{vector_id});
        try idx.putMetadata(&txn, vector_id, metadata);
    }

    const ids = [_]u64{ 1, 2, 3 };
    const positions = [_]usize{ 0, 1, 2 };
    var matrix: [6]f32 = .{0} ** 6;
    var matrix_again: [6]f32 = .{0} ** 6;
    var lookups: [ids.len]FixedKeyLookup = undefined;
    var key_views: [ids.len][]const u8 = undefined;
    var values: [ids.len]?[]const u8 = undefined;
    var scratch: [2]f32 = undefined;

    const baseline_apply_bytes = resource_manager.sliceStats(.dense_apply_working_set).used_bytes;
    idx.beginBulkSplitVectorWorkspace();
    var workspace_active = true;
    defer if (workspace_active) idx.endBulkSplitVectorWorkspace();

    try std.testing.expect(try idx.loadExternalVectorsTransformedIntoMatrix(
        &txn,
        &ids,
        &positions,
        &matrix,
        &lookups,
        &key_views,
        &values,
        &scratch,
    ));
    try std.testing.expectEqual(@as(usize, 1), loader_ctx.calls);
    try std.testing.expectEqual(@as(usize, ids.len), loader_ctx.loaded);
    try std.testing.expect(resource_manager.sliceStats(.dense_apply_working_set).used_bytes > baseline_apply_bytes);

    try std.testing.expect(try idx.loadExternalVectorsTransformedIntoMatrix(
        &txn,
        &ids,
        &positions,
        &matrix_again,
        &lookups,
        &key_views,
        &values,
        &scratch,
    ));
    try std.testing.expectEqual(@as(usize, 1), loader_ctx.calls);
    try std.testing.expectEqualSlices(f32, &matrix, &matrix_again);

    idx.endBulkSplitVectorWorkspace();
    workspace_active = false;
    try std.testing.expectEqual(baseline_apply_bytes, resource_manager.sliceStats(.dense_apply_working_set).used_bytes);
}

test "bulk ingest deferred leaf splits publish in bounded windows" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
        .prefer_key_local_leaf_splits = true,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    const seed_items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 100.0, 0.0 }, .metadata = "doc:3" },
    };
    try idx.batchInsertWithMetadata(&seed_items);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 4, .vector = &.{ 100.1, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 100.2, 0.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 100.3, 0.0 }, .metadata = "doc:6" },
        .{ .vector_id = 7, .vector = &.{ 100.4, 0.0 }, .metadata = "doc:7" },
        .{ .vector_id = 8, .vector = &.{ 100.5, 0.0 }, .metadata = "doc:8" },
        .{ .vector_id = 9, .vector = &.{ 100.6, 0.0 }, .metadata = "doc:9" },
        .{ .vector_id = 10, .vector = &.{ 100.7, 0.0 }, .metadata = "doc:10" },
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
    });
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());

    try idx.finishBulkIngestSessionWithOptions(.{
        .compact = false,
        .max_deferred_hbc_leaf_splits_per_publish = 1,
        .bulk_rebuild_hbc_leaf_min_members = 999,
    });
    session_open = false;

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expect(profile.deferred_leaf_split_publish_windows > 1);
    try std.testing.expectEqual(@as(u64, 1), profile.deferred_leaf_split_window_max_steps);
    try std.testing.expectEqual(profile.split_leaf_calls, profile.deferred_leaf_split_steps);
}

test "bulk ingest deferred leaf split member budget bounds publish windows" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.2, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 0.3, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 0.4, 0.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 0.5, 0.0 }, .metadata = "doc:6" },
        .{ .vector_id = 7, .vector = &.{ 0.6, 0.0 }, .metadata = "doc:7" },
        .{ .vector_id = 8, .vector = &.{ 0.7, 0.0 }, .metadata = "doc:8" },
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
    });
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());

    try idx.finishBulkIngestSessionWithOptions(.{
        .compact = false,
        .max_deferred_hbc_leaf_splits_per_publish = 999,
        .max_deferred_hbc_leaf_split_members_per_publish = 3,
        .bulk_rebuild_hbc_leaf_min_members = 999,
    });
    session_open = false;

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expect(profile.deferred_leaf_split_publish_windows > 1);
    try std.testing.expectEqual(@as(u64, 1), profile.deferred_leaf_split_window_max_steps);
    try std.testing.expectEqual(profile.split_leaf_calls, profile.deferred_leaf_split_steps);
}

test "bulk ingest deferred leaf split quantized publish reuses split vectors" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = true,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.2, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 10.0, 10.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 10.1, 10.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 10.2, 10.0 }, .metadata = "doc:6" },
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .defer_quantized_rebuild_to_bulk_finish = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
    });
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u64, 0), idx.getWriteProfile().ns_quant_put_calls);

    try idx.finishBulkIngestSessionWithOptions(.{
        .compact = false,
        .bulk_rebuild_hbc_leaf_min_members = 999,
    });
    session_open = false;

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_quantized_nodes.count());
    try std.testing.expect(profile.split_leaf_vector_load_ns > 0);
    try std.testing.expectEqual(@as(u64, 0), profile.quantized_leaf_vector_load_ns);
    try std.testing.expect(profile.ns_quant_put_calls > 0);

    var results = try idx.search(&[_]f32{ 10.1, 10.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
}

test "bulk ingest oversized leaf finish rebuilds local subtree instead of repeated binary splits" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    var seed_items: [5]BatchInsertItem = undefined;
    var seed_vectors: [5][2]f32 = undefined;
    var seed_metadata: [5][8]u8 = undefined;
    for (&seed_items, 0..) |*item, i| {
        seed_vectors[i] = if (i < 4)
            .{ @as(f32, @floatFromInt(i)) * 0.01, 0.0 }
        else
            .{ 100.0, 0.0 };
        const key = try std.fmt.bufPrint(&seed_metadata[i], "doc:{d:0>4}", .{i});
        item.* = .{
            .vector_id = @intCast(i + 1),
            .vector = &seed_vectors[i],
            .metadata = key,
        };
    }
    try idx.batchInsertWithMetadata(&seed_items);

    const item_count = 15;
    var vectors = try alloc.alloc([2]f32, item_count);
    defer alloc.free(vectors);
    const items = try alloc.alloc(BatchInsertItem, item_count);
    defer {
        for (items) |item| alloc.free(@constCast(item.metadata));
        alloc.free(items);
    }
    for (items, 0..) |*item, i| {
        vectors[i] = .{ 100.0 + @as(f32, @floatFromInt(i + 1)) * 0.01, 0.0 };
        item.* = .{
            .vector_id = @intCast(i + seed_items.len + 1),
            .vector = vectors[i][0..],
            .metadata = try std.fmt.allocPrint(alloc, "doc:{d}", .{i + seed_items.len + 1}),
        };
    }

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_bulk_finish = true,
        .bulk_rebuild_leaf_min_members = 8,
    });
    try std.testing.expectEqual(@as(u32, 1), idx.deferred_oversized_leaves.count());

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;

    const profile = idx.getWriteProfile();
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expectEqual(@as(u64, 1), profile.bulk_leaf_rebuild_calls);
    try std.testing.expect(profile.bulk_leaf_rebuild_members_max > item_count);
    try std.testing.expect(idx.stats().node_count > 1);

    var results = try idx.search(vectors[item_count - 1][0..], 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, item_count + seed_items.len), results.getHits()[0].vector_id);
}

test "bulk ingest batch-finish leaf splits normalize before commit" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.2, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 0.3, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 0.4, 0.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 0.5, 0.0 }, .metadata = "doc:6" },
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
        .defer_leaf_splits_to_batch_finish = true,
    });

    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expect(idx.getWriteProfile().split_leaf_calls > 0);
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_node_keys.count());

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());

    var results = try idx.search(&[_]f32{ 0.5, 0.0 }, 1);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 1), results.getHits().len);
    try std.testing.expectEqual(@as(u64, 6), results.getHits()[0].vector_id);
}

test "bulk ingest splits oversized leaves incrementally by default" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(alloc, path, .{
        .dims = 2,
        .leaf_size = 4,
        .branching_factor = 8,
        .search_width = 8,
        .use_quantization = false,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{
            .flush_threshold = 1,
            .bulk_ingest_flush_threshold_multiplier = 4,
        },
    });
    defer idx.close();

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 0.1, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.2, 0.0 }, .metadata = "doc:3" },
        .{ .vector_id = 4, .vector = &.{ 0.3, 0.0 }, .metadata = "doc:4" },
        .{ .vector_id = 5, .vector = &.{ 0.4, 0.0 }, .metadata = "doc:5" },
        .{ .vector_id = 6, .vector = &.{ 0.5, 0.0 }, .metadata = "doc:6" },
    };

    try idx.beginBulkIngestSession();
    var session_open = true;
    errdefer if (session_open) idx.abortBulkIngestSession();

    idx.resetWriteProfile();
    try idx.batchInsertWithMetadataOptions(&items, .{
        .assume_absent_ids = true,
        .coalesce_leaf_writes = true,
        .defer_quantized_rebuild = true,
        .skip_vector_store = false,
        .bulk_ingest = true,
    });

    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
    try std.testing.expect(idx.getWriteProfile().split_leaf_calls > 0);

    try idx.finishBulkIngestSessionWithOptions(.{ .compact = false });
    session_open = false;
    try std.testing.expectEqual(@as(u32, 0), idx.deferred_oversized_leaves.count());
}

test "hbc bulk build skip vector store can delete using live vector cache" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 8,
        .use_quantization = true,
        .storage_backend = .lsm,
    });
    defer idx.close();
    idx.setRetainedVectorCacheEnabled(true);

    const items = [_]BatchInsertItem{
        .{ .vector_id = 1, .vector = &.{ 0.0, 0.0 }, .metadata = "doc:1" },
        .{ .vector_id = 2, .vector = &.{ 1.0, 0.0 }, .metadata = "doc:2" },
        .{ .vector_id = 3, .vector = &.{ 0.0, 1.0 }, .metadata = "doc:3" },
    };
    try idx.bulkBuildWithMetadataOptions(&items, .{ .skip_vector_store = true });
    try std.testing.expectEqual(@as(u64, 3), idx.stats().active_count);

    {
        var txn = try idx.beginRuntimeReadTxn();
        defer txn.abort();
        var key_buf: [10]u8 = undefined;
        const maybe_raw: ?[]const u8 = idx.getNamespaced(&txn, .vecs, encodeVecKey(&key_buf, 2)) catch |err| blk: {
            if (isNotFound(err)) break :blk null;
            return err;
        };
        // The raw vector is intentionally not persisted in HBC's private vector
        // namespace; live mutations use the cache.
        try std.testing.expect(maybe_raw == null);
    }

    try idx.batchApply(&.{}, &.{2});
    try std.testing.expectEqual(@as(u64, 2), idx.stats().active_count);
    try std.testing.expect((try idx.getMetadata(2)) == null);
}

test "hbc routes dense lsm profile options" {
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.openWithLsmOptions(std.testing.allocator, path, .{
        .dims = 3,
        .storage_backend = .lsm,
    }, .{
        .backend_options = .{ .flush_threshold = 123 },
    });
    defer idx.close();

    switch (idx.env_owner) {
        .lsm => |handle| try std.testing.expectEqual(@as(usize, 123), handle.backend.options.flush_threshold),
        else => return error.TestUnexpectedResult,
    }
}

test "large insert and search" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 8,
        .leaf_size = 10,
        .branching_factor = 4,
        .search_width = 16,
    });
    defer idx.close();

    // Insert 100 vectors
    var prng = std.Random.DefaultPrng.init(12345);
    const random = prng.random();
    for (0..100) |i| {
        var v: [8]f32 = undefined;
        for (&v) |*x| x.* = random.float(f32) * 10.0;
        try idx.insert(@intCast(i + 1), &v);
    }

    try std.testing.expectEqual(@as(u64, 100), idx.stats().active_count);
    try std.testing.expect(idx.stats().node_count > 1);

    // Search
    var results = try idx.search(&[_]f32{ 5.0, 5.0, 5.0, 5.0, 5.0, 5.0, 5.0, 5.0 }, 10);
    defer results.deinit();
    try std.testing.expectEqual(@as(usize, 10), results.getHits().len);

    // Results should be sorted by distance
    const hits = results.getHits();
    for (1..hits.len) |j| {
        try std.testing.expect(hits[j].distance >= hits[j - 1].distance);
    }
}

test "findLeafWithOptions does not use-after-free when cache evicts mid-traversal" {
    // Regression test for a use-after-free in findLeafWithOptions.
    //
    // The old traversal API returned an unretained pointer into the node
    // cache, then iterated node.children while later child loads could clear
    // the cache and leave that slice dangling.
    //
    // By setting max_cached_nodes very small we guarantee clearNodeCache fires
    // during traversal once the tree has more than a couple of internal nodes.
    // Without the fix (copying children to local storage), this test segfaults
    // or triggers Zig's debug allocator use-after-free detection (0xaa poison).
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .max_cached_nodes = 2,
        .max_cached_vectors = 2,
    });
    defer idx.close();

    // Phase 1: build a multi-level tree.  With leaf_size=2 and
    // branching_factor=2 the tree will start splitting after a handful of
    // inserts, creating internal nodes with children slices.
    var prng = std.Random.DefaultPrng.init(0xdeadbeef);
    const random = prng.random();
    for (0..30) |i| {
        var v: [4]f32 = undefined;
        for (&v) |*x| x.* = random.float(f32) * 10.0;
        try idx.insert(@intCast(i + 1), &v);
    }

    // Sanity: we must have multiple tree nodes for the bug to manifest.
    try std.testing.expect(idx.stats().node_count > 3);

    // Phase 2: insert more vectors.  Each insert calls findLeafWithOptions
    // which traverses the tree.  With max_cached_nodes=2, nearly every
    // child load forced a full cache clear — exactly the scenario that caused
    // the original crash before retained handles and owned traversal copies.
    for (30..60) |i| {
        var v: [4]f32 = undefined;
        for (&v) |*x| x.* = random.float(f32) * 10.0;
        try idx.insert(@intCast(i + 1), &v);
    }

    try std.testing.expectEqual(@as(u64, 60), idx.stats().active_count);

    // Verify the index is still functional by searching.
    var results = try idx.search(&[_]f32{ 5.0, 5.0, 5.0, 5.0 }, 5);
    defer results.deinit();
    try std.testing.expect(results.getHits().len > 0);
}

test "findLeafWithOptions fails closed when cached internal children are stale" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 2,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .max_cached_nodes = 2,
    });
    defer idx.close();

    var centroid = [_]f32{ 0.0, 0.0 };
    var children = [_]u64{999};
    const root = Node{
        .id = 1,
        .is_leaf = false,
        .level = 1,
        .parent = 0,
        .centroid = centroid[0..],
        .children = children[0..],
        .members = &.{},
    };

    var write_txn = try idx.beginWriteTxn();
    errdefer write_txn.abort();
    const centroid_bytes = std.mem.sliceAsBytes(root.centroid);
    const ids_bytes = std.mem.sliceAsBytes(root.children);
    const packed_len = vectorindex_hbc.packedNodeValueSize(centroid_bytes.len, ids_bytes.len);
    const packed_buf = try alloc.alloc(u8, packed_len);
    defer alloc.free(packed_buf);
    const header = NodeHeader{ .is_leaf = false, .level = 1, .parent = 0 };
    const encoded = try vectorindex_hbc.encodePackedNodeValue(packed_buf, header, root.covering_radius, centroid_bytes, ids_bytes);
    var key_buf: [12]u8 = undefined;
    try idx.putNamespaced(&write_txn, .nodes, encodeNodeKey(&key_buf, root.id, .packed_node), encoded);
    try idx.cacheNode(&root);
    try idx.finishWriteTxn(&write_txn);

    var read_txn = try idx.beginRuntimeReadTxn();
    defer read_txn.abort();
    try std.testing.expectError(
        error.Corrupted,
        idx.findLeafWithOptions(&read_txn, idx.metadata.root_node, &.{ 1.0, 0.0 }, true),
    );
}

test "insert routes while search cache admission is disabled" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 2,
        .branching_factor = 2,
        .search_width = 8,
        .max_cached_nodes = 2,
        .max_cached_vectors = 2,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0xbadc0de);
    const random = prng.random();
    for (0..30) |i| {
        var v: [4]f32 = undefined;
        for (&v) |*x| x.* = random.float(f32) * 10.0;
        try idx.insert(@intCast(i + 1), &v);
    }

    idx.clearNodeCache();
    idx.active_searches.store(2, .release);
    defer idx.active_searches.store(0, .release);

    var v: [4]f32 = undefined;
    for (&v) |*x| x.* = random.float(f32) * 10.0;
    try idx.insert(31, &v);

    try std.testing.expectEqual(@as(u64, 31), idx.stats().active_count);
}

test "scoreLeafMembers does not use-after-free when cache evicts during member scoring" {
    const alloc = std.testing.allocator;
    var tp: TestPath = .{};
    const path = tp.init();
    defer tp.cleanup();

    var idx = try HBCIndex.open(alloc, path, .{
        .dims = 4,
        .leaf_size = 64,
        .branching_factor = 2,
        .search_width = 8,
        .use_quantization = false,
        .max_cached_nodes = 2,
        .max_cached_vectors = 8,
    });
    defer idx.close();

    var prng = std.Random.DefaultPrng.init(0xfacefeed);
    const random = prng.random();
    for (0..24) |i| {
        var v: [4]f32 = undefined;
        for (&v) |*x| x.* = random.float(f32) * 10.0;
        try idx.insert(@intCast(i + 1), &v);
    }

    const HookCtx = struct {
        idx: *HBCIndex,
        fired: bool = false,

        fn onVectorLoad(ctx_ptr: ?*anyopaque, hook_idx: *HBCIndex, _: u64) void {
            const ctx: *@This() = @ptrCast(@alignCast(ctx_ptr.?));
            if (ctx.fired) return;
            ctx.fired = true;
            std.debug.assert(ctx.idx == hook_idx);
            hook_idx.clearNodeCache();
        }
    };

    var ctx = HookCtx{ .idx = &idx };
    test_get_vector_view_or_scratch_ctx = &ctx;
    test_get_vector_view_or_scratch_hook = HookCtx.onVectorLoad;
    defer {
        test_get_vector_view_or_scratch_ctx = null;
        test_get_vector_view_or_scratch_hook = null;
    }

    var results = try idx.search(&[_]f32{ 5.0, 5.0, 5.0, 5.0 }, 5);
    defer results.deinit();
    try std.testing.expect(ctx.fired);
    try std.testing.expect(results.getHits().len > 0);
}

test "HBC runtime batch mode preserves bulk only for non-direct LSM bulk sessions" {
    try std.testing.expectEqual(vectorindex_store.BatchMode.default, hbcRuntimeBatchMode(false, false));
    try std.testing.expectEqual(vectorindex_store.BatchMode.default, hbcRuntimeBatchMode(false, true));
    try std.testing.expectEqual(vectorindex_store.BatchMode.default, hbcRuntimeBatchMode(false, null));

    try std.testing.expectEqual(vectorindex_store.BatchMode.bulk_ingest, hbcRuntimeBatchMode(true, false));
    try std.testing.expectEqual(vectorindex_store.BatchMode.default, hbcRuntimeBatchMode(true, true));
    try std.testing.expectEqual(vectorindex_store.BatchMode.default, hbcRuntimeBatchMode(true, null));
}

// ============================================================================
// Test helpers
// ============================================================================

const TestPath = struct {
    buf: [256]u8 = undefined,

    fn init(self: *TestPath) [*:0]const u8 {
        const ts = platform_time.monotonicNs();
        const nonce = @atomicRmw(u64, &temp_path_nonce, .Add, 1, .monotonic);
        const slice = std.fmt.bufPrint(&self.buf, "/tmp/antfly-hbc-test-{d}-{d}\x00", .{ ts, nonce }) catch unreachable;
        var io_impl = std.Io.Threaded.init(std.heap.page_allocator, .{});
        defer io_impl.deinit();
        std.Io.Dir.cwd().createDirPath(io_impl.io(), std.mem.span(@as([*:0]const u8, @ptrCast(slice.ptr)))) catch {};
        return @ptrCast(slice.ptr);
    }

    fn cleanup(self: *TestPath) void {
        var io_impl = std.Io.Threaded.init(std.heap.page_allocator, .{});
        defer io_impl.deinit();
        std.Io.Dir.cwd().deleteTree(io_impl.io(), std.mem.span(@as([*:0]const u8, @ptrCast(&self.buf)))) catch {};
    }
};
