// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const abi = @import("kernel_owner_abi");
const error_identity = @import("kernel_error_identity");
const local_query_client = @import("local_query_client");
const client = @import("kernel_owner_client.zig");
const wal_client = @import("kernel_wal_client.zig");
const data_apply_client = @import("data_raft_apply_client.zig");
const metadata_apply_client = @import("metadata_raft_apply_client.zig");

test "local query identity relay preserves origin and attributes protocol defects to consumer" {
    const failure = error_identity.failureFromError(
        error.InvalidQueryRequest,
        .local_query,
        abi.abi_version,
        @intFromEnum(abi.LocalQueryOperation.parse_internal_request),
    );
    var forwarded: abi.FailureIdentity = .{};
    try local_query_client.acceptProviderFailure(
        failure.status,
        failure,
        .validate_provider_response,
        &forwarded,
    );
    try std.testing.expectEqualDeep(failure, forwarded);

    @import("../test_error_logs.zig").expectErrorLogs(1);
    var malformed = failure;
    malformed.operation = 0;
    var replacement: abi.FailureIdentity = .{};
    try std.testing.expectError(
        error.InvalidBoundaryFailureIdentity,
        local_query_client.acceptProviderFailure(
            malformed.status,
            malformed,
            .validate_provider_response,
            &replacement,
        ),
    );
    try std.testing.expectEqual(abi.Status.invalid_boundary_failure_identity, replacement.status);
    try std.testing.expectEqual(abi.FailureBoundary.storage_owner, replacement.boundary);
    try std.testing.expectEqual(abi.abi_version, replacement.boundary_version);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.validate_provider_response),
        replacement.operation,
    );
    try std.testing.expectEqualStrings("InvalidBoundaryFailureIdentity", replacement.errorName());
}

test "HA seed storage-owner boundary preserves exact operational errors" {
    try std.testing.expectError(
        error.InvalidArgument,
        client.haSeedActivate("{"),
    );
    try std.testing.expectError(
        error.InvalidStagingRoot,
        client.haSeedActivate(
            \\{"staging_root":"relative","target_root":"/valid","expected":{"generation":"gen","slot_name":"slot","identity":{"cluster_id":1,"timeline_id":1,"epoch":1}}}
        ),
    );
}

fn cleanup(path: []const u8) void {
    var io_impl = std.Io.Threaded.init(std.heap.page_allocator, .{});
    defer io_impl.deinit();
    std.Io.Dir.cwd().deleteTree(io_impl.io(), path) catch {};
}

const TestWalOptions = struct {
    const Backend = enum { lmdb, lsm, lsm_memory };
    const CommitBackend = enum { sync, worker_thread, async_io, adaptive };
    const Empty = struct {};

    backend: ?Backend = null,
    storage: ?*anyopaque = null,
    lsm_options: Empty = .{},
    clock: @import("sim_runtime.zig").Clock = @import("sim_runtime.zig").real_clock,
    commit_scheduler: @import("sim_runtime.zig").CompletionScheduler = @import("sim_runtime.zig").real_completion_scheduler,
    artificial_sync_delay_ns: u64 = 0,
    group_commit_window_ns: u64 = 0,
    group_commit_max_requests: usize = 64,
    commit_backend: CommitBackend = .adaptive,
    no_sync: bool = false,
    read_only: bool = false,
    model_commit_backend_completions: bool = false,

    pub fn resolvedBackend(self: @This()) Backend {
        return self.backend orelse .lsm;
    }
};

test "opaque WAL preserves durable operations and exact failure identity" {
    const root = "/tmp/antfly-storage-kernel-wal-owner";
    const path = root ++ "/wal";
    const bootstrap_path = root ++ "/bootstrap";
    const read_only_path = root ++ "/read-only";
    cleanup(root);
    defer cleanup(root);

    const path_z = try std.testing.allocator.dupeZ(u8, path);
    defer std.testing.allocator.free(path_z);
    var wal = try wal_client.WAL.open(path_z.ptr, TestWalOptions{});
    defer wal.close();
    try std.testing.expectEqual(@as(u64, 1), try wal.append("alpha"));
    try std.testing.expectEqual(@as(u64, 2), try wal.append("beta"));
    try std.testing.expectEqual(@as(u64, 2), wal.lastLsn());

    const entries = try wal.iterateFrom(std.testing.allocator, 1);
    defer {
        for (entries) |entry| std.testing.allocator.free(@constCast(entry.data));
        std.testing.allocator.free(entries);
    }
    try std.testing.expectEqual(@as(usize, 2), entries.len);
    try std.testing.expectEqualStrings("alpha", entries[0].data);
    try std.testing.expectEqualStrings("beta", entries[1].data);

    const second = (try wal.readAt(std.testing.allocator, 2)).?;
    defer std.testing.allocator.free(@constCast(second.data));
    try std.testing.expectEqualStrings("beta", second.data);
    try wal.truncate(1);
    try std.testing.expect((try wal.readAt(std.testing.allocator, 1)) == null);

    try std.testing.expectError(error.WalLsnMismatch, wal.appendAt(9, "must-not-append"));
    try wal.truncateAfter(1);
    try std.testing.expectEqual(@as(u64, 2), try wal.append("gamma"));
    const stats = wal.statsSnapshot();
    try std.testing.expectEqual(@as(u64, 3), stats.append_calls);
    try std.testing.expectEqual(@as(u64, 3), stats.logical_entries);
    try std.testing.expectError(error.Overflow, wal.truncateAfter(std.math.maxInt(u64)));

    const bootstrap_z = try std.testing.allocator.dupeZ(u8, bootstrap_path);
    defer std.testing.allocator.free(bootstrap_z);
    var bootstrap = try wal_client.WAL.open(bootstrap_z.ptr, TestWalOptions{});
    defer bootstrap.close();
    try std.testing.expectEqual(@as(u64, 7), try bootstrap.appendAt(7, "timeline"));

    const read_only_z = try std.testing.allocator.dupeZ(u8, read_only_path);
    defer std.testing.allocator.free(read_only_z);
    {
        var writable = try wal_client.WAL.open(read_only_z.ptr, TestWalOptions{});
        defer writable.close();
        _ = try writable.append("durable");
    }
    var read_only = try wal_client.WAL.open(read_only_z.ptr, TestWalOptions{ .read_only = true });
    defer read_only.close();
    try std.testing.expectError(error.ReadOnly, read_only.append("rejected"));

    try std.testing.expectError(
        error.UnsupportedKernelWalOptions,
        wal_client.WAL.open(path_z.ptr, TestWalOptions{ .backend = .lsm_memory }),
    );
}

test "opaque WAL idempotency survives truncation and reopen" {
    const path = "/tmp/antfly-storage-kernel-wal-idempotency";
    cleanup(path);
    defer cleanup(path);
    {
        var wal = try wal_client.WAL.open(path, wal_client.WalOptions{});
        defer wal.close();
        const first = try wal.appendIdempotent("receipt-1", "digest-a", "payload");
        try std.testing.expect(first.appended);
        try std.testing.expectEqual(@as(u64, 1), first.lsn);
        const retry = try wal.appendIdempotent("receipt-1", "digest-a", "payload");
        try std.testing.expect(!retry.appended);
        try std.testing.expectEqual(first.lsn, retry.lsn);
        try std.testing.expectError(error.IdempotencyConflict, wal.appendIdempotent("receipt-1", "digest-b", "changed"));
        try std.testing.expectError(error.InvalidIdempotencyKey, wal.appendIdempotent("", "digest", "payload"));
        _ = try wal.append("second");
        try wal.truncate(first.lsn);
        try std.testing.expect((try wal.readAt(std.testing.allocator, first.lsn)) == null);
    }
    var reopened = try wal_client.WAL.open(path, wal_client.WalOptions{});
    defer reopened.close();
    const retained = try reopened.appendIdempotent("receipt-1", "digest-a", "payload");
    try std.testing.expect(!retained.appended);
    try std.testing.expectEqual(@as(u64, 1), retained.lsn);
    try std.testing.expectEqual(@as(u64, 2), reopened.lastLsn());
    try std.testing.expectError(error.IdempotencyConflict, reopened.appendIdempotent("receipt-1", "digest-b", "changed"));
}

test "coarse aggregation ABI preserves results and semantic error identities" {
    const hit_bodies = [_][]const u8{
        "{\"category\":\"alpha\",\"price\":10}",
        "{\"category\":\"alpha\",\"price\":20}",
        "{\"category\":\"beta\",\"price\":30}",
    };
    var hits: [hit_bodies.len]client.AggregationHit = undefined;
    for (hit_bodies, 0..) |body, i| hits[i] = .{ .stored_data = .fromSlice(body) };

    const base = client.AggregationRequest{
        .total_hits = hit_bodies.len,
        .context_json = .fromSlice("{}"),
        .hits = &hits,
        .hit_count = hits.len,
    };
    var response = try client.aggregate(.{
        .total_hits = base.total_hits,
        .aggregations_json = .fromSlice("{\"by_category\":{\"type\":\"terms\",\"field\":\"category\",\"size\":10}}"),
        .context_json = base.context_json,
        .hits = base.hits,
        .hit_count = base.hit_count,
    });
    defer response.deinit();
    try std.testing.expect(std.mem.indexOf(u8, response.bytes(), "by_category") != null);
    try std.testing.expect(std.mem.indexOf(u8, response.bytes(), "alpha") != null);
    try std.testing.expect(std.mem.indexOf(u8, response.bytes(), "beta") != null);

    try std.testing.expectError(error.InvalidAggregation, client.aggregate(.{
        .total_hits = base.total_hits,
        .aggregations_json = .fromSlice("{\"bad\":{\"type\":\"histogram\",\"field\":\"price\",\"interval\":0}}"),
        .context_json = base.context_json,
        .hits = base.hits,
        .hit_count = base.hit_count,
    }));
    try std.testing.expectError(error.UnsupportedAggregation, client.aggregate(.{
        .total_hits = base.total_hits,
        .aggregations_json = .fromSlice("{\"unsupported_without_text_context\":{\"type\":\"significant_terms\",\"field\":\"category\",\"size\":10}}"),
        .context_json = base.context_json,
        .hits = base.hits,
        .hit_count = base.hit_count,
    }));
}

test "opaque storage context owns Lite system namespaces auth and table owners" {
    const root = "/tmp/antfly-storage-kernel-context-lite";
    const lite_path = root ++ "/standalone.aflite";
    const auth_path = root ++ "/auth";
    cleanup(root);
    defer cleanup(root);
    var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
    defer io_impl.deinit();
    try std.Io.Dir.cwd().createDirPath(io_impl.io(), root);

    var context = client.Context{};
    try context.ensureWith(.{
        .storage_kind = .lite,
        .storage_path = .fromSlice(lite_path),
        .auth_storage_path = .fromSlice(auth_path),
    });

    var catalog = try context.systemStore(std.testing.allocator, "system/metadata");
    var write = try catalog.beginWrite();
    try write.put("catalog", "{\"epoch\":2}");
    try write.commit();
    var read = try catalog.beginRead();
    try std.testing.expectEqualStrings("{\"epoch\":2}", try read.get("catalog"));
    read.abort();

    var auth_users = try context.systemStore(std.testing.allocator, "system/auth-users");
    var users = try client.singleNamespaceStore(
        std.testing.allocator,
        &auth_users,
        "usermgr_users",
    );
    var auth_write = try users.beginWrite();
    try auth_write.put(.{ .name = "usermgr_users" }, "userpass:admin", "hash");
    try auth_write.commit();
    var auth_read = try users.beginRead();
    var auth_cursor = try auth_read.openCursor(.{ .name = "usermgr_users" });
    const first = (try auth_cursor.first()).?;
    try std.testing.expectEqualStrings("userpass:admin", first.key);
    try std.testing.expectEqualStrings("hash", first.value);
    auth_cursor.close();
    auth_read.abort();

    var owner = try client.Owner.open(.{
        .context = context.handle,
        .path = .fromSlice("group-7001/table-db"),
        .table_name = .fromSlice("docs"),
        .group_id = 7001,
        .has_identity_namespace = 1,
        .identity_table_id = 7,
        .identity_shard_id = 7001,
        .identity_range_id = 7001,
    });
    var response = try owner.batchJson(
        "docs",
        "{\"inserts\":{\"doc:a\":{\"title\":\"alpha\"}},\"sync_level\":\"full_index\"}",
    );
    try std.testing.expect(std.mem.indexOf(u8, response.bytes(), "\"inserted\":1") != null);
    response.deinit();
    try std.testing.expectEqual(
        abi.Status.busy,
        abi.antfly_storage_context_destroy(context.handle),
    );

    const maintenance_status = context.maintenanceSource().status();
    try std.testing.expectEqualStrings("lite", maintenance_status.engine);
    try std.testing.expect(maintenance_status.maintenance.check);

    owner.deinit();
    users.deinit();
    auth_users.deinit();
    catalog.deinit();
    context.deinit();
}

test "opaque storage owner preserves source-vector policy and status across reopen" {
    const alloc = std.testing.allocator;
    var directory = try @import("../common/test_directory.zig").TestDirectory.init("owner-source-vectors");
    defer directory.cleanup();
    const path = std.mem.span(directory.path().ptr);
    for ([_]abi.DenseEmbeddingStorage{ .vector_store, .persisted }, 0..) |policy, iteration| {
        var owner = try client.Owner.open(.{
            .path = .fromSlice(path),
            .table_name = .fromSlice("docs"),
            .group_id = 7001,
            .dense_embedding_storage = policy,
            .indexes_json = .fromSlice("{\"model\":{\"type\":\"embeddings\",\"external\":true,\"dimension\":3}}"),
        });
        defer owner.deinit();
        if (iteration == 0) {
            var response = try owner.batchJson("docs",
                \\{"inserts":{"a":{"_embeddings":{"model":[1,0,0]}}},"sync_level":"full_index"}
            );
            defer response.deinit();
        }
        // Observe immediately after reopen, before a query or write could
        // initialize a missing source store or repair its cached accounting.
        var response = try ownerStatusEventually(&owner);
        defer response.deinit();
        const Status = struct {
            source_vectors: ?struct { retained_payloads: u64 } = null,
        };
        var parsed = try std.json.parseFromSlice(Status, alloc, response.bytes(), .{ .ignore_unknown_fields = true });
        defer parsed.deinit();
        const source = parsed.value.source_vectors orelse return error.MissingSourceVectorStatus;
        try std.testing.expect(source.retained_payloads > 0);
    }
    var invalid_owner: ?*anyopaque = null;
    try std.testing.expectEqual(abi.Status.invalid_argument, abi.antfly_storage_owner_open(&.{
        .path = .fromSlice(path),
        .table_name = .fromSlice("docs"),
        .dense_embedding_storage = @enumFromInt(999),
    }, &invalid_owner));
    try std.testing.expect(invalid_owner == null);
}

test "opaque storage owner fences exact source targets before acknowledging writes" {
    const Observer = struct {
        calls: std.atomic.Value(usize) = .init(0),
        additive: std.atomic.Value(bool) = .init(false),
        reducing: std.atomic.Value(bool) = .init(false),
        invalid: std.atomic.Value(bool) = .init(false),
        fn notify(ptr: ?*anyopaque, table: abi.BorrowedBytes, group: u64, sequence: u64, has_sequence: u8, json: abi.BorrowedBytes) callconv(.c) void {
            const self: *@This() = @ptrCast(@alignCast(ptr.?));
            if (!std.mem.eql(u8, table.slice(), "docs") or group != 7001 or has_sequence == 0 or sequence == 0) self.invalid.store(true, .release);
            var targets = std.json.parseFromSlice([]@import("db/types.zig").IndexTargetVisibility, std.heap.page_allocator, json.slice(), .{}) catch {
                self.invalid.store(true, .release);
                return;
            };
            defer targets.deinit();
            for (targets.value) |target| {
                if (!std.mem.eql(u8, target.index_name, "model")) continue;
                switch (target.serving_set_effect) {
                    .additive_only => self.additive.store(true, .release),
                    .may_reduce => self.reducing.store(true, .release),
                }
            }
            _ = self.calls.fetchAdd(1, .release);
        }
    };
    var observer = Observer{};
    var directory = try @import("../common/test_directory.zig").TestDirectory.init("owner-target-observer");
    defer directory.cleanup();
    var owner = try client.Owner.open(.{
        .path = .fromSlice(std.mem.span(directory.path().ptr)),
        .table_name = .fromSlice("docs"),
        .group_id = 7001,
        .indexes_json = .fromSlice("{\"model\":{\"type\":\"embeddings\",\"external\":true,\"dimension\":3}}"),
        .target_observer = .{ .ctx = &observer, .notify = Observer.notify },
    });
    defer owner.deinit();
    var inserted = try owner.batchJson("docs", "{\"inserts\":{\"a\":{\"_embeddings\":{\"model\":[1,0,0]}}},\"sync_level\":\"full_index\"}");
    defer inserted.deinit();
    try std.testing.expect(observer.calls.load(.acquire) > 0);
    try std.testing.expect(observer.additive.load(.acquire));
    var deleted = try owner.batchJson("docs", "{\"deletes\":[\"a\"],\"sync_level\":\"full_index\"}");
    defer deleted.deinit();
    try std.testing.expect(observer.reducing.load(.acquire));
    try std.testing.expect(!observer.invalid.load(.acquire));
}

fn ownerStatusEventually(owner: *client.Owner) !client.Response {
    const time = @import("antfly_platform").time;
    const deadline = time.monotonicNs() + 5 * std.time.ns_per_s;
    while (true) {
        return owner.runtimeStatusJson("docs") catch |err| switch (err) {
            error.StorageBusy => {
                if (time.monotonicNs() >= deadline) return err;
                try std.testing.io.sleep(.fromMilliseconds(2), .awake);
                continue;
            },
            else => return err,
        };
    }
}

test "opaque storage owner performs coarse batch and query on one live DB" {
    const path = "/tmp/antfly-storage-kernel-owner-batch-query";
    const backup_root = "/tmp/antfly-storage-kernel-owner-backups";
    cleanup(path);
    cleanup(backup_root);
    defer cleanup(path);
    defer cleanup(backup_root);

    var owner = try client.Owner.open(.{
        .path = .fromSlice(path),
        .table_name = .fromSlice("docs"),
        .group_id = 7001,
        .lsm_root_generation = 0,
        .has_identity_namespace = 1,
        .identity_table_id = 7,
        .identity_shard_id = 7001,
        .identity_range_id = 7001,
    });
    defer owner.deinit();

    var duplicate_owner: ?*anyopaque = null;
    try std.testing.expectEqual(abi.Status.lsm_root_writer_already_open, abi.antfly_storage_owner_open(&.{
        .path = .fromSlice(path),
        .table_name = .fromSlice("docs"),
        .has_identity_namespace = 1,
        .identity_table_id = 7,
        .identity_shard_id = 7001,
        .identity_range_id = 7001,
    }, &duplicate_owner));
    try std.testing.expect(duplicate_owner == null);

    const batch_json =
        \\{"inserts":{"doc:a":{"title":"alpha"},"doc:b":{"title":"beta"}},"sync_level":"full_index"}
    ;
    var batch_response = try owner.batchJson("docs", batch_json);
    defer batch_response.deinit();
    try std.testing.expect(std.mem.indexOf(u8, batch_response.bytes(), "\"inserted\":2") != null);

    // Streaming crosses the real provider archive, retains backpressure and
    // propagates caller-owned errors without passing Zig error-set ordinals.
    const ScanCapture = struct {
        starts: usize = 0,
        rows: usize = 0,
        stop_on_start: bool = false,
        stop_on_row: bool = false,
        fn start(ptr: ?*anyopaque) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr.?));
            self.starts += 1;
            if (self.stop_on_start) return error.ConsumerStoppedBeforeScan;
        }
        fn write(ptr: ?*anyopaque, bytes: []const u8) !void {
            const self: *@This() = @ptrCast(@alignCast(ptr.?));
            try std.testing.expect(std.mem.endsWith(u8, bytes, "\n"));
            self.rows += 1;
            if (self.stop_on_row) return error.ConsumerStoppedAfterRow;
        }
        fn sink(self: *@This()) @import("../runtime_scan_sink.zig").ScanStreamSink {
            return .{ .context = self, .start_fn = start, .write_fn = write };
        }
    };
    const scan_json = "{\"from_key\":\"\",\"to_key\":\"\",\"include_documents\":true}";
    var capture: ScanCapture = .{};
    try owner.scanStream("docs", scan_json, capture.sink());
    try std.testing.expectEqual(@as(usize, 1), capture.starts);
    try std.testing.expectEqual(@as(usize, 2), capture.rows);
    capture = .{ .stop_on_start = true };
    try std.testing.expectError(error.ConsumerStoppedBeforeScan, owner.scanStream("docs", scan_json, capture.sink()));
    try std.testing.expectEqual(@as(usize, 0), capture.rows);
    capture = .{ .stop_on_row = true };
    try std.testing.expectError(error.ConsumerStoppedAfterRow, owner.scanStream("docs", scan_json, capture.sink()));
    try std.testing.expectEqual(@as(usize, 1), capture.rows);
    try std.testing.expectError(error.InvalidGraphMetricAction, owner.graphMetricMaintenanceJson("docs", "{\"operation\":\"metric_action_v1\"}"));

    // Status reads deliberately avoid waiting under the owner lease for a
    // background writer. A full-index acknowledgement does not make the next
    // observational read uncontended, so retry this transient status here.
    var status_response = status: {
        const time = @import("antfly_platform").time;
        const deadline = time.monotonicNs() + 5 * std.time.ns_per_s;
        var io_impl = std.Io.Threaded.init(std.testing.allocator, .{});
        defer io_impl.deinit();
        while (true) {
            break :status owner.runtimeStatusJson("docs") catch |err| switch (err) {
                error.StorageBusy => {
                    if (time.monotonicNs() >= deadline) return err;
                    try io_impl.io().sleep(.fromMilliseconds(2), .awake);
                    continue;
                },
                else => return err,
            };
        }
    };
    defer status_response.deinit();
    try std.testing.expect(std.mem.indexOf(u8, status_response.bytes(), "\"source_doc_count\":2") != null);

    var observed_response = try owner.observedDynamicFieldCapabilitySetsJson(
        "docs",
        "{\"fields\":[],\"coverage_read_mode\":\"cached_only\"}",
        null,
        null,
        null,
    );
    defer observed_response.deinit();
    try std.testing.expectEqualStrings("[]", observed_response.bytes());

    const maintenance = try owner.maintenance("docs", .inspect);
    try std.testing.expectEqual(abi.abi_version, maintenance.version);
    try std.testing.expectEqual(@as(u8, 0), maintenance.progressed);
    try std.testing.expectError(error.InvalidArgument, owner.maintenance("articles", .inspect));
    const invalid_maintenance = abi.MaintenanceRequest{
        .action = std.math.maxInt(u32),
        .table_name = .fromSlice("docs"),
    };
    var invalid_maintenance_result: abi.MaintenanceResult = .{};
    try std.testing.expectEqual(
        abi.Status.invalid_argument,
        abi.antfly_storage_owner_maintenance(owner.handle, &invalid_maintenance, &invalid_maintenance_result),
    );

    var replicated_response = try owner.replicatedBatchJson(
        "docs",
        "{\"inserts\":{\"doc:c\":{\"title\":\"gamma\"}},\"sync_level\":\"full_index\"}",
    );
    defer replicated_response.deinit();
    try std.testing.expect(std.mem.indexOf(u8, replicated_response.bytes(), "\"inserted\":1") != null);

    // Exact Raft-entry apply must preserve the physical idempotence fence
    // across the opaque storage-owner ABI. In particular, replaying a
    // non-idempotent transform at the same term/index must be a no-op.
    var raft_insert_response = try owner.replicatedBatchAtRaftEntryJson(
        "docs",
        "{\"inserts\":{\"doc:raft-counter\":{\"count\":0}},\"sync_level\":\"write\"}",
        3,
        40,
    );
    defer raft_insert_response.deinit();
    var raft_increment_response = try owner.replicatedBatchAtRaftEntryJson(
        "docs",
        "{\"transforms\":[{\"key\":\"doc:raft-counter\",\"operations\":[{\"op\":\"$inc\",\"path\":\"count\",\"value\":1}]}],\"sync_level\":\"write\"}",
        3,
        41,
    );
    defer raft_increment_response.deinit();
    var raft_replay_response = try owner.replicatedBatchAtRaftEntryJson(
        "docs",
        "{\"transforms\":[{\"key\":\"doc:raft-counter\",\"operations\":[{\"op\":\"$inc\",\"path\":\"count\",\"value\":1}]}],\"sync_level\":\"write\"}",
        3,
        41,
    );
    defer raft_replay_response.deinit();
    var raft_counter = try owner.lookupJson(
        "docs",
        "{\"key\":\"doc:raft-counter\",\"include_all_fields\":true}",
    );
    defer raft_counter.deinit();
    try std.testing.expect(std.mem.indexOf(u8, raft_counter.bytes(), "\"count\":1") != null);
    try std.testing.expectError(
        error.InvalidArgument,
        owner.replicatedBatchAtRaftEntryJson("docs", "{}", 0, 42),
    );
    try std.testing.expectError(error.InvalidBatchRequest, owner.batchJson("docs", "{"));
    try std.testing.expectError(error.InvalidBatchRequest, owner.replicatedBatchJson("docs", "{"));
    try owner.waitForSync("docs", .full_index);
    try owner.applyHAReplicationRecord("docs", .{
        .record_kind = 0x0012,
        .payload_codec = 0,
        .cluster_id = 1,
        .shard_id = 7001,
        .table_id = 7,
        .timeline_id = 1,
        .epoch = 1,
        .lsn = 1,
        .previous_lsn = 0,
    });

    const txn_id: [16]u8 = .{ 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15 };
    var txn_begin = try owner.replicatedBatchJson(
        "docs",
        "{\"_transaction\":{\"phase\":\"begin\",\"txn_id\":\"000102030405060708090a0b0c0d0e0f\",\"begin_timestamp\":\"42\",\"created_at_ns\":\"43\",\"topology_epoch\":\"7\",\"participants\":[\"table2:00000004:docs:7001\"]},\"sync_level\":\"write\"}",
    );
    defer txn_begin.deinit();
    try std.testing.expectEqual(abi.TxnStatus.pending, try owner.transactionStatus("docs", txn_id));
    var txn_prepare = try owner.replicatedBatchJson(
        "docs",
        "{\"inserts\":{\"doc:txn\":{\"title\":\"transactional\"}},\"_transaction\":{\"phase\":\"prepare\",\"txn_id\":\"000102030405060708090a0b0c0d0e0f\",\"topology_epoch\":\"7\"},\"sync_level\":\"write\"}",
    );
    defer txn_prepare.deinit();
    var txn_resolve = try owner.replicatedBatchJson(
        "docs",
        "{\"_transaction\":{\"phase\":\"resolve\",\"txn_id\":\"000102030405060708090a0b0c0d0e0f\",\"status\":\"committed\",\"commit_version\":\"44\"},\"sync_level\":\"full_index\"}",
    );
    defer txn_resolve.deinit();
    try std.testing.expectEqual(abi.TxnStatus.committed, try owner.transactionStatus("docs", txn_id));

    const query_json =
        \\{"query":{"match_all":{}},"limit":10}
    ;
    try std.testing.expectError(error.Timeout, owner.queryJsonWithOptions("docs", query_json, .{
        .execution_deadline_ns = 1,
    }));
    const MidQueryCancellation = struct {
        checks: usize = 0,
        fn requested(ctx: ?*anyopaque) callconv(.c) u8 {
            const self: *@This() = @ptrCast(@alignCast(ctx.?));
            self.checks += 1;
            // First admission succeeds; cancellation changes as the request
            // crosses from the owner into the physical query provider.
            return @intFromBool(self.checks >= 2);
        }
    };
    var cancellation: MidQueryCancellation = .{};
    try std.testing.expectError(error.Cancelled, owner.queryJsonWithOptions("docs", query_json, .{
        .cancellation_ctx = &cancellation,
        .cancellation_fn = MidQueryCancellation.requested,
    }));
    try std.testing.expect(cancellation.checks >= 2);
    // A canceled operation must release all borrowed controls and read state.
    var after_cancel = try owner.queryJson("docs", query_json);
    after_cancel.deinit();

    try std.testing.expectError(error.InvalidArgument, owner.queryJson("articles", query_json));
    try std.testing.expectError(error.InvalidQueryRequest, owner.queryJson("docs", "{"));

    // The nested distributed -> storage-owner -> local-query path must retain
    // both the semantic status and its originating stage, not merely rethrow a
    // broad failure after the inner provider unwinds.
    var invalid_query_response: abi.QueryOwnedResponse = .{};
    var invalid_query_failure: abi.FailureIdentity = .{};
    const invalid_query_status = abi.antfly_storage_owner_query_json(
        owner.handle,
        &.{ .control = .{
            .table_name = .fromSlice("docs"),
            .request_json = .fromSlice("{"),
        } },
        &invalid_query_response,
        &invalid_query_failure,
    );
    try std.testing.expectEqual(abi.Status.invalid_query, invalid_query_status);
    try std.testing.expectEqual(invalid_query_status, invalid_query_failure.status);
    try std.testing.expectEqual(abi.FailureBoundary.local_query, invalid_query_failure.boundary);
    try std.testing.expectEqual(abi.abi_version, invalid_query_failure.boundary_version);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.parse_internal_request),
        invalid_query_failure.operation,
    );
    try std.testing.expectEqualStrings("InvalidQueryRequest", invalid_query_failure.errorName());
    try std.testing.expect(invalid_query_failure.error_name_hash != 0);
    try std.testing.expectEqual(@as(u64, 0), invalid_query_response.buffer.len);

    var invalid_abi_response: abi.QueryOwnedResponse = .{};
    var invalid_abi_failure: abi.FailureIdentity = .{};
    var invalid_abi_request = abi.LocalQueryRequest{};
    invalid_abi_request.version = abi.abi_version - 1;
    const invalid_abi_status = abi.antfly_local_query_execute(
        &invalid_abi_request,
        &invalid_abi_response,
        &invalid_abi_failure,
    );
    try std.testing.expectEqual(abi.Status.invalid_abi, invalid_abi_status);
    try std.testing.expectEqual(invalid_abi_status, invalid_abi_failure.status);
    try std.testing.expectEqual(abi.FailureBoundary.local_query, invalid_abi_failure.boundary);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.validate_request),
        invalid_abi_failure.operation,
    );
    try std.testing.expectEqualStrings("InvalidAbiVersion", invalid_abi_failure.errorName());

    // Every operation family crossing the storage-owner boundary carries the
    // same complete envelope even when it remains in the storage unit.
    var operation_response: abi.OwnedBytes = .{};
    defer abi.antfly_storage_owner_buffer_destroy(&operation_response);
    var operation_failure: abi.FailureIdentity = .{};
    const invalid_algebraic_status = abi.antfly_storage_owner_algebraic_partials_json(
        owner.handle,
        &.{ .table_name = .fromSlice("docs"), .request_json = .fromSlice("{") },
        &operation_response,
        &operation_failure,
    );
    try std.testing.expect(invalid_algebraic_status != .ok);
    try std.testing.expectEqual(invalid_algebraic_status, operation_failure.status);
    try std.testing.expectEqual(abi.FailureBoundary.local_query, operation_failure.boundary);
    try std.testing.expectEqual(abi.abi_version, operation_failure.boundary_version);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.algebraic_partials),
        operation_failure.operation,
    );
    try std.testing.expect(operation_failure.error_name_hash != 0);

    const invalid_text_stats_status = abi.antfly_storage_owner_text_stats_json(
        owner.handle,
        &.{ .table_name = .fromSlice("docs"), .request_json = .fromSlice("{") },
        &operation_response,
        &operation_failure,
    );
    try std.testing.expect(invalid_text_stats_status != .ok);
    try std.testing.expectEqual(invalid_text_stats_status, operation_failure.status);
    try std.testing.expectEqual(abi.FailureBoundary.local_query, operation_failure.boundary);
    try std.testing.expectEqual(abi.abi_version, operation_failure.boundary_version);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.text_stats),
        operation_failure.operation,
    );
    try std.testing.expect(operation_failure.error_name_hash != 0);

    const invalid_preflight_status = abi.antfly_storage_owner_preflight_json(
        owner.handle,
        &.{ .table_name = .fromSlice("docs"), .request_json = .fromSlice("{") },
        &operation_response,
        &operation_failure,
    );
    try std.testing.expect(invalid_preflight_status != .ok);
    try std.testing.expectEqual(invalid_preflight_status, operation_failure.status);
    try std.testing.expectEqual(abi.FailureBoundary.local_query, operation_failure.boundary);
    try std.testing.expectEqual(abi.abi_version, operation_failure.boundary_version);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.preflight),
        operation_failure.operation,
    );
    try std.testing.expect(operation_failure.error_name_hash != 0);

    const invalid_graph_status = abi.antfly_storage_owner_graph_expand_json(
        owner.handle,
        &.{ .table_name = .fromSlice("docs"), .request_json = .fromSlice("{") },
        &operation_response,
        &operation_failure,
    );
    try std.testing.expect(invalid_graph_status != .ok);
    try std.testing.expectEqual(invalid_graph_status, operation_failure.status);
    try std.testing.expectEqual(abi.FailureBoundary.local_query, operation_failure.boundary);
    try std.testing.expectEqual(abi.abi_version, operation_failure.boundary_version);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.parse_graph_expand),
        operation_failure.operation,
    );
    try std.testing.expect(operation_failure.error_name_hash != 0);

    const invalid_aggregation_status = abi.antfly_storage_aggregate_json(
        &.{ .context_json = .fromSlice("{"), .aggregations_json = .fromSlice("[]") },
        &operation_response,
        &operation_failure,
    );
    try std.testing.expect(invalid_aggregation_status != .ok);
    try std.testing.expectEqual(abi.FailureBoundary.storage_owner, operation_failure.boundary);
    try std.testing.expectEqual(
        @intFromEnum(abi.LocalQueryOperation.parse_aggregation),
        operation_failure.operation,
    );

    var query_response = try owner.queryJson("docs", query_json);
    defer query_response.deinit();
    try std.testing.expect(query_response.identityReadGeneration() != null);
    try std.testing.expect(std.mem.indexOf(u8, query_response.bytes(), "docs") != null);
    try std.testing.expect(std.mem.indexOf(u8, query_response.bytes(), "doc:a") != null);
    try std.testing.expect(std.mem.indexOf(u8, query_response.bytes(), "doc:b") != null);
    try std.testing.expect(std.mem.indexOf(u8, query_response.bytes(), "doc:txn") != null);

    var reconciled = false;
    for (0..64) |_| {
        const result = try owner.reconcile(
            "docs",
            "",
            "{\"full_text_index_v1\":{\"type\":\"full_text\"}}",
            "full_text_index_v1",
            true,
        );
        try std.testing.expect(result.state != .degraded);
        if (result.state == .complete) {
            reconciled = true;
            break;
        }
    }
    try std.testing.expect(reconciled);
    var text_response = try owner.queryJson(
        "docs",
        "{\"full_text_search\":{\"match\":\"alpha\",\"field\":\"title\"},\"indexes\":[\"full_text_index_v1\"],\"limit\":10}",
    );
    defer text_response.deinit();
    try std.testing.expect(std.mem.indexOf(u8, text_response.bytes(), "doc:a") != null);
    try std.testing.expect(std.mem.indexOf(u8, text_response.bytes(), "doc:b") == null);

    try std.testing.expectError(
        error.InvalidArgument,
        owner.configure("articles", "", "{}"),
    );
    const replacement_indexes_json =
        \\{"dense_idx":{"type":"embeddings","external":true,"dimension":3},
        \\ "full_text_index_v0":{"type":"full_text","enrichments":[{"name":"document_units_v1","kind":"asset","field":"url","content_type":"application/json","producer_json":"{\"type\":\"document_extraction\",\"config\":{}}"}]}}
    ;
    try owner.configure("docs", "", replacement_indexes_json);
    var dense_reconciled = false;
    for (0..64) |_| {
        const result = try owner.reconcile(
            "docs",
            "",
            replacement_indexes_json,
            "dense_idx",
            true,
        );
        try std.testing.expect(result.state != .degraded);
        if (result.state == .complete) {
            dense_reconciled = true;
            break;
        }
    }
    try std.testing.expect(dense_reconciled);
    // A target reconcile cannot replace its sibling dense index, even when
    // the desired catalog carries a newer sibling definition.
    const sibling_changed = try std.mem.replaceOwned(u8, std.testing.allocator, replacement_indexes_json, "\"dimension\":3", "\"dimension\":4");
    defer std.testing.allocator.free(sibling_changed);
    _ = try owner.reconcile("docs", "", sibling_changed, "full_text_index_v0", false);

    var indexed_batch = try owner.batchJson(
        "docs",
        "{\"inserts\":{\"doc:artifact\":{\"title\":\"artifact\",\"url\":\"data:text/plain;base64,YWxwaGEgYmV0YQ==\"},\"doc:c\":{\"title\":\"gamma\",\"_embeddings\":{\"dense_idx\":[1,0,0]}},\"doc:d\":{\"title\":\"delta\",\"_embeddings\":{\"dense_idx\":[0,1,0]}}},\"sync_level\":\"full_index\"}",
    );
    defer indexed_batch.deinit();
    var text_memory = try owner.textMemoryJson("docs");
    defer text_memory.deinit();
    try std.testing.expect(std.mem.indexOf(u8, text_memory.bytes(), "\"text_indexes\":1") != null);
    var dense_response = try owner.queryJson(
        "docs",
        "{\"embeddings\":{\"dense_idx\":[1,0,0]},\"indexes\":[\"dense_idx\"],\"limit\":2}",
    );
    defer dense_response.deinit();
    const doc_c = std.mem.indexOf(u8, dense_response.bytes(), "doc:c") orelse return error.MissingDenseHit;
    const doc_d = std.mem.indexOf(u8, dense_response.bytes(), "doc:d") orelse return error.MissingDenseHit;
    try std.testing.expect(doc_c < doc_d);

    var reprocessed = try owner.artifactOperationJson(
        "docs",
        .reprocess_document,
        "{\"doc_key\":\"doc:artifact\",\"artifact_name\":\"document_units_v1\"}",
        null,
        null,
        false,
    );
    defer reprocessed.deinit();
    try std.testing.expect(std.mem.indexOf(u8, reprocessed.bytes(), "\"handled\":true") != null);

    var reprocessed_range = try owner.artifactOperationJson(
        "docs",
        .reprocess_document_range,
        "{\"artifact_name\":\"document_units_v1\",\"request\":{\"from_key\":\"doc:artifact\",\"to_key\":\"doc:b\",\"limit\":1}}",
        null,
        null,
        false,
    );
    defer reprocessed_range.deinit();
    try std.testing.expect(std.mem.indexOf(u8, reprocessed_range.bytes(), "\"reprocessed\":1") != null);

    var placement = try owner.artifactOperationJson(
        "docs",
        .update_child_range_placement,
        "{\"doc_key\":\"doc:artifact\",\"artifact_name\":\"document_units_v1\",\"update\":{\"range_id\":\"range:000000\",\"placement\":\"remote\",\"owner_group_id\":7002,\"placement_generation\":3,\"route_status\":\"remote_committed\",\"split_eligible\":true}}",
        null,
        null,
        false,
    );
    defer placement.deinit();
    try std.testing.expect(std.mem.indexOf(u8, placement.bytes(), "\"handled\":true") != null);

    const ChildRangeCapture = struct {
        calls: usize = 0,
        owner_group_id: u64 = 0,
        saw_document: bool = false,
        saw_artifact: bool = false,

        fn dispatch(
            ptr: ?*anyopaque,
            owner_group_id: u64,
            request_json: abi.BorrowedBytes,
        ) callconv(.c) abi.Status {
            const self: *@This() = @ptrCast(@alignCast(ptr orelse return .invalid_argument));
            self.calls += 1;
            self.owner_group_id = owner_group_id;
            self.saw_document = std.mem.indexOf(u8, request_json.slice(), "\"doc_key\":\"doc:artifact\"") != null;
            self.saw_artifact = std.mem.indexOf(u8, request_json.slice(), "\"artifact_name\":\"document_units_v1\"") != null;
            return .ok;
        }
    };
    var child_range_capture = ChildRangeCapture{};
    var routed_batch = try owner.batchJsonWithDocumentChildRangeDispatcher(
        "docs",
        "{\"inserts\":{\"doc:artifact\":{\"title\":\"artifact updated\",\"url\":\"data:text/plain;base64,YmV0YQ==\"}},\"sync_level\":\"full_index\"}",
        &child_range_capture,
        ChildRangeCapture.dispatch,
    );
    defer routed_batch.deinit();
    try std.testing.expectEqual(@as(usize, 1), child_range_capture.calls);
    try std.testing.expectEqual(@as(u64, 7002), child_range_capture.owner_group_id);
    try std.testing.expect(child_range_capture.saw_document);
    try std.testing.expect(child_range_capture.saw_artifact);

    var empty_child_batch = try owner.artifactOperationJson(
        "docs",
        .apply_child_range_batch,
        "{\"doc_key\":\"doc:artifact\",\"artifact_name\":\"document_units_v1\",\"batch\":{}}",
        null,
        null,
        false,
    );
    defer empty_child_batch.deinit();
    try std.testing.expect(std.mem.indexOf(u8, empty_child_batch.bytes(), "\"sequence\":0") != null);

    var corrupted = try owner.artifactOperationJson(
        "docs",
        .corrupt_embedding,
        "{\"doc_key\":\"doc:c\",\"index_name\":\"dense_idx\"}",
        null,
        null,
        false,
    );
    defer corrupted.deinit();
    var repair_issues = try owner.artifactOperationJson(
        "docs",
        .list_repair_issues,
        "{\"artifact_kind\":\"embedding\",\"limit\":10}",
        null,
        null,
        false,
    );
    defer repair_issues.deinit();
    try std.testing.expect(std.mem.indexOf(u8, repair_issues.bytes(), "\"issues\"") != null);

    const Cancel = struct {
        fn requested(_: ?*anyopaque) callconv(.c) u8 {
            return 1;
        }
    };
    try std.testing.expectError(error.Canceled, owner.artifactOperationJson(
        "docs",
        .repair_issues,
        "{\"artifact_kind\":\"embedding\",\"limit\":10}",
        null,
        Cancel.requested,
        false,
    ));
    var repaired = try owner.artifactOperationJson(
        "docs",
        .repair_issues,
        "{\"artifact_kind\":\"embedding\",\"limit\":10}",
        null,
        null,
        false,
    );
    defer repaired.deinit();
    try std.testing.expect(std.mem.indexOf(u8, repaired.bytes(), "\"scanned\":0") != null);

    try std.testing.expectError(error.InvalidArgument, owner.beginBulkIngest("articles"));
    try owner.beginBulkIngest("docs");
    var bulk_batch = try owner.batchJson(
        "docs",
        "{\"inserts\":{\"doc:bulk\":{\"title\":\"bulk\"}},\"sync_level\":\"write\"}",
    );
    defer bulk_batch.deinit();
    try owner.finishBulkIngest(&.{
        .compact = 0,
        .table_name = .fromSlice("docs"),
    });
    var bulk_query = try owner.queryJson(
        "docs",
        "{\"query\":{\"match_all\":{}},\"limit\":10}",
    );
    defer bulk_query.deinit();
    try std.testing.expect(std.mem.indexOf(u8, bulk_query.bytes(), "doc:bulk") != null);

    try owner.beginBulkIngest("docs");
    try owner.abortBulkIngest("docs");
    var post_abort_batch = try owner.batchJson(
        "docs",
        "{\"inserts\":{\"doc:after-abort\":{\"title\":\"ordinary\"}},\"sync_level\":\"full_index\"}",
    );
    defer post_abort_batch.deinit();

    var portable_backup = try owner.backupJson(
        "docs",
        backup_root,
        "portable-owner",
        .portable,
    );
    defer portable_backup.deinit();
    try std.testing.expect(std.mem.indexOf(u8, portable_backup.bytes(), "\"group_id\":7001") != null);
    try std.testing.expect(std.mem.indexOf(u8, portable_backup.bytes(), "portable-owner/groups/7001.afb") != null);
    try std.testing.expect(std.mem.indexOf(u8, portable_backup.bytes(), "\"artifact_sha256\"") != null);

    var native_backup = try owner.backupJson(
        "docs",
        backup_root,
        "native-owner",
        .native,
    );
    defer native_backup.deinit();
    try std.testing.expect(std.mem.indexOf(u8, native_backup.bytes(), "\"group_id\":7001") != null);
    try std.testing.expect(std.mem.indexOf(u8, native_backup.bytes(), "native-owner") != null);
    try std.testing.expectError(
        error.InvalidArgument,
        owner.backupJson("articles", backup_root, "wrong-table", .native),
    );
}

test "opaque storage owner validates ABI and destruction is idempotent" {
    var invalid_context: ?*anyopaque = undefined;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_context_create(&.{ .version = abi.abi_version + 1 }, &invalid_context),
    );
    try std.testing.expect(invalid_context == null);
    try std.testing.expectEqual(abi.Status.ok, abi.antfly_storage_context_destroy(null));

    var owner: ?*anyopaque = undefined;
    var invalid: abi.OpenRequest = .{};
    invalid.version = abi.abi_version + 1;
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_open(&invalid, &owner));
    try std.testing.expect(owner == null);

    var invalid_configure: abi.ConfigureRequest = .{};
    invalid_configure.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_configure(null, &invalid_configure),
    );

    var invalid_reconcile: abi.ReconcileRequest = .{};
    invalid_reconcile.version = abi.abi_version + 1;
    var reconcile_result: abi.ReconcileResult = .{};
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_reconcile(null, &invalid_reconcile, &reconcile_result),
    );
    try std.testing.expectEqual(abi.abi_version, reconcile_result.version);

    var invalid_table: abi.TableRequest = .{};
    invalid_table.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_bulk_begin(null, &invalid_table),
    );
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_bulk_abort(null, &invalid_table),
    );
    var invalid_txn_status: abi.TransactionStatusRequest = .{};
    invalid_txn_status.version = abi.abi_version + 1;
    var txn_status_result: abi.TransactionStatusResult = .{};
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_transaction_status(null, &invalid_txn_status, &txn_status_result),
    );
    try std.testing.expectEqual(abi.abi_version, txn_status_result.version);
    var invalid_bulk_finish: abi.BulkFinishRequest = .{};
    invalid_bulk_finish.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_bulk_finish(null, &invalid_bulk_finish),
    );
    var invalid_maintenance: abi.MaintenanceRequest = .{};
    invalid_maintenance.version = abi.abi_version + 1;
    var maintenance_result: abi.MaintenanceResult = .{};
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_maintenance(null, &invalid_maintenance, &maintenance_result),
    );
    try std.testing.expectEqual(abi.abi_version, maintenance_result.version);
    try std.testing.expectEqual(
        abi.Status.invalid_argument,
        abi.antfly_storage_owner_maintenance(null, &.{}, &maintenance_result),
    );

    var response: abi.OwnedBytes = .{};
    var invalid_batch_operation: abi.BatchJsonOperationRequest = .{
        .table_name = .fromSlice("docs"),
        .request_json = .fromSlice("{}"),
    };
    invalid_batch_operation.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_batch_json(null, &invalid_batch_operation, &response),
    );
    try std.testing.expectEqual(@as(u64, 0), response.len);
    var invalid_operation: abi.JsonOperationRequest = .{
        .table_name = .fromSlice("docs"),
        .request_json = .fromSlice("{}"),
    };
    invalid_operation.version = abi.abi_version + 1;
    var invalid_sync: abi.SyncRequest = .{};
    invalid_sync.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_wait_for_sync(null, &invalid_sync),
    );
    var invalid_ha: abi.HAReplicationRecordRequest = .{};
    invalid_ha.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_apply_ha_replication_record(null, &invalid_ha),
    );
    var invalid_backup: abi.BackupRequest = .{};
    invalid_backup.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_backup_json(null, &invalid_backup, &response),
    );
    try std.testing.expectEqual(@as(u64, 0), response.len);
    const invalid_backup_format = abi.BackupRequest{ .format = std.math.maxInt(u32) };
    try std.testing.expectEqual(
        abi.Status.invalid_argument,
        abi.antfly_storage_owner_backup_json(null, &invalid_backup_format, &response),
    );
    try std.testing.expectEqual(@as(u64, 0), response.len);
    var snapshot: ?*anyopaque = undefined;
    var invalid_snapshot: abi.SnapshotPrepareRequest = .{};
    invalid_snapshot.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_snapshot_prepare(&invalid_snapshot, &snapshot),
    );
    try std.testing.expect(snapshot == null);
    var invalid_restore: abi.RestorePrepareRequest = .{};
    invalid_restore.version = abi.abi_version + 1;
    var restore_result: abi.RestorePrepareResult = .{ .snapshot = @ptrFromInt(1) };
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_restore_prepare(&invalid_restore, &restore_result),
    );
    try std.testing.expectEqual(abi.abi_version, restore_result.version);
    try std.testing.expect(restore_result.snapshot == null);
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_restore_reconcile(&invalid_restore),
    );
    var invalid_restore_bootstrap: abi.RestoreBootstrapRequest = .{};
    invalid_restore_bootstrap.version = abi.abi_version + 1;
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_restore_apply_bootstrap(&invalid_restore_bootstrap),
    );
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_restore_repair(null, &invalid_restore),
    );
    try std.testing.expectEqual(abi.Status.invalid_argument, abi.antfly_storage_snapshot_promote(null));
    var snapshot_publish_result: abi.SnapshotPublishResult = .{ .durability_uncertain = 1 };
    try std.testing.expectEqual(
        abi.Status.invalid_argument,
        abi.antfly_storage_snapshot_publish_prepared(null, &snapshot_publish_result),
    );
    try std.testing.expectEqual(@as(u8, 0), snapshot_publish_result.durability_uncertain);
    try std.testing.expectEqual(abi.Status.invalid_argument, abi.antfly_storage_snapshot_commit(null));
    try std.testing.expectEqual(abi.Status.invalid_argument, abi.antfly_storage_snapshot_rollback(null));
    abi.antfly_storage_snapshot_destroy(null);
    abi.antfly_storage_snapshot_destroy(null);
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_replicated_batch_json(null, &invalid_operation, &response),
    );
    try std.testing.expectEqual(@as(u64, 0), response.len);
    var query_failure: abi.FailureIdentity = .{};
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_preflight_json(null, &invalid_operation, &response, &query_failure),
    );
    try std.testing.expectEqual(abi.FailureBoundary.storage_owner, query_failure.boundary);
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_text_stats_json(null, &invalid_operation, &response, &query_failure),
    );
    try std.testing.expectEqual(abi.FailureBoundary.storage_owner, query_failure.boundary);
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_algebraic_partials_json(null, &invalid_operation, &response, &query_failure),
    );
    try std.testing.expectEqual(@as(u64, 0), response.len);
    const invalid_controlled = abi.ControlledJsonOperationRequest{ .version = abi.abi_version + 1 };
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_graph_expand_json(null, &invalid_controlled, &response, &query_failure));
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_graph_hydrate_json(null, &invalid_controlled, &response, &query_failure));
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_graph_edges_json(null, &invalid_controlled, &response, &query_failure));
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_document_artifact_manifest_json(null, &invalid_operation, &response));
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_document_artifact_manifests_json(null, &invalid_operation, &response));
    try std.testing.expectEqual(@as(u64, 0), response.len);
    const invalid_artifact_operation = abi.ArtifactOperationRequest{ .version = abi.abi_version + 1 };
    try std.testing.expectEqual(
        abi.Status.invalid_abi,
        abi.antfly_storage_owner_artifact_operation_json(null, &invalid_artifact_operation, &response),
    );
    try std.testing.expectEqual(@as(u64, 0), response.len);
    const invalid_artifact_tag = abi.ArtifactOperationRequest{ .operation = std.math.maxInt(u32) };
    try std.testing.expectEqual(
        abi.Status.invalid_argument,
        abi.antfly_storage_owner_artifact_operation_json(null, &invalid_artifact_tag, &response),
    );
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_runtime_status_json(null, &invalid_operation, &response));
    try std.testing.expectEqual(@as(u64, 0), response.len);
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_storage_owner_restore_state_json(null, &invalid_operation, &response));
    try std.testing.expectEqual(@as(u64, 0), response.len);

    try std.testing.expectError(error.InvalidQueryRequest, client.statusToError(.invalid_query));
    try std.testing.expectError(error.UnsupportedQueryRequest, client.statusToError(.unsupported_query));
    try std.testing.expectError(error.IndexNotFound, client.statusToError(.index_not_found));
    try std.testing.expectError(error.IdentityReadGenerationChanged, client.statusToError(.identity_read_generation_changed));
    try std.testing.expectError(error.Timeout, client.statusToError(.timeout));
    try std.testing.expectError(error.Cancelled, client.statusToError(.cancelled));
    try std.testing.expectError(
        error.DenseRepairBackpressure,
        client.statusToError(.dense_repair_backpressure),
    );

    var empty: abi.OwnedBytes = .{};
    abi.antfly_storage_owner_buffer_destroy(&empty);
    abi.antfly_storage_owner_buffer_destroy(&empty);
}

test "opaque storage owner transaction recovery crosses callback ABI" {
    const path = "/tmp/antfly-storage-kernel-owner-transaction-recovery";
    cleanup(path);
    defer cleanup(path);

    const Capture = struct {
        calls: std.atomic.Value(u32) = .init(0),

        fn resolve(
            ptr: ?*anyopaque,
            txn_id: *const abi.TxnId,
            participant: abi.BorrowedBytes,
            status: abi.TxnStatus,
            commit_version: u64,
        ) callconv(.c) abi.Status {
            const self: *@This() = @ptrCast(@alignCast(ptr orelse return .invalid_argument));
            const expected_txn_id: [16]u8 = @splat(0x2a);
            if (!std.mem.eql(u8, &txn_id.bytes, &expected_txn_id) or
                !std.mem.eql(u8, participant.slice(), "table2:00000006:remote:9002") or
                status != .committed or commit_version != 102)
                return .invalid_argument;
            _ = self.calls.fetchAdd(1, .release);
            return .ok;
        }
    };
    var capture = Capture{};
    var owner = try client.Owner.open(.{
        .path = .fromSlice(path),
        .table_name = .fromSlice("docs"),
        .group_id = 9001,
        .transaction_recovery = .{
            .enabled = 1,
            .lease_owned = 1,
            .interval_ms = 10,
            .cutoff_ns = 1,
            .callback_ctx = &capture,
            .owner_id = .fromSlice("owner-test"),
            .resolve_participant_fn = Capture.resolve,
        },
    });
    defer owner.deinit();

    const txn_id: [16]u8 = @splat(0x2a);
    var begin = try owner.replicatedBatchJson(
        "docs",
        "{\"_transaction\":{\"phase\":\"begin\",\"txn_id\":\"2a2a2a2a2a2a2a2a2a2a2a2a2a2a2a2a\",\"begin_timestamp\":\"100\",\"created_at_ns\":\"1\",\"topology_epoch\":\"0\",\"participants\":[\"table2:00000004:docs:9001\",\"table2:00000006:remote:9002\"]},\"sync_level\":\"write\"}",
    );
    begin.deinit();
    var resolve = try owner.replicatedBatchJson(
        "docs",
        "{\"_transaction\":{\"phase\":\"resolve\",\"txn_id\":\"2a2a2a2a2a2a2a2a2a2a2a2a2a2a2a2a\",\"status\":\"committed\",\"commit_version\":\"102\"},\"sync_level\":\"write\"}",
    );
    resolve.deinit();

    var io_impl = std.Io.Threaded.init(std.heap.page_allocator, .{});
    defer io_impl.deinit();
    for (0..500) |_| {
        if (capture.calls.load(.acquire) > 0) break;
        try io_impl.io().sleep(.fromMilliseconds(2), .awake);
    }
    try std.testing.expectEqual(@as(u32, 1), capture.calls.load(.acquire));

    var cleaned = false;
    for (0..500) |_| {
        _ = owner.transactionStatus("docs", txn_id) catch |err| {
            if (err == error.TxnNotFound) {
                cleaned = true;
                break;
            }
            return err;
        };
        try io_impl.io().sleep(.fromMilliseconds(2), .awake);
    }
    try std.testing.expect(cleaned);
}

test "opaque storage context enforces owner lifetime and shares process storage state" {
    const first_path = "/tmp/antfly-storage-kernel-context-first";
    const second_path = "/tmp/antfly-storage-kernel-context-second";
    cleanup(first_path);
    cleanup(second_path);
    defer cleanup(first_path);
    defer cleanup(second_path);

    var context: ?*anyopaque = null;
    try std.testing.expectEqual(abi.Status.ok, abi.antfly_storage_context_create(&.{}, &context));
    try std.testing.expect(context != null);
    var metrics: abi.ContextMetricsResult = undefined;
    try std.testing.expectEqual(abi.Status.invalid_argument, abi.antfly_storage_context_metrics(null, &metrics));
    try std.testing.expectEqual(abi.Status.ok, abi.antfly_storage_context_metrics(context, &metrics));
    try std.testing.expectEqual(abi.abi_version, metrics.version);
    try std.testing.expectEqual(@as(u64, 0), metrics.lsm_cache_entry_count);
    try std.testing.expectEqual(
        abi.Status.invalid_argument,
        abi.antfly_storage_context_attach_inference_provider(context, null),
    );
    const fake_inference_handle: *anyopaque = @ptrFromInt(@alignOf(usize));
    try std.testing.expectEqual(
        abi.Status.ok,
        abi.antfly_storage_context_attach_inference_provider(context, fake_inference_handle),
    );

    var first = try client.Owner.open(.{
        .context = context,
        .path = .fromSlice(first_path),
        .table_name = .fromSlice("first"),
    });
    var second = try client.Owner.open(.{
        .context = context,
        .path = .fromSlice(second_path),
        .table_name = .fromSlice("second"),
    });
    try std.testing.expectEqual(
        abi.Status.busy,
        abi.antfly_storage_context_attach_inference_provider(context, fake_inference_handle),
    );
    try std.testing.expectEqual(abi.Status.busy, abi.antfly_storage_context_destroy(context));

    first.deinit();
    try std.testing.expectEqual(abi.Status.busy, abi.antfly_storage_context_destroy(context));
    second.deinit();
    try std.testing.expectEqual(abi.Status.ok, abi.antfly_storage_context_destroy(context));
}

test "opaque data raft apply owner preserves batch snapshot and placement lifecycle" {
    const source_root = "/tmp/antfly-storage-kernel-data-apply-source";
    const restored_root = "/tmp/antfly-storage-kernel-data-apply-restored";
    const authoritative_root = "/tmp/antfly-storage-kernel-data-apply-authoritative";
    cleanup(source_root);
    cleanup(restored_root);
    cleanup(authoritative_root);
    defer cleanup(source_root);
    defer cleanup(restored_root);
    defer cleanup(authoritative_root);

    var context = client.Context{};
    try context.ensure();
    defer context.deinit();

    var invalid_store: ?*anyopaque = @ptrFromInt(1);
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_data_apply_store_open(&.{
        .version = abi.abi_version + 1,
        .root_dir = .fromSlice(source_root),
    }, &invalid_store));
    try std.testing.expect(invalid_store == null);

    var source = try data_apply_client.RaftApplyStore.init(std.testing.allocator, .{
        .root_dir = source_root,
        .context = context.handle,
    });
    defer source.deinit();

    const payload = "opaque-data-apply";
    var encoded: [4 + 8 + 8 + 1 + 4 + payload.len]u8 = undefined;
    var pos: usize = 0;
    std.mem.writeInt(u32, encoded[pos..][0..4], 1, .little);
    pos += 4;
    std.mem.writeInt(u64, encoded[pos..][0..8], 4, .little);
    pos += 8;
    std.mem.writeInt(u64, encoded[pos..][0..8], 9, .little);
    pos += 8;
    encoded[pos] = 0;
    pos += 1;
    std.mem.writeInt(u32, encoded[pos..][0..4], payload.len, .little);
    pos += 4;
    @memcpy(encoded[pos..], payload);
    try source.applyBatch(81, 9, &encoded);
    const latest = (try source.latestBatch(81)) orelse return error.TestExpectedEqual;
    try std.testing.expectEqual(@as(u64, 9), latest.commit_index);
    try std.testing.expectEqual(@as(u64, 9), latest.last_entry_index);
    try std.testing.expectEqual(@as(usize, 1), latest.normal_entry_count);
    const transition_latest = (try source.latestBatchForTransition(81)) orelse return error.TestExpectedEqual;
    try std.testing.expectEqual(latest, transition_latest);
    var empty_observation = try source.observeSplitControl(std.testing.allocator, 81);
    defer empty_observation.deinit(std.testing.allocator);
    try std.testing.expect(empty_observation.state == null);
    try std.testing.expectEqual(@as(u64, 0), empty_observation.delta_sequence);

    var authoritative = try client.Owner.open(.{
        .context = context.handle,
        .path = .fromSlice(authoritative_root),
        .table_name = .fromSlice("docs"),
        .group_id = 81,
    });
    defer authoritative.deinit();
    var authoritative_batch = try authoritative.batchJson(
        "docs",
        "{\"inserts\":{\"doc:a\":{\"title\":\"alpha\"},\"doc:b\":{\"title\":\"beta\"}},\"sync_level\":\"write\"}",
    );
    authoritative_batch.deinit();
    var reconcile = try source.reconcileAuthoritativeOwner(
        std.testing.allocator,
        authoritative.handle,
        81,
        latest,
        false,
        1,
        1024 * 1024,
    );
    defer reconcile.deinit(std.testing.allocator);
    try std.testing.expect(reconcile == .reconciled);
    var projection_range = try source.currentRange(std.testing.allocator, 81);
    defer projection_range.deinit(std.testing.allocator);
    var projection_page = try source.groupStatePageInRange(
        std.testing.allocator,
        81,
        .{ .start = projection_range.start, .end = projection_range.end },
        null,
        8,
        1024 * 1024,
    );
    defer projection_page.deinit(std.testing.allocator);
    try std.testing.expect(projection_page.entries.len > 0);
    var invalid_projection: abi.OwnedBytes = .{};
    try std.testing.expectEqual(abi.Status.invalid_abi, abi.antfly_data_apply_store_projection(
        source.handle,
        &.{ .version = abi.abi_version + 1 },
        &invalid_projection,
    ));
    try std.testing.expectEqual(@as(u64, 0), invalid_projection.len);

    var placement = try source.beginActiveGroupTransition(&.{81});
    placement.commit();
    placement.deinit();
    try source.retainActiveGroups(&.{81});
    try std.testing.expectEqual(abi.Status.invalid_argument, abi.antfly_data_apply_store_retain_groups(
        source.handle,
        &.{ .group_count = 1 },
    ));
    var aborted = try source.beginActiveGroupTransition(&.{ 81, 82 });
    aborted.abort();
    aborted.deinit();

    var prepared = (try source.prepareSnapshot(81, 9)) orelse return error.TestExpectedEqual;
    defer prepared.deinit();
    var materialized = try prepared.materializeFile(std.testing.allocator);
    defer materialized.deinit(std.testing.allocator);
    const materialized_bytes = try std.Io.Dir.cwd().readFileAlloc(
        std.Options.debug_io,
        materialized.path,
        std.testing.allocator,
        .limited(materialized.size + 1),
    );
    defer std.testing.allocator.free(materialized_bytes);
    try std.testing.expectEqual(materialized.size, @as(u64, @intCast(materialized_bytes.len)));

    var cancelled = (try source.prepareSnapshot(81, 9)) orelse return error.TestExpectedEqual;
    defer cancelled.deinit();
    cancelled.cancel();
    try std.testing.expectError(error.SnapshotBuildCancelled, cancelled.materializeFile(std.testing.allocator));

    const snapshot = try source.buildSnapshot(std.testing.allocator, 81);
    defer std.testing.allocator.free(snapshot);
    try std.testing.expect(snapshot.len > 0);

    var restored = try data_apply_client.RaftApplyStore.init(std.testing.allocator, .{
        .root_dir = restored_root,
        .context = context.handle,
    });
    defer restored.deinit();
    try restored.installSnapshot(81, 9, snapshot);
    const restored_latest = (try restored.latestBatch(81)) orelse return error.TestExpectedEqual;
    try std.testing.expectEqual(latest.commit_index, restored_latest.commit_index);
    try std.testing.expectEqual(latest.last_entry_index, restored_latest.last_entry_index);

    try std.testing.expectEqual(abi.Status.busy, abi.antfly_storage_context_destroy(context.handle));
}

test "opaque metadata apply owner preserves semantic error identity" {
    const path = "/tmp/antfly-storage-kernel-metadata-errors";
    cleanup(path);
    defer cleanup(path);

    var store = try metadata_apply_client.RaftApplyStore.init(std.testing.allocator, .{
        .root_dir = path,
        .no_sync = true,
    });
    defer store.deinit();
    const snapshots = store.snapshotBuilder();

    var catalog_json: abi.OwnedBytes = .{};
    defer abi.antfly_storage_owner_buffer_destroy(&catalog_json);
    try std.testing.expectEqual(
        abi.Status.ok,
        abi.antfly_metadata_apply_store_projection(
            store.handle,
            &.{ .kind = .catalog_projection, .group_id = 91 },
            &catalog_json,
        ),
    );
    try std.testing.expect(std.mem.indexOf(u8, catalog_json.slice(), "\"tables\":[]") != null);
    try std.testing.expect(std.mem.indexOf(u8, catalog_json.slice(), "\"ranges\":[]") != null);
    var timeout_json: abi.OwnedBytes = .{};
    defer abi.antfly_storage_owner_buffer_destroy(&timeout_json);
    try std.testing.expectEqual(
        abi.Status.timeout,
        abi.antfly_metadata_apply_store_projection(
            store.handle,
            &.{ .kind = .catalog_projection, .group_id = 91, .arg0 = 1 },
            &timeout_json,
        ),
    );

    try std.testing.expectError(
        error.InvalidMetadataSnapshot,
        snapshots.installSnapshot(std.testing.allocator, 91, 7, "not-a-metadata-snapshot"),
    );

    // The concrete store deliberately preserves the committed watermark even
    // when a batch has no projectable metadata commands. That gives this
    // cross-archive test a real provider-side index mismatch to round-trip.
    try snapshots.applyBatch(.{
        .group_id = 91,
        .commit_index = 7,
        .entries_bytes = "not-projectable-entries",
    });
    const latest = (try store.latestBatch(91)) orelse return error.TestExpectedEqual;
    try std.testing.expectEqual(@as(u64, 7), latest.commit_index);
    try std.testing.expectEqualStrings("not-projectable-entries", latest.entries_bytes);
    try std.testing.expectError(
        error.AppliedSnapshotIndexMismatch,
        snapshots.prepareSnapshot(91, 8),
    );
}

test "opaque metadata listener boundary preserves incarnation commit ordering" {
    const path = "/tmp/antfly-storage-kernel-metadata-listener-ordering";
    cleanup(path);
    defer cleanup(path);

    var store = try metadata_apply_client.RaftApplyStore.init(std.testing.allocator, .{
        .root_dir = path,
        .no_sync = true,
    });
    defer store.deinit();

    const Capture = struct {
        barrier_active: bool = false,
        ordering_violation: bool = false,
        began: usize = 0,
        ended: usize = 0,
        signals: usize = 0,
        last_kind: ?metadata_apply_client.ProjectionSignalKind = null,
        last_group_id: u64 = 0,

        fn begin(ptr: *anyopaque) void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            if (self.barrier_active) self.ordering_violation = true;
            self.barrier_active = true;
            self.began += 1;
        }

        fn end(ptr: *anyopaque) void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            if (!self.barrier_active) self.ordering_violation = true;
            self.barrier_active = false;
            self.ended += 1;
        }

        fn matchesKey(_: *anyopaque, _: metadata_apply_client.CommittedKeySignal) bool {
            return false;
        }
        fn onKey(_: *anyopaque, _: metadata_apply_client.CommittedKeySignal) void {}

        fn onProjection(ptr: *anyopaque, signal: metadata_apply_client.ProjectionSignal) void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            if (!self.barrier_active) self.ordering_violation = true;
            self.signals += 1;
            self.last_kind = signal.kind;
            self.last_group_id = signal.metadata_group_id;
        }
    };
    const RawCallbacks = struct {
        fn projection(_: ?*anyopaque, _: *const abi.MetadataProjectionSignal) callconv(.c) void {}
        fn barrier(_: ?*anyopaque) callconv(.c) void {}
    };

    // ABI booleans are canonical 0/1 values. Rejecting other bit patterns
    // keeps foreign callers from accidentally selecting a different contract
    // than the storage owner installed.
    var registration_id: u64 = 0;
    try std.testing.expectEqual(abi.Status.invalid_argument, abi.antfly_metadata_apply_store_add_listeners(
        store.handle,
        &.{
            .projection_fn = RawCallbacks.projection,
            .has_commit_barrier_kind = 2,
            .before_projection_commit_fn = RawCallbacks.barrier,
            .after_projection_commit_fn = RawCallbacks.barrier,
        },
        &registration_id,
    ));

    var capture = Capture{};
    try std.testing.expectError(error.InvalidProjectionCommitBarrier, store.addProjectionListener(.{
        .ptr = &capture,
        .commit_barrier_kind = .metadata_incarnation,
        .vtable = &.{ .on_projection_signal = Capture.onProjection },
    }));
    const registration = try store.addLifecycleListeners(.{
        .ptr = &capture,
        .commit_barrier_kind = .metadata_incarnation,
        .vtable = &.{
            .on_projection_signal = Capture.onProjection,
            .before_projection_commit = Capture.begin,
            .after_projection_commit = Capture.end,
        },
    }, .{ .ptr = &capture, .vtable = &.{ .matches_key = Capture.matchesKey, .on_committed_key = Capture.onKey } });

    // Keep this fixture at the actual compiled-owner wire. The transition is
    // `initialize_metadata_incarnation` (tag 45), wrapped in one normal Raft
    // entry using the stable committed-entry envelope.
    const transition = "afmd1\x2d0123456789abcdef0123456789abcdef";
    var encoded: [4 + 8 + 8 + 1 + 4 + transition.len]u8 = undefined;
    var pos: usize = 0;
    std.mem.writeInt(u32, encoded[pos..][0..4], 1, .little);
    pos += 4;
    std.mem.writeInt(u64, encoded[pos..][0..8], 1, .little);
    pos += 8;
    std.mem.writeInt(u64, encoded[pos..][0..8], 1, .little);
    pos += 8;
    encoded[pos] = 0;
    pos += 1;
    std.mem.writeInt(u32, encoded[pos..][0..4], transition.len, .little);
    pos += 4;
    @memcpy(encoded[pos..], transition);

    try store.snapshotBuilder().applyBatch(.{
        .group_id = 91,
        .commit_index = 1,
        .entries_bytes = &encoded,
    });
    try std.testing.expectEqual(@as(usize, 1), capture.began);
    try std.testing.expectEqual(@as(usize, 1), capture.signals);
    try std.testing.expectEqual(@as(usize, 1), capture.ended);
    try std.testing.expectEqual(metadata_apply_client.ProjectionSignalKind.metadata_incarnation, capture.last_kind.?);
    try std.testing.expectEqual(@as(u64, 91), capture.last_group_id);
    try std.testing.expect(!capture.barrier_active);
    try std.testing.expect(!capture.ordering_violation);
    try std.testing.expect(store.removeLifecycleListeners(registration));
    try std.testing.expect(!store.removeLifecycleListeners(registration));
    // A different metadata group initializes the same incarnation after detach.
    // No callback may retain the caller-owned capture after removal returns.
    try store.snapshotBuilder().applyBatch(.{ .group_id = 92, .commit_index = 1, .entries_bytes = &encoded });
    try std.testing.expectEqual(@as(usize, 1), capture.signals);
    try std.testing.expectEqual(@as(usize, 1), capture.began);
    try std.testing.expectEqual(@as(usize, 1), capture.ended);
}

test "storage kernel status registry is unique and lossless" {
    try @import("kernel_error_identity").validateForTest();
}

test "failed owner configuration releases its writer and context lease" {
    const path = "/tmp/antfly-storage-owner-failed-configuration";
    cleanup(path);
    defer cleanup(path);
    var context = client.Context{};
    try context.ensure();
    defer context.deinit();
    var owner: ?*anyopaque = null;
    const failed = abi.antfly_storage_owner_open(&.{
        .context = context.handle,
        .path = .fromSlice(path),
        .table_name = .fromSlice("docs"),
        .indexes_json = .fromSlice("{"),
    }, &owner);
    try std.testing.expect(failed != .ok);
    try std.testing.expect(owner == null);
    var reopened = try client.Owner.open(.{
        .context = context.handle,
        .path = .fromSlice(path),
        .table_name = .fromSlice("docs"),
    });
    reopened.deinit();
    try std.testing.expectEqual(abi.Status.ok, abi.antfly_storage_context_destroy(context.handle));
    context.handle = null;
}

test "status text preserves its string wire representation and rejects oversized input" {
    const Text = @import("db/types.zig").InlineStatusText(4);
    const alloc = std.testing.allocator;
    const json = try std.json.Stringify.valueAlloc(alloc, Text.init("test"), .{});
    defer alloc.free(json);
    var decoded = try std.json.parseFromSlice(Text, alloc, json, .{});
    defer decoded.deinit();
    try std.testing.expectEqualStrings("test", decoded.value.slice());
    try std.testing.expectError(error.Overflow, std.json.parseFromSlice(Text, alloc, "\"large\"", .{}));
}

test "interactive admission state is shared with the physical owner" {
    const activity = @import("db/enrichment/enrichment_types.zig");
    const before = abi.antfly_storage_interactive_activity(0, 0);
    _ = activity.interactive_embed_inflight.fetchAdd(1, .monotonic);
    defer _ = activity.interactive_embed_inflight.fetchSub(1, .monotonic);
    try std.testing.expectEqual(before + 1, abi.antfly_storage_interactive_activity(0, 0));
    const generating = abi.antfly_storage_interactive_activity(1, 0);
    _ = activity.interactive_generate_inflight.fetchAdd(1, .monotonic);
    defer _ = activity.interactive_generate_inflight.fetchSub(1, .monotonic);
    try std.testing.expectEqual(generating + 1, abi.antfly_storage_interactive_activity(1, 0));
}

test "storage query wire preserves empty projection and decoded sort profile lifetime" {
    const alloc = std.testing.allocator;
    const contract = @import("../api/local_query_contract.zig");
    const query = @import("../api/query_contract.zig");
    const types = @import("db/types.zig");
    const wire = try contract.encodeStorageKernelQueryRequest(alloc, .{
        .include_all_fields = false,
        .include_stored = false,
    });
    defer alloc.free(wire);
    var request = try query.parseQueryRequest(alloc, null, "docs", wire);
    defer request.deinit(alloc);
    try std.testing.expect(!request.req.include_all_fields);
    try std.testing.expect(!request.req.include_stored);
    try std.testing.expectEqual(@as(usize, 0), request.req.fields.len);

    const native: types.SearchResult = .{
        .alloc = alloc,
        .hits = &.{},
        .total_hits = 0,
        .sort_profile = .{
            .plan = "ordered_scan",
            .source = "native_doc_values",
            .candidate_source = "primary_key",
            .candidate_count = 13,
            .require_native = true,
            .sort_rejection_field = .init("nested.created_at"),
        },
    };
    var decoded = blk: {
        var encoded = try query.encodeQueryResponses(alloc, "docs", .{ .profile = true }, .{}, native);
        defer encoded.deinit(alloc);
        break :blk try contract.parseStorageKernelSearchResult(alloc, encoded.json);
    };
    defer decoded.deinit();
    const profile = decoded.sort_profile orelse return error.TestUnexpectedResult;
    try std.testing.expectEqualStrings("ordered_scan", profile.plan);
    try std.testing.expectEqualStrings("native_doc_values", profile.source);
    try std.testing.expectEqualStrings("primary_key", profile.candidate_source);
    try std.testing.expectEqual(@as(u64, 13), profile.candidate_count);
    try std.testing.expect(profile.require_native);
    try std.testing.expectEqualStrings("nested.created_at", profile.sort_rejection_field.slice());
    // Replacing a decoded profile frees the previous string block, including
    // when the replacement's source strings alias the current owned block.
    try decoded.setOwnedSortProfile(profile);
    try std.testing.expectEqualStrings("ordered_scan", decoded.sort_profile.?.plan);
    var allocation_wire = try query.encodeQueryResponses(alloc, "docs", .{ .profile = true }, .{}, native);
    defer allocation_wire.deinit(alloc);
    try std.testing.checkAllAllocationFailures(alloc, struct {
        fn decode(failing: std.mem.Allocator, bytes: []const u8) !void {
            var result = try @import("../api/local_query_contract.zig").parseStorageKernelSearchResult(failing, bytes);
            defer result.deinit();
            try result.setOwnedSortProfile(result.sort_profile.?);
        }
    }.decode, .{allocation_wire.json});
}

test "storage query contract preserves each vector candidate budget" {
    const alloc = std.testing.allocator;
    const contract = @import("../api/local_query_contract.zig");
    const query = @import("../api/query_contract.zig");
    const controls = @import("local_query_controls.zig");
    const wire = try contract.encodeStorageKernelQueryRequest(alloc, .{
        .limit = 100,
        .dense_queries = &.{
            .{ .name = "a", .index_name = "a", .query = .{ .vector = &.{ 1, 0 }, .k = 3 } },
            .{ .name = "b", .index_name = "b", .query = .{ .vector = &.{ 0, 1 }, .k = 9 } },
        },
        .sparse_queries = &.{
            .{ .name = "s", .index_name = "s", .query = .{ .indices = &.{1}, .values = &.{1}, .k = 0 } },
        },
    });
    defer alloc.free(wire);
    var parsed = try query.parseQueryRequest(alloc, null, "docs", wire);
    defer parsed.deinit(alloc);
    controls.applyExecutionOptions(&parsed.req, .{ .enabled = 1 });
    try std.testing.expectEqual(@as(u32, 3), parsed.req.dense_queries[0].query.k);
    try std.testing.expectEqual(@as(u32, 9), parsed.req.dense_queries[1].query.k);
    try std.testing.expectEqual(@as(u32, 0), parsed.req.sparse_queries[0].query.k);
    try std.testing.expectEqual(@as(u32, 100), parsed.req.limit);
    const default_wire = try contract.encodeStorageKernelQueryRequest(alloc, .{
        .limit = 7,
        .dense_queries = &.{.{ .name = "a", .index_name = "a", .query = .{ .vector = &.{ 1, 0 }, .k = 7 } }},
    });
    defer alloc.free(default_wire);
    try std.testing.expect(std.mem.indexOf(u8, default_wire, "_embedding_limits") == null);
    // The extension is admitted only on internal query paths.
    try std.testing.expectError(error.InvalidQueryRequest, query.parsePublicQueryRequest(alloc, null, "docs", wire));

    const public_wire = "{\"embeddings\":{\"sparse_idx\":{\"indices\":[1,5],\"values\":[0.5,0.75],\"k\":4}},\"indexes\":[\"sparse_idx\"],\"limit\":9}";
    var original = try query.parsePublicQueryRequest(alloc, null, "docs", public_wire);
    defer original.deinit(alloc);
    const internal_wire = try contract.encodeStorageKernelQueryRequest(alloc, original.req);
    defer alloc.free(internal_wire);
    var restored = try query.parseQueryRequest(alloc, null, "docs", internal_wire);
    defer restored.deinit(alloc);
    try std.testing.expectEqual(@as(u32, 4), restored.req.sparse_queries[0].query.k);

    const legacy_wire = try contract.encodeStorageKernelQueryRequest(alloc, .{
        .index_name = "a",
        .dense = .{ .vector = &.{ 1, 0 }, .k = 7 },
        .limit = 100,
    });
    defer alloc.free(legacy_wire);
    var legacy = try query.parseQueryRequest(alloc, null, "docs", legacy_wire);
    defer legacy.deinit(alloc);
    try std.testing.expectEqual(@as(u32, 7), legacy.req.dense_queries[0].query.k);

    // Match by index, not iteration order, and reject malformed or unknown
    // budgets instead of silently reverting to the public result limit.
    const reordered = "{\"embeddings\":{\"a\":[1,0],\"b\":[0,1]},\"indexes\":[\"b\",\"a\"],\"_embedding_limits\":{\"a\":3,\"b\":9},\"limit\":100}";
    var order = try query.parseQueryRequest(alloc, null, "docs", reordered);
    defer order.deinit(alloc);
    try std.testing.expectEqualStrings("b", order.req.dense_queries[0].index_name);
    try std.testing.expectEqual(@as(u32, 9), order.req.dense_queries[0].query.k);
    try std.testing.expectEqual(@as(u32, 3), order.req.dense_queries[1].query.k);
    for ([_][]const u8{
        "{\"embeddings\":{\"a\":[1,0]},\"_embedding_limits\":{\"missing\":3}}",
        "{\"embeddings\":{\"a\":[1,0]},\"_embedding_limits\":{\"a\":-1}}",
        "{\"embeddings\":{\"a\":[1,0]},\"_embedding_limits\":{\"a\":4294967296}}",
    }) |invalid| {
        try std.testing.expectError(error.InvalidQueryRequest, query.parseQueryRequest(alloc, null, "docs", invalid));
    }
}

fn contextAllocationLifecycle(alloc: std.mem.Allocator) !void {
    const services = @import("kernel_runtime_services.zig");
    var bridge = services.memory.Allocator.fromStd(&alloc);
    // Keep executor creation outside the allocation sweep. std.Io.Threaded
    // reports thread admission failure as ConcurrencyUnavailable; this sweep
    // verifies the context's own fallible construction and unwind paths.
    var executor = services.executor.Borrow.init(&std.testing.io);
    var context = client.Context{};
    try context.ensureWithRuntime(.{ .allocator = &bridge, .io = &executor });
    defer context.deinit();
    try std.testing.expectEqual(@as(u64, 0), (try context.metrics()).lsm_cache_entry_count);
}

test "opaque context allocator failures release partial construction" {
    try std.testing.checkAllAllocationFailures(std.testing.allocator, contextAllocationLifecycle, .{});
}

test "opaque context rejects invalid runtime contracts before allocation" {
    const services = @import("kernel_runtime_services.zig");
    var context = client.Context{};
    try std.testing.expectError(error.InvalidAbiVersion, context.ensureWithRuntime(.{ .version = services.abi_version + 1 }));
    try std.testing.expect(context.handle == null);
    const alloc = std.testing.allocator;
    var bridge = services.memory.Allocator.fromStd(&alloc);
    bridge.version += 1;
    try std.testing.expectError(error.InvalidArgument, context.ensureWithRuntime(.{ .allocator = &bridge }));
    try std.testing.expect(context.handle == null);
}

test "opaque context stores use the borrowed VOPR filesystem and release handles" {
    const services = @import("kernel_runtime_services.zig");
    const alloc = std.testing.allocator;
    var simulator = try @import("vopr").vopr_io.VoprIo.init(.{
        .task_allocator = alloc,
        .file_allocator = alloc,
        .net_allocator = alloc,
        .process_allocator = alloc,
        .instrumentation_allocator = alloc,
    });
    defer simulator.deinit();
    const io = simulator.io();
    var borrow = services.executor.Borrow.init(&io);
    var allocator_bridge = services.memory.Allocator.fromStd(&alloc);
    {
        var context = client.Context{};
        try context.ensureWithRuntime(.{
            .context = .{ .auth_storage_path = .fromSlice("/vopr/linked-context/auth") },
            .allocator = &allocator_bridge,
            .io = &borrow,
        });
        defer context.deinit();
        var users = try context.systemStore(alloc, "system/auth-users");
        defer users.deinit();
        var write = try users.beginWrite();
        errdefer write.abort();
        try write.put("user", "value");
        try write.commit();
        var read = try users.beginRead();
        defer read.abort();
        try std.testing.expectEqualStrings("value", try read.get("user"));
        try std.testing.expect(try simulator.storageBytesUnderPrefix("/vopr/linked-context") > 0);
    }
    const resources = simulator.resourceSnapshot();
    try std.testing.expectEqual(@as(usize, 0), resources.open_file_handles);
    try std.testing.expectEqual(@as(usize, 0), resources.active_tasks);
}

test "opaque WAL rejects custom simulation hooks even without a context pointer" {
    const Hooks = struct {
        fn now(_: ?*anyopaque) u64 {
            return 1;
        }
        fn sleep(_: ?*anyopaque, _: u64) void {}
        fn wait(_: ?*anyopaque, _: u64) !void {}
    };
    try std.testing.expectError(error.UnsupportedKernelWalOptions, wal_client.WAL.open("/unused", wal_client.WalOptions{ .clock = .{ .now_ns_fn = Hooks.now, .sleep_ns_fn = Hooks.sleep } }));
    try std.testing.expectError(error.UnsupportedKernelWalOptions, wal_client.WAL.open("/unused", TestWalOptions{ .commit_scheduler = .{ .wait_ns_fn = Hooks.wait } }));
}
