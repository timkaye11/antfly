// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const platform_sync = @import("antfly_platform").sync;
const builtin = @import("builtin");
const contract = @import("secret_contract.zig");
const fs_paths = @import("antfly_runtime_fs").fs_paths;
const runtime_callback_abi = @import("../runtime_callback_abi.zig");

const c_env = if (builtin.link_libc and builtin.os.tag != .windows) struct {
    extern "c" var environ: [*:null]?[*:0]u8;
} else struct {};

/// Startup-only resolver configuration. Sources are ordered and never API-writable.
pub const Config = struct {
    pub const Native = struct {
        name: []const u8 = "native",
        backend: enum { file, distributed, serverless } = .file,
        path: []const u8 = "",
        scope: []const u8 = "default",
        keyring_path: ?[]const u8 = null,
        // Dedicated per-consumer delivery credentials, independent of wrapping keys.
        grants: []const Grant = &.{},
        reader: ?Reader = null,
        pub const Grant = struct { name: []const u8, credential_path: []const u8, keys: []const []const u8 };
        pub const Reader = struct { name: []const u8, credential_path: []const u8, urls: []const []const u8 };
    };
    pub const Source = struct { name: []const u8, type: enum { file }, path: []const u8 };
    native: ?Native = null,
    sources: []const Source = &.{},
    environment: bool = true,

    pub fn validate(self: Config) !void {
        if (self.native) |native| {
            try validateSourceName(native.name);
            try contract.validateName(native.scope);
            try validateSourcePath(native.scope);
            if (native.backend == .file) {
                try validateSourcePath(native.path);
                if (native.keyring_path != null or native.reader != null or native.grants.len != 0) return error.InvalidConfig;
            } else if (native.reader) |reader| {
                if (native.backend != .distributed or native.keyring_path != null or native.grants.len != 0 or reader.urls.len == 0) return error.InvalidConfig;
                try validateSourceName(reader.name);
                try validateSourcePath(reader.credential_path);
                for (reader.urls) |url| {
                    try validateSourcePath(url);
                    if (!(std.mem.startsWith(u8, url, "http://") or std.mem.startsWith(u8, url, "https://")) or std.mem.indexOfAny(u8, url, "?#") != null) return error.InvalidConfig;
                }
            } else {
                try validateSourcePath(native.keyring_path orelse return error.InvalidConfig);
                if (native.backend == .serverless) {
                    try validateSourcePath(native.path);
                    if (native.grants.len != 0) return error.InvalidConfig;
                }
            }
            for (native.grants, 0..) |grant, i| {
                try validateSourceName(grant.name);
                try validateSourcePath(grant.credential_path);
                if (grant.keys.len == 0) return error.InvalidConfig;
                for (grant.keys) |key| try validateKey(key);
                for (native.grants[0..i]) |prior| if (std.mem.eql(u8, prior.name, grant.name)) return error.InvalidConfig;
            }
        }
        for (self.sources, 0..) |source, index| {
            try validateSourceName(source.name);
            try validateSourcePath(source.path);
            if (self.native) |native| {
                if (std.mem.eql(u8, native.name, source.name) or std.mem.eql(u8, native.path, source.path)) return error.InvalidConfig;
            }
            for (self.sources[0..index]) |previous| {
                if (std.mem.eql(u8, previous.name, source.name)) return error.InvalidConfig;
            }
        }
    }

    fn validateSourceName(name: []const u8) !void {
        if (name.len == 0 or std.mem.eql(u8, name, "environment")) return error.InvalidConfig;
        for (name) |ch| {
            if (!std.ascii.isAlphanumeric(ch) and ch != '-' and ch != '_' and ch != '.') return error.InvalidConfig;
        }
    }

    fn validateSourcePath(path: []const u8) !void {
        if (path.len == 0 or std.mem.indexOfScalar(u8, path, 0) != null or std.mem.indexOf(u8, path, "${") != null) return error.InvalidConfig;
    }
};

/// Resolve existing symlinks and parent directories before normalizing missing
/// components. This also covers files/directories that will be created on PUT.
fn canonicalSecretPath(alloc: std.mem.Allocator, io: std.Io, path: []const u8, depth: usize) anyerror![]u8 {
    if (depth > 128) return error.InvalidConfig;
    if (std.Io.Dir.cwd().realPathFileAlloc(io, path, alloc)) |resolved| {
        defer alloc.free(resolved);
        return alloc.dupe(u8, resolved);
    } else |err| switch (err) {
        error.FileNotFound => {},
        else => return err,
    }
    // realpath fails on dangling links; resolve their targets explicitly.
    var link_buf: [std.fs.max_path_bytes]u8 = undefined;
    const link_len = std.Io.Dir.cwd().readLink(io, path, &link_buf) catch |err| switch (err) {
        error.FileNotFound, error.NotLink => null,
        else => return err,
    };
    if (link_len) |len| {
        const target = link_buf[0..len];
        const joined = if (std.fs.path.isAbsolute(target))
            try alloc.dupe(u8, target)
        else
            try std.fs.path.join(alloc, &.{ std.fs.path.dirname(path) orelse ".", target });
        defer alloc.free(joined);
        return canonicalSecretPath(alloc, io, joined, depth + 1);
    }
    const parent = std.fs.path.dirname(path) orelse ".";
    if (std.mem.eql(u8, parent, path)) return error.InvalidConfig;
    const resolved_parent = try canonicalSecretPath(alloc, io, parent, depth + 1);
    defer alloc.free(resolved_parent);
    return std.fs.path.resolve(alloc, &.{ resolved_parent, std.fs.path.basename(path) });
}

fn rejectSourceAlias(alloc: std.mem.Allocator, io: std.Io, native_path: []const u8, source_path: []const u8) !void {
    const native_canonical = try canonicalSecretPath(alloc, io, native_path, 0);
    defer alloc.free(native_canonical);
    const source_canonical = try canonicalSecretPath(alloc, io, source_path, 0);
    defer alloc.free(source_canonical);
    if (std.mem.eql(u8, native_canonical, source_canonical)) return error.InvalidConfig;
}

pub fn parseConfig(alloc: std.mem.Allocator, value: std.json.Value) !std.json.Parsed(Config) {
    var parsed = std.json.parseFromValue(Config, alloc, value, .{}) catch |err| switch (err) {
        error.OutOfMemory => return err,
        else => return error.InvalidConfig,
    };
    errdefer parsed.deinit();
    try parsed.value.validate();
    return parsed;
}

/// Bootstrap before resolving any credential references in the main config.
/// Explicit configuration and legacy flags are mutually exclusive.
pub fn initFromConfigPathWithIo(alloc: std.mem.Allocator, io: std.Io, config_path: ?[]const u8, legacy_paths: []const []const u8) !?FileStore {
    if (config_path) |path| {
        const raw = try std.Io.Dir.cwd().readFileAlloc(io, path, alloc, .limited(16 * 1024 * 1024));
        defer alloc.free(raw);
        var tree = try std.json.parseFromSlice(std.json.Value, alloc, raw, .{});
        defer tree.deinit();
        if (tree.value != .object) return error.InvalidConfig;
        if (tree.value.object.get("secrets")) |value| {
            if (legacy_paths.len != 0) return error.InvalidConfig;
            var parsed = try parseConfig(alloc, value);
            defer parsed.deinit();
            return try FileStore.initConfiguredWithIo(alloc, io, parsed.value);
        }
    }
    if (legacy_paths.len > 0) return try FileStore.initLayeredWithIo(alloc, io, legacy_paths);
    return null;
}

pub const SecretStatus = enum {
    configured_file,
    configured_env,
    configured_both,
};

pub const ListedSecret = struct {
    key: []u8,
    status: SecretStatus,
    source: ?[]u8 = null,
    managed: bool = false,
    revision: ?u64 = null,
    env_var: ?[]u8 = null,
    created_at: ?[]u8 = null,
    updated_at: ?[]u8 = null,

    pub fn deinit(self: *ListedSecret, alloc: std.mem.Allocator) void {
        alloc.free(self.key);
        if (self.source) |source| alloc.free(source);
        if (self.env_var) |env_var| alloc.free(env_var);
        if (self.created_at) |created_at| alloc.free(created_at);
        if (self.updated_at) |updated_at| alloc.free(updated_at);
        self.* = undefined;
    }
};

pub const SecretValue = union(enum) {
    literal: []u8,
    secret_ref: []u8,
    env_var: []u8,
    /// Optional canonical provider key, resolved through the store and its environment policy.
    provider_default: []u8,

    pub fn initConfig(alloc: std.mem.Allocator, configured_value: ?[]const u8) !?SecretValue {
        const value = configured_value orelse return null;
        if (parseSecretReference(value)) |key| {
            return .{ .secret_ref = try alloc.dupe(u8, key) };
        }
        return .{ .literal = try alloc.dupe(u8, value) };
    }

    pub fn initConfigOrEnv(alloc: std.mem.Allocator, configured_value: ?[]const u8, env_name: []const u8) !SecretValue {
        if (configured_value) |value| {
            if (parseSecretReference(value)) |key| {
                return .{ .secret_ref = try alloc.dupe(u8, key) };
            }
            return .{ .literal = try alloc.dupe(u8, value) };
        }
        return .{ .env_var = try alloc.dupe(u8, env_name) };
    }

    pub fn initConfigOrProviderDefault(alloc: std.mem.Allocator, configured_value: ?[]const u8, env_name: []const u8) !SecretValue {
        if (try initConfig(alloc, configured_value)) |value| return value;
        const key = secretKeyForEnvVar(alloc, env_name) orelse return error.OutOfMemory;
        return .{ .provider_default = key };
    }

    pub fn deinit(self: *SecretValue, alloc: std.mem.Allocator) void {
        switch (self.*) {
            .literal => |value| alloc.free(value),
            .secret_ref => |value| alloc.free(value),
            .env_var, .provider_default => |value| alloc.free(value),
        }
        self.* = undefined;
    }

    pub fn resolveOwned(self: *const SecretValue, alloc: std.mem.Allocator, secret_store: ?*FileStore) !?[]u8 {
        return switch (self.*) {
            .literal => |value| try alloc.dupe(u8, value),
            .secret_ref => |key| blk: {
                if (secret_store) |store| {
                    break :blk (try store.getOwned(alloc, key)) orelse return error.SecretNotFound;
                }
                const env_var = try envVarForKey(alloc, key);
                defer alloc.free(env_var);
                break :blk envValueOwned(alloc, env_var) orelse return error.SecretNotFound;
            },
            .provider_default => |key| blk: {
                if (secret_store) |store| break :blk try store.getOwned(alloc, key);
                const env_var = try envVarForKey(alloc, key);
                defer alloc.free(env_var);
                break :blk envValueOwned(alloc, env_var);
            },
            .env_var => |env_var| envValueOwned(alloc, env_var),
        };
    }

    pub fn resolveOwnedWithGeneration(self: *const SecretValue, alloc: std.mem.Allocator, secret_store: ?*FileStore) !ResolvedSecret {
        return switch (self.*) {
            .literal => |value| .{
                .value = try alloc.dupe(u8, value),
                .generation = 0,
                .source = .literal,
            },
            .secret_ref, .provider_default => |key| blk: {
                if (secret_store) |store| {
                    break :blk try store.getOwnedWithGeneration(alloc, key);
                }
                const env_var = try envVarForKey(alloc, key);
                defer alloc.free(env_var);
                const value = envValueOwned(alloc, env_var) orelse return error.SecretNotFound;
                break :blk .{
                    .value = value,
                    .generation = 0,
                    .source = .env_var,
                };
            },
            .env_var => |env_var| blk: {
                const value = envValueOwned(alloc, env_var) orelse return error.SecretNotFound;
                break :blk .{
                    .value = value,
                    .generation = 0,
                    .source = .env_var,
                };
            },
        };
    }

    pub fn identityHash(self: *const SecretValue) u64 {
        return switch (self.*) {
            .literal => |value| std.hash.Wyhash.hash(0, value),
            .secret_ref => |value| std.hash.Wyhash.hash(1, value),
            .env_var => |value| std.hash.Wyhash.hash(2, value),
            .provider_default => |value| std.hash.Wyhash.hash(3, value),
        };
    }
};

pub const ResolvedSecretSource = enum {
    literal,
    file_store,
    env_var,
};

pub const ResolvedSecret = struct {
    value: []u8,
    generation: u64,
    source: ResolvedSecretSource,

    pub fn deinit(self: *ResolvedSecret, alloc: std.mem.Allocator) void {
        alloc.free(self.value);
        self.* = undefined;
    }

    pub fn cacheGeneration(self: ResolvedSecret) u64 {
        return self.generation;
    }
};

pub const ReloadHealth = struct {
    generation: u64,
    content_hash: [std.crypto.hash.sha2.Sha256.digest_length]u8,
    /// Whether this store can expose one exact control-plane publication
    /// generation. Layered stores intentionally cannot: more than one file
    /// contributes to the served snapshot.
    supports_source_generation: bool,
    source_generation: ?[std.crypto.hash.sha2.Sha256.digest_length]u8 = null,
    entry_count: usize,
    last_reload_failed: bool,
    stale_snapshot: bool,
    reload_successes: u64,
    reload_failures: u64,
    last_success_ns: u64,
    last_failure_ns: u64,
};

pub const BearerAuthHeaderCache = struct {
    mutex: std.atomic.Mutex = .unlocked,
    generation: u64 = 0,
    header: ?[]u8 = null,

    pub fn deinit(self: *BearerAuthHeaderCache, alloc: std.mem.Allocator) void {
        if (self.header) |value| alloc.free(value);
        self.* = undefined;
    }

    pub fn getOwned(
        self: *BearerAuthHeaderCache,
        cache_alloc: std.mem.Allocator,
        out_alloc: std.mem.Allocator,
        secret: *const SecretValue,
        secret_store: ?*FileStore,
    ) ![]u8 {
        var resolved = try secret.resolveOwnedWithGeneration(out_alloc, secret_store);
        defer resolved.deinit(out_alloc);

        platform_sync.lockYielding(&self.mutex);
        defer self.mutex.unlock();

        // Generations are scoped to a source and may collide across fallback
        // transitions (or remain zero for environment/literal values). Resolution
        // already fetched the current value; never reuse a different credential.
        const matches = if (self.header) |header| std.mem.eql(u8, header["Bearer ".len..], resolved.value) else false;
        if (!matches or self.generation != resolved.cacheGeneration()) {
            const header = try std.fmt.allocPrint(cache_alloc, "Bearer {s}", .{resolved.value});
            if (self.header) |value| cache_alloc.free(value);
            self.header = header;
            self.generation = resolved.cacheGeneration();
        }
        return try out_alloc.dupe(u8, self.header.?);
    }
};

const StoredSecret = struct {
    value: []u8,
    created_at_ns: u64,
    updated_at_ns: u64,

    fn deinit(self: *StoredSecret, alloc: std.mem.Allocator) void {
        alloc.free(self.value);
        self.* = undefined;
    }
};

// Status is polled independently of request traffic. Refresh there as well so
// control planes can acknowledge a projected Secret before the first request,
// while bounding filesystem work under aggressive health polling.
const health_refresh_interval_ns: u64 = 500 * std.time.ns_per_ms;

const PersistedSecret = struct {
    key: []const u8,
    value: []const u8,
    created_at_ns: ?u64 = null,
    updated_at_ns: ?u64 = null,
};

const PersistedSecretsFile = struct {
    /// Opaque, non-secret control-plane generation used to acknowledge an
    /// exact projected file without exposing a digest of its secret values.
    generation: ?[]const u8 = null,
    secrets: []const PersistedSecret,
};

const FileMetadata = struct {
    inode: std.Io.File.INode,
    size: u64,
    mtime_ns: i128,

    fn eql(self: FileMetadata, other: FileMetadata) bool {
        return self.inode == other.inode and self.size == other.size and self.mtime_ns == other.mtime_ns;
    }
};

pub const FileStore = struct {
    // A store is borrowed by separately compiled API/runtime archives. Execute
    // I/O in its creating archive: std.Io error integers are compilation-local,
    // even when the interface layout and Zig toolchain are identical.
    const Operations = struct {
        refresh: *const fn (*FileStore) anyerror!bool = refreshLocal,
        refresh_throttled: *const fn (*FileStore, u64) anyerror!bool = refreshThrottledLocal,
        list: *const fn (*FileStore, std.mem.Allocator) anyerror![]ListedSecret = listLocal,
        put: *const fn (*FileStore, std.mem.Allocator, []const u8, []const u8) anyerror!ListedSecret = putLocal,
        delete: *const fn (*FileStore, []const u8) anyerror!bool = deleteLocal,
        get_owned: *const fn (*FileStore, std.mem.Allocator, []const u8) anyerror!?[]u8 = getOwnedLocal,
        get_owned_with_generation: *const fn (*FileStore, std.mem.Allocator, []const u8) anyerror!ResolvedSecret = getOwnedWithGenerationLocal,
    };
    const Boundary = runtime_callback_abi.Boundary(Operations);

    alloc: std.mem.Allocator,
    io: std.Io,
    operations: Operations = .{},
    dispatch: Boundary.Dispatch = Boundary.local_dispatch,
    path: []u8,
    has_file: bool = true,
    writable: bool = true,
    environment_enabled: bool = true,
    source_name: ?[]u8 = null,
    fallbacks: []FileStore = &.{},
    native_config: ?std.json.Parsed(Config.Native) = null,
    native_source: ?contract.Source = null,
    native_writer: ?contract.NativeStore.Writer = null,
    native_revision: u64 = 0,
    mutex: std.atomic.Mutex = .unlocked,
    entries: std.StringArrayHashMapUnmanaged(StoredSecret) = .{},
    observed_metadata: ?FileMetadata = null,
    generation_value: u64 = 0,
    generation_snapshot: @import("antfly_platform").atomic.Value(u64) = .init(0),
    content_hash: [std.crypto.hash.sha2.Sha256.digest_length]u8 = [_]u8{0} ** std.crypto.hash.sha2.Sha256.digest_length,
    source_generation: ?[std.crypto.hash.sha2.Sha256.digest_length]u8 = null,
    last_reload_failed: bool = false,
    reload_success_count: u64 = 0,
    reload_failure_count: u64 = 0,
    last_success_ns: u64 = 0,
    last_failure_ns: u64 = 0,
    next_throttled_refresh_ns: @import("antfly_platform").atomic.Value(u64) = .init(0),

    pub fn init(alloc: std.mem.Allocator, path: []const u8) !FileStore {
        return initWithIo(alloc, std.Options.debug_io, path);
    }

    pub fn initWithIo(alloc: std.mem.Allocator, io: std.Io, path: []const u8) !FileStore {
        var store = FileStore{
            .alloc = alloc,
            .io = io,
            .path = try alloc.dupe(u8, path),
        };
        errdefer store.deinit();
        try store.load();
        return store;
    }

    pub fn initLayered(alloc: std.mem.Allocator, paths: []const []const u8) !FileStore {
        return initLayeredWithIo(alloc, std.Options.debug_io, paths);
    }

    pub fn initLayeredWithIo(alloc: std.mem.Allocator, io: std.Io, paths: []const []const u8) !FileStore {
        if (paths.len == 0) return error.InvalidArguments;

        var store = try FileStore.initWithIo(alloc, io, paths[0]);
        errdefer store.deinit();

        if (paths.len > 1) {
            store.fallbacks = try alloc.alloc(FileStore, paths.len - 1);
            var initialized: usize = 0;
            errdefer {
                for (store.fallbacks[0..initialized]) |*fallback| fallback.deinit();
                alloc.free(store.fallbacks);
                store.fallbacks = &.{};
            }
            for (paths[1..]) |path| {
                store.fallbacks[initialized] = try FileStore.initWithIo(alloc, io, path);
                store.fallbacks[initialized].writable = false;
                initialized += 1;
            }
        }

        return store;
    }

    pub fn initConfiguredWithIo(alloc: std.mem.Allocator, io: std.Io, cfg: Config) !FileStore {
        try cfg.validate();
        const native_file = if (cfg.native) |native| native.backend == .file else false;
        if (native_file) for (cfg.sources) |source| try rejectSourceAlias(alloc, io, cfg.native.?.path, source.path);
        // Encrypted native stores have a pathless root, with all files as fallbacks.
        const encrypted = cfg.native != null and !native_file;
        const offset: usize = if (native_file) 1 else 0;
        const count = cfg.sources.len + offset;
        var paths = try alloc.alloc([]const u8, count);
        defer alloc.free(paths);
        if (native_file) paths[0] = cfg.native.?.path;
        for (cfg.sources, offset..) |source, i| paths[i] = source.path;
        var store = if (count > 0 and !encrypted)
            try initLayeredWithIo(alloc, io, paths)
        else
            FileStore{ .alloc = alloc, .io = io, .path = try alloc.dupe(u8, ""), .has_file = false };
        errdefer store.deinit();
        if (encrypted and count > 0) {
            var fallback_list = std.ArrayList(FileStore).empty;
            errdefer {
                for (fallback_list.items) |*item| item.deinit();
                fallback_list.deinit(alloc);
            }
            for (paths) |path| {
                var item = try FileStore.initWithIo(alloc, io, path);
                errdefer item.deinit();
                try fallback_list.append(alloc, item);
            }
            store.fallbacks = try fallback_list.toOwnedSlice(alloc);
        }
        store.writable = native_file;
        store.environment_enabled = cfg.environment;
        if (cfg.native) |native| {
            store.source_name = try alloc.dupe(u8, native.name);
            if (encrypted) {
                const bytes = try std.json.Stringify.valueAlloc(alloc, native, .{});
                defer alloc.free(bytes);
                store.native_config = try std.json.parseFromSlice(Config.Native, alloc, bytes, .{ .allocate = .alloc_always });
            }
        } else if (cfg.sources.len > 0) {
            store.source_name = try alloc.dupe(u8, cfg.sources[0].name);
        }
        for (store.fallbacks, 0..) |*fallback, i| {
            fallback.writable = false;
            fallback.environment_enabled = cfg.environment;
            fallback.source_name = try alloc.dupe(u8, cfg.sources[if (encrypted) i else i + 1 - offset].name);
        }
        return store;
    }

    /// Attach before listeners start. Handles borrow their runtime owner.
    pub fn attachNative(self: *FileStore, source: contract.Source, writer: ?contract.NativeStore.Writer) void {
        self.native_source = source;
        self.native_writer = writer;
        self.writable = writer != null;
    }

    pub fn deinit(self: *FileStore) void {
        if (self.native_config) |*cfg| cfg.deinit();
        for (self.fallbacks) |*fallback| fallback.deinit();
        if (self.fallbacks.len > 0) self.alloc.free(self.fallbacks);
        deinitEntries(self.alloc, &self.entries);
        self.entries.deinit(self.alloc);
        self.alloc.free(self.path);
        if (self.source_name) |name| self.alloc.free(name);
        self.* = undefined;
    }

    pub fn generation(self: *FileStore) u64 {
        self.lock();
        defer self.unlock();
        return self.generationLocked();
    }

    pub fn generationFast(self: *FileStore) u64 {
        if (self.fallbacks.len == 0) return self.generation_snapshot.load(.acquire);
        return self.generation();
    }

    pub fn reloadFailed(self: *FileStore) bool {
        self.lock();
        defer self.unlock();
        if (self.last_reload_failed) return true;
        for (self.fallbacks) |*fallback| {
            if (fallback.reloadFailed()) return true;
        }
        return false;
    }

    pub fn healthSnapshot(self: *FileStore) ReloadHealth {
        _ = self.refreshIfChangedThrottled(health_refresh_interval_ns) catch {
            self.lock();
            self.markReloadFailedLocked();
            self.unlock();
        };
        self.lock();
        defer self.unlock();
        var health = self.healthSnapshotLocked();
        for (self.fallbacks, 1..) |*fallback, index| {
            const fallback_health = fallback.healthSnapshot();
            var combined_hash: [std.crypto.hash.sha2.Sha256.digest_length]u8 = undefined;
            var hasher = std.crypto.hash.sha2.Sha256.init(.{});
            hasher.update(&health.content_hash);
            hasher.update(&fallback_health.content_hash);
            hasher.final(&combined_hash);
            health.content_hash = combined_hash;
            health.generation +%= fallback_health.generation *% @as(u64, @intCast(index + 1));
            health.entry_count += fallback_health.entry_count;
            health.last_reload_failed = health.last_reload_failed or fallback_health.last_reload_failed;
            health.stale_snapshot = health.stale_snapshot or fallback_health.stale_snapshot;
            health.reload_successes += fallback_health.reload_successes;
            health.reload_failures += fallback_health.reload_failures;
            health.last_success_ns = @max(health.last_success_ns, fallback_health.last_success_ns);
            health.last_failure_ns = @max(health.last_failure_ns, fallback_health.last_failure_ns);
        }
        return health;
    }

    pub fn refreshIfChanged(self: *FileStore) !bool {
        return Boundary.call("refresh", self.dispatch, self.operations.refresh, .{self});
    }

    fn refreshLocal(self: *FileStore) !bool {
        self.lock();
        defer self.unlock();
        var changed = try self.refreshIfChangedLocked();
        for (self.fallbacks) |*fallback| {
            changed = (try fallback.refreshIfChanged()) or changed;
        }
        return changed;
    }

    /// Refresh at most once per interval. The atomic fast path avoids taking
    /// the store lock or issuing a stat syscall on every cache lookup.
    pub fn refreshIfChangedThrottled(self: *FileStore, interval_ns: u64) !bool {
        return Boundary.call("refresh_throttled", self.dispatch, self.operations.refresh_throttled, .{ self, interval_ns });
    }

    fn refreshThrottledLocal(self: *FileStore, interval_ns: u64) !bool {
        if (interval_ns == 0) return try self.refreshIfChanged();
        const now_ns = nowNsWithIo(self.io);
        if (now_ns < self.next_throttled_refresh_ns.load(.acquire)) return false;

        self.lock();
        defer self.unlock();
        const locked_now_ns = nowNsWithIo(self.io);
        if (locked_now_ns < self.next_throttled_refresh_ns.load(.acquire)) return false;
        self.next_throttled_refresh_ns.store(locked_now_ns +| interval_ns, .release);
        errdefer self.next_throttled_refresh_ns.store(0, .release);

        var changed = try self.refreshIfChangedLocked();
        for (self.fallbacks) |*fallback| {
            changed = (try fallback.refreshIfChangedThrottled(interval_ns)) or changed;
        }
        return changed;
    }

    pub fn list(self: *FileStore, alloc: std.mem.Allocator) ![]ListedSecret {
        return Boundary.call("list", self.dispatch, self.operations.list, .{ self, alloc });
    }

    fn listLocal(self: *FileStore, alloc: std.mem.Allocator) ![]ListedSecret {
        self.lock();
        defer self.unlock();
        _ = try self.refreshIfChangedLocked();

        var out = std.ArrayList(ListedSecret).empty;
        errdefer {
            for (out.items) |*item| item.deinit(alloc);
            out.deinit(alloc);
        }
        if (self.native_config != null and self.native_source == null) return error.Unavailable;
        if (self.native_source) |source| {
            var listing = try source.listMetadata(alloc, self.native_config.?.value.scope, .{ .min_revision = self.native_revision });
            defer listing.deinit(alloc);
            self.observeNativeRevision(listing.revision);
            for (listing.entries) |entry| {
                var described = try self.describeNative(alloc, entry.key, entry.revision);
                errdefer described.deinit(alloc);
                try out.append(alloc, described);
            }
        }
        var it = self.entries.iterator();
        while (it.next()) |entry| {
            try out.append(alloc, try self.describeStored(alloc, entry.key_ptr.*, entry.value_ptr.*));
        }

        for (self.fallbacks) |*fallback| try fallback.appendFileEntriesForList(alloc, &out);

        const env_only = if (self.environment_enabled) try listEnvironmentSecrets(alloc) else try alloc.alloc(ListedSecret, 0);
        defer freeListedSecrets(alloc, env_only);
        for (env_only) |item| {
            if (listedSecretsContain(out.items, item.key)) continue;
            try out.append(alloc, .{
                .key = try alloc.dupe(u8, item.key),
                .status = item.status,
                .source = try alloc.dupe(u8, "environment"),
                .env_var = if (item.env_var) |env_var| try alloc.dupe(u8, env_var) else null,
                .created_at = null,
                .updated_at = null,
            });
        }

        std.sort.block(ListedSecret, out.items, {}, lessThanListedSecret);
        return try out.toOwnedSlice(alloc);
    }

    pub fn put(self: *FileStore, alloc: std.mem.Allocator, key: []const u8, value: []const u8) !ListedSecret {
        return Boundary.call("put", self.dispatch, self.operations.put, .{ self, alloc, key, value });
    }

    fn putLocal(self: *FileStore, alloc: std.mem.Allocator, key: []const u8, value: []const u8) !ListedSecret {
        if (!self.writable) return error.WriteUnavailable;
        try validateKey(key);
        self.lock();
        defer self.unlock();
        if (self.native_writer) |writer| {
            const result = try writer.put(self.native_config.?.value.scope, key, value, .any);
            self.observeNativeRevision(result.revision);
            return self.describeNative(alloc, key, result.revision);
        }
        _ = try self.refreshIfChangedLocked();

        var next = try cloneEntries(self.alloc, self.entries);
        errdefer {
            deinitEntries(self.alloc, &next);
            next.deinit(self.alloc);
        }

        const now_ns = wallClockNsWithIo(self.io);
        if (next.getPtr(key)) |existing| {
            const new_value = try self.alloc.dupe(u8, value);
            self.alloc.free(existing.value);
            existing.value = new_value;
            existing.updated_at_ns = now_ns;
        } else insert: {
            const new_key = try self.alloc.dupe(u8, key);
            errdefer self.alloc.free(new_key);
            const new_value = try self.alloc.dupe(u8, value);
            errdefer self.alloc.free(new_value);
            try next.put(self.alloc, new_key, .{
                .value = new_value,
                .created_at_ns = now_ns,
                .updated_at_ns = now_ns,
            });
            break :insert;
        }
        const content_hash = try self.persistEntries(&next);
        try self.replaceEntriesAfterLocalWriteLocked(&next, content_hash);
        return try self.describeOneLocked(alloc, key);
    }

    pub fn delete(self: *FileStore, key: []const u8) !bool {
        return Boundary.call("delete", self.dispatch, self.operations.delete, .{ self, key });
    }

    fn deleteLocal(self: *FileStore, key: []const u8) !bool {
        if (!self.writable) return error.WriteUnavailable;
        self.lock();
        defer self.unlock();
        if (self.native_writer) |writer| {
            const result = try writer.removeOverride(self.native_config.?.value.scope, key, .any);
            self.observeNativeRevision(result.revision);
            return result.changed;
        }
        _ = try self.refreshIfChangedLocked();

        const index = self.entries.getIndex(key) orelse return false;
        var next = try cloneEntries(self.alloc, self.entries);
        errdefer {
            deinitEntries(self.alloc, &next);
            next.deinit(self.alloc);
        }

        const next_index = next.getIndex(key) orelse return false;
        self.alloc.free(next.keys()[next_index]);
        var stored = next.values()[next_index];
        stored.deinit(self.alloc);
        _ = next.swapRemoveAt(next_index);
        _ = index;
        const content_hash = try self.persistEntries(&next);
        try self.replaceEntriesAfterLocalWriteLocked(&next, content_hash);
        return true;
    }

    pub fn getOwned(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !?[]u8 {
        return Boundary.call("get_owned", self.dispatch, self.operations.get_owned, .{ self, alloc, key });
    }

    fn getOwnedLocal(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !?[]u8 {
        self.lock();
        defer self.unlock();
        if (try self.resolveNativeLocked(alloc, key)) |value| return value;
        if (self.native_source == null) _ = try self.refreshIfChangedLocked();

        if (self.entries.get(key)) |stored| return try alloc.dupe(u8, stored.value);
        for (self.fallbacks) |*fallback| {
            if (try fallback.getOwnedFromFilesNoEnv(alloc, key)) |value| return value;
        }
        if (!self.environment_enabled) return null;
        const env_var = try envVarForKey(alloc, key);
        defer alloc.free(env_var);
        return envValueOwned(alloc, env_var);
    }

    pub fn getOwnedWithGeneration(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !ResolvedSecret {
        return Boundary.call("get_owned_with_generation", self.dispatch, self.operations.get_owned_with_generation, .{ self, alloc, key });
    }

    fn getOwnedWithGenerationLocal(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !ResolvedSecret {
        self.lock();
        defer self.unlock();
        if (try self.resolveNativeLocked(alloc, key)) |value| return .{ .value = value, .generation = self.generationLocked(), .source = .file_store };
        if (self.native_source == null) _ = try self.refreshIfChangedLocked();

        if (self.entries.get(key)) |stored| {
            return .{
                .value = try alloc.dupe(u8, stored.value),
                .generation = self.generationLocked(),
                .source = .file_store,
            };
        }
        for (self.fallbacks) |*fallback| {
            if (try fallback.getOwnedWithGenerationFromFilesNoEnv(alloc, key)) |value| {
                return .{
                    .value = value.value,
                    .generation = self.generationLocked(),
                    .source = value.source,
                };
            }
        }
        if (!self.environment_enabled) return error.SecretNotFound;
        const env_var = try envVarForKey(alloc, key);
        defer alloc.free(env_var);
        const value = envValueOwned(alloc, env_var) orelse return error.SecretNotFound;
        return .{
            .value = value,
            .generation = self.generationLocked(),
            .source = .env_var,
        };
    }

    pub fn resolveValueOwned(self: *FileStore, alloc: std.mem.Allocator, raw: []const u8) ![]u8 {
        const key = parseSecretReference(raw) orelse return try alloc.dupe(u8, raw);
        return (try self.getOwned(alloc, key)) orelse return error.SecretNotFound;
    }

    pub fn resolveValueWithGenerationOwned(self: *FileStore, alloc: std.mem.Allocator, raw: []const u8) !ResolvedSecret {
        const key = parseSecretReference(raw) orelse return .{
            .value = try alloc.dupe(u8, raw),
            .generation = 0,
            .source = .literal,
        };
        return try self.getOwnedWithGeneration(alloc, key);
    }

    fn describeStored(self: *FileStore, alloc: std.mem.Allocator, key: []const u8, stored: StoredSecret) !ListedSecret {
        const env_var = try envVarForKey(alloc, key);
        const has_env = self.environment_enabled and hasEnvVar(env_var);
        return .{
            .source = try alloc.dupe(u8, self.source_name orelse if (self.writable) "native" else "file"),
            .managed = self.writable,
            .key = try alloc.dupe(u8, key),
            .status = if (has_env) .configured_both else .configured_file,
            .env_var = env_var,
            .created_at = if (stored.created_at_ns > 0) try formatTimestampOwned(alloc, stored.created_at_ns) else null,
            .updated_at = if (stored.updated_at_ns > 0) try formatTimestampOwned(alloc, stored.updated_at_ns) else null,
        };
    }

    fn describeOneLocked(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !ListedSecret {
        const stored = self.entries.get(key) orelse return error.SecretNotFound;
        return try self.describeStored(alloc, key, stored);
    }

    fn appendFileEntriesForList(self: *FileStore, alloc: std.mem.Allocator, out: *std.ArrayList(ListedSecret)) !void {
        self.lock();
        defer self.unlock();
        _ = try self.refreshIfChangedLocked();

        var it = self.entries.iterator();
        while (it.next()) |entry| {
            if (listedSecretsContain(out.items, entry.key_ptr.*)) continue;
            try out.append(alloc, try self.describeStored(alloc, entry.key_ptr.*, entry.value_ptr.*));
        }
        for (self.fallbacks) |*fallback| try fallback.appendFileEntriesForList(alloc, out);
    }

    fn getOwnedFromFilesNoEnv(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !?[]u8 {
        self.lock();
        defer self.unlock();
        _ = try self.refreshIfChangedLocked();

        if (self.entries.get(key)) |stored| return try alloc.dupe(u8, stored.value);
        for (self.fallbacks) |*fallback| {
            if (try fallback.getOwnedFromFilesNoEnv(alloc, key)) |value| return value;
        }
        return null;
    }

    fn getOwnedWithGenerationFromFilesNoEnv(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !?ResolvedSecret {
        self.lock();
        defer self.unlock();
        _ = try self.refreshIfChangedLocked();

        if (self.entries.get(key)) |stored| {
            return .{
                .value = try alloc.dupe(u8, stored.value),
                .generation = self.generationLocked(),
                .source = .file_store,
            };
        }
        for (self.fallbacks) |*fallback| {
            if (try fallback.getOwnedWithGenerationFromFilesNoEnv(alloc, key)) |value| return value;
        }
        return null;
    }

    fn load(self: *FileStore) !void {
        const metadata = statFileMetadataWithIo(self.io, self.path) catch |err| switch (err) {
            error.FileNotFound => {
                self.observed_metadata = null;
                self.markReloadHealthyLocked(false);
                return;
            },
            else => return err,
        };
        if (metadata == null) {
            self.observed_metadata = null;
            self.markReloadHealthyLocked(false);
            return;
        }

        const loaded = try loadEntriesFromFileWithIo(self.alloc, self.io, self.path);
        var next = loaded.entries;
        errdefer {
            deinitEntries(self.alloc, &next);
            next.deinit(self.alloc);
        }

        deinitEntries(self.alloc, &self.entries);
        self.entries.deinit(self.alloc);
        self.entries = next;
        next = .{};
        self.content_hash = loaded.content_hash;
        self.source_generation = loaded.source_generation;
        self.observed_metadata = metadata;
        self.markReloadHealthyLocked(true);
    }

    fn observeNativeRevision(self: *FileStore, revision: u64) void {
        if (revision != self.native_revision) {
            self.native_revision = revision;
            self.generation_value +%= 1;
            self.generation_snapshot.store(self.generation_value, .release);
        }
    }

    fn describeNative(self: *FileStore, alloc: std.mem.Allocator, key: []const u8, revision: u64) !ListedSecret {
        const owned_key = try alloc.dupe(u8, key);
        errdefer alloc.free(owned_key);
        return .{ .key = owned_key, .status = .configured_file, .source = try alloc.dupe(u8, self.source_name orelse "native"), .managed = self.writable, .revision = revision };
    }

    fn resolveNativeLocked(self: *FileStore, alloc: std.mem.Allocator, key: []const u8) !?[]u8 {
        const source = self.native_source orelse return null; // Startup bootstrap uses only files/environment.
        const result = try source.resolve(alloc, self.native_config.?.value.scope, key, .{ .min_revision = self.native_revision });
        self.observeNativeRevision(result.revision);
        return if (result.value) |value| value.secret.bytes else null;
    }

    fn refreshIfChangedLocked(self: *FileStore) !bool {
        if (self.native_source) |source| {
            const result = source.refresh(self.native_config.?.value.scope) catch |err| {
                self.markReloadFailedLocked();
                return err;
            };
            if (!result.available or result.stale or result.revision < self.native_revision) return error.Unavailable;
            const changed = result.revision != self.native_revision;
            self.observeNativeRevision(result.revision);
            self.last_reload_failed = false;
            return changed;
        }
        if (!self.has_file) return false;
        const metadata = statFileMetadataWithIo(self.io, self.path) catch |err| switch (err) {
            error.FileNotFound => {
                if (self.observed_metadata != null) {
                    const first_failure = !self.last_reload_failed;
                    self.markReloadFailedLocked();
                    if (first_failure) std.log.warn("secret store file missing; keeping last known good snapshot path={s}", .{self.path});
                } else {
                    self.markReloadHealthyLocked(false);
                }
                return false;
            },
            else => return err,
        };
        if (metadata == null) {
            if (self.observed_metadata != null) {
                const first_failure = !self.last_reload_failed;
                self.markReloadFailedLocked();
                if (first_failure) std.log.warn("secret store file missing; keeping last known good snapshot path={s}", .{self.path});
            } else {
                self.markReloadHealthyLocked(false);
            }
            return false;
        }
        if (self.observed_metadata) |observed| {
            if (observed.eql(metadata.?) and !self.last_reload_failed) {
                return false;
            }
        }

        const loaded = loadEntriesFromFileWithIo(self.alloc, self.io, self.path) catch |err| {
            const first_failure = !self.last_reload_failed;
            self.markReloadFailedLocked();
            if (first_failure) std.log.warn("secret store reload failed; keeping last known good snapshot path={s} err={}", .{ self.path, err });
            return false;
        };
        var next = loaded.entries;
        errdefer {
            deinitEntries(self.alloc, &next);
            next.deinit(self.alloc);
        }
        self.replaceEntriesLocked(&next);
        self.content_hash = loaded.content_hash;
        self.source_generation = loaded.source_generation;
        self.observed_metadata = metadata;
        self.generation_value +%= 1;
        self.generation_snapshot.store(self.generation_value, .release);
        self.markReloadHealthyLocked(true);
        return true;
    }

    fn persistEntries(self: *FileStore, entries: *const std.StringArrayHashMapUnmanaged(StoredSecret)) ![std.crypto.hash.sha2.Sha256.digest_length]u8 {
        // Mounted source paths can change after startup. Fail before creating
        // directories or replacing the destination if they now alias native.
        if (self.source_name != null) {
            for (self.fallbacks) |*fallback| try rejectSourceAlias(self.alloc, self.io, self.path, fallback.path);
        }
        const alloc = self.alloc;
        var persisted = try alloc.alloc(PersistedSecret, entries.count());
        defer alloc.free(persisted);

        var it = entries.iterator();
        var index: usize = 0;
        while (it.next()) |entry| {
            persisted[index] = .{
                .key = entry.key_ptr.*,
                .value = entry.value_ptr.value,
                .created_at_ns = entry.value_ptr.created_at_ns,
                .updated_at_ns = entry.value_ptr.updated_at_ns,
            };
            index += 1;
        }
        std.sort.block(PersistedSecret, persisted, {}, lessThanPersistedSecret);

        const encoded = try std.fmt.allocPrint(alloc, "{f}", .{
            std.json.fmt(PersistedSecretsFile{ .secrets = persisted }, .{}),
        });
        defer alloc.free(encoded);

        try ensureParentDirWithIo(self.io, self.path);
        // Resolve only for this write: renaming over the logical path would
        // replace its final symlink. Keep self.path unchanged for future reads
        // and resolve again on the next write after an external rotation.
        const write_path: ?[:0]u8 = std.Io.Dir.cwd().realPathFileAlloc(self.io, self.path, alloc) catch |err| switch (err) {
            error.FileNotFound => missing: {
                // A new regular store may be created, but a dangling symlink
                // must survive a missing target so a later rotation can recover.
                _ = std.Io.Dir.cwd().statFile(self.io, self.path, .{ .follow_symlinks = false }) catch |stat_err| switch (stat_err) {
                    error.FileNotFound => break :missing null,
                    else => return stat_err,
                };
                return err;
            },
            else => return err,
        };
        defer if (write_path) |path| alloc.free(path);
        try writeFileAtomicallyWithIo(self.io, write_path orelse self.path, encoded);
        var content_hash: [std.crypto.hash.sha2.Sha256.digest_length]u8 = undefined;
        std.crypto.hash.sha2.Sha256.hash(encoded, &content_hash, .{});
        return content_hash;
    }

    fn replaceEntriesAfterLocalWriteLocked(self: *FileStore, next: *std.StringArrayHashMapUnmanaged(StoredSecret), content_hash: [std.crypto.hash.sha2.Sha256.digest_length]u8) !void {
        self.replaceEntriesLocked(next);
        self.content_hash = content_hash;
        // Local API writes are not control-plane publications and therefore
        // must not preserve an acknowledgement generation from older bytes.
        self.source_generation = null;
        self.observed_metadata = try statFileMetadataWithIo(self.io, self.path);
        self.generation_value +%= 1;
        self.generation_snapshot.store(self.generation_value, .release);
        self.markReloadHealthyLocked(true);
    }

    fn replaceEntriesLocked(self: *FileStore, next: *std.StringArrayHashMapUnmanaged(StoredSecret)) void {
        deinitEntries(self.alloc, &self.entries);
        self.entries.deinit(self.alloc);
        self.entries = next.*;
        next.* = .{};
    }

    fn generationLocked(self: *FileStore) u64 {
        var out = self.generation_value;
        for (self.fallbacks, 1..) |*fallback, index| {
            out +%= fallback.generation() *% @as(u64, @intCast(index + 1));
        }
        return out;
    }

    fn lock(self: *FileStore) void {
        platform_sync.lockYielding(&self.mutex);
    }

    fn unlock(self: *FileStore) void {
        self.mutex.unlock();
    }

    fn healthSnapshotLocked(self: *FileStore) ReloadHealth {
        return .{
            .generation = self.generation_value,
            .content_hash = self.content_hash,
            .supports_source_generation = self.has_file and self.fallbacks.len == 0,
            .source_generation = if (self.fallbacks.len == 0) self.source_generation else null,
            .entry_count = self.entries.count(),
            .last_reload_failed = self.last_reload_failed,
            .stale_snapshot = self.last_reload_failed and self.observed_metadata != null,
            .reload_successes = self.reload_success_count,
            .reload_failures = self.reload_failure_count,
            .last_success_ns = self.last_success_ns,
            .last_failure_ns = self.last_failure_ns,
        };
    }

    fn markReloadHealthyLocked(self: *FileStore, count_success: bool) void {
        self.last_reload_failed = false;
        if (count_success) {
            self.reload_success_count +%= 1;
            self.last_success_ns = nowNsWithIo(self.io);
        }
    }

    fn markReloadFailedLocked(self: *FileStore) void {
        if (!self.last_reload_failed) self.reload_failure_count +%= 1;
        self.last_reload_failed = true;
        self.last_failure_ns = nowNsWithIo(self.io);
    }
};

const LoadedEntries = struct {
    entries: std.StringArrayHashMapUnmanaged(StoredSecret),
    content_hash: [std.crypto.hash.sha2.Sha256.digest_length]u8,
    source_generation: ?[std.crypto.hash.sha2.Sha256.digest_length]u8,
};

fn loadEntriesFromFile(alloc: std.mem.Allocator, path: []const u8) !LoadedEntries {
    return loadEntriesFromFileWithIo(alloc, std.Options.debug_io, path);
}

fn loadEntriesFromFileWithIo(alloc: std.mem.Allocator, io: std.Io, path: []const u8) !LoadedEntries {
    const raw = try readFileAllocWithIo(alloc, io, path);
    defer alloc.free(raw);
    var content_hash: [std.crypto.hash.sha2.Sha256.digest_length]u8 = undefined;
    std.crypto.hash.sha2.Sha256.hash(raw, &content_hash, .{});

    var parsed = try std.json.parseFromSlice(PersistedSecretsFile, alloc, raw, .{
        .allocate = .alloc_always,
        .ignore_unknown_fields = true,
    });
    defer parsed.deinit();

    const source_generation = if (parsed.value.generation) |encoded| blk: {
        if (encoded.len != std.crypto.hash.sha2.Sha256.digest_length * 2) return error.InvalidSecretStoreGeneration;
        var decoded: [std.crypto.hash.sha2.Sha256.digest_length]u8 = undefined;
        _ = std.fmt.hexToBytes(&decoded, encoded) catch return error.InvalidSecretStoreGeneration;
        break :blk decoded;
    } else null;

    var entries: std.StringArrayHashMapUnmanaged(StoredSecret) = .{};
    errdefer {
        deinitEntries(alloc, &entries);
        entries.deinit(alloc);
    }

    for (parsed.value.secrets) |item| {
        try validateKey(item.key);
        if (entries.contains(item.key)) return error.DuplicateSecretKey;
        const key = try alloc.dupe(u8, item.key);
        errdefer alloc.free(key);
        const value = try alloc.dupe(u8, item.value);
        errdefer alloc.free(value);
        try entries.put(alloc, key, .{
            .value = value,
            .created_at_ns = item.created_at_ns orelse 0,
            .updated_at_ns = item.updated_at_ns orelse item.created_at_ns orelse 0,
        });
    }

    return .{ .entries = entries, .content_hash = content_hash, .source_generation = source_generation };
}

fn cloneEntries(
    alloc: std.mem.Allocator,
    source: std.StringArrayHashMapUnmanaged(StoredSecret),
) !std.StringArrayHashMapUnmanaged(StoredSecret) {
    var out: std.StringArrayHashMapUnmanaged(StoredSecret) = .{};
    errdefer {
        deinitEntries(alloc, &out);
        out.deinit(alloc);
    }

    var it = source.iterator();
    while (it.next()) |entry| {
        const key = try alloc.dupe(u8, entry.key_ptr.*);
        errdefer alloc.free(key);
        const value = try alloc.dupe(u8, entry.value_ptr.value);
        errdefer alloc.free(value);
        try out.put(alloc, key, .{
            .value = value,
            .created_at_ns = entry.value_ptr.created_at_ns,
            .updated_at_ns = entry.value_ptr.updated_at_ns,
        });
    }
    return out;
}

fn deinitEntries(alloc: std.mem.Allocator, entries: *std.StringArrayHashMapUnmanaged(StoredSecret)) void {
    var it = entries.iterator();
    while (it.next()) |entry| {
        alloc.free(entry.key_ptr.*);
        entry.value_ptr.deinit(alloc);
    }
}

fn listedSecretsContain(items: []const ListedSecret, key: []const u8) bool {
    for (items) |item| {
        if (std.mem.eql(u8, item.key, key)) return true;
    }
    return false;
}

pub fn freeListedSecrets(alloc: std.mem.Allocator, items: []ListedSecret) void {
    for (items) |*item| item.deinit(alloc);
    alloc.free(items);
}

pub fn listEnvironmentSecrets(alloc: std.mem.Allocator) ![]ListedSecret {
    if (comptime (!builtin.link_libc or builtin.os.tag == .windows)) return try alloc.alloc(ListedSecret, 0);

    var out = std.ArrayList(ListedSecret).empty;
    errdefer {
        for (out.items) |*item| item.deinit(alloc);
        out.deinit(alloc);
    }

    var index: usize = 0;
    while (c_env.environ[index]) |entry_z| : (index += 1) {
        const entry = std.mem.span(entry_z);
        const eq = std.mem.indexOfScalar(u8, entry, '=') orelse continue;
        const env_var = entry[0..eq];
        const key = secretKeyForEnvVar(alloc, env_var) orelse continue;
        errdefer alloc.free(key);
        try out.append(alloc, .{
            .key = key,
            .status = .configured_env,
            .env_var = try alloc.dupe(u8, env_var),
        });
    }

    std.sort.block(ListedSecret, out.items, {}, lessThanListedSecret);
    return try out.toOwnedSlice(alloc);
}

pub fn envVarForKey(alloc: std.mem.Allocator, key: []const u8) ![]u8 {
    var out = try alloc.alloc(u8, key.len);
    for (key, 0..) |ch, i| {
        out[i] = switch (ch) {
            'a'...'z' => std.ascii.toUpper(ch),
            'A'...'Z', '0'...'9' => ch,
            '.', '-', ':' => '_',
            else => '_',
        };
    }
    return out;
}

pub fn parseSecretReference(raw: []const u8) ?[]const u8 {
    if (!std.mem.startsWith(u8, raw, "${secret:")) return null;
    if (raw.len < 11) return null;
    if (raw[raw.len - 1] != '}') return null;
    const key = raw[9 .. raw.len - 1];
    if (key.len == 0) return null;
    return key;
}

pub fn resolveReferenceOwned(
    alloc: std.mem.Allocator,
    secret_store: ?*FileStore,
    raw: []const u8,
) ![]u8 {
    const key = parseSecretReference(raw) orelse return try alloc.dupe(u8, raw);
    if (secret_store) |store| return try store.resolveValueOwned(alloc, raw);
    const env_var = try envVarForKey(alloc, key);
    defer alloc.free(env_var);
    return envValueOwned(alloc, env_var) orelse return error.SecretNotFound;
}

pub fn resolveReferenceWithGenerationOwned(
    alloc: std.mem.Allocator,
    secret_store: ?*FileStore,
    raw: []const u8,
) !ResolvedSecret {
    const key = parseSecretReference(raw) orelse return .{
        .value = try alloc.dupe(u8, raw),
        .generation = 0,
        .source = .literal,
    };
    if (secret_store) |store| return try store.getOwnedWithGeneration(alloc, key);
    const env_var = try envVarForKey(alloc, key);
    defer alloc.free(env_var);
    const value = envValueOwned(alloc, env_var) orelse return error.SecretNotFound;
    return .{
        .value = value,
        .generation = 0,
        .source = .env_var,
    };
}

pub fn validateKey(key: []const u8) !void {
    if (key.len == 0) return error.InvalidSecretKey;
    if (key[0] == '.' or key[key.len - 1] == '.') return error.InvalidSecretKey;
    var prev_dot = false;
    for (key) |ch| {
        switch (ch) {
            'a'...'z', 'A'...'Z', '0'...'9', '_', '-', '.' => {},
            else => return error.InvalidSecretKey,
        }
        if (ch == '.') {
            if (prev_dot) return error.InvalidSecretKey;
            prev_dot = true;
        } else {
            prev_dot = false;
        }
    }
}

fn secretKeyForEnvVar(alloc: std.mem.Allocator, env_var: []const u8) ?[]u8 {
    if (!std.mem.endsWith(u8, env_var, "_API_KEY")) return null;
    if (env_var.len <= "_API_KEY".len) return null;
    const prefix = env_var[0 .. env_var.len - "_API_KEY".len];
    var out = alloc.alloc(u8, prefix.len + ".api_key".len) catch return null;
    var index: usize = 0;
    for (prefix) |ch| {
        switch (ch) {
            'A'...'Z' => {
                out[index] = std.ascii.toLower(ch);
                index += 1;
            },
            '0'...'9' => {
                out[index] = ch;
                index += 1;
            },
            '_' => {
                out[index] = '.';
                index += 1;
            },
            else => {
                alloc.free(out);
                return null;
            },
        }
    }
    @memcpy(out[index .. index + ".api_key".len], ".api_key");
    index += ".api_key".len;
    return out[0..index];
}

fn hasEnvVar(env_var: []const u8) bool {
    if (!builtin.link_libc) return false;
    const env_var_z = std.heap.smp_allocator.dupeZ(u8, env_var) catch return false;
    defer std.heap.smp_allocator.free(env_var_z);
    return std.c.getenv(env_var_z.ptr) != null;
}

pub fn envValueOwned(alloc: std.mem.Allocator, env_var: []const u8) ?[]u8 {
    if (!builtin.link_libc) return null;
    const env_var_z = alloc.dupeZ(u8, env_var) catch return null;
    defer alloc.free(env_var_z);
    const raw = std.c.getenv(env_var_z.ptr) orelse return null;
    return alloc.dupe(u8, std.mem.span(raw)) catch null;
}

fn formatTimestampOwned(alloc: std.mem.Allocator, ns: u64) ![]u8 {
    const epoch_seconds = std.time.epoch.EpochSeconds{
        .secs = @divFloor(ns, std.time.ns_per_s),
    };
    const year_day = epoch_seconds.getEpochDay().calculateYearDay();
    const month_day = year_day.calculateMonthDay();
    const day_seconds = epoch_seconds.getDaySeconds();
    return try std.fmt.allocPrint(alloc, "{d:0>4}-{d:0>2}-{d:0>2}T{d:0>2}:{d:0>2}:{d:0>2}Z", .{
        year_day.year,
        month_day.month.numeric(),
        month_day.day_index + 1,
        day_seconds.getHoursIntoDay(),
        day_seconds.getMinutesIntoHour(),
        day_seconds.getSecondsIntoMinute(),
    });
}

fn readFileAlloc(alloc: std.mem.Allocator, path: []const u8) ![]u8 {
    return readFileAllocWithIo(alloc, std.Options.debug_io, path);
}

fn readFileAllocWithIo(alloc: std.mem.Allocator, io: std.Io, path: []const u8) ![]u8 {
    return std.Io.Dir.cwd().readFileAlloc(io, path, alloc, .limited(16 * 1024 * 1024));
}

fn statFileMetadata(path: []const u8) !?FileMetadata {
    return statFileMetadataWithIo(std.Options.debug_io, path);
}

fn statFileMetadataWithIo(io: std.Io, path: []const u8) !?FileMetadata {
    const stat = if (std.fs.path.isAbsolute(path)) blk: {
        var file = std.Io.Dir.openFileAbsolute(io, path, .{}) catch |err| switch (err) {
            error.FileNotFound => return null,
            else => return err,
        };
        defer file.close(io);
        break :blk try file.stat(io);
    } else std.Io.Dir.cwd().statFile(io, path, .{}) catch |err| switch (err) {
        error.FileNotFound => return null,
        else => return err,
    };
    return .{
        .inode = stat.inode,
        .size = stat.size,
        .mtime_ns = stat.mtime.toNanoseconds(),
    };
}

fn ensureParentDir(path: []const u8) !void {
    return ensureParentDirWithIo(std.Options.debug_io, path);
}

fn ensureParentDirWithIo(io: std.Io, path: []const u8) !void {
    const parent = std.fs.path.dirname(path) orelse return;
    try fs_paths.createDirPathPortable(io, parent);
}

fn writeFileAtomically(path: []const u8, contents: []const u8) !void {
    return writeFileAtomicallyWithIo(std.Options.debug_io, path, contents);
}

fn writeFileAtomicallyWithIo(io: std.Io, path: []const u8, contents: []const u8) !void {
    const tmp_path = try std.fmt.allocPrint(std.heap.page_allocator, "{s}.tmp-secrets-{d}", .{ path, nowNsWithIo(io) });
    defer std.heap.page_allocator.free(tmp_path);

    var tmp_exists = false;
    defer if (tmp_exists) deleteFileWithIo(io, tmp_path) catch {};

    {
        var file = try fs_paths.createFilePortable(io, tmp_path, .{ .truncate = true, .exclusive = true });
        tmp_exists = true;
        defer file.close(io);
        if (builtin.os.tag != .windows and builtin.os.tag != .wasi and builtin.os.tag != .freestanding) {
            try file.setPermissions(io, @enumFromInt(0o600));
        }
        var buf: [4096]u8 = undefined;
        var writer = file.writer(io, &buf);
        try writer.interface.writeAll(contents);
        try writer.end();
        try file.sync(io);
    }

    if (std.fs.path.isAbsolute(path)) {
        try std.Io.Dir.renameAbsolute(tmp_path, path, io);
    } else {
        try std.Io.Dir.rename(std.Io.Dir.cwd(), tmp_path, std.Io.Dir.cwd(), path, io);
    }
    tmp_exists = false;
    try fs_paths.syncDirPortable(io, std.fs.path.dirname(path) orelse ".");
}

fn deleteFile(path: []const u8) !void {
    try deleteFileWithIo(std.Options.debug_io, path);
}

fn deleteFileWithIo(io: std.Io, path: []const u8) !void {
    if (std.fs.path.isAbsolute(path)) {
        try std.Io.Dir.deleteFileAbsolute(io, path);
    } else {
        try std.Io.Dir.cwd().deleteFile(io, path);
    }
}

fn nowNs() u64 {
    return nowNsWithIo(std.Options.debug_io);
}

fn nowNsWithIo(io: std.Io) u64 {
    const now = std.Io.Timestamp.now(io, .awake);
    return @intCast(now.toNanoseconds());
}

fn wallClockNsWithIo(io: std.Io) u64 {
    const now = std.Io.Timestamp.now(io, .real);
    return @intCast(now.toNanoseconds());
}

fn lessThanListedSecret(_: void, lhs: ListedSecret, rhs: ListedSecret) bool {
    return std.mem.order(u8, lhs.key, rhs.key) == .lt;
}

fn lessThanPersistedSecret(_: void, lhs: PersistedSecret, rhs: PersistedSecret) bool {
    return std.mem.order(u8, lhs.key, rhs.key) == .lt;
}

test "file secret store persists values and overlays env status" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    var store = try FileStore.init(alloc, path);
    defer store.deinit();

    var entry = try store.put(alloc, "openai.api_key", "abc123");
    defer entry.deinit(alloc);
    try std.testing.expectEqual(SecretStatus.configured_file, entry.status);
    try std.testing.expectEqualStrings("OPENAI_API_KEY", entry.env_var.?);
    try std.testing.expect(entry.created_at != null);
    try std.testing.expect(entry.updated_at != null);
    try std.testing.expect(!std.mem.startsWith(u8, entry.created_at.?, "1970-"));
    try std.testing.expect(!std.mem.startsWith(u8, entry.updated_at.?, "1970-"));
    if (builtin.os.tag != .windows and builtin.os.tag != .wasi and builtin.os.tag != .freestanding) {
        var file = try std.Io.Dir.cwd().openFile(std.testing.io, path, .{ .mode = .read_only });
        defer file.close(std.testing.io);
        const stat = try file.stat(std.testing.io);
        try std.testing.expectEqual(@as(std.posix.mode_t, 0), stat.permissions.toMode() & 0o077);
    }

    const stored = try store.getOwned(alloc, "openai.api_key");
    defer if (stored) |value| alloc.free(value);
    try std.testing.expectEqualStrings("abc123", stored.?);

    var reloaded = try FileStore.init(alloc, path);
    defer reloaded.deinit();
    const reloaded_value = try reloaded.getOwned(alloc, "openai.api_key");
    defer if (reloaded_value) |value| alloc.free(value);
    try std.testing.expectEqualStrings("abc123", reloaded_value.?);

    const deleted = try reloaded.delete("openai.api_key");
    try std.testing.expect(deleted);
    try std.testing.expectEqual(@as(?[]u8, null), try reloaded.getOwned(alloc, "openai.api_key"));
}

test "file secret store reloads valid external replacements including deletions" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-reload-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"first","created_at_ns":1,"updated_at_ns":1},{"key":"deleted.dynamic_secret","value":"deleted","created_at_ns":1,"updated_at_ns":1}]}
    );

    var store = try FileStore.init(alloc, path);
    defer store.deinit();
    const initial_generation = store.generation();
    try std.testing.expectEqual(initial_generation, store.generationFast());

    const first = try store.getOwned(alloc, "openai.api_key");
    defer if (first) |value| alloc.free(value);
    try std.testing.expectEqualStrings("first", first.?);

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"second-longer","created_at_ns":1,"updated_at_ns":2}]}
    );

    const second = try store.getOwned(alloc, "openai.api_key");
    defer if (second) |value| alloc.free(value);
    try std.testing.expectEqualStrings("second-longer", second.?);
    try std.testing.expect(store.generation() == initial_generation + 1);

    const deleted = try store.getOwned(alloc, "deleted.dynamic_secret");
    defer if (deleted) |value| alloc.free(value);
    try std.testing.expectEqual(@as(?[]u8, null), deleted);
}

test "file secret store writes preserve symlinks across target rotation" {
    try @import("secret_projection_test_support.zig").expectRuntimeWrites(FileStore.initLayeredWithIo);
}

test "file secret store refuses to replace a dangling symlink" {
    const projection_test = @import("secret_projection_test_support.zig");
    const alloc = std.testing.allocator;
    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    var projection = try projection_test.Projection.init(io, "projected.primary");
    defer projection.deinit();
    var store = try FileStore.initWithIo(alloc, io, projection.path);
    defer store.deinit();
    try projection.tmp.dir.deleteFile(io, "..2026_01/secrets.json");
    try std.testing.expectError(error.FileNotFound, store.put(alloc, "projected.primary", "updated"));
    try std.testing.expectError(error.FileNotFound, store.delete("projected.primary"));
    try std.testing.expectEqual(.sym_link, (try projection.tmp.dir.statFile(io, "secrets.json", .{ .follow_symlinks = false })).kind);
    try projection_test.expectValue(&store, "projected.primary", "first");
    try projection.rotate();
    try projection_test.expectValue(&store, "projected.primary", "other");
}

test "file secret store detects projected volume symlink target replacement" {
    if (builtin.os.tag == .windows or builtin.os.tag == .wasi or builtin.os.tag == .freestanding) {
        return error.SkipZigTest;
    }

    const alloc = std.testing.allocator;
    const root = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-projected-{d}", .{nowNs()});
    defer alloc.free(root);

    var io_impl = std.Io.Threaded.init(alloc, .{});
    defer io_impl.deinit();
    const io = io_impl.io();
    defer std.Io.Dir.cwd().deleteTree(io, root) catch {};

    const first_dir = try std.fmt.allocPrint(alloc, "{s}/..2026_01", .{root});
    defer alloc.free(first_dir);
    const second_dir = try std.fmt.allocPrint(alloc, "{s}/..2026_02", .{root});
    defer alloc.free(second_dir);
    try fs_paths.createDirPathPortable(io, first_dir);
    try fs_paths.createDirPathPortable(io, second_dir);

    const first_path = try std.fmt.allocPrint(alloc, "{s}/secrets.json", .{first_dir});
    defer alloc.free(first_path);
    const second_path = try std.fmt.allocPrint(alloc, "{s}/secrets.json", .{second_dir});
    defer alloc.free(second_path);
    const first_json =
        \\{"generation":"aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa","secrets":[{"key":"openai.api_key","value":"first","created_at_ns":1,"updated_at_ns":1}]}
    ;
    const second_json =
        \\{"generation":"bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb","secrets":[{"key":"openai.api_key","value":"other","created_at_ns":1,"updated_at_ns":1}]}
    ;
    try std.testing.expectEqual(first_json.len, second_json.len);
    try writeFileAtomically(first_path, first_json);
    try writeFileAtomically(second_path, second_json);

    // Reproduce the hard case for size+mtime detection: projected generations
    // contain same-length values and carry the same modification timestamp.
    const first_stat = try std.Io.Dir.cwd().statFile(io, first_path, .{});
    try std.Io.Dir.cwd().setTimestamps(io, second_path, .{
        .modify_timestamp = .{ .new = first_stat.mtime },
    });

    const data_link = try std.fmt.allocPrint(alloc, "{s}/..data", .{root});
    defer alloc.free(data_link);
    const next_data_link = try std.fmt.allocPrint(alloc, "{s}/..data-next", .{root});
    defer alloc.free(next_data_link);
    const secret_link = try std.fmt.allocPrint(alloc, "{s}/secrets.json", .{root});
    defer alloc.free(secret_link);
    try std.Io.Dir.cwd().symLink(io, "..2026_01", data_link, .{ .is_directory = true });
    try std.Io.Dir.cwd().symLink(io, "..data/secrets.json", secret_link, .{});

    var store = try FileStore.init(alloc, secret_link);
    defer store.deinit();
    const initial_generation = store.generation();
    const initial_metadata = store.observed_metadata.?;

    try std.Io.Dir.cwd().symLink(io, "..2026_02", next_data_link, .{ .is_directory = true });
    try std.Io.Dir.rename(std.Io.Dir.cwd(), next_data_link, std.Io.Dir.cwd(), data_link, io);

    const replacement_metadata = (try statFileMetadata(secret_link)).?;
    try std.testing.expectEqual(initial_metadata.size, replacement_metadata.size);
    try std.testing.expectEqual(initial_metadata.mtime_ns, replacement_metadata.mtime_ns);
    try std.testing.expect(initial_metadata.inode != replacement_metadata.inode);

    const reloaded = try store.getOwned(alloc, "openai.api_key");
    defer if (reloaded) |value| alloc.free(value);
    try std.testing.expectEqualStrings("other", reloaded.?);
    try std.testing.expectEqual(initial_generation + 1, store.generation());
    const health = store.healthSnapshot();
    const expected_source_generation = [_]u8{0xbb} ** 32;
    try std.testing.expect(health.supports_source_generation);
    try std.testing.expectEqualSlices(u8, &expected_source_generation, &health.source_generation.?);
}

test "file secret store throttles cache-key freshness checks" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-throttle-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"first","created_at_ns":1,"updated_at_ns":1}]}
    );
    var store = try FileStore.init(alloc, path);
    defer store.deinit();
    const initial_generation = store.generation();
    try std.testing.expect(!(try store.refreshIfChangedThrottled(std.time.ns_per_hour)));

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"second-longer","created_at_ns":1,"updated_at_ns":2}]}
    );
    try std.testing.expect(!(try store.refreshIfChangedThrottled(std.time.ns_per_hour)));
    try std.testing.expectEqual(initial_generation, store.generation());

    try std.testing.expect(try store.refreshIfChanged());
    try std.testing.expectEqual(initial_generation + 1, store.generation());
    try std.testing.expectEqual(initial_generation + 1, store.generationFast());
}

test "file secret store keeps last known good snapshot for malformed and missing files" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-bad-reload-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    const stable_json =
        \\{"secrets":[{"key":"openai.api_key","value":"stable","created_at_ns":1,"updated_at_ns":1}]}
    ;
    try writeFileAtomically(path, stable_json);

    var store = try FileStore.init(alloc, path);
    defer store.deinit();
    const initial_health = store.healthSnapshot();

    try writeFileAtomically(path, "{not-json");
    const malformed_generation = store.generation();
    const after_malformed = try store.getOwned(alloc, "openai.api_key");
    defer if (after_malformed) |value| alloc.free(value);
    try std.testing.expectEqualStrings("stable", after_malformed.?);
    try std.testing.expect(store.reloadFailed());
    try std.testing.expectEqual(malformed_generation, store.generation());
    const malformed_health = store.healthSnapshot();
    try std.testing.expect(malformed_health.last_reload_failed);
    try std.testing.expect(malformed_health.stale_snapshot);
    try std.testing.expectEqual(@as(u64, 1), malformed_health.reload_failures);
    try std.testing.expect(malformed_health.last_failure_ns != 0);

    try deleteFile(path);
    const missing_generation = store.generation();
    const after_missing = try store.getOwned(alloc, "openai.api_key");
    defer if (after_missing) |value| alloc.free(value);
    try std.testing.expectEqualStrings("stable", after_missing.?);
    try std.testing.expect(store.reloadFailed());
    try std.testing.expectEqual(missing_generation, store.generation());
    const missing_health = store.healthSnapshot();
    try std.testing.expect(missing_health.last_reload_failed);
    try std.testing.expect(missing_health.stale_snapshot);
    try std.testing.expectEqual(@as(u64, 1), missing_health.reload_failures);
    try std.testing.expectEqualSlices(u8, &initial_health.content_hash, &missing_health.content_hash);

    // Recovery must be observable through status polling even before another
    // secret-backed request arrives. Restoring the exact original bytes also
    // verifies that a previous failure does not suppress revalidation.
    try writeFileAtomically(path, stable_json);
    store.next_throttled_refresh_ns.store(0, .release);
    const recovered_health = store.healthSnapshot();
    try std.testing.expect(!recovered_health.last_reload_failed);
    try std.testing.expect(!recovered_health.stale_snapshot);
    try std.testing.expectEqual(missing_generation + 1, recovered_health.generation);
    try std.testing.expectEqualSlices(u8, &initial_health.content_hash, &recovered_health.content_hash);
}

test "file secret store write refreshes first and preserves external keys" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-write-refresh-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    var store = try FileStore.init(alloc, path);
    defer store.deinit();

    var entry = try store.put(alloc, "openai.api_key", "first");
    defer entry.deinit(alloc);

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"external","created_at_ns":1,"updated_at_ns":2},{"key":"gemini.api_key","value":"gemini","created_at_ns":1,"updated_at_ns":1}]}
    );

    var updated = try store.put(alloc, "anthropic.api_key", "anthropic");
    defer updated.deinit(alloc);

    const openai = try store.getOwned(alloc, "openai.api_key");
    defer if (openai) |value| alloc.free(value);
    try std.testing.expectEqualStrings("external", openai.?);

    const gemini = try store.getOwned(alloc, "gemini.api_key");
    defer if (gemini) |value| alloc.free(value);
    try std.testing.expectEqualStrings("gemini", gemini.?);

    const anthropic = try store.getOwned(alloc, "anthropic.api_key");
    defer if (anthropic) |value| alloc.free(value);
    try std.testing.expectEqualStrings("anthropic", anthropic.?);
}

test "layered file secret store resolves primary before fallback and writes primary" {
    const alloc = std.testing.allocator;
    const primary_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-layer-primary-{d}.json", .{nowNs()});
    defer alloc.free(primary_path);
    defer deleteFile(primary_path) catch {};
    const fallback_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-layer-fallback-{d}.json", .{nowNs()});
    defer alloc.free(fallback_path);
    defer deleteFile(fallback_path) catch {};

    try writeFileAtomically(primary_path,
        \\{"generation":"aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa","secrets":[{"key":"openai.api_key","value":"primary-openai","created_at_ns":1,"updated_at_ns":1},{"key":"shared.key","value":"primary-shared","created_at_ns":1,"updated_at_ns":1}]}
    );
    try writeFileAtomically(fallback_path,
        \\{"secrets":[{"key":"antfly.runtime.test.token","value":"fallback-token","created_at_ns":1,"updated_at_ns":1},{"key":"shared.key","value":"fallback-shared","created_at_ns":1,"updated_at_ns":1}]}
    );

    var store = try FileStore.initLayered(alloc, &.{ primary_path, fallback_path });
    defer store.deinit();

    const health = store.healthSnapshot();
    try std.testing.expect(!health.supports_source_generation);
    try std.testing.expectEqual(@as(?[std.crypto.hash.sha2.Sha256.digest_length]u8, null), health.source_generation);

    const primary = try store.getOwned(alloc, "openai.api_key");
    defer if (primary) |value| alloc.free(value);
    try std.testing.expectEqualStrings("primary-openai", primary.?);

    const fallback = try store.getOwned(alloc, "antfly.runtime.test.token");
    defer if (fallback) |value| alloc.free(value);
    try std.testing.expectEqualStrings("fallback-token", fallback.?);

    const shared = try store.getOwned(alloc, "shared.key");
    defer if (shared) |value| alloc.free(value);
    try std.testing.expectEqualStrings("primary-shared", shared.?);

    var written = try store.put(alloc, "anthropic.api_key", "primary-write");
    defer written.deinit(alloc);

    var reloaded_primary = try FileStore.init(alloc, primary_path);
    defer reloaded_primary.deinit();
    const primary_write = try reloaded_primary.getOwned(alloc, "anthropic.api_key");
    defer if (primary_write) |value| alloc.free(value);
    try std.testing.expectEqualStrings("primary-write", primary_write.?);

    var reloaded_fallback = try FileStore.init(alloc, fallback_path);
    defer reloaded_fallback.deinit();
    const fallback_write = try reloaded_fallback.getOwned(alloc, "anthropic.api_key");
    defer if (fallback_write) |value| alloc.free(value);
    try std.testing.expectEqual(@as(?[]u8, null), fallback_write);
}

test "layered file secret store generation changes when fallback changes" {
    const alloc = std.testing.allocator;
    const primary_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-layer-generation-primary-{d}.json", .{nowNs()});
    defer alloc.free(primary_path);
    defer deleteFile(primary_path) catch {};
    const fallback_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-layer-generation-fallback-{d}.json", .{nowNs()});
    defer alloc.free(fallback_path);
    defer deleteFile(fallback_path) catch {};

    try writeFileAtomically(primary_path,
        \\{"secrets":[]}
    );
    try writeFileAtomically(fallback_path,
        \\{"secrets":[{"key":"antfly.runtime.test.token","value":"first","created_at_ns":1,"updated_at_ns":1}]}
    );

    var store = try FileStore.initLayered(alloc, &.{ primary_path, fallback_path });
    defer store.deinit();

    var first = try resolveReferenceWithGenerationOwned(alloc, &store, "${secret:antfly.runtime.test.token}");
    defer first.deinit(alloc);
    try std.testing.expectEqualStrings("first", first.value);

    try writeFileAtomically(fallback_path,
        \\{"secrets":[{"key":"antfly.runtime.test.token","value":"second","created_at_ns":1,"updated_at_ns":2}]}
    );

    var second = try resolveReferenceWithGenerationOwned(alloc, &store, "${secret:antfly.runtime.test.token}");
    defer second.deinit(alloc);
    try std.testing.expectEqualStrings("second", second.value);
    try std.testing.expect(second.generation > first.generation);
}

test "parse secret reference extracts key name" {
    try std.testing.expectEqualStrings("pg_dsn", parseSecretReference("${secret:pg_dsn}").?);
    try std.testing.expect(parseSecretReference("plain") == null);
    try std.testing.expect(parseSecretReference("${secret:}") == null);
}

test "secret value resolves file-backed references at request time" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secret-value-reload-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"first","created_at_ns":1,"updated_at_ns":1}]}
    );

    var store = try FileStore.init(alloc, path);
    defer store.deinit();

    var value = try SecretValue.initConfigOrEnv(alloc, "${secret:openai.api_key}", "OPENAI_API_KEY");
    defer value.deinit(alloc);

    const first = try value.resolveOwned(alloc, &store);
    defer if (first) |resolved| alloc.free(resolved);
    try std.testing.expectEqualStrings("first", first.?);

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"second-longer","created_at_ns":1,"updated_at_ns":2}]}
    );
    const second = try value.resolveOwned(alloc, &store);
    defer if (second) |resolved| alloc.free(resolved);
    try std.testing.expectEqualStrings("second-longer", second.?);

    try writeFileAtomically(path, "{\"secrets\":[]}");
    try std.testing.expectError(error.SecretNotFound, value.resolveOwned(alloc, &store));
}

test "secret resolution reports file generation for cache invalidation" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secret-generation-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"pg.dsn","value":"first","created_at_ns":1,"updated_at_ns":1}]}
    );

    var store = try FileStore.init(alloc, path);
    defer store.deinit();

    var first = try resolveReferenceWithGenerationOwned(alloc, &store, "${secret:pg.dsn}");
    defer first.deinit(alloc);
    try std.testing.expectEqualStrings("first", first.value);
    try std.testing.expectEqual(ResolvedSecretSource.file_store, first.source);
    try std.testing.expectEqual(store.generation(), first.generation);

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"pg.dsn","value":"second-longer","created_at_ns":1,"updated_at_ns":2}]}
    );

    var second = try resolveReferenceWithGenerationOwned(alloc, &store, "${secret:pg.dsn}");
    defer second.deinit(alloc);
    try std.testing.expectEqualStrings("second-longer", second.value);
    try std.testing.expectEqual(ResolvedSecretSource.file_store, second.source);
    try std.testing.expect(second.generation > first.generation);

    var literal = try resolveReferenceWithGenerationOwned(alloc, &store, "postgres://literal");
    defer literal.deinit(alloc);
    try std.testing.expectEqualStrings("postgres://literal", literal.value);
    try std.testing.expectEqual(ResolvedSecretSource.literal, literal.source);
    try std.testing.expectEqual(@as(u64, 0), literal.generation);
}

test "bearer auth header cache rebuilds on file generation change" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secret-auth-header-generation-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"first","created_at_ns":1,"updated_at_ns":1}]}
    );

    var store = try FileStore.init(alloc, path);
    defer store.deinit();

    var secret = try SecretValue.initConfig(alloc, "${secret:openai.api_key}") orelse return error.TestUnexpectedResult;
    defer secret.deinit(alloc);

    var cache = BearerAuthHeaderCache{};
    defer cache.deinit(alloc);

    const first = try cache.getOwned(alloc, alloc, &secret, &store);
    defer alloc.free(first);
    try std.testing.expectEqualStrings("Bearer first", first);
    const first_generation = cache.generation;

    const first_again = try cache.getOwned(alloc, alloc, &secret, &store);
    defer alloc.free(first_again);
    try std.testing.expectEqualStrings("Bearer first", first_again);
    try std.testing.expectEqual(first_generation, cache.generation);

    try writeFileAtomically(path,
        \\{"secrets":[{"key":"openai.api_key","value":"second","created_at_ns":1,"updated_at_ns":2}]}
    );

    const second = try cache.getOwned(alloc, alloc, &secret, &store);
    defer alloc.free(second);
    try std.testing.expectEqualStrings("Bearer second", second);
    try std.testing.expect(cache.generation > first_generation);
}

test "bearer auth header cache distinguishes credentials with equal generations" {
    const alloc = std.testing.allocator;
    var first = try SecretValue.initConfig(alloc, "first") orelse return error.TestUnexpectedResult;
    defer first.deinit(alloc);
    var second = try SecretValue.initConfig(alloc, "second") orelse return error.TestUnexpectedResult;
    defer second.deinit(alloc);
    var cache = BearerAuthHeaderCache{};
    defer cache.deinit(alloc);
    const before = try cache.getOwned(alloc, alloc, &first, null);
    defer alloc.free(before);
    const generation = cache.generation;
    const after = try cache.getOwned(alloc, alloc, &second, null);
    defer alloc.free(after);
    try std.testing.expectEqual(generation, cache.generation);
    try std.testing.expectEqualStrings("Bearer first", before);
    try std.testing.expectEqualStrings("Bearer second", after);
}

test "environment secret discovery maps API key env vars" {
    const alloc = std.testing.allocator;
    const env_var = try envVarForKey(alloc, "anthropic.api_key");
    defer alloc.free(env_var);
    try std.testing.expectEqualStrings("ANTHROPIC_API_KEY", env_var);
    const key = secretKeyForEnvVar(alloc, "ANTHROPIC_API_KEY").?;
    defer alloc.free(key);
    try std.testing.expectEqualStrings("anthropic.api_key", key);
}

test "file secret store configured secret sources preserve order and native ownership" {
    const alloc = std.testing.allocator;
    const native_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-native-{d}.json", .{nowNs()});
    defer alloc.free(native_path);
    defer deleteFile(native_path) catch {};
    const source_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-source-{d}.json", .{nowNs()});
    defer alloc.free(source_path);
    defer deleteFile(source_path) catch {};
    const other_path = try std.fmt.allocPrint(alloc, ".zig-cache/test-other-source-{d}.json", .{nowNs()});
    defer alloc.free(other_path);
    defer deleteFile(other_path) catch {};
    try writeFileAtomically(source_path,
        \\{"secrets":[{"key":"test.token","value":"external","created_at_ns":1,"updated_at_ns":1}]}
    );
    try writeFileAtomically(other_path,
        \\{"secrets":[{"key":"test.token","value":"lower-priority","created_at_ns":1,"updated_at_ns":1}]}
    );
    const sources = [_]Config.Source{
        .{ .name = "tenant", .type = .file, .path = source_path },
        .{ .name = "system", .type = .file, .path = other_path },
    };
    var store = try FileStore.initConfiguredWithIo(alloc, std.Options.debug_io, .{
        .native = .{ .path = native_path },
        .sources = &sources,
        .environment = false,
    });
    defer store.deinit();
    const first = (try store.getOwned(alloc, "test.token")).?;
    defer alloc.free(first);
    try std.testing.expectEqualStrings("external", first);
    try std.testing.expect(!try store.delete("test.token"));
    var written = try store.put(alloc, "test.token", "override");
    defer written.deinit(alloc);
    try std.testing.expect(written.managed);
    try std.testing.expectEqualStrings("native", written.source.?);
    const overridden = (try store.getOwned(alloc, "test.token")).?;
    defer alloc.free(overridden);
    try std.testing.expectEqualStrings("override", overridden);
    try std.testing.expect(try store.delete("test.token"));
    const listed = try store.list(alloc);
    defer freeListedSecrets(alloc, listed);
    try std.testing.expectEqual(@as(usize, 1), listed.len);
    try std.testing.expectEqualStrings("tenant", listed[0].source.?);
    try std.testing.expect(!listed[0].managed);
    try std.testing.expectError(error.WriteUnavailable, store.fallbacks[0].put(alloc, "test.token", "bad"));
    var readonly = try FileStore.initConfiguredWithIo(alloc, std.Options.debug_io, .{ .sources = &sources });
    defer readonly.deinit();
    try std.testing.expect(!readonly.writable);
    try std.testing.expectError(error.WriteUnavailable, readonly.put(alloc, "test.token", "bad"));
    try std.testing.expectError(error.WriteUnavailable, readonly.delete("test.token"));
    const fallback = try readonly.getOwnedWithGeneration(alloc, "test.token");
    defer alloc.free(fallback.value);
    try std.testing.expectEqualStrings("external", fallback.value);
    // Rotation still follows the external file and invalidates its generation.
    try writeFileAtomically(source_path, "{\"secrets\":[]}");
    const rotated = try readonly.getOwnedWithGeneration(alloc, "test.token");
    defer alloc.free(rotated.value);
    try std.testing.expectEqualStrings("lower-priority", rotated.value);
    try std.testing.expect(rotated.generation != fallback.generation);
}

test "file secret store configured secret environment defaults enabled and can be disabled without files" {
    const alloc = std.testing.allocator;
    var enabled = try FileStore.initConfiguredWithIo(alloc, std.Options.debug_io, .{});
    defer enabled.deinit();
    try std.testing.expect(enabled.environment_enabled);
    try std.testing.expect(!enabled.writable);
    var disabled = try FileStore.initConfiguredWithIo(alloc, std.Options.debug_io, .{ .environment = false });
    defer disabled.deinit();
    const listed = try disabled.list(alloc);
    defer freeListedSecrets(alloc, listed);
    try std.testing.expectEqual(@as(usize, 0), listed.len);
    try std.testing.expect((try disabled.getOwned(alloc, "path")) == null);
    try std.testing.expectError(error.SecretNotFound, disabled.getOwnedWithGeneration(alloc, "path"));
    const path = envValueOwned(alloc, "PATH") orelse return error.SkipZigTest;
    defer alloc.free(path);
    const resolved = (try enabled.getOwned(alloc, "path")).?;
    defer alloc.free(resolved);
    try std.testing.expectEqualStrings(path, resolved);
}

test "file secret store source configuration rejects ambiguity and bootstraps before reference resolution" {
    const alloc = std.testing.allocator;
    for ([_][]const u8{
        "null",
        "{\"environment\":null}",
        "{\"environment\":\"false\"}",
        "{\"files\":[]}",
        "{\"native\":{\"path\":\"${secret:path}\"}}",
        "{\"sources\":[{\"name\":\"environment\",\"type\":\"file\",\"path\":\"a\"}]}",
        "{\"sources\":[{\"name\":\"x\",\"type\":\"vault\",\"path\":\"a\"}]}",
        "{\"sources\":[{\"name\":\"x\",\"type\":\"file\",\"path\":\"a\"},{\"name\":\"x\",\"type\":\"file\",\"path\":\"b\"}]}",
        "{\"native\":{\"path\":\"a\"},\"sources\":[{\"name\":\"native\",\"type\":\"file\",\"path\":\"b\"}]}",
        "{\"native\":{\"path\":\"a\"},\"sources\":[{\"name\":\"external\",\"type\":\"file\",\"path\":\"a\"}]}",
    }) |raw| {
        var tree = try std.json.parseFromSlice(std.json.Value, alloc, raw, .{});
        defer tree.deinit();
        try std.testing.expectError(error.InvalidConfig, parseConfig(alloc, tree.value));
    }
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-secrets-config-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};
    try writeFileAtomically(path, "{\"secrets\":{\"environment\":false},\"unresolved\":\"${secret:missing}\"}");
    var store = (try initFromConfigPathWithIo(alloc, std.Options.debug_io, path, &.{})).?;
    defer store.deinit();
    try std.testing.expect(!store.environment_enabled);
    try std.testing.expectError(error.InvalidConfig, initFromConfigPathWithIo(alloc, std.Options.debug_io, path, &.{"legacy.json"}));
    try writeFileAtomically(path, "{}");
    try std.testing.expect((try initFromConfigPathWithIo(alloc, std.Options.debug_io, path, &.{})) == null);
}

test "file secret store rejects canonical native source aliases including missing destinations" {
    const alloc = std.testing.allocator;
    const io = std.Options.debug_io;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const dir = try tmp.dir.realPathFileAlloc(io, ".", alloc);
    defer alloc.free(dir);
    const relative = try std.fmt.allocPrint(alloc, ".zig-cache/tmp/{s}/external.json", .{tmp.sub_path});
    defer alloc.free(relative);
    const dotted = try std.fmt.allocPrint(alloc, "./{s}", .{relative});
    defer alloc.free(dotted);
    const absolute = try std.fs.path.join(alloc, &.{ dir, "external.json" });
    defer alloc.free(absolute);
    const parent_alias = try std.fs.path.join(alloc, &.{ dir, "child", "..", "external.json" });
    defer alloc.free(parent_alias);
    try tmp.dir.createDir(io, "child", .default_dir);
    for ([_]bool{ false, true }) |exists| {
        if (exists) try tmp.dir.writeFile(io, .{ .sub_path = "external.json", .data = "{\"secrets\":[{\"key\":\"test.token\",\"value\":\"external\"}]}" });
        for ([_][]const u8{ dotted, absolute, parent_alias }) |alias| {
            try std.testing.expectError(error.InvalidConfig, FileStore.initConfiguredWithIo(alloc, io, .{
                .native = .{ .path = alias },
                .sources = &.{.{ .name = "external", .type = .file, .path = relative }},
            }));
        }
    }
    var external = try FileStore.initWithIo(alloc, io, relative);
    defer external.deinit();
    const value = (try external.getOwned(alloc, "test.token")).?;
    defer alloc.free(value);
    try std.testing.expectEqualStrings("external", value);
}

test "file secret store rejects symlink aliases at startup and after source replacement" {
    if (builtin.os.tag == .windows or builtin.os.tag == .wasi) return error.SkipZigTest;
    const alloc = std.testing.allocator;
    const io = std.Options.debug_io;
    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const dir = try tmp.dir.realPathFileAlloc(io, ".", alloc);
    defer alloc.free(dir);
    const path = try std.fs.path.join(alloc, &.{ dir, "external.json" });
    defer alloc.free(path);
    const link = try std.fs.path.join(alloc, &.{ dir, "link.json" });
    defer alloc.free(link);
    const directory_link = try std.fs.path.join(alloc, &.{ dir, "directory-link", "missing", "external.json" });
    defer alloc.free(directory_link);
    const directory_target = try std.fs.path.join(alloc, &.{ dir, "missing", "external.json" });
    defer alloc.free(directory_target);
    try tmp.dir.symLink(io, ".", "directory-link", .{ .is_directory = true });
    try std.testing.expectError(error.InvalidConfig, FileStore.initConfiguredWithIo(alloc, io, .{
        .native = .{ .path = directory_target },
        .sources = &.{.{ .name = "external", .type = .file, .path = directory_link }},
    }));
    try tmp.dir.symLink(io, "external.json", "link.json", .{});
    for ([_]bool{ false, true }) |exists| {
        if (exists) try tmp.dir.writeFile(io, .{ .sub_path = "external.json", .data = "{\"secrets\":[]}" });
        try std.testing.expectError(error.InvalidConfig, FileStore.initConfiguredWithIo(alloc, io, .{
            .native = .{ .path = path },
            .sources = &.{.{ .name = "external", .type = .file, .path = link }},
        }));
    }
    try tmp.dir.deleteFile(io, "link.json");
    try tmp.dir.writeFile(io, .{ .sub_path = "link.json", .data = "{\"secrets\":[]}" });
    var store = try FileStore.initConfiguredWithIo(alloc, io, .{
        .native = .{ .path = path },
        .sources = &.{.{ .name = "external", .type = .file, .path = link }},
    });
    defer store.deinit();
    var added = try store.put(alloc, "test.token", "preserved");
    defer added.deinit(alloc);
    try tmp.dir.deleteFile(io, "link.json");
    try tmp.dir.symLink(io, "external.json", "link.json", .{});
    try std.testing.expectError(error.InvalidConfig, store.put(alloc, "test.token", "bad"));
    try std.testing.expectError(error.InvalidConfig, store.delete("test.token"));
    var external = try FileStore.initWithIo(alloc, io, path);
    defer external.deinit();
    const value = (try external.getOwned(alloc, "test.token")).?;
    defer alloc.free(value);
    try std.testing.expectEqualStrings("preserved", value);
}

test "bearer auth header cache provider defaults use canonical secrets and observe rotation" {
    const alloc = std.testing.allocator;
    const path = try std.fmt.allocPrint(alloc, ".zig-cache/test-provider-defaults-{d}.json", .{nowNs()});
    defer alloc.free(path);
    defer deleteFile(path) catch {};
    var store = try FileStore.init(alloc, path);
    defer store.deinit();
    inline for (.{ "OPENAI_API_KEY", "OPENROUTER_API_KEY", "GEMINI_API_KEY", "COHERE_API_KEY", "ANTFLY_INFERENCE_API_KEY" }) |env_name| {
        var value = try SecretValue.initConfigOrProviderDefault(alloc, null, env_name);
        defer value.deinit(alloc);
        const expected_env = envValueOwned(alloc, env_name);
        defer if (expected_env) |env| alloc.free(env);
        const without_store = try value.resolveOwned(alloc, null);
        defer if (without_store) |env| alloc.free(env);
        if (expected_env) |env| {
            try std.testing.expectEqualStrings(env, without_store.?);
        } else {
            try std.testing.expectEqual(@as(?[]u8, null), without_store);
        }
        var entry = try store.put(alloc, value.provider_default, "first");
        entry.deinit(alloc);
        var cache = BearerAuthHeaderCache{};
        defer cache.deinit(alloc);
        const first = try cache.getOwned(alloc, alloc, &value, &store);
        defer alloc.free(first);
        try std.testing.expectEqualStrings("Bearer first", first);
        var updated = try store.put(alloc, value.provider_default, "second");
        updated.deinit(alloc);
        const second = try cache.getOwned(alloc, alloc, &value, &store);
        defer alloc.free(second);
        try std.testing.expectEqualStrings("Bearer second", second);
        var explicit = try SecretValue.initConfigOrProviderDefault(alloc, "explicit", env_name);
        defer explicit.deinit(alloc);
        const resolved = (try explicit.resolveOwned(alloc, &store)).?;
        defer alloc.free(resolved);
        try std.testing.expectEqualStrings("explicit", resolved);
    }
    var missing = try SecretValue.initConfigOrProviderDefault(alloc, null, "ANTFLY_TEST_MISSING_API_KEY");
    defer missing.deinit(alloc);
    try std.testing.expectEqual(@as(?[]u8, null), try missing.resolveOwned(alloc, &store));
    try std.testing.expectError(error.SecretNotFound, missing.resolveOwnedWithGeneration(alloc, &store));
    var explicit_missing = try SecretValue.initConfigOrProviderDefault(alloc, "${secret:antfly.test.missing.api_key}", "OPENAI_API_KEY");
    defer explicit_missing.deinit(alloc);
    try std.testing.expectError(error.SecretNotFound, explicit_missing.resolveOwned(alloc, &store));
}
