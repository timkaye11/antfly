// Copyright 2026 Antfly, Inc.
//
// Licensed under the Elastic License 2.0 (ELv2); you may not use this file
// except in compliance with the Elastic License 2.0. You may obtain a copy of
// the Elastic License 2.0 at
//
//     https://www.antfly.io/licensing/ELv2-license
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the Elastic License 2.0 is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
// Elastic License 2.0 for the specific language governing permissions and
// limitations.

const std = @import("std");
const raft_engine = @import("raft_engine");
const catalog = @import("../catalog.zig");
const host = @import("../host.zig");
const state_machine = @import("../state_machine/mod.zig");
const storage_mod = @import("mod.zig");
const replica_state = @import("replica_state.zig");

pub const PersistentReplicaProviderConfig = struct {
    root_dir: []const u8,
};

pub const PersistentReplicaProvider = struct {
    alloc: std.mem.Allocator,
    cfg: PersistentReplicaProviderConfig,
    root_dir: []u8,
    base_factory: host.ReplicaDescriptorFactory,
    states: std.AutoHashMapUnmanaged(u64, *replica_state.PersistentReplicaState) = .empty,

    pub fn init(
        alloc: std.mem.Allocator,
        cfg: PersistentReplicaProviderConfig,
        base_factory: host.ReplicaDescriptorFactory,
    ) !PersistentReplicaProvider {
        return .{
            .alloc = alloc,
            .cfg = cfg,
            .root_dir = try alloc.dupe(u8, cfg.root_dir),
            .base_factory = base_factory,
        };
    }

    pub fn deinit(self: *PersistentReplicaProvider) void {
        var it = self.states.valueIterator();
        while (it.next()) |state| {
            state.*.deinit();
            self.alloc.destroy(state.*);
        }
        self.states.deinit(self.alloc);
        self.alloc.free(self.root_dir);
        self.* = undefined;
    }

    pub fn descriptorFactory(self: *PersistentReplicaProvider) host.ReplicaDescriptorFactory {
        return .{
            .ptr = self,
            .vtable = &.{
                .build_descriptor = buildDescriptor,
                .free_descriptor = freeDescriptor,
                .accepts_record = acceptsRecord,
            },
        };
    }

    pub fn runtimeHooks(self: *PersistentReplicaProvider) host.RuntimeHooks {
        return .{
            .group_storage = .{
                .ptr = self,
                .vtable = &.{
                    .persist_ready = persistReady,
                    .compact_snapshot = compactSnapshot,
                    .compact_snapshot_artifact = compactSnapshotArtifact,
                    .retire_group = retireGroup,
                },
            },
        };
    }

    pub fn appliedIndexSink(self: *PersistentReplicaProvider) state_machine.AppliedIndexSink {
        return .{
            .ptr = self,
            .vtable = &.{
                .set_applied_index = setAppliedIndex,
            },
        };
    }

    pub fn stateForGroup(self: *PersistentReplicaProvider, group_id: u64) ?*replica_state.PersistentReplicaState {
        return self.states.get(group_id);
    }

    fn buildDescriptor(ptr: *anyopaque, record: catalog.ReplicaRecord) !raft_engine.runtime.ReplicaDescriptor {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        const state = try self.ensureState(record);
        // Snapshot bootstrap is a one-shot recovery source. Once this exact
        // local replica path has durable Raft state, restart must use it rather
        // than depend on a remote artifact that was released after the first
        // successful fetch. Empty state still preserves the original source.
        var effective_record = record;
        if (try state.storage().lastIndex() > 0) {
            effective_record.bootstrap_mode = .persisted;
            effective_record.snapshot_bootstrap = null;
        }
        var desc = try self.base_factory.buildDescriptor(effective_record);
        errdefer self.base_factory.freeDescriptor(self.alloc, &desc);
        try state.seedConfStateIfEmpty(desc.initial_voters orelse desc.group.raft_config.peers);
        desc.group.storage = state.storage();
        desc.group.raft_config.applied = state.appliedIndex();
        return desc;
    }

    fn acceptsRecord(ptr: *anyopaque, record: catalog.ReplicaRecord) bool {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        return self.base_factory.acceptsRecord(record);
    }

    fn freeDescriptor(ptr: *anyopaque, alloc: std.mem.Allocator, desc: *raft_engine.runtime.ReplicaDescriptor) void {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        self.base_factory.freeDescriptor(alloc, desc);
    }

    fn persistReady(ptr: *anyopaque, group_id: u64, ready: raft_engine.core.Ready) !void {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        const state = self.states.get(group_id) orelse return error.UnknownGroup;
        try state.groupStorage().persistReady(group_id, ready);
    }

    fn compactSnapshot(ptr: *anyopaque, group_id: u64, snapshot: raft_engine.core.types.Snapshot, compact_index: u64) !void {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        const state = self.states.get(group_id) orelse return error.UnknownGroup;
        try state.groupStorage().compactSnapshot(group_id, snapshot, compact_index);
    }

    fn compactSnapshotArtifact(
        ptr: *anyopaque,
        group_id: u64,
        metadata: raft_engine.core.types.SnapshotMetadata,
        artifact: raft_engine.runtime.storage_iface.SnapshotArtifact,
        compact_index: u64,
    ) !void {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        const state = self.states.get(group_id) orelse return error.UnknownGroup;
        try state.groupStorage().compactSnapshotArtifact(self.alloc, group_id, metadata, artifact, compact_index);
    }

    fn setAppliedIndex(
        ptr: *anyopaque,
        group_id: raft_engine.core.types.GroupId,
        index: raft_engine.core.types.Index,
    ) !void {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        const state = self.states.get(group_id) orelse return error.UnknownGroup;
        try state.setAppliedIndex(index);
    }

    fn retireGroup(ptr: *anyopaque, group_id: u64) void {
        const self: *PersistentReplicaProvider = @ptrCast(@alignCast(ptr));
        const removed = self.states.fetchRemove(group_id) orelse {
            return;
        };
        removed.value.deinit();
        self.alloc.destroy(removed.value);
    }

    fn ensureState(self: *PersistentReplicaProvider, record: catalog.ReplicaRecord) !*replica_state.PersistentReplicaState {
        if (self.states.get(record.group_id)) |state| return state;

        var layout = try storage_mod.ReplicaPathLayout.initForLocalNode(self.alloc, self.root_dir, record.group_id, record.local_node_id);
        defer layout.deinit(self.alloc);

        const state = try self.alloc.create(replica_state.PersistentReplicaState);
        errdefer self.alloc.destroy(state);
        state.* = try replica_state.PersistentReplicaState.init(self.alloc, layout);
        errdefer state.deinit();

        try self.states.put(self.alloc, record.group_id, state);
        return state;
    }
};

test "persistent replica provider wires host through persisted local state" {
    const BaseFactory = struct {
        alloc: std.mem.Allocator,
        dummy_store: *raft_engine.core.MemoryStorage,

        fn iface(self: *@This()) host.ReplicaDescriptorFactory {
            return .{
                .ptr = self,
                .vtable = &.{
                    .build_descriptor = buildDescriptor,
                    .free_descriptor = freeDescriptor,
                },
            };
        }

        fn buildDescriptor(ptr: *anyopaque, record: catalog.ReplicaRecord) !raft_engine.runtime.ReplicaDescriptor {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            const peers = try self.alloc.dupe(u64, &.{record.local_node_id});
            return .{
                .group = .{
                    .group_id = record.group_id,
                    .local_node_id = record.local_node_id,
                    .raft_config = .{
                        .id = record.local_node_id,
                        .group_id = record.group_id,
                        .peers = peers,
                        .election_tick = 5,
                        .heartbeat_tick = 1,
                        .pre_vote = false,
                        .check_quorum = true,
                    },
                    .storage = self.dummy_store.storage(),
                },
                .bootstrap = .persisted,
            };
        }

        fn freeDescriptor(ptr: *anyopaque, alloc: std.mem.Allocator, desc: *raft_engine.runtime.ReplicaDescriptor) void {
            const self: *@This() = @ptrCast(@alignCast(ptr));
            _ = alloc;
            self.alloc.free(desc.group.raft_config.peers);
        }
    };

    var tmp = std.testing.tmpDir(.{});
    defer tmp.cleanup();
    const root = try std.fmt.allocPrint(std.testing.allocator, ".zig-cache/tmp/{s}/provider", .{tmp.sub_path});
    defer std.testing.allocator.free(root);

    var dummy_store = raft_engine.core.MemoryStorage.init(std.testing.allocator);
    defer dummy_store.deinit();
    var base_factory = BaseFactory{ .alloc = std.testing.allocator, .dummy_store = &dummy_store };
    {
        var provider = try PersistentReplicaProvider.init(std.testing.allocator, .{ .root_dir = root }, base_factory.iface());
        defer provider.deinit();

        var local_host = host.Host.init(std.testing.allocator, .{ .local_node_id = 1 }, .{
            .descriptor_factory = provider.descriptorFactory(),
            .runtime_hooks = provider.runtimeHooks(),
        });
        defer local_host.deinit();

        _ = try local_host.ensureReplica(.{
            .group_id = 401,
            .replica_id = 1,
            .local_node_id = 1,
            .bootstrap_mode = .persisted,
        });
        try local_host.campaignGroup(401);
        _ = try local_host.runRound(1, 1);
        try local_host.propose(401, "persisted");
        _ = try local_host.runRound(1, 1);

        const state = provider.stateForGroup(401) orelse return error.MissingState;
        try std.testing.expect((try state.storage().lastIndex()) >= 1);

        _ = try local_host.ensureReplica(.{
            .group_id = 401,
            .replica_id = 2,
            .local_node_id = 1,
            .bootstrap_mode = .persisted,
        });
        try local_host.removeReplica(401);
        try std.testing.expect(provider.stateForGroup(401) == null);
        _ = try local_host.ensureReplica(.{
            .group_id = 401,
            .replica_id = 2,
            .local_node_id = 1,
            .bootstrap_mode = .persisted,
        });
        const replacement = provider.stateForGroup(401) orelse return error.MissingState;
        try std.testing.expect((try replacement.storage().lastIndex()) >= 1);
    }

    {
        var provider = try PersistentReplicaProvider.init(std.testing.allocator, .{ .root_dir = root }, base_factory.iface());
        defer provider.deinit();

        var local_host = host.Host.init(std.testing.allocator, .{ .local_node_id = 1 }, .{
            .descriptor_factory = provider.descriptorFactory(),
            .runtime_hooks = provider.runtimeHooks(),
        });
        defer local_host.deinit();

        _ = try local_host.ensureReplica(.{
            .group_id = 401,
            .replica_id = 1,
            .local_node_id = 1,
            .bootstrap_mode = .persisted,
        });

        const state = provider.stateForGroup(401) orelse return error.MissingState;
        try std.testing.expect((try state.storage().lastIndex()) >= 1);
    }
}
