// Copyright 2026 Antfly, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

const std = @import("std");
const Allocator = std.mem.Allocator;
const types = @import("types.zig");
const search_results = @import("search_results.zig");

pub const CancellationToken = struct {
    ptr: *const anyopaque,
    is_cancelled_fn: *const fn (*const anyopaque) bool,

    pub fn fromAtomic(signal: *const std.atomic.Value(bool)) CancellationToken {
        return .{
            .ptr = signal,
            .is_cancelled_fn = struct {
                fn call(raw: *const anyopaque) bool {
                    const value: *const std.atomic.Value(bool) = @ptrCast(@alignCast(raw));
                    return value.load(.acquire);
                }
            }.call,
        };
    }

    pub fn fromCallback(
        ptr: ?*const anyopaque,
        is_cancelled_fn: ?*const fn (*const anyopaque) bool,
    ) ?CancellationToken {
        return .{
            .ptr = ptr orelse return null,
            .is_cancelled_fn = is_cancelled_fn orelse return null,
        };
    }

    pub fn isCancelled(self: CancellationToken) bool {
        return self.is_cancelled_fn(self.ptr);
    }
};

/// One scored posting in the flat centroid frontier. This lives in the shared
/// search types module so SearchScratch can own both the frontier and its merge
/// workspace without introducing an import cycle with spfresh_index.
pub const FlatCentroidProbe = struct {
    posting_id: u64,
    distance: f32,
    error_bound: f32,
    member_lower_bound: f32 = -std.math.inf(f32),
    bound_resolved: bool = false,
};

pub const SearchResult = search_results.SearchResult;
pub const SearchResults = search_results.SearchResults;

pub const SearchRequest = struct {
    query: []const f32,
    k: usize,
    rerank_k: ?usize = null,
    /// Normalized public search effort. The storage layer may still provide a
    /// tuned search_width, but HBC owns topology-sensitive semantics such as
    /// effort 1.0 exhausting the published search snapshot.
    search_effort: ?f32 = null,
    search_width: ?u32 = null,
    epsilon: ?f32 = null,
    // Multiplier on k for how many approximate candidates are retained for
    // exact reranking. Defaults to the epsilon-derived legacy factor.
    rerank_factor: ?usize = null,
    load_metadata: bool = true,
    filter_prefix: []const u8 = "",
    distance_over: ?f32 = null,
    distance_under: ?f32 = null,
    filter_ids: []const u64 = &.{},
    exclude_ids: []const u64 = &.{},
    /// Borrowed request-lifecycle signal. Search loops poll this at bounded
    /// intervals so a disconnected caller does not retain query capacity.
    cancellation: ?CancellationToken = null,
};

pub fn normalizedSearchEffort(req: SearchRequest) ?f32 {
    const effort = req.search_effort orelse return null;
    if (std.math.isNan(effort)) return null;
    return std.math.clamp(effort, 0, 1);
}

/// Controls whether search may tolerate a partially readable index snapshot.
/// Approximate requests preserve availability by skipping missing artifacts;
/// full-effort requests must either cover the published snapshot or fail.
pub const CoveragePolicy = enum {
    best_effort,
    complete_snapshot,
};

pub fn coveragePolicy(req: SearchRequest) CoveragePolicy {
    return if ((normalizedSearchEffort(req) orelse return .best_effort) >= 1)
        .complete_snapshot
    else
        .best_effort;
}

pub fn requiresExhaustiveCoverage(req: SearchRequest) bool {
    return coveragePolicy(req) == .complete_snapshot;
}

pub fn checkCancelled(req: SearchRequest) !void {
    if (req.cancellation) |cancellation| {
        if (cancellation.isCancelled()) return error.Cancelled;
    }
}

pub const SearchProfile = struct {
    total_ns: u64 = 0,
    setup_ns: u64 = 0,
    runtime_txn_ns: u64 = 0,
    scratch_acquire_ns: u64 = 0,
    root_load_ns: u64 = 0,
    node_cache_lookup_ns: u64 = 0,
    node_cache_miss_ns: u64 = 0,
    node_cache_misses: u64 = 0,
    quantized_cache_lookup_ns: u64 = 0,
    quantized_cache_miss_ns: u64 = 0,
    quantized_cache_misses: u64 = 0,
    child_expand_ns: u64 = 0,
    leaf_score_ns: u64 = 0,
    rerank_ns: u64 = 0,
    rerank_prepare_ns: u64 = 0,
    rerank_select_positions_ns: u64 = 0,
    rerank_vector_load_ns: u64 = 0,
    rerank_prefetch_ns: u64 = 0,
    rerank_metadata_lookup_ns: u64 = 0,
    /// Vector-to-document metadata rows requested after decoded-cache probing.
    /// Warm decoded vectors must not contribute to this count.
    rerank_metadata_vectors_loaded: u64 = 0,
    rerank_artifact_key_ns: u64 = 0,
    rerank_artifact_read_ns: u64 = 0,
    rerank_artifact_decode_ns: u64 = 0,
    rerank_artifact_distance_ns: u64 = 0,
    rerank_lsm_cache_hits: u64 = 0,
    rerank_lsm_cache_misses: u64 = 0,
    /// Candidates scored without reading an external artifact. This includes
    /// governed decoded residency and a request-local hit when a caller has a
    /// legitimate reuse opportunity.
    rerank_artifact_cache_hits: u64 = 0,
    /// External artifact values requested, including missing/corrupt values.
    /// Unlike LSM block-cache counters, this is a per-vector work signal.
    rerank_artifact_vectors_loaded: u64 = 0,
    vector_cache_hits: u64 = 0,
    vector_cache_misses: u64 = 0,
    metadata_cache_hits: u64 = 0,
    metadata_cache_misses: u64 = 0,
    metadata_cache_miss_ns: u64 = 0,
    rerank_vector_view_ns: u64 = 0,
    rerank_distance_ns: u64 = 0,
    rerank_apply_ns: u64 = 0,
    rerank_resort_ns: u64 = 0,
    rerank_batches: u64 = 0,
    rerank_max_batch_size: u64 = 0,
    rerank_candidates_skipped_by_bound: u64 = 0,
    rerank_finalize_ns: u64 = 0,
    rerank_metadata_ns: u64 = 0,
    filter_candidates: u64 = 0,
    filter_rejected: u64 = 0,
    filter_metadata_batches: u64 = 0,
    filter_metadata_batch_ns: u64 = 0,
    traversal_waves: u64 = 0,
    traversal_initial_wave_leaves: u64 = 0,
    traversal_max_wave_leaves: u64 = 0,
    traversal_bound_resolutions: u64 = 0,
    traversal_bound_fallbacks: u64 = 0,
    traversal_bound_stops: u64 = 0,
    traversal_yield_stops: u64 = 0,
    traversal_frontier_remaining: u64 = 0,
    traversal_eligible_vectors: u64 = 0,
    traversal_stop_lower_bound: f32 = 0,
    traversal_stop_result_upper_bound: f32 = 0,
    nodes_visited: u64 = 0,
    leaves_explored: u64 = 0,
    approx_nodes_expanded: u64 = 0,
    approx_leaves_scored: u64 = 0,
    approx_vectors_scored: u64 = 0,
    exact_vectors_scored: u64 = 0,
    // Leaves that fell back to exact member scoring because their quantized
    // payload was stale (payload_dirty) or absent from storage.
    leaf_payload_stale: u64 = 0,
    leaf_payload_missing: u64 = 0,
    reranked_vectors: u64 = 0,
    approx_candidate_count: u64 = 0,
    rerank_candidate_count: u64 = 0,
    ambiguous_top_k_pairs: u64 = 0,
    ambiguous_boundary_pairs: u64 = 0,
    ambiguous_distance_over_hits: u64 = 0,
    ambiguous_distance_under_hits: u64 = 0,
    full_rerank_due_to_threshold: bool = false,
    top_k_count: u64 = 0,
    min_distance_gap_top_k: f32 = 0,
    min_interval_gap_top_k: f32 = 0,
    closest_pair_top_k: ?DebugPair = null,
    boundary_pair: ?DebugPair = null,
    boundary_tail_error_avg: f32 = 0,
    boundary_tail_error_max: f32 = 0,
    boundary_tail_distance_gap_avg: f32 = 0,
    boundary_tail_distance_gap_min: f32 = 0,
    boundary_tail_distance_gap_max: f32 = 0,
    boundary_tail_interval_gap_avg: f32 = 0,
    boundary_tail_interval_gap_min: f32 = 0,
    boundary_tail_interval_gap_max: f32 = 0,
    approx_top_count: u64 = 0,
    approx_top: [5]DebugHit = .{ .{}, .{}, .{}, .{}, .{} },
};

pub const ProfiledSearchResults = struct {
    results: SearchResults,
    profile: SearchProfile,
};

pub const DebugLeafScore = struct {
    vector_id: u64,
    approx_distance: f32,
    error_bound: f32,
    exact_distance: f32,
};

pub const DebugHit = struct {
    id: u64 = 0,
    distance: f32 = 0,
    error_bound: f32 = 0,
    lower_bound: f32 = 0,
    upper_bound: f32 = 0,
};

pub const DebugPair = struct {
    left: DebugHit = .{},
    right: DebugHit = .{},
    distance_gap: f32 = 0,
    interval_gap: f32 = 0,
    overlaps: bool = false,
};

pub const DebugNodeDistance = struct {
    node_id: u64,
    distance: f32,
};

pub const IndexStats = struct {
    dims: u32,
    active_count: u64,
    node_count: u64,
    root_node: u64,
    branching_factor: u32,
    leaf_size: u32,
};

pub const HBCDebugNode = struct {
    id: u64,
    is_leaf: bool,
    parent: u64,
    level: u16,
    children: []u64,
    members: []u64,

    pub fn deinit(self: *HBCDebugNode, alloc: Allocator) void {
        alloc.free(self.children);
        alloc.free(self.members);
    }
};

pub const RequestFilterState = struct {
    include: std.AutoHashMapUnmanaged(u64, void) = .empty,
    exclude: std.AutoHashMapUnmanaged(u64, void) = .empty,

    pub fn init(alloc: Allocator, req: SearchRequest) !RequestFilterState {
        var state = RequestFilterState{};
        errdefer state.deinit(alloc);

        if (req.filter_ids.len > 0) {
            try state.include.ensureTotalCapacity(alloc, @intCast(req.filter_ids.len));
            for (req.filter_ids) |id| try state.include.put(alloc, id, {});
        }
        if (req.exclude_ids.len > 0) {
            try state.exclude.ensureTotalCapacity(alloc, @intCast(req.exclude_ids.len));
            for (req.exclude_ids) |id| try state.exclude.put(alloc, id, {});
        }
        return state;
    }

    pub fn deinit(self: *RequestFilterState, alloc: Allocator) void {
        self.include.deinit(alloc);
        self.exclude.deinit(alloc);
        self.* = .{};
    }

    pub fn rejects(self: *const RequestFilterState, vector_id: u64) bool {
        if (self.exclude.count() > 0 and self.exclude.contains(vector_id)) return true;
        if (self.include.count() > 0 and !self.include.contains(vector_id)) return true;
        return false;
    }

    pub fn isTrivial(self: *const RequestFilterState) bool {
        return self.include.count() == 0 and self.exclude.count() == 0;
    }
};

pub fn candidateLessThan(_: void, a: types.PriorityItem, b: types.PriorityItem) std.math.Order {
    const a_score = candidatePriorityScore(a);
    const b_score = candidatePriorityScore(b);
    if (std.math.isNan(a_score)) {
        if (std.math.isNan(b_score)) return std.math.order(a.id, b.id);
        return .gt;
    }
    if (std.math.isNan(b_score)) return .lt;
    const order = std.math.order(a_score, b_score);
    if (order != .eq) return order;
    return std.math.order(a.id, b.id);
}

fn candidatePriorityScore(item: types.PriorityItem) f32 {
    // Covering-radius bounds are proof metadata, not an ANN-quality ranking.
    // Ordering by them preferentially visits broad postings whose lower bound
    // is near zero even when their centroid is far from the query.
    return item.distance - item.error_bound;
}

test "candidateLessThan gives NaN scores deterministic lowest priority" {
    const finite = types.PriorityItem{ .id = 10, .distance = 1, .error_bound = 0 };
    const nan_distance = types.PriorityItem{ .id = 20, .distance = std.math.nan(f32), .error_bound = 0 };
    const nan_bound = types.PriorityItem{ .id = 30, .distance = 1, .error_bound = std.math.nan(f32) };

    try std.testing.expectEqual(std.math.Order.lt, candidateLessThan({}, finite, nan_distance));
    try std.testing.expectEqual(std.math.Order.gt, candidateLessThan({}, nan_distance, finite));
    try std.testing.expectEqual(std.math.Order.lt, candidateLessThan({}, nan_distance, nan_bound));
}

test "candidateLessThan does not promote unresolved children over nearer siblings" {
    const nearer_resolved = types.PriorityItem{
        .id = 10,
        .distance = 1,
        .error_bound = 0.1,
        .lower_bound = 0.9,
        .bound_resolved = true,
    };
    const farther_unresolved = types.PriorityItem{
        .id = 20,
        .distance = 10,
        .error_bound = 0.1,
        .bound_resolved = false,
    };

    try std.testing.expectEqual(std.math.Order.lt, candidateLessThan({}, nearer_resolved, farther_unresolved));
    try std.testing.expectEqual(std.math.Order.gt, candidateLessThan({}, farther_unresolved, nearer_resolved));
}

test "candidateLessThan does not substitute covering lower bounds for ANN priority" {
    const nearer = types.PriorityItem{
        .id = 10,
        .distance = 1,
        .error_bound = 0.1,
        .lower_bound = 0.9,
    };
    const farther_broad = types.PriorityItem{
        .id = 20,
        .distance = 10,
        .error_bound = 0.1,
        .lower_bound = 0,
    };

    try std.testing.expectEqual(std.math.Order.lt, candidateLessThan({}, nearer, farther_broad));
    try std.testing.expectEqual(std.math.Order.gt, candidateLessThan({}, farther_broad, nearer));
}
