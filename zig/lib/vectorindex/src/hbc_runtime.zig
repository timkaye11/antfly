// Copyright 2026 Antfly, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

const std = @import("std");
const builtin = @import("builtin");
const Allocator = std.mem.Allocator;
const vec = @import("antfly_vector").vector;
const proto = @import("antfly_vector").proto;
const search_runtime = @import("search_runtime.zig");
const types = @import("types.zig");

pub const QuantizedSet = union(enum) {
    rabit: proto.RaBitQuantizedVectorSet,
    nonquant: proto.NonQuantizedVectorSet,

    pub fn getCount(self: *const QuantizedSet) usize {
        return switch (self.*) {
            .rabit => |*set| set.getCount(),
            .nonquant => |*set| set.getCount(),
        };
    }

    pub fn clone(self: *const QuantizedSet, alloc: Allocator) !QuantizedSet {
        return switch (self.*) {
            .rabit => |*set| .{ .rabit = try set.clone(alloc) },
            .nonquant => |*set| .{ .nonquant = try set.clone(alloc) },
        };
    }

    /// Copy an ordered subset without changing its scoring origin or error
    /// metadata. The caller binds row offsets to one posting mutation version;
    /// offsets from another membership revision must never be reused here.
    pub fn selectRows(self: *const QuantizedSet, alloc: Allocator, rows: []const usize) !QuantizedSet {
        const count = switch (self.*) {
            .rabit => |set| set.getCount(),
            .nonquant => |set| std.math.cast(usize, set.vectors.count) orelse return error.InvalidPostingRows,
        };
        for (rows, 0..) |row, i| {
            if (row >= count or (i != 0 and row <= rows[i - 1])) return error.InvalidPostingRows;
        }
        var result: QuantizedSet = switch (self.*) {
            .nonquant => .{ .nonquant = .{} },
            .rabit => .{ .rabit = .{} },
        };
        errdefer result.deinit(alloc);
        switch (self.*) {
            .nonquant => |set| {
                const dims = std.math.cast(usize, set.vectors.dims) orelse return error.InvalidPostingRows;
                if (set.vectors.data.len != try std.math.mul(usize, count, dims)) return error.InvalidPostingRows;
                result.nonquant.vectors = .{
                    .dims = set.vectors.dims,
                    .count = @intCast(rows.len),
                    .data = try selectRowPlane(f32, alloc, set.vectors.data, dims, rows),
                };
            },
            .rabit => |set| {
                const width = std.math.cast(usize, set.codes.width) orelse return error.InvalidPostingRows;
                if (width == 0 or set.codes.count != count or
                    set.codes.data.len != try std.math.mul(usize, count, width) or
                    set.centroid_distances.len != count or set.quantized_dot_products.len != count or
                    (set.centroid_dot_products.len != 0 and set.centroid_dot_products.len != count) or
                    (set.metric != .l2_squared and set.centroid_dot_products.len != count)) return error.InvalidPostingRows;
                const out = &result.rabit;
                out.metric = set.metric;
                out.centroid_norm = set.centroid_norm;
                out.centroid = try alloc.dupe(f32, set.centroid);
                out.codes = .{ .count = @intCast(rows.len), .width = set.codes.width, .data = try selectRowPlane(u64, alloc, set.codes.data, width, rows) };
                out.code_counts = try selectRowPlane(u32, alloc, set.code_counts, 1, rows);
                out.centroid_distances = try selectRowPlane(f32, alloc, set.centroid_distances, 1, rows);
                out.quantized_dot_products = try selectRowPlane(f32, alloc, set.quantized_dot_products, 1, rows);
                if (set.centroid_dot_products.len != 0) out.centroid_dot_products = try selectRowPlane(f32, alloc, set.centroid_dot_products, 1, rows);
            },
        }
        return result;
    }

    fn selectRowPlane(comptime T: type, alloc: Allocator, data: []const T, width: usize, rows: []const usize) ![]T {
        const out = try alloc.alloc(T, try std.math.mul(usize, rows.len, width));
        for (rows, 0..) |row, i| @memcpy(out[i * width ..][0..width], data[row * width ..][0..width]);
        return out;
    }

    pub fn deinit(self: *QuantizedSet, alloc: Allocator) void {
        switch (self.*) {
            .rabit => |*set| set.deinit(alloc),
            .nonquant => |*set| set.deinit(alloc),
        }
        self.* = undefined;
    }
};

test "posting row selection preserves scoring origin and is allocation safe" {
    const alloc = std.testing.allocator;
    const Attempt = struct {
        fn run(a: Allocator, source: *const QuantizedSet) !void {
            var selected = try source.selectRows(a, &.{ 0, 2 });
            defer selected.deinit(a);
            switch (source.*) {
                .nonquant => |set| {
                    try std.testing.expectEqualSlices(f32, set.vectors.data[0..2], selected.nonquant.vectors.data[0..2]);
                    try std.testing.expectEqualSlices(f32, set.vectors.data[4..6], selected.nonquant.vectors.data[2..4]);
                },
                .rabit => |set| {
                    try std.testing.expectEqualSlices(f32, set.centroid, selected.rabit.centroid);
                    try std.testing.expectEqual(set.centroid_norm, selected.rabit.centroid_norm);
                    for ([_]usize{ 0, 2 }, 0..) |row, i| {
                        try std.testing.expectEqualSlices(u64, set.codes.atConst(row), selected.rabit.codes.atConst(i));
                        try std.testing.expectEqual(set.code_counts[row], selected.rabit.code_counts[i]);
                        try std.testing.expectEqual(set.centroid_distances[row], selected.rabit.centroid_distances[i]);
                        try std.testing.expectEqual(set.quantized_dot_products[row], selected.rabit.quantized_dot_products[i]);
                    }
                },
            }
        }
    };
    for ([_]vec.DistanceMetric{ .l2_squared, .cosine, .inner_product }) |metric| {
        var q = try @import("antfly_vector").quantizer.RaBitQuantizer.init(alloc, 2, 42, metric);
        defer q.deinit();
        var source: QuantizedSet = .{ .rabit = try q.quantize(&.{ 0.5, 0.5 }, &.{ 1, 2, 3, 4, 5, 6 }, 3) };
        defer source.deinit(alloc);
        try std.testing.checkAllAllocationFailures(alloc, Attempt.run, .{&source});
        try std.testing.expectError(error.InvalidPostingRows, source.selectRows(alloc, &.{ 1, 1 }));
        try std.testing.expectError(error.InvalidPostingRows, source.selectRows(alloc, &.{ 2, 0 }));
        try std.testing.expectError(error.InvalidPostingRows, source.selectRows(alloc, &.{3}));
        var empty = try source.selectRows(alloc, &.{});
        defer empty.deinit(alloc);
        try std.testing.expectEqual(@as(usize, 0), empty.getCount());
    }
    var source: QuantizedSet = .{ .nonquant = .{ .vectors = .{ .dims = 2, .count = 3, .data = try alloc.dupe(f32, &.{ 1, 2, 3, 4, 5, 6 }) } } };
    defer source.deinit(alloc);
    try std.testing.checkAllAllocationFailures(alloc, Attempt.run, .{&source});
}

/// One immutable leaf scoring row borrowed from a generation lease. Keeping
/// membership beside the fixed-width candidate plane removes the packed-node
/// lookup and copy from the flat-directory search path. The source generation
/// remains authoritative: adapters decline this view as soon as any leaf
/// membership, posting state, or quantized payload is shadowed by a delta.
pub const NativeLeafScanView = struct {
    member_ids: []const u64,
    quantized: QuantizedSet,
    /// Native base/delta rows under the same complete generation lease. When
    /// present, quantized is only a placeholder and must not be scored/freed.
    row_snapshot: ?*const @import("posting_row_delta.zig").Snapshot = null,
    /// Optional source-space float16 rows owned by the same immutable posting
    /// generation. Rows follow member_ids exactly; per-row metadata makes the
    /// resulting score interval conservative enough to defer authoritative
    /// residual reads until the public top-k boundary is known.
    projections: ?NativeProjectionPlane = null,
    subgroup_plan: ?@import("posting_subgroups.zig").View = null,
};

pub const NativeProjectionPlane = struct {
    dims: usize,
    values: []const f16,
    scales: []const f32,
    error_norms: []const f32,
    decoded_norm_lower_bounds: []const f32,
    /// Source vector payload CRCs. Version-three posting generations omit
    /// this column and remain scoreable, but cannot use residual-only exact
    /// completion without revalidating the projection payload.
    checksums: []const u32 = &.{},
    verification: ?[]std.atomic.Value(u8) = null,
    /// Optional generation-bound locations of the lossless residuals in the
    /// shared exact-vector store. Older posting generations omit this plane;
    /// callers must then resolve the artifact key through the authoritative
    /// directory. A location is only a hint until the exact-vector generation
    /// validates its generation, shard, sequence, projection checksum, and
    /// residual checksum.
    residual_locations: ?NativeResidualLocationPlane = null,

    pub fn validateRow(self: @This(), row: usize) !void {
        if (row >= self.scales.len or row >= self.checksums.len or self.dims == 0 or
            row >= self.values.len / self.dims) return error.InvalidQuantizedDirectory;
        const values = self.values[row * self.dims ..][0..self.dims];
        try validateProjectionPayload(values, self.checksums[row], if (self.verification) |states| &states[row] else null);
    }

    pub fn validFor(self: @This(), count: usize, dims: usize) bool {
        const expected_values = std.math.mul(usize, count, dims) catch return false;
        return self.dims == dims and
            self.values.len == expected_values and
            self.scales.len == count and
            self.error_norms.len == count and
            self.decoded_norm_lower_bounds.len == count and
            (self.checksums.len == 0 or self.checksums.len == count) and
            (self.verification == null or self.verification.?.len == count) and
            (self.residual_locations == null or self.residual_locations.?.validFor(count));
    }
};

/// Memoization belongs to the immutable generation, never to an artifact ID
/// that can be reused by a later mutation. Concurrent first readers may both
/// hash a row; neither waits or holds a lock across mmap faults.
pub fn validateProjectionPayload(values: []const f16, checksum: u32, verification: ?*std.atomic.Value(u8)) !void {
    if (verification) |state| switch (state.load(.acquire)) {
        1 => return,
        2 => return error.QuantizedDirectoryChecksumMismatch,
        else => {},
    };
    const valid = @import("antfly_hash").Crc32.hash(std.mem.sliceAsBytes(values)) == checksum;
    if (verification) |state| state.store(if (valid) 1 else 2, .release);
    if (!valid) return error.QuantizedDirectoryChecksumMismatch;
}

pub const NativeResidualLocation = types.NativeResidualLocation;
pub const NativeResidualLocationPlane = types.NativeResidualLocationPlane;

/// One transient projection returned to an immutable checkpoint builder. The
/// callback owns the bytes only until it returns; the posting codec copies the
/// complete leaf plane before the next callback invocation.
pub const NativeProjectionBuildValue = struct {
    bytes: []const u8 = &.{},
    scale: f32 = 1,
    error_norm: f32 = 0,
    decoded_norm_lower_bound: f32 = 0,
    checksum: u32 = 0,
    residual_location: ?NativeResidualLocation = null,
};

pub const NativeProjectionBuildLoader = *const fn (
    ctx: *anyopaque,
    vector_ids: []const u64,
    metadata: []const ?[]const u8,
    values: []NativeProjectionBuildValue,
    payload_scratch: []u8,
    dims: usize,
    source_sequence: u64,
) anyerror!void;

pub const NativeProjectionBuildBegin = *const fn (ctx: *anyopaque, source_sequence: u64) anyerror!void;
pub const NativeProjectionBuildEnd = *const fn (ctx: *anyopaque) void;

pub const NativeProjectionBuildSource = struct {
    /// Training reads the shared projection, but need not duplicate that
    /// matrix in the posting generation.
    retain_projection_plane: bool = true,
    subgroup_count: u8 = 0,
    ctx: *anyopaque,
    loader: NativeProjectionBuildLoader,
    begin: ?NativeProjectionBuildBegin = null,
    end: ?NativeProjectionBuildEnd = null,
    /// Layout policy, independent of whether the source is available at this
    /// instant. Missing optional acceleration remains retryable serving debt.
    required: bool = false,
};

pub const WriteProfile = struct {
    bulk_build_store_ns: u64 = 0,
    bulk_build_tree_ns: u64 = 0,
    kmeans_assignment_calls: u64 = 0,
    kmeans_assignment_cpu_calls: u64 = 0,
    kmeans_assignment_metal_calls: u64 = 0,
    kmeans_assignment_points_total: u64 = 0,
    kmeans_assignment_ns: u64 = 0,
    kmeans_assignment_cpu_ns: u64 = 0,
    kmeans_assignment_metal_ns: u64 = 0,
    kmeans_update_calls: u64 = 0,
    kmeans_update_cpu_calls: u64 = 0,
    kmeans_update_metal_calls: u64 = 0,
    kmeans_update_ns: u64 = 0,
    kmeans_update_cpu_ns: u64 = 0,
    kmeans_update_metal_ns: u64 = 0,
    insert_transform_ns: u64 = 0,
    insert_store_vector_ns: u64 = 0,
    insert_find_leaf_ns: u64 = 0,
    insert_mutate_leaf_ns: u64 = 0,
    insert_flush_metadata_ns: u64 = 0,
    insert_commit_ns: u64 = 0,
    save_node_ns: u64 = 0,
    refresh_quantized_ns: u64 = 0,
    quantized_vector_load_ns: u64 = 0,
    quantized_leaf_vector_load_ns: u64 = 0,
    quantized_internal_child_load_ns: u64 = 0,
    quantized_compute_ns: u64 = 0,
    quantized_store_ns: u64 = 0,
    quantized_encode_ns: u64 = 0,
    quantized_put_ns: u64 = 0,
    external_vector_cache_hits: u64 = 0,
    external_vector_cache_misses: u64 = 0,
    centroid_recompute_calls: u64 = 0,
    delete_reused_vector_rows: u64 = 0,
    delete_preserved_vector_rows: u64 = 0,
    delete_native_vector_rows: u64 = 0,
    centroid_recompute_members_total: u64 = 0,
    centroid_recompute_members_max: u64 = 0,
    save_split_range_ns: u64 = 0,
    update_parent_ns: u64 = 0,
    split_leaf_ns: u64 = 0,
    split_leaf_vector_load_ns: u64 = 0,
    split_leaf_partition_ns: u64 = 0,
    split_leaf_finalize_ns: u64 = 0,
    split_internal_ns: u64 = 0,
    insert_calls: u64 = 0,
    save_node_calls: u64 = 0,
    update_parent_calls: u64 = 0,
    split_leaf_calls: u64 = 0,
    split_internal_calls: u64 = 0,
    deferred_leaf_split_publish_windows: u64 = 0,
    deferred_leaf_split_steps: u64 = 0,
    deferred_leaf_split_window_max_steps: u64 = 0,
    grouped_leaf_groups: u64 = 0,
    grouped_items: u64 = 0,
    grouped_fallback_items: u64 = 0,
    noop_existing_skips: u64 = 0,
    grouped_split_candidates: u64 = 0,
    grouped_recursive_splits: u64 = 0,
    grouped_split_scan_iterations: u64 = 0,
    grouped_split_queue_peak_total: u64 = 0,
    grouped_leaf_range_writes: u64 = 0,
    grouped_ancestor_range_refreshes: u64 = 0,
    grouped_ancestor_range_nodes: u64 = 0,
    grouped_node_body_writes: u64 = 0,
    grouped_vec_leaf_writes: u64 = 0,
    batch_route_calls: u64 = 0,
    batch_route_internal_nodes: u64 = 0,
    batch_route_leaf_groups: u64 = 0,
    batch_route_items: u64 = 0,
    batch_route_quantized_nodes: u64 = 0,
    batch_route_exact_child_scores: u64 = 0,
    batch_route_fallback_nodes: u64 = 0,
    split_leaf_input_members_total: u64 = 0,
    split_leaf_input_overflow_members_total: u64 = 0,
    bulk_leaf_rebuild_calls: u64 = 0,
    bulk_leaf_rebuild_members_total: u64 = 0,
    bulk_leaf_rebuild_members_max: u64 = 0,
    ns_nodes_put_calls: u64 = 0,
    ns_nodes_append_calls: u64 = 0,
    ns_nodes_delete_calls: u64 = 0,
    ns_nodes_key_bytes: u64 = 0,
    ns_nodes_value_bytes: u64 = 0,
    ns_meta_put_calls: u64 = 0,
    ns_meta_append_calls: u64 = 0,
    ns_meta_delete_calls: u64 = 0,
    ns_meta_key_bytes: u64 = 0,
    ns_meta_value_bytes: u64 = 0,
    ns_quant_put_calls: u64 = 0,
    ns_quant_append_calls: u64 = 0,
    ns_quant_delete_calls: u64 = 0,
    ns_quant_key_bytes: u64 = 0,
    ns_quant_value_bytes: u64 = 0,
    ns_vecs_put_calls: u64 = 0,
    ns_vecs_append_calls: u64 = 0,
    ns_vecs_delete_calls: u64 = 0,
    ns_vecs_key_bytes: u64 = 0,
    ns_vecs_value_bytes: u64 = 0,
    posting_maintenance_scanned_nodes: u64 = 0,
    posting_maintenance_scanned_postings: u64 = 0,
    posting_maintenance_dirty_postings: u64 = 0,
    posting_maintenance_repaired_postings: u64 = 0,
    posting_maintenance_centroid_refreshed: u64 = 0,
    posting_maintenance_payload_refreshed: u64 = 0,
    posting_maintenance_ancestor_refresh_roots: u64 = 0,
    posting_maintenance_split_postings: u64 = 0,
    posting_maintenance_merged_postings: u64 = 0,
    posting_maintenance_boundary_reassigned_vectors: u64 = 0,
    posting_lazy_centroid_deferrals: u64 = 0,
    posting_lazy_payload_deferrals: u64 = 0,
    posting_lazy_ancestor_deferrals: u64 = 0,
    range_put_calls: u64 = 0,
    range_delete_calls: u64 = 0,
    range_key_bytes: u64 = 0,
    range_value_bytes: u64 = 0,
};

pub const BatchInsertItem = struct {
    vector_id: u64,
    vector: []const f32,
    transformed: ?[]const f32 = null,
    metadata: []const u8 = "",
};

pub const BatchVectorLookup = struct {
    ptr: *const anyopaque,
    getFn: *const fn (ptr: *const anyopaque, vector_id: u64) ?[]const f32,

    pub fn get(self: BatchVectorLookup, vector_id: u64) ?[]const f32 {
        return self.getFn(self.ptr, vector_id);
    }
};

pub const BatchInsertOptions = struct {
    /// Share one authoritative transformed leaf matrix between centroid and
    /// payload refresh on eager batch deletes. Does not defer either refresh.
    reuse_delete_vectors: bool = false,
    preserve_delete_rows: bool = false,
    defer_quantized_rebuild: bool = false,
    defer_quantized_rebuild_to_bulk_finish: bool = false,
    centroid_only_routing: bool = false,
    allow_quantized_routing: bool = false,
    assume_absent_ids: bool = false,
    coalesce_leaf_writes: bool = false,
    skip_vector_store: bool = false,
    bulk_ingest: bool = false,
    defer_leaf_splits_to_batch_finish: bool = false,
    defer_leaf_splits_to_bulk_finish: bool = false,
    suppress_quantized_payload_persist: bool = false,
    bulk_rebuild_leaf_min_members: usize = 0,
    batch_vectors: ?BatchVectorLookup = null,
};

pub const SearchScratch = search_runtime.SearchScratch;

pub const ScratchHandle = struct {
    scratch: SearchScratch,
    from_cache: bool,
    accounted_bytes: u64 = 0,
};

fn lockAtomic(mutex: *std.atomic.Mutex) void {
    @import("antfly_platform").sync.lockYielding(mutex);
}

fn nodeCacheValueType(self: anytype) type {
    return @FieldType(@TypeOf(self.node_cache).KV, "value");
}

fn quantizedCacheValueType(self: anytype) type {
    return @FieldType(@TypeOf(self.quantized_cache).KV, "value");
}

fn nodeCacheValuePtr(value_ptr: anytype) *types.Node {
    const Value = @TypeOf(value_ptr.*);
    return switch (@typeInfo(Value)) {
        .pointer => &value_ptr.*.node,
        else => value_ptr,
    };
}

fn quantizedCacheValuePtr(value_ptr: anytype) *QuantizedSet {
    const Value = @TypeOf(value_ptr.*);
    return switch (@typeInfo(Value)) {
        .pointer => &value_ptr.*.quantized,
        else => value_ptr,
    };
}

fn deinitNodeCacheValue(alloc: Allocator, value: anytype) void {
    const Value = @TypeOf(value);
    switch (@typeInfo(Value)) {
        .pointer => {
            var node = value.node;
            node.deinit(alloc);
            alloc.destroy(value);
        },
        else => {
            var node = value;
            node.deinit(alloc);
        },
    }
}

fn deinitQuantizedCacheValue(alloc: Allocator, value: anytype) void {
    const Value = @TypeOf(value);
    switch (@typeInfo(Value)) {
        .pointer => {
            var quantized = value.quantized;
            quantized.deinit(alloc);
            alloc.destroy(value);
        },
        else => {
            var quantized = value;
            quantized.deinit(alloc);
        },
    }
}

fn cloneNodeCacheValue(value_ptr: anytype, alloc: Allocator) !types.Node {
    return try nodeCacheValuePtr(value_ptr).clone(alloc);
}

fn cloneQuantizedCacheValue(value_ptr: anytype, alloc: Allocator) !QuantizedSet {
    return try quantizedCacheValuePtr(value_ptr).clone(alloc);
}

fn initNodeCacheValue(self: anytype, node: types.Node) !nodeCacheValueType(self) {
    const Value = comptime nodeCacheValueType(self);
    return switch (@typeInfo(Value)) {
        .pointer => blk: {
            const entry = try self.alloc.create(@typeInfo(Value).pointer.child);
            entry.* = .{ .node = node };
            break :blk entry;
        },
        else => node,
    };
}

fn initQuantizedCacheValue(self: anytype, qs: QuantizedSet) !quantizedCacheValueType(self) {
    const Value = comptime quantizedCacheValueType(self);
    return switch (@typeInfo(Value)) {
        .pointer => blk: {
            const entry = try self.alloc.create(@typeInfo(Value).pointer.child);
            entry.* = .{ .quantized = qs };
            break :blk entry;
        },
        else => qs,
    };
}

pub fn clearNodeCache(self: anytype) void {
    var it = self.node_cache.iterator();
    while (it.next()) |entry| deinitNodeCacheValue(self.alloc, entry.value_ptr.*);
    self.node_cache.deinit(self.alloc);
    self.node_cache = .empty;
    self.node_cache_slots.deinit(self.alloc);
    self.node_cache_slots = .empty;
    @memset(self.node_clock_keys, 0);
    @memset(self.node_clock_refs, false);
    self.node_clock_hand = 0;
}

pub fn clearQuantizedCache(self: anytype) void {
    var it = self.quantized_cache.iterator();
    while (it.next()) |entry| deinitQuantizedCacheValue(self.alloc, entry.value_ptr.*);
    self.quantized_cache.deinit(self.alloc);
    self.quantized_cache = .empty;
    self.quantized_cache_slots.deinit(self.alloc);
    self.quantized_cache_slots = .empty;
    @memset(self.quantized_clock_keys, 0);
    @memset(self.quantized_clock_refs, false);
    self.quantized_clock_hand = 0;
}

pub fn clearVectorCache(self: anytype) void {
    var it = self.vector_cache.iterator();
    while (it.next()) |entry| self.alloc.free(entry.value_ptr.*);
    self.vector_cache.deinit(self.alloc);
    self.vector_cache = .empty;
    self.vector_cache_slots.deinit(self.alloc);
    self.vector_cache_slots = .empty;
    @memset(self.vector_clock_keys, 0);
    @memset(self.vector_clock_refs, false);
    self.vector_clock_hand = 0;
}

pub fn clearMetadataCache(self: anytype) void {
    var it = self.metadata_cache.iterator();
    while (it.next()) |entry| self.alloc.free(entry.value_ptr.*);
    self.metadata_cache.deinit(self.alloc);
    self.metadata_cache = .empty;
    self.metadata_cache_slots.deinit(self.alloc);
    self.metadata_cache_slots = .empty;
    @memset(self.metadata_clock_keys, 0);
    @memset(self.metadata_clock_refs, false);
    self.metadata_clock_hand = 0;
}

pub fn invalidateNodeCache(self: anytype, node_id: u64) void {
    if (self.node_cache_slots.fetchRemove(node_id)) |removed_slot| {
        self.node_clock_keys[removed_slot.value] = 0;
        self.node_clock_refs[removed_slot.value] = false;
    }
    if (self.node_cache.fetchRemove(node_id)) |removed| deinitNodeCacheValue(self.alloc, removed.value);
}

pub fn invalidateQuantizedCache(self: anytype, node_id: u64) void {
    if (self.quantized_cache_slots.fetchRemove(node_id)) |removed_slot| {
        self.quantized_clock_keys[removed_slot.value] = 0;
        self.quantized_clock_refs[removed_slot.value] = false;
    }
    if (self.quantized_cache.fetchRemove(node_id)) |removed| deinitQuantizedCacheValue(self.alloc, removed.value);
}

pub fn invalidateVectorCache(self: anytype, vector_id: u64) void {
    if (self.vector_cache_slots.fetchRemove(vector_id)) |removed_slot| {
        self.vector_clock_keys[removed_slot.value] = 0;
        self.vector_clock_refs[removed_slot.value] = false;
    }
    if (self.vector_cache.fetchRemove(vector_id)) |removed| self.alloc.free(removed.value);
}

pub fn invalidateMetadataCache(self: anytype, vector_id: u64) void {
    if (self.metadata_cache_slots.fetchRemove(vector_id)) |removed_slot| {
        self.metadata_clock_keys[removed_slot.value] = 0;
        self.metadata_clock_refs[removed_slot.value] = false;
    }
    if (self.metadata_cache.fetchRemove(vector_id)) |removed| self.alloc.free(removed.value);
}

fn touchClock(refs: []bool, slot_map: anytype, key: u64) void {
    if (slot_map.get(key)) |slot| refs[slot] = true;
}

fn claimClockSlot(clock_keys: []u64, start_slot: usize, key: u64) ?usize {
    if (clock_keys.len == 0) return null;
    for (0..clock_keys.len) |offset| {
        const slot = (start_slot + offset) % clock_keys.len;
        const slot_key = clock_keys[slot];
        if (slot_key == 0) {
            clock_keys[slot] = key;
            return slot;
        }
    }
    return null;
}

fn evictClockVictim(
    clock_keys: []u64,
    clock_refs: []bool,
    hand: *usize,
) ?u64 {
    if (clock_keys.len == 0) return null;
    var scanned: usize = 0;
    const limit = clock_keys.len * 2;
    while (scanned < limit) : (scanned += 1) {
        const slot = hand.*;
        const key = clock_keys[slot];
        if (key != 0) {
            if (clock_refs[slot]) {
                clock_refs[slot] = false;
            } else {
                hand.* = (slot + 1) % clock_keys.len;
                return key;
            }
        }
        hand.* = (slot + 1) % clock_keys.len;
    }
    return null;
}

fn ensureNodeCacheCapacity(self: anytype, key: u64) ?usize {
    if (self.config.max_cached_nodes == 0) return null;
    if (self.node_cache.contains(key)) return null;
    while (self.node_cache.count() >= self.config.max_cached_nodes) {
        const victim = evictClockVictim(self.node_clock_keys, self.node_clock_refs, &self.node_clock_hand) orelse break;
        const slot = self.node_cache_slots.get(victim).?;
        invalidateNodeCache(self, victim);
        return slot;
    }
    return null;
}

fn ensureQuantizedCacheCapacity(self: anytype, key: u64) ?usize {
    if (self.config.max_cached_nodes == 0) return null;
    if (self.quantized_cache.contains(key)) return null;
    while (self.quantized_cache.count() >= self.config.max_cached_nodes) {
        const victim = evictClockVictim(self.quantized_clock_keys, self.quantized_clock_refs, &self.quantized_clock_hand) orelse break;
        const slot = self.quantized_cache_slots.get(victim).?;
        invalidateQuantizedCache(self, victim);
        return slot;
    }
    return null;
}

fn ensureVectorCacheCapacity(self: anytype, key: u64) ?usize {
    if (self.config.max_cached_vectors == 0) return null;
    if (self.vector_cache.contains(key)) return null;
    while (self.vector_cache.count() >= self.config.max_cached_vectors) {
        const victim = evictClockVictim(self.vector_clock_keys, self.vector_clock_refs, &self.vector_clock_hand) orelse break;
        const slot = self.vector_cache_slots.get(victim).?;
        invalidateVectorCache(self, victim);
        return slot;
    }
    return null;
}

fn ensureMetadataCacheCapacity(self: anytype, key: u64) ?usize {
    if (self.config.max_cached_metadata == 0) return null;
    if (self.metadata_cache.contains(key)) return null;
    while (self.metadata_cache.count() >= self.config.max_cached_metadata) {
        const victim = evictClockVictim(self.metadata_clock_keys, self.metadata_clock_refs, &self.metadata_clock_hand) orelse break;
        const slot = self.metadata_cache_slots.get(victim).?;
        invalidateMetadataCache(self, victim);
        return slot;
    }
    return null;
}

pub fn getCachedNodeClone(self: anytype, node_id: u64) !?types.Node {
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.node_cache.getPtr(node_id)) |cached| {
        touchClock(self.node_clock_refs, self.node_cache_slots, node_id);
        return try cloneNodeCacheValue(cached, self.alloc);
    }
    return null;
}

pub fn getCachedQuantizedClone(self: anytype, node_id: u64) !?QuantizedSet {
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.quantized_cache.getPtr(node_id)) |cached| {
        touchClock(self.quantized_clock_refs, self.quantized_cache_slots, node_id);
        return try cloneQuantizedCacheValue(cached, self.alloc);
    }
    return null;
}

pub fn getCachedMetadata(self: anytype, vector_id: u64) ?[]const u8 {
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.metadata_cache.get(vector_id)) |cached| {
        touchClock(self.metadata_clock_refs, self.metadata_cache_slots, vector_id);
        return cached;
    }
    return null;
}

pub fn cacheNode(self: anytype, node: *const types.Node) !void {
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.config.max_cached_nodes == 0) return;
    const reserved_slot = ensureNodeCacheCapacity(self, node.id);
    invalidateNodeCache(self, node.id);
    const cached_value = try initNodeCacheValue(self, try node.clone(self.alloc));
    errdefer deinitNodeCacheValue(self.alloc, cached_value);
    try self.node_cache.put(self.alloc, node.id, cached_value);
    const slot = reserved_slot orelse claimClockSlot(self.node_clock_keys, self.node_clock_hand, node.id) orelse return error.CacheDisabled;
    self.node_clock_refs[slot] = true;
    try self.node_cache_slots.put(self.alloc, node.id, slot);
}

pub fn cacheQuantized(self: anytype, node_id: u64, qs: *const QuantizedSet) !void {
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.config.max_cached_nodes == 0) return;
    const reserved_slot = ensureQuantizedCacheCapacity(self, node_id);
    invalidateQuantizedCache(self, node_id);
    const cached_value = try initQuantizedCacheValue(self, try qs.clone(self.alloc));
    errdefer deinitQuantizedCacheValue(self.alloc, cached_value);
    try self.quantized_cache.put(self.alloc, node_id, cached_value);
    const slot = reserved_slot orelse claimClockSlot(self.quantized_clock_keys, self.quantized_clock_hand, node_id) orelse return error.CacheDisabled;
    self.quantized_clock_refs[slot] = true;
    try self.quantized_cache_slots.put(self.alloc, node_id, slot);
}

pub fn cacheQuantizedOwned(self: anytype, node_id: u64, qs: QuantizedSet) !void {
    var owned = qs;
    errdefer owned.deinit(self.alloc);
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.config.max_cached_nodes == 0) return error.CacheDisabled;
    const reserved_slot = ensureQuantizedCacheCapacity(self, node_id);
    invalidateQuantizedCache(self, node_id);
    const cached_value = try initQuantizedCacheValue(self, owned);
    errdefer deinitQuantizedCacheValue(self.alloc, cached_value);
    try self.quantized_cache.put(self.alloc, node_id, cached_value);
    const slot = reserved_slot orelse claimClockSlot(self.quantized_clock_keys, self.quantized_clock_hand, node_id) orelse return error.CacheDisabled;
    self.quantized_clock_refs[slot] = true;
    try self.quantized_cache_slots.put(self.alloc, node_id, slot);
}

pub fn cacheVector(self: anytype, vector_id: u64, vector_data: []const f32) ![]const f32 {
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.active_searches.load(.acquire) > 1) return vector_data;
    if (self.config.max_cached_vectors == 0) return vector_data;
    const reserved_slot = ensureVectorCacheCapacity(self, vector_id);
    invalidateVectorCache(self, vector_id);
    try self.vector_cache.put(self.alloc, vector_id, try self.alloc.dupe(f32, vector_data));
    const slot = reserved_slot orelse claimClockSlot(self.vector_clock_keys, self.vector_clock_hand, vector_id) orelse return error.CacheDisabled;
    self.vector_clock_refs[slot] = true;
    try self.vector_cache_slots.put(self.alloc, vector_id, slot);
    return self.vector_cache.get(vector_id).?;
}

pub fn cacheMetadata(self: anytype, vector_id: u64, metadata: []const u8) ![]const u8 {
    self.cache_mu.lockExclusive();
    defer self.cache_mu.unlockExclusive();
    if (self.active_searches.load(.acquire) > 1) return metadata;
    if (self.config.max_cached_metadata == 0) return metadata;
    const reserved_slot = ensureMetadataCacheCapacity(self, vector_id);
    invalidateMetadataCache(self, vector_id);
    try self.metadata_cache.put(self.alloc, vector_id, try self.alloc.dupe(u8, metadata));
    const slot = reserved_slot orelse claimClockSlot(self.metadata_clock_keys, self.metadata_clock_hand, vector_id) orelse return error.CacheDisabled;
    self.metadata_clock_refs[slot] = true;
    try self.metadata_cache_slots.put(self.alloc, vector_id, slot);
    // The cache owns its copy. Callers retain the transaction/request view;
    // returning cache memory here would outlive the eviction lock.
    return metadata;
}

pub fn acquireSearchScratch(self: anytype) !ScratchHandle {
    lockAtomic(&self.scratch_mu);
    if (self.cached_scratch) |scratch| {
        self.cached_scratch = null;
        self.scratch_mu.unlock();
        return .{ .scratch = scratch, .from_cache = true, .accounted_bytes = scratch.bytes() };
    }
    const Index = @TypeOf(self.*);
    if (comptime @hasField(Index, "cached_search_scratches")) {
        if (self.cached_search_scratches.pop()) |scratch| {
            self.scratch_mu.unlock();
            return .{ .scratch = scratch, .from_cache = true, .accounted_bytes = scratch.bytes() };
        }
    }
    self.scratch_mu.unlock();

    const dims: usize = @intCast(self.metadata.dims);
    const branching_factor: usize = @intCast(self.metadata.branching_factor);
    const leaf_size: usize = @intCast(self.metadata.leaf_size);
    const initial_bytes = try SearchScratch.initialBytes(dims, branching_factor, leaf_size);
    const pre_admitted = comptime @hasDecl(Index, "admitNewSearchScratchBytes") and
        @hasDecl(Index, "releaseSearchScratchBytes");
    if (pre_admitted) try self.admitNewSearchScratchBytes(initial_bytes);
    errdefer if (pre_admitted) self.releaseSearchScratchBytes(initial_bytes);
    var scratch = try SearchScratch.init(
        self.alloc,
        dims,
        branching_factor,
        leaf_size,
    );
    errdefer scratch.deinit(self.alloc);
    std.debug.assert(scratch.bytes() == initial_bytes);
    if (!pre_admitted and comptime @hasDecl(Index, "observeSearchWorkspaceBytes")) {
        self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted + scratch.bytes());
    }
    return .{
        .scratch = scratch,
        .from_cache = false,
        .accounted_bytes = scratch.bytes(),
    };
}

pub fn refreshSearchScratchAccounting(self: anytype, handle: *ScratchHandle) void {
    const next = handle.scratch.bytes();
    if (next == handle.accounted_bytes) return;
    if (comptime @hasDecl(@TypeOf(self.*), "reconcileSearchScratchBytes")) {
        self.reconcileSearchScratchBytes(handle, next);
        return;
    }
    if (comptime @hasDecl(@TypeOf(self.*), "observeSearchWorkspaceBytes")) {
        if (next > handle.accounted_bytes) {
            self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted + (next - handle.accounted_bytes));
        } else if (next < handle.accounted_bytes) {
            self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted -| (handle.accounted_bytes - next));
        }
        handle.accounted_bytes = next;
    }
}

pub fn beginSearchEpoch(self: anytype) void {
    _ = self.active_searches.fetchAdd(1, .acq_rel);
}

pub fn endSearchEpoch(self: anytype) void {
    _ = self.active_searches.fetchSub(1, .acq_rel);
}

pub fn releaseSearchScratch(self: anytype, handle: *ScratchHandle) void {
    lockAtomic(&self.scratch_mu);
    if (self.cached_scratch == null) {
        self.cached_scratch = handle.scratch;
        self.scratch_mu.unlock();
        return;
    }
    const Index = @TypeOf(self.*);
    if (comptime @hasField(Index, "cached_search_scratches") and @hasDecl(Index, "searchScratchCacheLimit")) {
        const cached_count = self.cached_search_scratches.items.len + 1;
        if (cached_count < self.searchScratchCacheLimit()) {
            self.cached_search_scratches.append(self.alloc, handle.scratch) catch {
                self.scratch_mu.unlock();
                deinitReleasedSearchScratch(self, handle);
                return;
            };
            self.scratch_mu.unlock();
            return;
        }
    }
    self.scratch_mu.unlock();
    deinitReleasedSearchScratch(self, handle);
}

fn deinitReleasedSearchScratch(self: anytype, handle: *ScratchHandle) void {
    var scratch = handle.scratch;
    const Index = @TypeOf(self.*);
    if (comptime @hasDecl(Index, "releaseSearchScratchBytes")) {
        self.releaseSearchScratchBytes(handle.accounted_bytes);
    } else if (comptime @hasDecl(Index, "observeSearchWorkspaceBytes")) {
        self.observeSearchWorkspaceBytes(self.search_workspace_bytes_accounted -| handle.accounted_bytes);
    }
    scratch.deinit(self.alloc);
}

pub fn transformVector(self: anytype, original: []const f32, transformed: []f32) []const f32 {
    _ = self.rot.transform(original, transformed);
    if (self.config.metric == .cosine) {
        _ = vec.normalize(transformed);
    }
    return transformed;
}

pub fn nextNodeId(self: anytype) u64 {
    self.metadata.node_count += 1;
    return self.metadata.node_count;
}
